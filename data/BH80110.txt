  PROCESS OPT(TIME);                                                    
 BH80110: /* BEREGNING AF LEJEVÆRDI FOR INDKOMSTÅR 1999 */              
 PROCEDURE (BH801,BH60101M) REORDER;                                    
 /*********************************************************************/
                                                                        
 DCL COPYRIGHT CHAR (30) STATIC INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');
                                                                        
 /**********************************************************************
 *                                                                     *
 * DETTE MODUL HAR TIL FORMÅL AT UDFØRE BEREGNING AF LEJEVÆRDI P.G.A.  *
 * OPLYSNINGERNE I BEREGNINGSBÅNDET HENTET FRA HENHOLDSVIS ESR-UDTRÆK, *
 * OVERGANGSORDNINGSBÅND FRA BEREGNINGEN FOR INDKOMSTÅR 1998           *
 *                                                                     *
 * NB.: DETTE MODUL BRUGES KUN TIL                                     *
 *      BEREGNING AF LEJEVÆRDI FOR INDKOMSTÅR 1999 TIL BOY 2000        *
 *      (LAVET P.G.A. MODUL BH95010)                                   *
 *                                                                     *
 *  PROGRAMMERET AF AAGE RASMUSSEN   PFB SKAT   SEP.   1999            *
 *  ÅRSTILRETTET                                                       *
 **********************************************************************/
                                                                        
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      DECLARE AF DIVERSE PARAMETRE TIL DETTE MODUL                 */
 /*********************************************************************/
                                                                        
 DCL     1 BH801,                                                       
           %INCLUDE BH80100N;;                                          
                                                                        
 DCL     1 BH60101M ALIGNED,                                            
         %INCLUDE BH60101M;                                             
                                                                        
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
                                                                        
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
                                                                        
 /********************************************************************* 
 *       DECLARE AF DIV. HJÆLPEFELTER                                 * 
 *********************************************************************/ 
 DCL     JA              BIT       (1)       INIT ('1'B);               
 DCL     NEJ             BIT       (1)       INIT ('0'B);               
 DCL     GRÆNSEVÆRDI     FIXED     (7);                                 
 DCL     PENSPCT         FIXED     (5,4);                               
 DCL     CREDUKT         PIC       '9';  /* KODE FOR REDUKTIONSBEREGN.*/
 DCL     CREDUKTTYP      PIC       '9';  /* KODE FOR REDUKTIONSTYPE   */
                                                                        
 DCL     1 BH60100M,                                                    
         %INCLUDE BH60100M;                                             
                                                                        
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
                                                                        
         /* INITIER/NULSTIL DIVERSE FELTER INDEN BEREGNING */           
                                                                        
         CALL SKAF_BEREGN_GRUNDLAG_1999;                                
                                                                        
         CALL NULSTIL_LEJEVÆRDI_FELTER;                                 
                                                                        
         CALL UNDTAGELSER_LEJEVÆRDI_EJENDOM;                            
                                                                        
         IF BEREGN = JA                /* DER SKAL BEREGNES LEJEVÆRDI */
         THEN                          /* FOR EJENDOMMEN              */
            DO;                                                         
               GRÆNSEVÆRDI = 2393000;  /* VÆRDI FOR 1999 */             
               PENSPCT     = 0.0100;   /* VÆRDI FOR 1999 */             
               /* FØRST BEREGNES UDEN HENSYN TIL OM DER ER TALE OM */   
               /* ET 'HELÅRSBEBOET' SOMMERHUS. DETTE GØRES AF      */   
               /* HENSYN TIL OVERFØRSEL AF DE 3 'FASTE' EJENDOMS-  */   
               /* LEJEVÆRDIER TIL ESR SAMT EVT. BEREGNING FOR 2000 */   
               CALL LEJEVÆRDI_EJENDOM(BH801.EJD_1999,PENSPCT,'1');      
               CALL UNDTAGELSER_REDUKTION;                              
               IF BH801.EJD_1999(1).BLEJE ^= 0 &                        
                  BEREGN = JA          /* BLEV BEREGNET SIDSTE ÅR OG  */
               THEN                    /* ER IKKE UNDTAGET REDUKTION  */
                  CALL REDUCER_EJENDOM(BH801.EJD_1999);                 
               CALL SKAF_NYE_EJD_LEJEVÆRDIER                            
                    (BH801.EJD_1999,BH801.EJD_2000);                    
               /* SÅ BEREGNES MEN  K U N                             */ 
               /* HVIS 'HELÅRSBEBOET' SOMMERHUS  M E D  DISPENSATION */ 
               /* AF HENSYN TIL VALG AF PERSONLIG LEJEVÆRDI.         */ 
               IF (BH801.CBENYT = '08' !                                
                   BH801.CBENYT = '28' !                                
                   BH801.CBENYT = '48') &                               
                  (BH801.C_2PCT = '1' !                                 
                   BH801.C_2PCT = '2' !                                 
                   BH801.C_2PCT = '3' !                                 
                   BH801.C_2PCT = '7' !                                 
                   BH801.C_2PCT = '8' !                                 
                   BH801.C_2PCT = '9')                                  
               THEN                                                     
                 DO;                                                    
                    CALL SKAF_GL_LEJEVÆRDIER;                           
                    CALL LEJEVÆRDI_EJENDOM(BH801.SOM_1999,PENSPCT,'2'); 
                    CALL UNDTAGELSER_REDUKTION;                         
                    IF BH801.SOM_1999(1).BLEJE ^= 0 &                   
                       BEREGN = JA                                      
                    THEN                                                
                       CALL REDUCER_EJENDOM(BH801.SOM_1999);            
                                                                        
                    /* OPSÆT OVERGANGSORDNINGSKODE FOR 1999 SAMT */     
                    /* GEM DE TRE 'NYE' EJENDOMSLEJEVÆRDIER I    */     
                    /* BEREGNINGSBÅNDET A.H.T. KORREKT BEREGNING */     
                    /* AF NÆSTE ÅRS LEJEVÆRDIER HVIS PERSONEN    */     
                    /* IGEN HAR 'HELÅRSSTATUS' I SOMMERHUS.      */     
                                                                        
                    /* OPLYSNINGERNE SKRIVES DESUDEN PÅ NYT  */         
                    /* OVERGANGSORDINGSBÅND I ORDINÆR ESKEMA */         
                    /* (SKER I PROGRAM BH60100).             */         
                                                                        
                    CALL SKAF_NYE_EJD_LEJEVÆRDIER                       
                         (BH801.SOM_1999,BH801.OVG_1999);               
                    BH801.COVG_1999 = '2';                              
                                                                        
                    CALL UNDTAGELSER_LEJEVÆRDI_PERSON;                  
                    IF BEREGN = JA     /* DER SKAL BEREGNES LEJEVÆRDI */
                    THEN               /* FOR PERSONEN                */
                      DO;                                               
                         CALL PERSONLIG_EJD_LEJEVÆRDI(BH801.SOM_1999);  
                         CALL LEJEVÆRDI_PERSON(BH801.SOM_1999);         
                      END;                                              
                 END;                                                   
               ELSE                                                     
                 DO;                                                    
                    CALL UNDTAGELSER_LEJEVÆRDI_PERSON;                  
                    IF BEREGN = JA     /* DER SKAL BEREGNES LEJEVÆRDI */
                    THEN               /* FOR PERSONEN                */
                      DO;                                               
                         CALL PERSONLIG_EJD_LEJEVÆRDI(BH801.EJD_1999);  
                         CALL LEJEVÆRDI_PERSON(BH801.EJD_1999);         
                      END;                                              
                 END;                                                   
            END;                                                        
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /* FORETAG OPSÆTNING AF BEREGNINGSGRUNDLAGET SOM SKAL BRUGES VED BE- */
 /* REGNING AF LEJEVÆRDI M.V. FOR 1999 P.G.A. EJENDOMSVÆRDI, EJERBO-  */
 /* LIG ELLER STUEHUSVÆRDI(ER) PR. 1/1-1999.                          */
 /*                                                                   */
 /*********************************************************************/
 SKAF_BEREGN_GRUNDLAG_1999:                                             
         PROC;                                                          
                                                                        
         SELECT;                                                        
                                                                        
            WHEN (BH801.ANT_FOR_1 > 0)                                  
               DO;                                                      
                  /* MINDRE UDLEJNINGSEJENDOM */                        
               END;                                                     
                                                                        
            WHEN (BH801.ANT_FOR_2 > 0)                                  
               DO;                                                      
                  /* BOFÆLLESSKAB           */                          
               END;                                                     
                                                                        
            WHEN (BH801.ANT_FOR_4 = 0 &                                 
                  BH801.ANT_FOR_5 = 0 &                                 
                  BH801.ANT_FOR_6 = 0)                                  
               DO;                                                      
                  /* INGEN EJERBOLIG- ELLER STUEHUSVÆRDI */             
                  /* ELLER MINDRE UDLEJN ELLER BOFÆLLES  */             
                  /* SÅ BENYT EJENDOMSVÆRDI              */             
                  HJVÆRDI = BH801.BEJDVÆR;                              
               END;                                                     
                                                                        
            WHEN (BH801.ANT_FOR_4 +                                     
                  BH801.ANT_FOR_5 +                                     
                  BH801.ANT_FOR_6 = 1)                                  
               DO;                                                      
                  /* DER FINDES KUN 1 EJERBOLIG- ELLER   */             
                  /* STUEHUSVÆRDI, SOM DERFOR BENYTTES I */             
                  /* STEDET FOR EJENDOMSVÆRDIEN          */             
                  HJVÆRDI = BH801.EJEBOL(1).BEJEBOL;                    
               END;                                                     
                                                                        
            OTHERWISE;                                                  
                                                                        
         END;                                                           
                                                                        
         BH801.BERGRL1 = HJVÆRDI;                                       
                                                                        
 END SKAF_BEREGN_GRUNDLAG_1999;                                         
                                                                        
 /********************************************************************* 
 *      MÅ DER BEREGNES REDUKTION (GÆLDER BÅDE EJENDOMMENS OG PERSO-  * 
 *      NENS LEJEVÆRDIER).                                            * 
 *********************************************************************/ 
 UNDTAGELSER_REDUKTION:                                                 
 PROC;                                                                  
                                                                        
        BEREGN = NEJ;                                                   
                                                                        
        IF BH801.CFORSKEL = '2'                                         
        THEN                                                            
           RETURN;                                                      
                                                                        
        IF (BH801.CBENYT = '21' !                                       
            BH801.CBENYT = '22' !                                       
            BH801.CBENYT = '26' !                                       
            BH801.CBENYT = '27' !                                       
            BH801.CBENYT = '28' !                                       
            BH801.CBENYT = '29') &                                      
           (BH801.CEJERL_GL = '1' &                                     
            BH801.CEJERL ^= '1')                                        
        THEN                                                            
           RETURN;                                                      
                                                                        
        BEREGN = JA;                                                    
                                                                        
 END UNDTAGELSER_REDUKTION;                                             
                                                                        
 /****************************************************************/     
 /*                                                              */     
 /*      FOR PERSONER SOM EJER SOMMERHUSE MED 'HELÅRSSTATUS'     */     
 /*      SKAL DER VED BEREGNING AF LEJEVÆRDIEN FOR INDKOMSTÅR    */     
 /*      1999 TAGES UDGANGSPUNKT I                               */     
 /*      E N T E N                                               */     
 /*      DE 3 1998-EJENDOMS-LEJEVÆRDIER HENTET FRA ESR (HVIS     */     
 /*      PERSONEN  I K K E  BLEV BEREGNET MED 'HELÅRSSTATUS'     */     
 /*      ÅRET FØR)                                               */     
 /*      E L L E R                                               */     
 /*      DE 3 1998-HELÅRSSOMMERHUS-LEJEVÆRDIER HENTET FRA OVER-  */     
 /*      ORDNINGSBÅNDET (HVIS PERSONEN  B L E V  BEREGNET MED    */     
 /*      'HELÅRSSTATUS' ÅRET FØR).                               */     
 /*                                                              */     
 /****************************************************************/     
 SKAF_GL_LEJEVÆRDIER:                                                   
 PROC;                                                                  
                                                                        
         IF BH801.COVG_1998 = '2'     /* HAVDE 'HELÅRSSTATUS' I 1998 */ 
         THEN                                                           
            DO;                                                         
               /* BENYT LEJEVÆRDIER FRA */                              
               /* OVERGANGSORDNINGSBÅND */                              
               BH801.SOM_1999(1).BLEJE   = BH801.OVG_1998.BLEJE;        
               BH801.SOM_1999(1).BLEJEFP = BH801.OVG_1998.BLEJEFP;      
               BH801.SOM_1999(1).BLEJE67 = BH801.OVG_1998.BLEJE67;      
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* BENYT LEJEVÆRDIER FRA ESR */                          
               BH801.SOM_1999(1).BLEJE   = BH801.EJD_1999(1).BLEJE;     
               BH801.SOM_1999(1).BLEJEFP = BH801.EJD_1999(1).BLEJEFP;   
               BH801.SOM_1999(1).BLEJE67 = BH801.EJD_1999(1).BLEJE67;   
            END;                                                        
                                                                        
 END SKAF_GL_LEJEVÆRDIER;                                               
 /*********************************************************************/
 /*      SKAF PERSONENS GL. LEJEVÆRDI VIA DEN AKTUELLE AF EJENDOMMENS */
 /*      3 GL. LEJEVÆRDIER (P.G.A. AKTUEL CPERTYPE) SAMT PERSONENS    */
 /*      NYE BEREGNEDE LEJEVÆRDI. DISSE 2 VÆRDIER INDGÅR I OPSÆTNING  */
 /*      AF KODER FOR LEJEVÆRDISTIGNING TIL SLS.                      */
 /*********************************************************************/
 SKAF_PERSONLIG_GL_LEJEVÆRDI:                                           
 PROC (GLBLEJE,NYBLEJE);                                                
 DCL     GLBLEJE        FIXED     (11,0);                               
 DCL     NYBLEJE        FIXED     (11,0);                               
         IF (BH801.CPER_ORD = 1 !                                       
             BH801.CPER_ORD = 4)                                        
         THEN                                                           
            /* IKKE PENSIONIST ELL. FØRTIDSPENS/EFTERLØN */             
            GLBLEJE = BH801.EJD_1999(1).BLEJE;                          
         ELSE                                                           
            IF BH801.CPER_ORD = 2                                       
            THEN                                                        
               /* FØRTIDSPENS ELL. EFTERLØN */                          
               GLBLEJE = BH801.EJD_1999(1).BLEJEFP;                     
            ELSE                                                        
               /* PENSIONIST */                                         
               GLBLEJE = BH801.EJD_1999(1).BLEJE67;                     
         NYBLEJE = BH801.PER_LEJEV(1).BELØB;                            
 END SKAF_PERSONLIG_GL_LEJEVÆRDI;                                       
                                                                        
 /*********************************************************************/
 /*       BEREGNING AF LEJEVÆRDI FOR PERSONEN EVT. REDUCERET HVIS     */
 /*       PERSONEN ER OMFATTET AF DEN GL. OVERGANGSORDNING.           */
 /* OBS.: UDFØRES HVIS DER ER BEREGNET LEJEVÆRDIER FOR EJENDOMMEN     */
 /*       OG PERSONEN IKKE ER UNDTAGET FOR BEREGNING.                 */
 /*       OPSÆTNINGEN AF CLEJVBEKOD HAR  K U N  BETYDNING FOR         */
 /*       ORDINÆR ESKEMA.                                             */
 /*********************************************************************/
 LEJEVÆRDI_PERSON:                                                      
       PROC(TAB_LEJEV);                                                 
                                                                        
 DCL   1 TAB_LEJEV(3),                  /* EJENDOMMENS 3 LEJEVÆRDIER  */
         2 BLEJE      FIXED  (11),      /* LEJEV. NORMAL              */
         2 BLEJEFP    FIXED  (11),      /* LEJEV. FØRTIDSPENS./EFTERL.*/
         2 BLEJE67    FIXED  (11);      /* LEJEVÆRDI PENSIONIST       */
                                        /* 1=SIDSTE ÅRS VÆRDIER       */
                                        /* 2=NYT ÅRS VÆRDIER UREDUC.  */
                                        /* 3=NYT ÅRS VÆRDIER REDUC.   */
                                                                        
 DCL     GLBLEJE        FIXED     (11,0)     INIT (0);                  
 DCL     NYBLEJE        FIXED     (11,0)     INIT (0);                  
                                                                        
         /* HEREFTER UNDERSØGES OM DER SKAL DER FORETAGES BEREGNING   */
         /* AF REDUCERET LEJEVÆRDI FOR PERSONEN. DETTE ER KUN AKTUELT */
         /* HVIS DENNE ER OMFATTET AF DEN GL. OVERGANGSORDNING.       */
                                                                        
         /* PERSONER, SOM ENTEN HAR KØBT ELLER SOLGT EJERANDELE I */    
         /* INDKOMSTÅRET, ER OGSÅ OMFATTET AF OVENNÆVNTE REGLER   */    
                                                                        
         /* DET ER ALTID DEN HELÅRLIGE LEJEVÆRDI, DER ER GEMT PÅ  */    
         /* OVERGANGSORDNINGSBÅNDET.                              */    
                                                                        
         CALL UNDTAGELSER_REDUKTION;                                    
                                                                        
         IF BH801.COVG_1998 ^= '1' !                                    
            BEREGN = NEJ                                                
         THEN                                                           
            DO;                                                         
               /* UNDTAGELSE HVIS PERSONEN IKKE ER OMFATTET AF */       
               /* GL. OVERGANGSORDNING                         */       
               /* ELLER                                        */       
               /* HVIS EJENDOMMEN ER UNDTAGET FOR REDUKTION    */       
                                                                        
               CALL SKAF_PERSONLIG_GL_LEJEVÆRDI (GLBLEJE,NYBLEJE);      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
               CALL INDSÆT_LEJEVÆRDI;                                   
               RETURN;                                                  
                                                                        
            END;                                                        
                                                                        
         GLBLEJE = BH801.OVLEJ_1998;                                    
                                                                        
         IF BH801.CPER_ORD = 1 !                                        
            BH801.CPER_ORD = 4                                          
         THEN                                                           
            DO;                                                         
               /* IKKE PENSIONIST ELL. FØRTIDSPENS/EFTERLØN */          
               CREDUKT = 1;                                             
               NYBLEJE = TAB_LEJEV(2).BLEJE;                            
            END;                                                        
         ELSE                                                           
            IF BH801.CPER_ORD = 2                                       
            THEN                                                        
               DO;                                                      
                  /* FØRTIDSPENS ELL. EFTERLØN */                       
                  CREDUKT = 2;                                          
                  NYBLEJE = TAB_LEJEV(2).BLEJEFP;                       
               END;                                                     
               ELSE                                                     
                  DO;                                                   
                     /* PENSIONIST */                                   
                     CREDUKT = 2;                                       
                     NYBLEJE = TAB_LEJEV(2).BLEJE67;                    
                  END;                                                  
                                                                        
         CALL REDUKTION (GLBLEJE,NYBLEJE,BH801.PER_LEJEV(2).BELØB,      
                         CREDUKT,CREDUKTTYP);                           
                                                                        
         IF BH801.PER_LEJEV(2).BELØB ^= 0 &                             
            BH801.PER_LEJEV(2).BELØB < BH801.PER_LEJEV(1).BELØB         
         THEN                                                           
            DO;                                                         
                                                                        
               /* DENNE PERSON ER STADIG OMFATTET AF */                 
               /* DEN GL. OVERGANGSORDNING           */                 
                                                                        
               BH801.CP20_3500 = CREDUKTTYP;                            
               PER_LEJEV(2).CREDTYP = CREDUKTTYP;                       
                                                                        
               NYBLEJE = BH801.PER_LEJEV(2).BELØB;                      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
                                                                        
               BH801.BLEJE2    = BH801.PER_LEJEV(2).BELØB;              
               BH801.BLEJE1999 = BH801.PER_LEJEV(2).BELØB;              
                                                                        
               /* GEM LEJEVÆRDI FRA OVERGANGSORDNING 1999 */            
               /* I BEREGNINGSBÅNDET A.H.T. TEST/KONTROL  */            
               /* SAMT EVT. BEREGNING FOR 2000 I BH80110. */            
                                                                        
               BH801.OVLEJ_1999 = BH801.PER_LEJEV(2).BELØB;             
               BH801.COVG_1999 = '1';                                   
                                                                        
            END;                                                        
         ELSE                                                           
            DO;                                                         
                                                                        
               /* PERSONEN ER IKKE LÆNGERE OMFATTET */                  
               /* AF OVERGANGSORDNINGERNE           */                  
                                                                        
               CALL SKAF_PERSONLIG_GL_LEJEVÆRDI (GLBLEJE,NYBLEJE);      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
               CALL INDSÆT_LEJEVÆRDI;                                   
                                                                        
            END;                                                        
                                                                        
 END LEJEVÆRDI_PERSON;                                                  
 /*********************************************************************/
 /*      OPSÆT KODER FOR LEJEVÆRDISTIGNING TIL SLS                    */
 /*      (VEDRØRER KUN DEN PERSONLIGE LEJEVÆRDI)                      */
 /* OBS: I N D E N  KALD AF PROCEDUREN SKAL                           */
 /*      GLBLEJE OG NYBLEJE SÆTTES LIG PERSONENS GL. LEJEVÆRDI OG     */
 /*      PERSONENS NY LEJEVÆRDI.                                      */
 /*********************************************************************/
 OPSÆT_CDIFLEJE:                                                        
 PROC(GLBLEJE,NYBLEJE);                                                 
 DCL     GLBLEJE        FIXED     (11,0);                               
 DCL     NYBLEJE        FIXED     (11,0);                               
 DCL     GLBLEJE20      FIXED     (15,2);                               
         GLBLEJE20  = GLBLEJE *  0.20;                                  
         IF GLBLEJE > 0                                                 
         THEN                                                           
            DO;                                                         
               IF NYBLEJE > GLBLEJE + 1000                              
               THEN                                                     
                  BH801.CDIFLEJE = 1;                                   
               IF NYBLEJE > ( GLBLEJE + GLBLEJE20 - 2500 )              
               THEN                                                     
                  BH801.CDIFLEJE2 = 2;                                  
            END;                                                        
 END OPSÆT_CDIFLEJE;                                                    
                                                                        
 /*********************************************************************/
 /*      SKAF PERSONENS ENDELIGE LEJEVÆRDI FOR 1999, HVIS HAN IKKE ER */
 /*      OMFATTET AF GL. OVERGANGSORDNING.                            */
 /*********************************************************************/
 INDSÆT_LEJEVÆRDI:                                                      
 PROC;                                                                  
                                                                        
         IF BH801.PER_LEJEV(1).CREDTYP = 0                              
         THEN                                                           
            BH801.BLEJE1 = BH801.PER_LEJEV(1).BELØB;                    
                            /* IKKE REDUCERET */                        
         ELSE                                                           
            DO;                                                         
               BH801.BLEJE2 = BH801.PER_LEJEV(1).BELØB;                 
                            /* REDUCERET */                             
               BH801.CP20_3500 = BH801.PER_LEJEV(1).CREDTYP;            
            END;                                                        
                                                                        
         BH801.BLEJE1999 = BH801.PER_LEJEV(1).BELØB; /* TIL BOY       */
                                                     /* NB. HELÅRLIG  */
 END INDSÆT_LEJEVÆRDI;                                                  
                                                                        
         %INCLUDE BH80111M; /* NULSTIL LEJEVÆRDI FELTER               */
         %INCLUDE BH80150M; /* TEST UNDTAGELSER LEJEVÆRDI EJENDOM     */
         %INCLUDE BH80155M; /* BEREGN LEJEVÆRDI EJENDOM               */
         %INCLUDE BH80156M; /* BEREGN EJENDOMS LEJEVÆRDIER REDUCERET  */
         %INCLUDE BH80160M; /* BEREGNING AF REDUKTION                 */
         %INCLUDE BH80158M; /* SKAF ENDELIGE EJENDOMS LEJEVÆRDIER     */
         %INCLUDE BH80151M; /* TEST UNDTAGELSER LEJEVÆRDI PERSON      */
         %INCLUDE BH80149M; /* SKAF PERSONLIG EJD-LEJEVÆRDI           */
                                                                        
                                                                        
 END BH80110;                                                           
