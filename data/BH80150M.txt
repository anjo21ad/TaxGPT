 /*********************************************************************/
 /*        BH80150M UNDERSØG GENERELLE UNDTAGELSER VEDR.              */
 /*                 BEREGNING AF LEJEVÆRDI FOR  E J E N D O M M E N   */
 /*                 FÆLLES MACRO TIL :                                */
 /*                 BH80110 BOY                                       */
 /*  OBS.: I N D E N  KALD AF PROCEDUREN  ----------------            */
 /*        HUSK AT OPSÆTTE HJVÆRDI (BEREGNINGSGRUNDLAG)               */
 /*********************************************************************/
 UNDTAGELSER_LEJEVÆRDI_EJENDOM:                                         
         PROC;                                                          
         BEREGN = NEJ;                                                  
         SELECT ( BH801.CBENYT );          /* BENYTTELSESKODER HVOR   */
                                           /* DER MÅSKE SKAL BEREGNES */
           WHEN ('01','02','05','06','07','08','13','21','22',          
                 '26','27','28','29','34','41','42','45','48');         
           OTHERWISE                                                    
              RETURN;                                                   
         END;                                                           
 /*****  GENERELLE  UNDTAGELSER   *************************************/
         IF BH801.DVURDÅR ^= INDKÅR1       !  /* UNDTAGELSE  7 */       
            BH801.CUDLEJNING = '1'         !  /* UNDTAGELSE  1 OG 2 */  
            BH801.ANT_FOR_1 > 0            !  /* UNDTAGELSE  4 */       
            BH801.ANT_FOR_2 > 0            !                            
           (BH801.ANT_FOR_4 +                                           
            BH801.ANT_FOR_5 +                                           
            BH801.ANT_FOR_6) > 1           !                            
            HJVÆRDI <= 0             /* UNDTAGELSE  5 D.V.S.          */
                                     /* ENTEN EJENDOMS-, EJERBOLIG-   */
                                     /* ELLER STUEHUSVÆRDI SOM BENYT- */
                                     /* TES TIL BEREGNING ER <= 0     */
         THEN                                                           
            RETURN;                                                     
         IF BH801.CEJEV = '5'               /* UNDTAGELSE  6 */         
         THEN                                                           
            RETURN;                                                     
         IF (BH801.CEJSO = '1' !            /* UNDTAGELSE  3 */         
             BH801.CEJSO = '3' !                                        
            (BH801.CEJERL = '1' & BH801.DOVTG <= ÅR0101 &               
             SOLGT_I_ÅR = JA)) &                                        
            (BH801.CBENYT = '21' !                                      
             BH801.CBENYT = '22' !                                      
             BH801.CBENYT = '26' !                                      
             BH801.CBENYT = '27' !                                      
             BH801.CBENYT = '28' !                                      
             BH801.CBENYT = '29')                                       
         THEN                                                           
            RETURN;                                                     
         IF (BH801.CEJERL = '1' & BH801.DOVTG <= ÅR0101) & /* HVIS    */
            (BH801.CBENYT = '21' !                         /* DET ER  */
             BH801.CBENYT = '22' !                         /* EN HEL- */
             BH801.CBENYT = '26' !                         /* ÅRSEJER */
             BH801.CBENYT = '27' !                                      
             BH801.CBENYT = '28' !                                      
             BH801.CBENYT = '29')                                       
         THEN                                                           
            RETURN;                                                     
 /****** TEST FOR SPECIELLE UNDTAGELSER FOR BEREGNING AF LEJEVÆRDI ****/
         SELECT ( BH801.CBENYT );                                       
            WHEN ('01','41','45')                                       
               DO;                                                      
                  IF BH801.CBENYT = '45' & BH801.FLEJL = 0              
                  THEN                                                  
                     RETURN;                                            
                  ELSE                                                  
                     IF BH801.FLEJL = 2 &                               
                        BH801.EJEBOL(1).EJBOLTYP < '5'                  
                     THEN                     /* INGEN EJERBOLIGVÆRDI */
                        RETURN;                                         
               END;                                                     
            WHEN ('02','22','34','42')                                  
               DO;                                                      
                  IF BH801.EJEBOL(1).EJBOLTYP < '5'                     
                  THEN                        /* INGEN EJERBOLIGVÆRDI */
                     RETURN;                                            
               END;                                                     
            WHEN ('05','07')                                            
               DO;                                                      
                  IF BH801.EJEBOL(1).EJBOLTYP ^= '4'                    
                  THEN                          /* INGEN STUEHUSVÆRDI */
                     RETURN;                                            
               END;                                                     
            WHEN ('06')                                                 
               DO;                                                      
                  IF BH801.EJEBOL(1).EJBOLTYP < '4'                     
                  THEN                          /* INGEN EJERBOLIG-   */
                     RETURN;                    /* ELLER STUEHUSVÆRDI */
               END;                                                     
            WHEN ('08','21','26','27','28','48')                        
               DO;                                                      
                  IF (BH801.ANT_FOR_4 +                                 
                      BH801.ANT_FOR_5 +                                 
                      BH801.ANT_FOR_6) > 0                              
                  THEN                                                  
                     RETURN;                                            
               END;                                                     
            WHEN ('13')                     /* ANDRE VURDERINGER      */
               DO;                                                      
                  IF (BH801.ANT_FOR_4 +                                 
                      BH801.ANT_FOR_5 +                                 
                      BH801.ANT_FOR_6) ^= 1                             
                  THEN                                                  
                     RETURN;                                            
               END;                                                     
            WHEN ('29')                /* EJERLEJLIGHED, PÅ FREM. GR. */
               DO;                                                      
                  IF BH801.FLEJL ^= 1 !                                 
                     (BH801.ANT_FOR_4 +                                 
                      BH801.ANT_FOR_5 +                                 
                      BH801.ANT_FOR_6) > 0                              
                  THEN                                                  
                     RETURN;                                            
               END;                                                     
            OTHERWISE;                                                  
         END;                                                           
 /***    DER SKAL ALTSÅ FORETAGES LEJEVÆRDIBEREGNING FOR EJENDOMMEN ***/
         BEREGN = JA;                                                   
 END UNDTAGELSER_LEJEVÆRDI_EJENDOM;                                     
