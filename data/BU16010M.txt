 /**********************************************************************
 * PROJEKT:     EVS SLUT 2016.                                         *
 * PROGRAMNAVN: BU16010M.                                              *
 * PROGRAMTYPE: MAKRO.                                                 *
 * FUNKTION:    HÅNDTERING AF DIVERSE FUNKTIONER (1-5).                *
 * TABELLER:    BQ20000T - EJENDOMSTABEL.                              *
 *              BQ21000T - EJENDOMS-FELTTABEL                          *
 *              BQ30000T - EJERSKABSTABEL                              *
 *              BQ31000T - EJERSKABS-FELTTABEL                         *
 * KONSTRUKTØR: BO SCHMIDT            BOB, LSU/ARB BRØNDBY  10.01.2001.*
 * ÆNDRINGSLOG: BO SCHMIDT            BOB, LSU     BALLERUP 19.11.2009.*
 *              ÅRSRULLET.                                             *
 * ÆNDRINGSLOG: GERD HOLM-PEDERSEN    GEH, PKF     BALLERUP 15.10.2010.*
 *              ÅRSRULLET.                                             *
 * ÆNDRINGSLOG: GERD HOLM-PEDERSEN    GEH, PKF     BALLERUP 02.10.2011.*
 *              ÅRSRULLET.                                             *
 * ÆNDRINGSLOG: LARS KAAE HARRITSHØJ  LKA, PR      BALLERUP SEP. 2012  *
 *              ÅRSRULLET.                                             *
 * ÆNDRINGSLOG: Jørgen SAUGMANN       SAU, AM      BALLERUP OKT. 2014  *
 *              ÅRSRULLET.                                             *
 * Årsrullet :  BQ14 --> BQ15, kopieret fra BQ14004N     JNK Okt. 2015 *
 * Årsrullet :  BQ15 --> BQ16, kopieret fra BQ14010M     QLE Juni 2016 *
 **********************************************************************/
 DCL HJ_TIMESTAMP  CHAR (26);                                           
 /*********************************************************************/
 /* SELECT PERSON + EJENDOM.                                          */
 /* VEDRØRER: FUNKTION 1 + 4.                                  RUTINE */
 /*********************************************************************/
 LÆS_TABELLER: PROC;                                                    
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
         CALL INIT_STRUKTURER;                                          
                                                                        
         CALL LÆS_EJENDOM_TABEL;                                        
         CALL LÆS_PERSON_TABEL;                                         
                                                                        
         RETURTEKST = RETURTEKST_EJD !! RETURTEKST_PER;                 
                                                                        
 END LÆS_TABELLER;                                                      
 /*********************************************************************/
 /* NULSTIL STRUKTURER OG OPSÆT KONSTANTER.                           */
 /* VEDRØRER: FUNKTION 1, 4, 11 & 14.                          RUTINE */
 /*********************************************************************/
 INIT_STRUKTURER: PROC;                                                 
                                   /* EVS EJENDOM                   */  
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
         EJENDOMTAB = '';                                               
         EJENDOMTAB.DINDKAAR     = SØG.DINDKAAR;                        
         EJENDOMTAB.EKOMNR       = SØG.EKOMNR;                          
         EJENDOMTAB.EEJDNR       = SØG.EEJDNR;                          
         EJENDOMTAB.DFRAGLD      = '1900-12-31-00.00.00.000001';        
                                                                        
         %INCLUDE BU16005M;                                             
                                                                        
                                   /* EVS PERSON                    */  
         PERSONTAB = '';                                                
         PERSONTAB.DINDKAAR      = SØG.DINDKAAR;                        
         PERSONTAB.DCPRN         = SØG.DCPRN;                           
         PERSONTAB.EEJDNR        = SØG.EEJDNR;                          
         PERSONTAB.EKOMNR        = SØG.EKOMNR;                          
         PERSONTAB.ELBNR         = SØG.ELBNR;                           
                                                                        
         PERSONTAB.DFRAGLD       = '1900-12-31-00.00.00.000001';        
         PERSONTAB.DTILGLD       = '9999-12-31-23.59.59.999999';        
         PERSONTAB.CSCREGISTMP   = '1900-12-31-00.00.00.000001';        
                                                                        
         %INCLUDE BU16006M;                                             
                                   /* EVS RESULTAT                  */  
         RESULTATTAB = '';                                              
         RESULTATTAB.DINDKAAR    = SØG.DINDKAAR;                        
         RESULTATTAB.DCPRN       = SØG.DCPRN;                           
         RESULTATTAB.EEJDNR      = SØG.EEJDNR;                          
         RESULTATTAB.EKOMNR      = SØG.EKOMNR;                          
         RESULTATTAB.ELBNR       = SØG.ELBNR;                           
                                                                        
         RESULTATTAB.DFRAGLD     = '1900-12-31-00.00.00.000001';        
         RESULTATTAB.DRESULTAT   = '1900-12-31-00.00.00.000001';        
                                                                        
         %INCLUDE BU16007M;                                             
                                                                        
 END INIT_STRUKTURER;                                                   
 /*********************************************************************/
 /* LÆS 1 RÆKKE PÅ EVS SLUT EJENDOMSTABEL OG EJENDOMSFELTTABEL        */
 /* VEDRØRER: FUNKTION 1, 4, 11, 14.                           RUTINE */
 /*********************************************************************/
 LÆS_EJENDOM_TABEL: PROC;                                               
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
         EXEC SQL                                                       
                                                                        
              SELECT DINDKAAR                                           
                    ,EKOMNR                                             
                    ,EEJDNR                                             
                    ,DFRAGLD                                            
                                                                        
              INTO   :EJENDOMTAB.DINDKAAR                               
                    ,:EJENDOMTAB.EKOMNR                                 
                    ,:EJENDOMTAB.EEJDNR                                 
                    ,:EJENDOMTAB.DFRAGLD                                
                                                                        
              FROM   BQ20000T                                           
                                                                        
              WHERE  DINDKAAR = :SØG.DINDKAAR                           
              AND    EEJDNR   = :SØG.EEJDNR                             
              AND    EKOMNR   = :SØG.EKOMNR                             
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_EJD = 'EJENDOMSTABEL: LÆST OK - ';            
               CALL LÆS_EJENDOMS_FELTTABEL;                             
             END;                                                       
                                                                        
           WHEN (100)                                                   
             DO;                                                        
               KOM_AREAL.RETURKODE = KOM_AREAL.RETURKODE + 1;           
               RETURTEKST_EJD = 'EJENDOMSTABEL: RÆKKE IKKE FUNDET - ';  
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST('BU16010M - LÆS_EJEMDOM_TABEL - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST_EJD = 'EJENDOMSTABEL: SQLFEJL VED LÆS - ';    
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END LÆS_EJENDOM_TABEL;                                                 
 /*********************************************************************/
 /* LÆS 1 RÆKKE PÅ EVS SLUT EJERSKABSTABEL OG EJERSKABSFELTTABEL      */
 /* VEDRØRER: FUNKTION 1 + 4.                                  RUTINE */
 /*********************************************************************/
 LÆS_PERSON_TABEL: PROC;                                                
                                                                        
         IF PUTSKIP THEN DO;                                            
            PUT SKIP LIST('*** '!!PROCNAME!!' ***');                    
            PUT SKIP LIST('KOMM.TYPE = '!!KOMM.TYPE);                   
            PUT SKIP LIST('SØG.ELBNR = '!!SØG.ELBNR);                   
         END;                                                           
                                                                        
         SELECT;                                                        
           WHEN (KOMM.TYPE = 0        /* LÆS DEN AKTUELLE*/             
                 & SØG.ELBNR > 0)                                       
             DO;                                                        
               IF PUTSKIP THEN PUT SKIP LIST('LÆS DEN AKTUELLE');       
               EXEC SQL                                                 
                                                                        
                    SELECT T1.DCPRN                                     
                          ,T1.DINDKAAR                                  
                          ,T1.EKOMNR                                    
                          ,T1.EEJDNR                                    
                          ,T1.ELBNR                                     
                          ,T1.EGENNR                                    
                          ,T1.CDATAFRA                                  
                          ,T1.DFRAGLD                                   
                          ,T1.DTILGLD                                   
                          ,T1.CSCREGISTMP                               
                          ,T1.PERSKOD                                   
                          ,T1.MYNDNR                                    
                          ,T1.COMPLA                                    
                          ,T1.DOMPLACPR                                 
                          ,T1.CAJOUR                                    
                          ,T1.CSUPPL                                    
                          ,T1.FEJLNR                                    
                                                                        
                     INTO  :PERSONTAB.DCPRN                             
                          ,:PERSONTAB.DINDKAAR                          
                          ,:PERSONTAB.EKOMNR                            
                          ,:PERSONTAB.EEJDNR                            
                          ,:PERSONTAB.ELBNR                             
                          ,:PERSONTAB.EGENNR                            
                          ,:PERSONTAB.CDATAFRA                          
                          ,:PERSONTAB.DFRAGLD                           
                          ,:PERSONTAB.DTILGLD                           
                          ,:PERSONTAB.CSCREGISTMP                       
                          ,:PERSONTAB.PERSKOD                           
                          ,:PERSONTAB.MYNDNR                            
                          ,:PERSONTAB.COMPLA                            
                          ,:PERSONTAB.DOMPLACPR                         
                          ,:PERSONTAB.CAJOUR                            
                          ,:PERSONTAB.CSUPPL                            
                          ,:PERSONTAB.FEJLNR                            
                                                                        
                    FROM   BQ30000T T1                                  
                                                                        
                    WHERE  T1.DINDKAAR = :SØG.DINDKAAR                  
                    AND    T1.DCPRN    = :SØG.DCPRN                     
                    AND    T1.EEJDNR   = :SØG.EEJDNR                    
                    AND    T1.EKOMNR   = :SØG.EKOMNR                    
                    AND    T1.ELBNR    = :SØG.ELBNR                     
                    AND    T1.DTILGLD  = '9999-12-31-23.59.59.999999';  
             END;                                                       
           WHEN (KOMM.TYPE   = 0     /* BRUGES AF SLUT */               
                 & SØG.ELBNR   = 0)                                     
             DO;                                                        
               IF PUTSKIP THEN PUT SKIP LIST('BRUGES AF SLUT');         
               EXEC SQL                                                 
                                                                        
                    SELECT T1.DCPRN                                     
                          ,T1.DINDKAAR                                  
                          ,T1.EKOMNR                                    
                          ,T1.EEJDNR                                    
                          ,T1.ELBNR                                     
                          ,T1.EGENNR                                    
                          ,T1.CDATAFRA                                  
                          ,T1.DFRAGLD                                   
                          ,T1.DTILGLD                                   
                          ,T1.CSCREGISTMP                               
                          ,T1.PERSKOD                                   
                          ,T1.MYNDNR                                    
                          ,T1.COMPLA                                    
                          ,T1.DOMPLACPR                                 
                          ,T1.CAJOUR                                    
                          ,T1.CSUPPL                                    
                          ,T1.FEJLNR                                    
                                                                        
                     INTO  :PERSONTAB.DCPRN                             
                          ,:PERSONTAB.DINDKAAR                          
                          ,:PERSONTAB.EKOMNR                            
                          ,:PERSONTAB.EEJDNR                            
                          ,:PERSONTAB.ELBNR                             
                          ,:PERSONTAB.EGENNR                            
                          ,:PERSONTAB.CDATAFRA                          
                          ,:PERSONTAB.DFRAGLD                           
                          ,:PERSONTAB.DTILGLD                           
                          ,:PERSONTAB.CSCREGISTMP                       
                          ,:PERSONTAB.PERSKOD                           
                          ,:PERSONTAB.MYNDNR                            
                          ,:PERSONTAB.COMPLA                            
                          ,:PERSONTAB.DOMPLACPR                         
                          ,:PERSONTAB.CAJOUR                            
                          ,:PERSONTAB.CSUPPL                            
                          ,:PERSONTAB.FEJLNR                            
                                                                        
                    FROM   BQ30000T T1                                  
                                                                        
                    WHERE  T1.DINDKAAR = :SØG.DINDKAAR                  
                    AND    T1.DCPRN    = :SØG.DCPRN                     
                    AND    T1.EEJDNR   = :SØG.EEJDNR                    
                    AND    T1.EKOMNR   = :SØG.EKOMNR                    
                    AND    T1.DTILGLD  = '9999-12-31-23.59.59.999999'   
                    AND    T1.ELBNR   IN (SELECT MAX(ELBNR)             
                                    FROM   BQ30000T T2                  
                                    WHERE  T1.DINDKAAR = T2.DINDKAAR    
                                    AND    T1.DCPRN    = T2.DCPRN       
                                    AND    T1.EEJDNR   = T2.EEJDNR      
                                    AND    T1.EKOMNR   = T2.EKOMNR      
                                    AND    T1.EEJDNR   = T2.EEJDNR);    
             END;                                                       
           WHEN (KOMM.TYPE   = 1)   /* MASKINEL OPRETTELSE */           
             DO;                                                        
               IF PUTSKIP THEN PUT SKIP LIST('MASKINEL OPRETTELSE');    
               EXEC SQL                                                 
                                                                        
                    SELECT T1.DCPRN                                     
                          ,T1.DINDKAAR                                  
                          ,T1.EKOMNR                                    
                          ,T1.EEJDNR                                    
                          ,T1.ELBNR                                     
                          ,T1.EGENNR                                    
                          ,T1.CDATAFRA                                  
                          ,T1.DFRAGLD                                   
                          ,T1.DTILGLD                                   
                          ,T1.CSCREGISTMP                               
                          ,T1.PERSKOD                                   
                          ,T1.MYNDNR                                    
                          ,T1.COMPLA                                    
                          ,T1.DOMPLACPR                                 
                          ,T1.CAJOUR                                    
                          ,T1.CSUPPL                                    
                          ,T1.FEJLNR                                    
                                                                        
                     INTO  :PERSONTAB.DCPRN                             
                          ,:PERSONTAB.DINDKAAR                          
                          ,:PERSONTAB.EKOMNR                            
                          ,:PERSONTAB.EEJDNR                            
                          ,:PERSONTAB.ELBNR                             
                          ,:PERSONTAB.EGENNR                            
                          ,:PERSONTAB.CDATAFRA                          
                          ,:PERSONTAB.DFRAGLD                           
                          ,:PERSONTAB.DTILGLD                           
                          ,:PERSONTAB.CSCREGISTMP                       
                          ,:PERSONTAB.PERSKOD                           
                          ,:PERSONTAB.MYNDNR                            
                          ,:PERSONTAB.COMPLA                            
                          ,:PERSONTAB.DOMPLACPR                         
                          ,:PERSONTAB.CAJOUR                            
                          ,:PERSONTAB.CSUPPL                            
                          ,:PERSONTAB.FEJLNR                            
                                                                        
                    FROM   BQ30000T T1                                  
                                                                        
                    WHERE  T1.DINDKAAR = :SØG.DINDKAAR                  
                    AND    T1.DCPRN    = :SØG.DCPRN                     
                    AND    T1.EEJDNR   = :SØG.EEJDNR                    
                    AND    T1.EKOMNR   = :SØG.EKOMNR                    
                    AND    T1.ELBNR    = :SØG.ELBNR                     
                    AND    T1.EGENNR = (SELECT MAX(EGENNR)              
                                   FROM   BQ30000T T2                   
                                   WHERE  T1.DINDKAAR = T2.DINDKAAR     
                                   AND    T1.DCPRN    = T2.DCPRN        
                                   AND    T1.EEJDNR   = T2.EEJDNR       
                                   AND    T1.EKOMNR   = T2.EKOMNR       
                                   AND    T1.EEJDNR   = T2.EEJDNR       
                                   AND    T1.ELBNR    = T2.ELBNR        
                                   AND    T2.CSUPPL  IN ('1','2','3')); 
             END;                                                       
           WHEN (KOMM.TYPE   = 2)     /* SENESTE ÆNDR. INDB. */         
             DO;                                                        
               IF PUTSKIP THEN PUT SKIP LIST('SENESTE ÆNDR. INDB.');    
               EXEC SQL                                                 
                                                                        
                    SELECT T1.DCPRN                                     
                          ,T1.DINDKAAR                                  
                          ,T1.EKOMNR                                    
                          ,T1.EEJDNR                                    
                          ,T1.ELBNR                                     
                          ,T1.EGENNR                                    
                          ,T1.CDATAFRA                                  
                          ,T1.DFRAGLD                                   
                          ,T1.DTILGLD                                   
                          ,T1.CSCREGISTMP                               
                          ,T1.PERSKOD                                   
                          ,T1.MYNDNR                                    
                          ,T1.COMPLA                                    
                          ,T1.DOMPLACPR                                 
                          ,T1.CAJOUR                                    
                          ,T1.CSUPPL                                    
                          ,T1.FEJLNR                                    
                                                                        
                     INTO  :PERSONTAB.DCPRN                             
                          ,:PERSONTAB.DINDKAAR                          
                          ,:PERSONTAB.EKOMNR                            
                          ,:PERSONTAB.EEJDNR                            
                          ,:PERSONTAB.ELBNR                             
                          ,:PERSONTAB.EGENNR                            
                          ,:PERSONTAB.CDATAFRA                          
                          ,:PERSONTAB.DFRAGLD                           
                          ,:PERSONTAB.DTILGLD                           
                          ,:PERSONTAB.CSCREGISTMP                       
                          ,:PERSONTAB.PERSKOD                           
                          ,:PERSONTAB.MYNDNR                            
                          ,:PERSONTAB.COMPLA                            
                          ,:PERSONTAB.DOMPLACPR                         
                          ,:PERSONTAB.CAJOUR                            
                          ,:PERSONTAB.CSUPPL                            
                          ,:PERSONTAB.FEJLNR                            
                                                                        
                    FROM   BQ30000T T1                                  
                                                                        
                    WHERE  T1.DINDKAAR = :SØG.DINDKAAR                  
                    AND    T1.DCPRN    = :SØG.DCPRN                     
                    AND    T1.EEJDNR   = :SØG.EEJDNR                    
                    AND    T1.EKOMNR   = :SØG.EKOMNR                    
                    AND    T1.ELBNR    = :SØG.ELBNR                     
                    AND    T1.DTILGLD  = '9999-12-31-23.59.59.999999'   
                    AND    T1.CSUPPL   = '4';                           
             END;                                                       
           WHEN (KOMM.TYPE   = 3)   /* HISTORIK            */           
             DO;                                                        
               IF PUTSKIP THEN PUT SKIP LIST('HISTORIK');               
               EXEC SQL                                                 
                                                                        
                    SELECT T1.DCPRN                                     
                          ,T1.DINDKAAR                                  
                          ,T1.EKOMNR                                    
                          ,T1.EEJDNR                                    
                          ,T1.ELBNR                                     
                          ,T1.EGENNR                                    
                          ,T1.CDATAFRA                                  
                          ,T1.DFRAGLD                                   
                          ,T1.DTILGLD                                   
                          ,T1.CSCREGISTMP                               
                          ,T1.PERSKOD                                   
                          ,T1.MYNDNR                                    
                          ,T1.COMPLA                                    
                          ,T1.DOMPLACPR                                 
                          ,T1.CAJOUR                                    
                          ,T1.CSUPPL                                    
                          ,T1.FEJLNR                                    
                                                                        
                     INTO  :PERSONTAB.DCPRN                             
                          ,:PERSONTAB.DINDKAAR                          
                          ,:PERSONTAB.EKOMNR                            
                          ,:PERSONTAB.EEJDNR                            
                          ,:PERSONTAB.ELBNR                             
                          ,:PERSONTAB.EGENNR                            
                          ,:PERSONTAB.CDATAFRA                          
                          ,:PERSONTAB.DFRAGLD                           
                          ,:PERSONTAB.DTILGLD                           
                          ,:PERSONTAB.CSCREGISTMP                       
                          ,:PERSONTAB.PERSKOD                           
                          ,:PERSONTAB.MYNDNR                            
                          ,:PERSONTAB.COMPLA                            
                          ,:PERSONTAB.DOMPLACPR                         
                          ,:PERSONTAB.CAJOUR                            
                          ,:PERSONTAB.CSUPPL                            
                          ,:PERSONTAB.FEJLNR                            
                                                                        
                    FROM   BQ30000T T1                                  
                                                                        
                    WHERE  T1.DINDKAAR = :SØG.DINDKAAR                  
                    AND    T1.DCPRN    = :SØG.DCPRN                     
                    AND    T1.EEJDNR   = :SØG.EEJDNR                    
                    AND    T1.EKOMNR   = :SØG.EKOMNR                    
                    AND    T1.ELBNR    = :SØG.ELBNR                     
                    AND    T1.EGENNR   = :SØG.EGENNR;                   
             END;                                                       
           OTHERWISE;                                                   
         END; /*SELECT*/                                                
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE = KOM_AREAL.RETURKODE + 0;           
               RETURTEKST_PER = 'PERSONTABEL: LÆST OK';                 
               CALL LÆS_EJERSKABS_FELTTABEL;                            
             END;                                                       
                                                                        
           WHEN (100)                                                   
             DO;                                                        
               KOM_AREAL.RETURKODE = KOM_AREAL.RETURKODE + 1;           
               RETURTEKST_PER = 'PERSONTABEL: RÆKKE IKKE FUNDET';       
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST('BU16010M - LÆS_PERSON_TABEL - SQLCODE '!! 
                             SQLCA.SQLCODE);                            
               RETURTEKST_PER = 'PERSONTABEL: SQLFEJL VED LÆS';         
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END LÆS_PERSON_TABEL;                                                  
 /*********************************************************************/
 /* LÆS EJENDOMSFELTTABEL OG FLYT DATA TIL EJENDOMTAB                 */
 /*********************************************************************/
 LÆS_EJENDOMS_FELTTABEL: PROC;                                          
                                                                        
            IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');    
                                                                        
            CALL DECLARE_EJD_FEL_CUR;                                   
                                                                        
            EXEC SQL OPEN EJD_FEL_CUR;                                  
                                                                        
            IF SQLCA.SQLCODE ^= 0                                       
            THEN                                                        
              DO;                                                       
                KOM_AREAL.RETURKODE  = -1;                              
                RETURTEKST = 'SQLFEJL: OPEN EJD_FEL_CUR';               
                KOMM.SQLKODE = SQLCA.SQLCODE;                           
                CALL SQL_FEJL;                                          
              END;                                                      
                                                                        
            CALL FETCH_EJD_FEL_CUR;                                     
                                                                        
            DO WHILE(SQLCODE = 0);                                      
               CALL FLYT_EJD_FEL;                                       
               CALL FETCH_EJD_FEL_CUR;                                  
            END;                                                        
                                                                        
            EXEC SQL CLOSE EJD_FEL_CUR;                                 
                                                                        
            IF SQLCA.SQLCODE ^= 0                                       
            THEN                                                        
              DO;                                                       
                KOM_AREAL.RETURKODE  = -1;                              
                RETURTEKST = 'SQLFEJL: CLOSE EJD_FEL_CUR';              
                KOMM.SQLKODE = SQLCA.SQLCODE;                           
                CALL SQL_FEJL;                                          
              END;                                                      
                                                                        
 END LÆS_EJENDOMS_FELTTABEL;                                            
 /*********************************************************************/
 /* DECLARE CURSOR TIL EVS_EJD_FELT                                   */
 /*********************************************************************/
 DECLARE_EJD_FEL_CUR: PROC;                                             
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              DECLARE EJD_FEL_CUR CURSOR FOR                            
                                                                        
              SELECT T1.DINDKAAR,                                       
                     T1.EKOMNR,                                         
                     T1.EEJDNR,                                         
                     T1.FELTKODE,                                       
                     T1.FELTKODETYPE,                                   
                     T1.INDHOLD_C,                                      
                     T1.INDHOLD_D                                       
                                                                        
              FROM   BQ21000T T1                                        
                                                                        
              WHERE  T1.DINDKAAR = :EJENDOMTAB.DINDKAAR                 
              AND    T1.EEJDNR   = :EJENDOMTAB.EEJDNR                   
              AND    T1.EKOMNR   = :EJENDOMTAB.EKOMNR                   
                                                                        
              FOR FETCH ONLY                                            
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE  = -1;                                 
             RETURTEKST = 'SQLFEJL: DECLARE EJD_FEL_CUR';               
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END DECLARE_EJD_FEL_CUR;                                               
 /*********************************************************************/
 /* FETCH CURSOR TIL EVS_EJD_FELT                                     */
 /*********************************************************************/
 FETCH_EJD_FEL_CUR: PROC;                                               
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              FETCH EJD_FEL_CUR                                         
                                                                        
              INTO  :EVS_EJD_FELT.DINDKAAR,                             
                    :EVS_EJD_FELT.EKOMNR,                               
                    :EVS_EJD_FELT.EEJDNR,                               
                    :EVS_EJD_FELT.FELTKODE,                             
                    :EVS_EJD_FELT.FELTKODETYPE,                         
                    :EVS_EJD_FELT.INDHOLD_C,                            
                    :EVS_EJD_FELT.INDHOLD_D;                            
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0) DO;                                                 
              IF PUTSKIP THEN DO;                                       
                 IF EVS_EJD_FELT.FELTKODE = 85 THEN DO;                 
                    PUT SKIP LIST('CPTYPGL LÆST FRA TABEL: '!!          
                                  EVS_EJD_FELT.INDHOLD_C);              
                 END;                                                   
              END;                                                      
                   /* SKRIV TS_KØ, SKER I PROCEDURE SKRIV_TS_EVS_OVS */ 
           END;                                                         
           WHEN (100);                                                  
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE  = -1;                               
               PUT SKIP LIST('BU16010M - FETCH_EJD_FEL_CUR - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST = 'SQLFEJL: FETCH EJD_FEL_CUR';               
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END FETCH_EJD_FEL_CUR;                                                 
 /*********************************************************************/
 /* LÆS EJERSKABSFELTTABEL OG FLYT DATA TIL PERSONTAB                 */
 /*********************************************************************/
 LÆS_EJERSKABS_FELTTABEL: PROC;                                         
                                                                        
            IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');    
                                                                        
            CALL DECLARE_ESK_FEL_CUR;                                   
                                                                        
            EXEC SQL OPEN ESK_FEL_CUR;                                  
                                                                        
            IF SQLCA.SQLCODE ^= 0                                       
            THEN                                                        
              DO;                                                       
                KOM_AREAL.RETURKODE  = -1;                              
                RETURTEKST = 'SQLFEJL: OPEN ESK_FEL_CUR';               
                KOMM.SQLKODE = SQLCA.SQLCODE;                           
                CALL SQL_FEJL;                                          
              END;                                                      
                                                                        
            CALL FETCH_ESK_FEL_CUR;                                     
                                                                        
            DO WHILE(SQLCODE = 0);                                      
               CALL FLYT_ESK_FEL;                                       
               CALL FETCH_ESK_FEL_CUR;                                  
            END;                                                        
                                                                        
            EXEC SQL CLOSE ESK_FEL_CUR;                                 
                                                                        
            IF SQLCA.SQLCODE ^= 0                                       
            THEN                                                        
              DO;                                                       
                KOM_AREAL.RETURKODE  = -1;                              
                RETURTEKST = 'SQLFEJL: CLOSE ESK_FEL_CUR';              
                KOMM.SQLKODE = SQLCA.SQLCODE;                           
                CALL SQL_FEJL;                                          
              END;                                                      
                                                                        
 END LÆS_EJERSKABS_FELTTABEL;                                           
 /*********************************************************************/
 /* DECLARE CURSOR TIL EVS_ESK_FELT                                   */
 /*********************************************************************/
 DECLARE_ESK_FEL_CUR: PROC;                                             
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              DECLARE ESK_FEL_CUR CURSOR FOR                            
                                                                        
              SELECT T1.DCPRN,                                          
                     T1.DINDKAAR,                                       
                     T1.EKOMNR,                                         
                     T1.EEJDNR,                                         
                     T1.ELBNR,                                          
                     T1.EGENNR,                                         
                     T1.FELTKODE,                                       
                     T1.INDHOLD_C,                                      
                     T1.INDHOLD_D                                       
                                                                        
              FROM   BQ31000T T1                                        
                                                                        
              WHERE  T1.DCPRN    = :PERSONTAB.DCPRN                     
              AND    T1.DINDKAAR = :PERSONTAB.DINDKAAR                  
              AND    T1.EKOMNR   = :PERSONTAB.EKOMNR                    
              AND    T1.EEJDNR   = :PERSONTAB.EEJDNR                    
              AND    T1.ELBNR    = :PERSONTAB.ELBNR                     
              AND    T1.EGENNR   = :PERSONTAB.EGENNR                    
                                                                        
              FOR FETCH ONLY                                            
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE  = -1;                                 
             RETURTEKST = 'SQLFEJL: DECLARE ESK_FEL_CUR';               
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END DECLARE_ESK_FEL_CUR;                                               
 /*********************************************************************/
 /* FETCH CURSOR TIL EVS_ESK_FELT                                     */
 /*********************************************************************/
 FETCH_ESK_FEL_CUR: PROC;                                               
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              FETCH ESK_FEL_CUR                                         
                                                                        
              INTO  :EVS_ESK_FELT.DCPRN,                                
                    :EVS_ESK_FELT.DINDKAAR,                             
                    :EVS_ESK_FELT.EKOMNR,                               
                    :EVS_ESK_FELT.EEJDNR,                               
                    :EVS_ESK_FELT.ELBNR,                                
                    :EVS_ESK_FELT.EGENNR,                               
                    :EVS_ESK_FELT.FELTKODE,                             
                    :EVS_ESK_FELT.INDHOLD_C,                            
                    :EVS_ESK_FELT.INDHOLD_D;                            
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0);                                                    
                   /* SKRIV TS_KØ, SKER I PROCEDURE SKRIV_TS_EVS_OVS */ 
                                                                        
           WHEN (100);                                                  
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE  = -1;                               
               PUT SKIP LIST('BU16010M - FETCH_ESK_FEL_CUR - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST = 'SQLFEJL: FETCH ESK_FEL_CUR';               
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END FETCH_ESK_FEL_CUR;                                                 
 /*********************************************************************/
 /* INSERT PERSON + EJENDOM.                                          */
 /* VEDRØRER: FUNKTION 2.                                      RUTINE */
 /* RETTET PGA. CSC HAR ÆNDRET I OPSÆTNING AF DFRAGLD I INDIVID 813   */
 /* FOR AT KUNNE MATCHE INDIVID 811 OG 813 LÆGGES CSCREGISTMP I       */
 /* I DFRAGLD NÅR DER MODTAGES INDIVID 811. DA CSCREGISTMP FREMSENDES */
 /* I DFRAGLD I INDIVID 813 Z8QEB 24-05-2016                          */
 /*********************************************************************/
 SKRIV_TABELLER: PROC;                                                  
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         PERSONTAB.EGENNR = PERSONTAB.EGENNR + 1;                       
                                                                        
         CALL HENT_DB2_TIMESTAMP;                                       
         IF PERSONTAB.CSCREGISTMP > '1900-12-31-00.00.00.000001'        
         THEN                                                           
           DO;                                                          
             EJENDOMTAB.DFRAGLD = PERSONTAB.CSCREGISTMP;                
             PERSONTAB.DFRAGLD  = PERSONTAB.CSCREGISTMP;                
           END;                                                         
         ELSE  /*BRUGES VED BATCH SUPLEMENT*/                           
           DO;                                                          
             EJENDOMTAB.DFRAGLD = HJ_TIMESTAMP;                         
             PERSONTAB.DFRAGLD = HJ_TIMESTAMP;                          
           END;                                                         
                                                                        
         PERSONTAB.DTILGLD = '9999-12-31-23.59.59.999999';              
                                                                        
         IF KOMM.TYPE = 2                       /* KUN VED NY EJENDOM */
         THEN                                                           
            CALL SKRIV_EJENDOM_TABEL;                                   
                                                                        
         IF KOMM.TYPE = 3                       /* KUN VED NY EJENDOM */
         THEN                                                           
            CALL OPDAT_EJENDOM_TABEL;                                   
                                                                        
         CALL SKRIV_PERSON_TABEL;                                       
                                                                        
         RETURTEKST = RETURTEKST_EJD !! RETURTEKST_PER;                 
                                                                        
 END SKRIV_TABELLER;                                                    
 /*********************************************************************/
 /* INSERT KUN PERSON UDEN ÆNDRING AF DFRAGLD                         */
 /* VEDRØRER: FUNKTION 4.                                      RUTINE */
 /*********************************************************************/
 SKRIV_KUN_PERSON_TABEL: PROC;                                          
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         PERSONTAB.DTILGLD = '9999-12-31-23.59.59.999999';              
         PERSONTAB.EGENNR = PERSONTAB.EGENNR + 1;                       
         CALL HENT_DB2_TIMESTAMP;     /* BRUGES I OPDATER GL RÆKKE */   
                                                                        
         CALL SKRIV_PERSON_TABEL;                                       
                                                                        
         RETURTEKST = RETURTEKST_EJD !! RETURTEKST_PER;                 
                                                                        
 END SKRIV_KUN_PERSON_TABEL;                                            
 /*********************************************************************/
 /* HENT DB2 TIMESTAMP (NUVÆRENDE CPU-TID).                           */
 /* VEDRØRER: FUNKTION 2.                                      RUTINE */
 /*********************************************************************/
 HENT_DB2_TIMESTAMP: PROC;                                              
                                                                        
         EXEC SQL                                                       
              SET :HJ_TIMESTAMP = CURRENT TIMESTAMP                     
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE    = -1;                               
             RETURTEKST   = 'SQLFEJL: HENT TIMESTAMP ';                 
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END HENT_DB2_TIMESTAMP;                                                
 /*********************************************************************/
 /* SKRIV 1 RÆKKE PÅ EVS SLUT 2003 EJENDOMSTABELLEN BQ20000T.         */
 /* VEDRØRER: FUNKTION 2.                                      RUTINE */
 /*********************************************************************/
 SKRIV_EJENDOM_TABEL: PROC;                                             
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              INSERT INTO BQ20000T                                      
                                                                        
                     (DINDKAAR                                          
                     ,EKOMNR                                            
                     ,EEJDNR                                            
                     ,DFRAGLD)                                          
                                                                        
              VALUES (:EJENDOMTAB.DINDKAAR                              
                     ,:EJENDOMTAB.EKOMNR                                
                     ,:EJENDOMTAB.EEJDNR                                
                     ,:EJENDOMTAB.DFRAGLD)                              
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
                                                                        
               BU_AREAL.EJENDOM.DFRAGLD = EJENDOMTAB.DFRAGLD;           
               CALL FLYT_EJD_FEL_TIL;                                   
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_EJD = 'EJENDOMSTABEL: SKRIV OK - ';           
             END;                                                       
                                                                        
           WHEN (-803)                                                  
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               RETURTEKST_EJD = 'EJENDOMSTABEL: RÆKKE FINDES - ';       
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST                                            
                          ('BU16010M - SKRIV_EJENDOM_TABEL - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST_EJD = 'EJENDOMSTABEL: SQLFEJL VED SKRIV - ';  
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END SKRIV_EJENDOM_TABEL;                                               
 /*********************************************************************/
 /* OPDATER RÆKKE PÅ EVS SLUT 2003 EJENDOMSTABELLEN BQ20000T.         */
 /* VEDRØRER: FUNKTION 2.  TYPE 3                              RUTINE */
 /*********************************************************************/
 OPDAT_EJENDOM_TABEL: PROC;                                             
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              UPDATE  BQ20000T                                          
                                                                        
                      SET DFRAGLD = : EJENDOMTAB.DFRAGLD                
                                                                        
              WHERE  DINDKAAR = :EJENDOMTAB.DINDKAAR                    
              AND    EKOMNR   = :EJENDOMTAB.EKOMNR                      
              AND    EEJDNR   = :EJENDOMTAB.EEJDNR                      
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
                                                                        
               BU_AREAL.EJENDOM.DFRAGLD = EJENDOMTAB.DFRAGLD;           
               CALL FLYT_EJD_FEL_TIL;                                   
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_EJD = 'EJENDOMSTABEL: SKRIV OK - ';           
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST                                            
                          ('BQ16010M - OPDAT_EJEMDOM_TABEL - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST_EJD = 'EJENDOMSTABEL: SQLFEJL VED OPDAT - ';  
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END OPDAT_EJENDOM_TABEL;                                               
                                                                        
 SKRIV_EVS_EJD_FELT: PROC;                                              
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              INSERT INTO BQ21000T                                      
                                                                        
                     (DINDKAAR                                          
                     ,EKOMNR                                            
                     ,EEJDNR                                            
                     ,FELTKODE                                          
                     ,FELTKODETYPE                                      
                     ,INDHOLD_C                                         
                     ,INDHOLD_D)                                        
                                                                        
              VALUES (:EJENDOMTAB.DINDKAAR                              
                     ,:EJENDOMTAB.EKOMNR                                
                     ,:EJENDOMTAB.EEJDNR                                
                     ,:EVS_EJD_FELT.FELTKODE                            
                     ,2                                                 
                     ,:EVS_EJD_FELT.INDHOLD_C                           
                     ,:EVS_EJD_FELT.INDHOLD_D)                          
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_EJD = 'EJENDOMSTABEL: SKRIV OK - ';           
             END;                                                       
                                                                        
           WHEN (-803)                                                  
             DO;                                                        
               CALL OPDAT_EVS_EJD_FELT;                                 
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST                                            
                           ('BU16010M - SKRIV_EVS_EJD_FELT - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               PUT SKIP LIST('BU16010M - FELTKODE '!!                   
                             EVS_EJD_FELT.FELTKODE);                    
               PUT SKIP LIST('BU16010M - INDH CHA '!!                   
                             EVS_EJD_FELT.INDHOLD_C);                   
               PUT SKIP LIST('BU16010M - INDH DEC '!!                   
                             EVS_EJD_FELT.INDHOLD_D);                   
               RETURTEKST_EJD = 'EJENDOMSTABEL: SQLFEJL VED SKRIV - ';  
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END SKRIV_EVS_EJD_FELT;                                                
                                                                        
 OPDAT_EVS_EJD_FELT: PROC;                                              
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              UPDATE BQ21000T                                           
                                                                        
                     SET  INDHOLD_C = :EVS_EJD_FELT.INDHOLD_C           
                         ,INDHOLD_D = :EVS_EJD_FELT.INDHOLD_D           
                                                                        
                     WHERE  DINDKAAR = :EJENDOMTAB.DINDKAAR             
                     AND    EKOMNR   = :EJENDOMTAB.EKOMNR               
                     AND    EEJDNR   = :EJENDOMTAB.EEJDNR               
                     AND    FELTKODE = :EVS_EJD_FELT.FELTKODE           
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE = 0                                           
         THEN                                                           
            DO;                                                         
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_EJD = 'EJENDOMSFELTTABEL:SKRIV OK';           
            END;                                                        
         IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)                 
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE = -1;                                  
             PUT SKIP LIST('BU16010M - OPDAT_EVS_EJD_FELT - SQLCODE '!! 
                             SQLCA.SQLCODE);                            
             PUT SKIP LIST('BU16010M - FELTKODE '!!                     
                           EVS_EJD_FELT.FELTKODE);                      
             RETURTEKST_EJD = 'EJENDOMFELTTABEL: SQLFEJL VED UPDATE';   
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END OPDAT_EVS_EJD_FELT;                                                
 /*********************************************************************/
 /* SKRIV 1 RÆKKE PÅ EVS SLUT EJERSKABTABELLEN BQ30000T.              */
 /* VEDRØRER: FUNKTION 2.                                      RUTINE */
 /*********************************************************************/
 SKRIV_PERSON_TABEL: PROC;                                              
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              INSERT INTO BQ30000T                                      
                                                                        
                    (DCPRN                                              
                    ,DINDKAAR                                           
                    ,EKOMNR                                             
                    ,EEJDNR                                             
                    ,ELBNR                                              
                    ,EGENNR                                             
                    ,CDATAFRA                                           
                    ,DFRAGLD                                            
                    ,DTILGLD                                            
                    ,CSCREGISTMP                                        
                    ,PERSKOD                                            
                    ,MYNDNR                                             
                    ,COMPLA                                             
                    ,DOMPLACPR                                          
                    ,CAJOUR                                             
                    ,CSUPPL                                             
                    ,FEJLNR                                             
                    ,DBRGN_MODT)                                        
                                                                        
              VALUES (:PERSONTAB.DCPRN                                  
                     ,:PERSONTAB.DINDKAAR                               
                     ,:PERSONTAB.EKOMNR                                 
                     ,:PERSONTAB.EEJDNR                                 
                     ,:PERSONTAB.ELBNR                                  
                     ,:PERSONTAB.EGENNR                                 
                     ,:PERSONTAB.CDATAFRA                               
                     ,:PERSONTAB.DFRAGLD                                
                     ,:PERSONTAB.DTILGLD                                
                     ,:PERSONTAB.CSCREGISTMP                            
                     ,:PERSONTAB.PERSKOD                                
                     ,:PERSONTAB.MYNDNR                                 
                     ,:PERSONTAB.COMPLA                                 
                     ,:PERSONTAB.DOMPLACPR                              
                     ,:PERSONTAB.CAJOUR                                 
                     ,:PERSONTAB.CSUPPL                                 
                     ,:PERSONTAB.FEJLNR                                 
                     ,'0001-01-01-00.00.00.000000')                     
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               CALL FLYT_ESK_FEL_TIL;                                   
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_PER = 'EJERSKABSTABEL: SKRIV OK';             
               BU_AREAL.PERSON.DFRAGLD = PERSONTAB.DFRAGLD;             
               CALL OPDATER_GL_PERSON_DTILGLD;                          
             END;                                                       
                                                                        
           WHEN (-803)                                                  
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               RETURTEKST_PER = 'EJERSKABSTABEL: RÆKKE FINDES';         
               PUT SKIP LIST('BU16010M - DUBLICATE ROW');               
               PUT SKIP LIST('BU16010M - DCPRN', PERSONTAB.DCPRN);      
               PUT SKIP LIST('BU16010M - DINDKAAR', PERSONTAB.DINDKAAR);
               PUT SKIP LIST('BU16010M - EKOMNR',  PERSONTAB.EKOMNR);   
               PUT SKIP LIST('BU16010M - EEJDNR', PERSONTAB.EEJDNR);    
               PUT SKIP LIST('BU16010M - ELBNR', PERSONTAB.ELBNR);      
               PUT SKIP LIST('BU16010M - EGENNR', PERSONTAB.EGENNR);    
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST                                            
                           ('BU16010M - SKRIV_PERSON_TABEL - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST_PER = 'EJERSKABSTABEL: SQLFEJL VED SKRIV';    
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END SKRIV_PERSON_TABEL;                                                
                                                                        
 SKRIV_EVS_ESK_FELT: PROC;                                              
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              INSERT INTO BQ31000T                                      
                                                                        
                     (DINDKAAR                                          
                     ,DCPRN                                             
                     ,EKOMNR                                            
                     ,EEJDNR                                            
                     ,ELBNR                                             
                     ,EGENNR                                            
                     ,FELTKODE                                          
                     ,FELTKODETYPE                                      
                     ,INDHOLD_C                                         
                     ,INDHOLD_D)                                        
                                                                        
              VALUES (:PERSONTAB.DINDKAAR                               
                     ,:PERSONTAB.DCPRN                                  
                     ,:PERSONTAB.EKOMNR                                 
                     ,:PERSONTAB.EEJDNR                                 
                     ,:PERSONTAB.ELBNR                                  
                     ,:PERSONTAB.EGENNR                                 
                     ,:EVS_ESK_FELT.FELTKODE                            
                     ,3                                                 
                     ,:EVS_ESK_FELT.INDHOLD_C                           
                     ,:EVS_ESK_FELT.INDHOLD_D)                          
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE = 0;                                 
               RETURTEKST_PER = 'EJERSKABSFELTTABEL: SKRIV OK - ';      
             END;                                                       
                                                                        
           WHEN (-803)                                                  
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               RETURTEKST_PER = 'EJERSKABSFELTTABEL: RÆKKE FINDES';     
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE = -1;                                
               PUT SKIP LIST                                            
                           ('BU16010M - SKRIV_EVS_ESK_FELT - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               PUT SKIP LIST('BU16010M - FELTKODE '!!                   
                             EVS_ESK_FELT.FELTKODE);                    
            RETURTEKST_PER = 'EJERSKABSFELTTABEL: SQLFEJL VED SKRIV - ';
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END SKRIV_EVS_ESK_FELT;                                                
 /*********************************************************************/
 /* OPDATER DEN TIDLIGERE TILGÆLDENDE DATO MED NY FRAGÆLDENDE DATO.   */
 /* VEDRØRER: FUNKTION 2.                                      RUTINE */
 /*********************************************************************/
 OPDATER_GL_PERSON_DTILGLD: PROC;                                       
                                                                        
 DCL HJ_EGENNR   BIN FIXED (15);                                        
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         HJ_EGENNR = BU_AREAL.PERSON.EGENNR;                            
                                                                        
         EXEC SQL                                                       
                                                                        
              UPDATE BQ30000T                                           
                                                                        
              SET    DTILGLD  = :HJ_TIMESTAMP                           
                                                                        
              WHERE  DCPRN    = :PERSONTAB.DCPRN                        
              AND    DINDKAAR = :PERSONTAB.DINDKAAR                     
              AND    EKOMNR   = :PERSONTAB.EKOMNR                       
              AND    EEJDNR   = :PERSONTAB.EEJDNR                       
              AND    ELBNR    = :PERSONTAB.ELBNR                        
              AND    EGENNR   = :HJ_EGENNR                              
         ;                                                              
                                                                        
         IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)                 
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE = -1;                                  
             PUT SKIP LIST                                              
                    ('BU16010M - OPDATER_GL_PERSON_DTILGLD - SQLCODE '!!
                             SQLCA.SQLCODE);                            
             RETURTEKST_PER = 'PERSONTABEL: SQLFEJL VED UPDATE';        
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END OPDATER_GL_PERSON_DTILGLD;                                         
 /*********************************************************************/
 /* OPTÆL ANTAL EJENDOMME FOR EN BESTEMT PERSON.                      */
 /* VEDRØRER: FUNKTION 3.                                      RUTINE */
 /*********************************************************************/
 OPTÆL_ANTAL_EJENDOMME: PROC;                                           
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              SELECT COUNT (*)                                          
                                                                        
              INTO  :ANTAL                                              
                                                                        
              FROM   BQ20000T T1, BQ30000T T2                           
                                                                        
              WHERE  T2.DINDKAAR = T1.DINDKAAR                          
              AND    T2.EKOMNR   = T1.EKOMNR                            
              AND    T2.EEJDNR   = T1.EEJDNR                            
              AND    T2.DCPRN    = :SØG.DCPRN                           
              AND    T2.DINDKAAR = :SØG.DINDKAAR                        
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE  = 0;                                
               RETURTEKST = 'MINIMUM 1 EJENDOM FUNDET FOR PERSONEN';    
             END;                                                       
                                                                        
           WHEN (100)                                                   
             DO;                                                        
               KOM_AREAL.RETURKODE  = 0;                                
               RETURTEKST = 'INGEN EJENDOMME FUNDET FOR PERSONEN';      
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE  = -1;                               
               PUT SKIP LIST                                            
                        ('BU16010M - OPTÆL_ANTAL_EJENDOMME - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST = 'SQLFEJL: OPTÆL ANTAL EJENDOMME';           
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END OPTÆL_ANTAL_EJENDOMME;                                             
 /*********************************************************************/
 /* SLET ALLE PERSONER FOR EN BESTEMT EJENDOM.                        */
 /* VEDRØRER: FUNKTION 5.                                      RUTINE */
 /*********************************************************************/
 SLET_ALLE_EJD_EJER: PROC;                                              
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              DELETE FROM BQ31000T                                      
                                                                        
                     WHERE  DINDKAAR = :SØG.DINDKAAR                    
                     AND    EEJDNR   = :SØG.EEJDNR                      
                     AND    EKOMNR   = :SØG.EKOMNR                      
         ;                                                              
                                                                        
         IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)                 
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE = -1;                                  
             PUT SKIP LIST('BU16010M - SLET_ALLE_EJD_EJER - SQLCODE '!! 
                             SQLCA.SQLCODE);                            
             RETURTEKST = 'SQLFEJL: DELETE PÅ BQ31000T';                
             RETURTEKST_PER = 'PERSONFELTTABEL: SQLFEJL VED DELETE';    
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
         EXEC SQL                                                       
                                                                        
              DELETE FROM BQ30000T                                      
                                                                        
                     WHERE  DINDKAAR = :SØG.DINDKAAR                    
                     AND    EEJDNR   = :SØG.EEJDNR                      
                     AND    EKOMNR   = :SØG.EKOMNR                      
                     AND    CSUPPL   = '4'                              
                     AND   (CAJOUR = '2' OR CAJOUR = '4')               
         ;                                                              
                                                                        
         IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)                 
         THEN                                                           
           DO;                                                          
             KOM_AREAL.RETURKODE = -1;                                  
             PUT SKIP LIST('BU16010M - SLET_ALLE_EJD_EJER - SQLCODE '!! 
                             SQLCA.SQLCODE);                            
             RETURTEKST = 'SQLFEJL: DELETE PÅ BQ30000T';                
             RETURTEKST_PER = 'PERSONTABEL: SQLFEJL VED DELETE';        
             KOMM.SQLKODE = SQLCA.SQLCODE;                              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END SLET_ALLE_EJD_EJER;                                                
 /*********************************************************************/
 /* TJEK OM CPRNR ALLEREDE ER OMPLACERET.                             */
 /* VEDRØRER: FUNKTION 16.                                     RUTINE */
 /*********************************************************************/
 CHECK_CPRNR_OMPLACERET: PROC;                                          
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');       
                                                                        
         EXEC SQL                                                       
                                                                        
              SELECT DISTINCT (DCPRN)                                   
                                                                        
              INTO  :PERSONTAB.DCPRN                                    
                                                                        
              FROM   BQ30000T                                           
                                                                        
              WHERE  DCPRN     = :SØG.DCPRN                             
              AND    DINDKAAR  = :SØG.DINDKAAR                          
              AND    COMPLA    = '1'                                    
              AND    DTILGLD   = '9999-12-31-23.59.59.999999'           
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               KOM_AREAL.RETURKODE  = 0;                                
               RETURTEKST = 'CPRNR ER BLEVET OMPLACERET';               
             END;                                                       
                                                                        
           WHEN (100)                                                   
             DO;                                                        
               KOM_AREAL.RETURKODE  = 1;                                
               RETURTEKST = 'CPRNR ER IKKE OMPLACERET';                 
             END;                                                       
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.RETURKODE  = -1;                               
               PUT SKIP LIST                                            
                       ('BU16010M - CHECK_CPRNR_OMPLACERET - SQLCODE '!!
                             SQLCA.SQLCODE);                            
               RETURTEKST = 'SQLFEJL: CHECK CPRNR OMPLACERET';          
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END CHECK_CPRNR_OMPLACERET;                                            
