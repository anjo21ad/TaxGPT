 BQ33000:  PROC OPTIONS (MAIN) REORDER;                                 
 Dcl Copyright Char (30) Static Init ('Copyright KMD A/S 2023');        
 /*-------------------------------------------------------------------- 
    Program        : BQ33000                                            
    Funktion       : Indlæs data fra vejregister                        
    Beskrivelse    :                                                    
                                                                        
    Programmør     : Z6ONE                                              
                                                                        
    Dato           : May 2023                                           
                                                                        
    JCL Test       : Udv.bevs.S22.jcl                                   
 --------------------------------------------------------------------*/ 
  /*------------------------------------------------------------------- 
    Includes                                                            
 -------------------------------------------------------------------*/  
                                                                        
 /*-------------------------------------------------------------------  
    Database                                                            
 -------------------------------------------------------------------*/  
    Exec Sql Include BQ80500T;    // vejregister                        
                                                                        
 /*********************************************************************/
 /*      DECLARES AF FILES                                            */
 /*********************************************************************/
  DCL BQ3300I FILE   RECORD INPUT;/*INDIVID 132 EJENDOMSDATA*//*ÅR-RET*/
  DCL BQ8050O FILE   RECORD OUTPUT; /* DB2 INDIVID 132 TIL LOAD*/       
  DCL SYSPRINT EXTERNAL FILE PRINT;                                     
                                                                        
 /********************************************************************* 
 *               DECLARE AF DIV. FÆLLESMACROER (EVS SLUT/FORSKUD)     * 
 *********************************************************************/ 
                                                                        
 DCL     1 BH55300M,                                                    
           %INCLUDE BH55300M;                                           
                                                                        
 /*-------------------------------------------------------------------  
    Declares                                                            
 -------------------------------------------------------------------*/  
 Dcl  (Addr, Null, Stg, Index, Low, Substr, Cstg, Trim,                 
       TIMESTAMP)  Builtin;                                             
 Dcl  ProgramNavn                 Char      (7)       Init ('BQ33000'); 
 Dcl  Putskip                     Bit       (1)       Init('0'B);       
 Dcl  Ja                          Bit       (1)       Init('1'B);       
 Dcl  Nej                         Bit       (1)       Init('0'B);       
 Dcl  TIDSSTAMP                   Char      (26);                       
 DCL  EOF_BQ3300I                 BIT(1)              INIT('0'B);       
 DCL  WS_TIL_EVS_AAR              BIN FIXED (15);                       
 DCL  WS_FORSKUD_SLUT_TXT         CHAR      (16)      INIT (' ');       
                                                                        
                                                                        
 /***************************************************************       
 /*      DECLARE AF ABEND-RUTINE                                        
 /**************************************************************/       
 DCL     ZS30101       ENTRY;                                           
 DCL     ABENDKODE     FIXED     (2) INIT(5);                           
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT (ProgramNavn),                
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARES AF input output FILES                               */
 /*********************************************************************/
                                                                        
 DCL LGD_INPUTFIL BIN FIXED(15);                                        
 DCL 1 INPUTFIL      CHAR(204) VAR; /* MED LIDT EKSTRA PLADS */         
 DCL 1 INPUTFIL_IND,                                                    
      2 DATA         CHAR(200);                                         
                                                                        
 DCL  1  Type       BASED (ADDR(INPUTFIL_IND.DATA)),                    
       %INCLUDE BQ33000N;;                                              
                                                                        
 DCL 1 INPUTFIL_001 UNALIGNED BASED(ADDR(INPUTFIL_IND.DATA)),           
       %INCLUDE BQ33001N;;                                              
                                                                        
 DCL 1 DB2_LOAD_FIL UNALIGNED,                                          
     %INCLUDE BQ80500N;;                                                
                                                                        
 /*-------------------------------------------------------------------  
     Housekeeping                                                       
 -------------------------------------------------------------------*/  
    On Error                                                            
         Begin;                                                         
            On Error System;                                            
            Call Afslut;                                                
            Call Plidump ('SHBO');                                      
         End;                                                           
                                                                        
         ON ENDFILE (BQ3300I)                                 /*ÅR-RET*/
         BEGIN;                                                         
            EOF_BQ3300I = JA;                                 /*ÅR-RET*/
         END;                                                           
                                                                        
                                                                        
 /*-------------------------------------------------------------------  
     Hovedprogram                                                       
 -------------------------------------------------------------------*/  
                                                                        
 SELECT (BH55300M.AFVIKLINGS_ID);                                       
            WHEN ('OPRET_EVS_SLUT ')                                    
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SLUT kørsel');         
                 WS_TIL_EVS_AAR = BH55300M.SLUTÅR;                      
                 WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;          
              END;                                                      
            WHEN ('OPRET_EVS_FORSK')                                    
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS FORSKUD kørsel');      
                 WS_TIL_EVS_AAR = BH55300M.FORSKÅR;                     
                 WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;          
              END;                                                      
            WHEN ('SUPPL_EVS_SLUT')                                     
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SUPPLEMENT kørsel');   
                 WS_TIL_EVS_AAR = BH55300M.SLUTÅR;                      
                 WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;          
              END;                                                      
            OTHERWISE                                                   
              DO;                                                       
                 ABENDTXT = 'Afviklings_id skal være '           !!     
                            'OPRET_EVS_FORSK, OPRET_EVS_SLUT ' !!       
                            'eller SUPPL_EVS_SLUT';                     
                 CALL PROGRAM_FEJL;                                     
              END;                                                      
         END;                                                           
                                                                        
 Call INIT;                                                             
                                                                        
 READ FILE (BQ3300I) INTO (INPUTFIL);                                   
 INPUTFIL_IND.DATA = INPUTFIL;                                          
                                                                        
 Do While (EOF_BQ3300I = nej);                                          
    If Type.Recordtype = '001' Then                                     
    Do;                                                                 
      DB2_LOAD_FIL.TIL_EVS_AAR  = WS_TIL_EVS_AAR;                       
      DB2_LOAD_FIL.AFVIKLINGS_ID = WS_FORSKUD_SLUT_TXT;                 
      DB2_LOAD_FIL.DATA_ID  = 1;   //ONE TODO  ??                       
      DB2_LOAD_FIL.EKOMNR  = INPUTFIL_001.KOMKOD;                       
      DB2_LOAD_FIL.VEJKOD  = INPUTFIL_001.VEJKOD;                       
      DB2_LOAD_FIL.VEJADRNVN = INPUTFIL_001.VEJADRNVN;                  
      DB2_LOAD_FIL.VEJNAVN = INPUTFIL_001.VEJNVN;                       
      DB2_LOAD_FIL.TIMESTAMP_CPR  = INPUTFIL_001.TIMESTAMP_CPR;         
      DB2_LOAD_FIL.HAENSTART_CPR  = INPUTFIL_001.HAENSTART;             
      DB2_LOAD_FIL.INSERT_TS  = TIDSSTAMP;                              
      DB2_LOAD_FIL.INSERT_AF = ProgramNavn;                             
                                                                        
      WRITE FILE (BQ8050O) FROM (DB2_LOAD_FIL);                         
    End;                                                                
    READ FILE (BQ3300I) INTO (INPUTFIL);                                
    INPUTFIL_IND.DATA = INPUTFIL;                                       
                                                                        
 End; //While                                                           
                                                                        
 Call Afslut;                                                           
                                                                        
                                                                        
 /*********************************************************************/
 /*      INIT                                                         */
 /*********************************************************************/
 INIT: PROC;                                                            
         OPEN FILE (BQ3300I) TITLE ('BQ33000I');                        
         OPEN FILE (BQ8050O) TITLE ('BQ80500O');                        
         TIDSSTAMP = TIMESTAMP;                                         
 END INIT;                                                              
 /*-------------------------------------------------------------------- 
    Afslutning - luk filer, cursorer osv.                               
 --------------------------------------------------------------------*/ 
 Afslut: Proc;                                                          
                                                                        
   Close File (BQ3300I);            // Luk inputfil                     
   Close File (BQ8050O);            // Luk outputfil                    
                                                                        
 End Afslut;                                                            
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
                                                                        
         Call Afslut;                                                   
                                                                        
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 End BQ33000;                                                           
                                                                        
