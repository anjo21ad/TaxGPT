 BH10000: PROCEDURE OPTIONS (MAIN);                                     
 /**********************************************************************
 * PROJEKT:     EVS FORSKUD 2015/EVS SLUT 2013                        **
 * PROGRAMNAVN: BH30000.                                               *
 * PROGRAMTYPE: BATCH.                                                 *
 * FUNKTION:    PROGRAMMET FINDER PERSONUMMER PÅ EJENDOM               *
 *              FRA EVS SLUT 2014.                                     *
 * PGMFORLØB:   1. ÅBEN FILER.                                         *
 *              2. FIND PERSONUMMER 1 ELLER MANGE                      *
 *              3. SKRIV PÅ FIL TIL CSC                                *
 *              4. LUK FILER.                                          *
 *             12. SKRIV KONTROLLISTE.                                 *
 * INDDATA:     B.H10000D   UDTRÆK FRA ESR                             *
 *              BQ30000T/BQ31000T - EVS PERSONTABEL.                   *
 * UDDATA:      BH10001O - FIL TIL CSC MED PERSON NUMMER OG EJENDOMSNR *
 *              BH10099Y - KONTROLLISTE.                               *
 * KONSTRUKTØR: HANS ERIK KJELDSEN                                     *
 **********************************************************************/
                                                                        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT. KALD AF EKSTERNE MODULER */
                                                                        
 DCL     COPYRIGHT      CHAR (30) INIT ('(C) KMD A/S');                 
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE SQLCA;        /* SQL KOMMUNIKATIONSAREAL    */
         EXEC SQL INCLUDE BQ30000T;     /* EVS SLUT EJERSKABSTABEL    */
                                                                        
 /**********************************************************************
 * DECLARE AF ABEND RUTINE                                             *
 **********************************************************************/
                                                                        
 DCL     ZS30101        ENTRY;                                          
 DCL     ZS67000        ENTRY;                                          
 DCL     ZS61900        ENTRY;                        /* KST/SSI INFO */
                                                                        
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
                                                                        
 DCL     HJ_DCPRN_PIC   PIC  '(10)9';                                   
 DCL     HJ_EKOMNR_PIC  PIC  '(3)9';                                    
 DCL     HJ_EEJDNR_PIC  PIC  '(6)9';                                    
 DCL     OK             CHAR (01)        INIT('J');                     
 DCL     FEJLTEKST      CHAR (20)        INIT(' ');                     
 DCL     ABENDMEDD      CHAR (56)        INIT(' ');                     
 DCL     FLAG_EN_RÆKKE  BIT(1)           INIT('0'B);                    
                                                                        
 DCL     DCPRN_NGL      DEC FIXED (11,0);                               
 DCL     KOM_NGL        BIN FIXED (15) UNAL;                            
 DCL     EJD_NGL        BIN FIXED (31) UNAL;                            
 DCL     FETCH_SQLKODE  FIXED BIN(15)    INIT(0);                       
                                                                        
 DCL     DT             CHAR (6);                                       
 DCL     1 DAT          BASED(ADDR(DT)),                                
           2 ÅR         PIC   '99',                                     
           2 MD         PIC   '99',                                     
           2 DAG        PIC   '99';                                     
                                                                        
 DCL     HJ_DATOEN       CHAR (8);                                      
 DCL     1 HJ_DATO       BASED(ADDR(HJ_DATOEN)),                        
           2 AAR         PIC  '9999',                                   
           2 MD          PIC  '99',                                     
           2 DG          PIC  '99';                                     
 /*********************************************************************/
 /* DECLARE DATASÆT                                                   */
 /*********************************************************************/
                                                                        
 DCL     SYSPRINT FILE STREAM PRINT;                                    
                                        /*RUL*/                         
 DCL     BH1000I FILE INPUT  RECORD; /* UDTRÆK FRA ESR EJENDOMSNR */    
 DCL     BH1001O FILE OUTPUT RECORD; /* FIL TIL ESR CPRN-EJENDOMSNR  */ 
                                                                        
 /*********************************************************************/
 /* DECLARE STRUKTURER                                                */
 /*********************************************************************/
                                                                        
 DCL     1 UDTRÆK_ESR,                                                  
           2 EKOMNR    PIC '999',                                       
           2 FILLER    CHAR(1),                                         
           2 EEJDNR    PIC '999999';                                    
                                                                        
 DCL     1 UDTRÆK_CSC,                                                  
           2 CPRNR     CHAR(10),                                        
           2 FILER1    CHAR(1),                                         
           2 EKOMNR    CHAR(3),                                         
           2 FILER2    CHAR(1),                                         
           2 EEJDNR    CHAR(6);                                         
                                                                        
 DCL   HJ_EKOMNR   BIN FIXED (15);                                      
 DCL   HJ_EEJDNR   BIN FIXED (31);                                      
                                                                        
 DCL     1 EVS_ESK,                     /* EVS EJERSKABSTABELEN       */
           %INCLUDE BQ30000N;;                                          
 DCL     EOF       BIT       (1);                                       
 DCL     JA        BIT       (1)     INIT ('1'B);                       
 DCL     NEJ       BIT       (1)     INIT ('0'B);                       
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     (ADDR,DATE,VERIFY,NULL,MOD,SUBSTR,PLIDUMP) BUILTIN;            
                                                                        
 /*********************************************************************/
 /* DECLARE AF TÆLLERE                                                */
 /*********************************************************************/
                                                                        
 DCL   ANTLÆS_ESR        BIN FIXED (31) INIT(0);/* LÆSTE PERSONTABEL  */
 DCL   ANTSKRIV_CSC      BIN FIXED (31) INIT(0);/* SKREVET UDTRÆKSFIL */
 DCL   ANT_EJFUNDET      BIN FIXED (31) INIT(0);/* FORKERT EJEN         
 /*********************************************************************/
 /* PRINTLINIER TIL SPOOL                                             */
 /*********************************************************************/
                                                                        
 DCL     LINIE           CHAR (133);                                    
 DCL     1 LIN           BASED(ADDR(LINIE)),                            
           2 TEKST       CHAR (56),                                     
           2 ANT         PIC  '---------9';                             
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SBHO','MODUL BH10000');                 
            END;                                                        
                                                                        
         ON ENDFILE (BH1000I)                                           
         BEGIN;                                                         
            EOF = JA;                                                   
         END;                                                           
                                                                        
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
         UDTRÆK_ESR        = '';                                        
                                                                        
         DT = DATE;                                                     
                                                                        
         OPEN FILE (BH1000I) TITLE ('BH10000I');                        
         OPEN FILE (BH1001O) TITLE ('BH10001O');                        
                                                                        
                                                                        
         CALL ZS61900;                              /* KST/SSI INFO   */
                                                                        
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
       READ FILE(BH1000I) INTO (UDTRÆK_ESR);                            
       ANTLÆS_ESR = ANTLÆS_ESR + 1;                                     
                                                                        
       DO WHILE (^EOF);                                                 
         HJ_EKOMNR_PIC = UDTRÆK_ESR.EKOMNR;                             
         HJ_EKOMNR = HJ_EKOMNR_PIC;                                     
         HJ_EEJDNR_PIC = UDTRÆK_ESR.EEJDNR;                             
         HJ_EEJDNR = HJ_EEJDNR_PIC;                                     
         CALL DECLARE_CURSOR;                                           
         CALL OPEN_PERSONEJENDOMSOPL;                                   
         CALL FETCH_PERSONEJENDOMSOPL;                                  
         DO WHILE (SQLCA.SQLCODE = 0);                                  
            UDTRÆK_CSC = '';                                            
            HJ_DCPRN_PIC          = EVS_ESK.DCPRN;                      
            HJ_EKOMNR_PIC         = EVS_ESK.EKOMNR;                     
            HJ_EEJDNR_PIC         = EVS_ESK.EEJDNR;                     
            UDTRÆK_CSC.CPRNR      = HJ_DCPRN_PIC;                       
            UDTRÆK_CSC.EKOMNR     = HJ_EKOMNR_PIC;                      
            UDTRÆK_CSC.EEJDNR     = HJ_EEJDNR_PIC;                      
            WRITE FILE (BH1001O) FROM (UDTRÆK_CSC);                     
            ANTSKRIV_CSC = ANTSKRIV_CSC + 1;                            
            CALL FETCH_PERSONEJENDOMSOPL;                               
         END;                                                           
         CALL CLOSE_PERSONEJENDOMSOPL;                                  
         READ FILE(BH1000I) INTO (UDTRÆK_ESR);                          
         IF ^EOF                                                        
         THEN                                                           
           ANTLÆS_ESR = ANTLÆS_ESR + 1;                                 
       END;/*DO WHILE*/                                                 
         CALL AFSLUT;                                                   
                                                                        
 /* DECLARE CURSOR TIL LÆS AF EJERSKABSTABEL BQ30000T                 */
 /*********************************************************************/
 DECLARE_CURSOR: PROC;                                                  
                                                                        
         EXEC SQL DECLARE PERSON_CURSOR CURSOR FOR                      
                                                                        
              SELECT T1.DINDKAAR,                                       
                     T1.DCPRN,                                          
                     T1.EKOMNR,                                         
                     T1.EEJDNR                                          
                                                                        
              FROM BQ30000T T1                                          
              WHERE  T1.DINDKAAR = 2014                                 
              AND    T1.DTILGLD  = '9999-12-31-23.59.59.999999'         
              AND    T1.EKOMNR   = :HJ_EKOMNR                           
              AND    T1.EEJDNR   = :HJ_EEJDNR                           
                                                                        
                                                                        
              GROUP BY T1.DINDKAAR,                                     
                     T1.DCPRN,                                          
                     T1.EKOMNR,                                         
                     T1.EEJDNR                                          
                                                                        
              ORDER BY T1.DINDKAAR,                                     
                    T1.DCPRN,                                           
                    T1.EKOMNR,                                          
                    T1.EEJDNR                                           
                                                                        
         FOR READ ONLY;                                                 
                                                                        
      /***************************/                                     
 END DECLARE_CURSOR;                                                    
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF EJERSKABSTABEL BQ30000T                   */
 /*********************************************************************/
 FETCH_PERSONEJENDOMSOPL: PROC;                                         
                                                                        
         EXEC SQL FETCH PERSON_CURSOR                                   
                                                                        
         INTO :EVS_ESK.DINDKAAR,                                        
              :EVS_ESK.DCPRN,                                           
              :EVS_ESK.EKOMNR,                                          
              :EVS_ESK.EEJDNR;                                          
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0);                                                    
           WHEN (100);                                                  
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST  =  'BH10000 ** 262  FETCH PERSON_CURSOR';     
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* SELECT*/                                               
 END FETCH_PERSONEJENDOMSOPL;                                           
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF TABEL BQ30000T/BQ31000T             RUTINE */
 /*********************************************************************/
 OPEN_PERSONEJENDOMSOPL: PROC;                                          
                                                                        
         EXEC SQL OPEN PERSON_CURSOR;                                   
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0);                                                    
           WHEN (100)                                                   
             ANT_EJFUNDET = ANT_EJFUNDET + 1;                           
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST  =  'BH10000 ** 262  OPEN PERSON_CURSOR';      
               CALL SQL_FEJL;                                           
             END;                                                       
         END; /* SELECT*/                                               
 END OPEN_PERSONEJENDOMSOPL;                                            
 /*********************************************************************/
 /*********************************************************************/
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF TABEL BQ01200T/BQ01100T            RUTINE */
 /*********************************************************************/
 CLOSE_PERSONEJENDOMSOPL: PROC;                                         
                                                                        
         EXEC SQL CLOSE PERSON_CURSOR;                                  
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'CLOSE PERSON_CURSOR';                        
              FEJLTEKST  = 'BH10000 ** 256 ' !! FEJLTEKST;              
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END CLOSE_PERSONEJENDOMSOPL;                                           
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CLOSE FILE(BH1000I);                                           
         CLOSE FILE(BH1001O);                                           
                                                                        
         LIN.TEKST = 'ANTAL LÆSTE FRA ESR UDTRÆK.... ................'; 
         LIN.ANT   = ANTLÆS_ESR;                                        
         PUT SKIP LIST(LINIE);                                          
                                                                        
         LIN.TEKST = 'ANTAL SKREVNE TIL CSC..........................'; 
         LIN.ANT   = ANTSKRIV_CSC;                                      
         PUT SKIP LIST(LINIE);                                          
                                                                        
         LIN.TEKST = 'ANTAL IKKE FUNDNE..............................'; 
         LIN.ANT   = ANT_EJFUNDET;                                      
         PUT SKIP LIST(LINIE);                                          
                                                                        
                                                                        
                                                                        
 END AFSLUT;                                                            
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST('SQL_FEJL.....');                                
         PUT SKIP LIST('SQLCA',SQLCA);                                  
         CLOSE FILE(BH1000I);                                           
         CLOSE FILE(BH1001O);                                           
         CALL ZS67000(SQLCA);                                           
                                                                        
         ABENDMEDD =  FEJLTEKST;                                        
                                                                        
         CALL ZS30101 (5, ABENDMEDD, 2);                                
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER STARTER APPLIKATIONENS EGNE INCLUDES                          */
 /*********************************************************************/
                                                                        
 %INCLUDE ZM90013M;             /* OMSÆT DATO TIL/FRA DB2-DATOFORMAT  */
 %INCLUDE ZM90051M;             /* KONV BIN15 TIL CHAR                */
 %INCLUDE BS34307M;             /* KONV BF31 TIL CHAR6                */
 %INCLUDE BH30001M;             /* KONV DATO FRA CHAR TIL DEC FIXED   */
                                                                        
 END BH10000;                                                           
