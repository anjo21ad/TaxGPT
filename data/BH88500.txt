 BH88500: PROCEDURE OPTIONS (MAIN);                                     
 /**********************************************************************
 * PROJEKT:     EVS FORSKUD / EVS SLUT                                 *
 * PROGRAMNAVN: BH88500.                                               *
 * PROGRAMTYPE: BATCH.                                                 *
 * FUNKTION:    Læs dataset med samtlige records der skal udtrækkes    *
 *              dog med en kendingsnøgle sat foran strukturen.         *
 *              Skriv dataset med cprnumre og ejendomme der skal       *
 *              udtrækkes. Dannet i BH88300.                           *
 *              Skrives UDEN kendingsnøgle.                            *
 *                                                                     *
 *              MANY - Mandtal forskud 2017                            *
 *              MAGL - Mandtal slut    2016                            *
 *              PENS - Pensionsudtræk                                  *
 *              ESR  - ESR (5 dataset)                                 *
 *                                                                     *
 * INDDATA:     TOTLIST  - Samlet dataset med alle filer               *
 *                                                                     *
 * UDDATA:      BH88501Y - KONTROLLISTE.                               *
 *              Mandtal 2017, mandtal 2016, Pension, ESR               *
 ***********************************************************************
 * OPRETTET   : Lene Maj Jensen   QLE             BALLERUP 04.04.2017  *
 **********************************************************************/
                                                                        
 DCL     COPYRIGHT      CHAR (30) INIT ('(C) KOMMUNEDATA A/S 2006');    
                                                                        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT. KALD AF EKSTERNE MODULER */
                                                                        
 /*********************************************************************/
 /* DECLARE DATASÆT                                                   */
 /*********************************************************************/
                                                                        
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
 DCL     BH885PRM FILE RECORD INPUT;     /* Parm data  */               
 DCL     TOTLIST  FILE RECORD INPUT;     /* Samtlige udtrukne */        
                                                                        
 DCL     MANYFIL  FILE RECORD OUTPUT;    /* Mandtal forskud 2017 */     
 DCL     MAGLFIL  FILE RECORD OUTPUT;    /* Mandtal slut    2016 */     
 DCL     PENSFIL  FILE RECORD OUTPUT;    /* Pensionsudtræk       */     
 DCL     ESRFIL   FILE RECORD OUTPUT;    /* ESR                  */     
                                                                        
 DCL     1 TOT_AR         CHAR      (2000) VAR;                         
                                                                        
 DCL     1 TOT_DET   BASED     (ADDR(TOT_AR)),                          
           3 LÆNGDE BIN FIXED (15),                                     
           3 NOEGLE   CHAR (4);                                         
                                                                        
      /*    MANDTAL forskud 2017    */                                  
                                                                        
 DCL     1 TOT_MANY   BASED     (ADDR(TOT_AR)),                         
           2 LÆNGDE BIN FIXED (15),                                     
           2 NOEGLE   CHAR (4),                                         
           2 TOT_DET_MANY,                                              
             3 LÆNGDE BIN FIXED (15),                                   
             %INCLUDE BR74250N;                                         
                                                                        
 DCL     MANY_AR          CHAR      (2000) VAR;                         
                                                                        
 DCL     1 MANY_DET   BASED     (ADDR(MANY_AR)),                        
           %INCLUDE BR74250N;                                           
                                                                        
      /*    MANDTAL slut 2016    */                                     
                                                                        
 DCL     1 TOT_MAGL   BASED     (ADDR(TOT_AR)),                         
           2 LÆNGDE BIN FIXED (15),                                     
           2 NOEGLE   CHAR (4),                                         
           2 TOT_DET_MAGL,                                              
             3 LÆNGDE BIN FIXED (15),                                   
             %INCLUDE BH65501N;;                                        
                                                                        
 DCL     MAGL_AR          CHAR      (2000) VAR;                         
 DCL     1 MAGL_DET   BASED     (ADDR(MAGL_AR)),                        
           %INCLUDE BH65501N;;                                          
                                                                        
      /*    PENSION    */                                               
                                                                        
 DCL     1 TOT_PENS   BASED     (ADDR(TOT_AR)),                         
           3 LÆNGDE BIN FIXED (15),                                     
           3 NOEGLE   CHAR(4),                                          
           3 TOT_DET_PENS,                                              
             4 CITYP           CHAR      (3),                           
             4 DCPRN           PIC       '(10)9';                       
             /* %INCLUDE BH65200N; */                                   
                                                                        
 DCL     1 PENS_DET,                                                    
             4 CITYP           CHAR      (3),                           
             4 DCPRN           PIC       '(10)9';                       
             /* %INCLUDE BH65200N; */                                   
                                                                        
      /*    ESR    */                                                   
                                                                        
 DCL     1 TOT_ESR   BASED     (ADDR(TOT_AR)),                          
           3 LÆNGDE BIN FIXED (15),                                     
           3 NOEGLE   CHAR(4),                                          
           3 TOT_DET_ESR,                                               
             4 LÆNGDE BIN FIXED (15),                                   
             %INCLUDE BH31800N;                                         
                                                                        
 DCL     ESR_AR          CHAR      (2000) VAR;                          
 DCL     1 ESR_DET   BASED     (ADDR(ESR_AR)),                          
           %INCLUDE BH31800N;                                           
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE SQLCA;           /* SQL KOMMUNIKATIONSAREAL */
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2 strukturer                                         */
 /*********************************************************************/
                                                                        
                                                                        
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
 DCL     WS_PROGRAMNAVN         CHAR(8)  INIT ('BH88500');              
                                                                        
 DCL     WS_FORSKUD_SLUT_TXT    CHAR(16) INIT (' ');                    
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     1 BH885_PARM,                                                  
           %INCLUDE BH88500N;                                           
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;                                           
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     EOF_TOTLIST       BIT       (01)      INIT      ('0'B);        
 DCL     EOF_BH885PRM      BIT       (01)      INIT      ('0'B);        
                                                                        
 DCL     JA                 BIT       (1)     INIT ('1'B);              
 DCL     NEJ                BIT       (1)     INIT ('0'B);              
                                                                        
 DCL  ANT_LÆS_TOT            dec fixed (11) init (0);                   
                                                                        
 DCL  ANT_SKRIV_MANY         dec fixed (11) init (0);                   
 DCL  ANT_SKRIV_MAGL         dec fixed (11) init (0);                   
 DCL  ANT_SKRIV_PENS         dec fixed (11) init (0);                   
 DCL  ANT_SKRIV_ESR          dec fixed (11) init (0);                   
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH88500'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH88500L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'TESTUDTRÆK '),                                              
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (65),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZZ.ZZ9',                     
           2 F1          CHAR      (54)      INIT      (' ');           
                                                                        
 DCL     1 DETLIN3,                                                     
           2 CCA         CHAR      (1),                                 
           2 TXT1       CHAR      (30),                                 
           2 TXT2       CHAR      (102);                                
                                                                        
 DCL     1 PCTLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (66),                                
           2 ANTAL       PIC       'Z9V,999999999',                     
           2 F1          CHAR      (50)      INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
                                                                        
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /* Declare af CURSORS                                                */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         OPEN FILE (TOTLIST) TITLE ('BH885TOT');                        
         OPEN FILE (MANYFIL) TITLE ('BH885MAY');                        
         OPEN FILE (MAGLFIL) TITLE ('BH885MAG');                        
         OPEN FILE (PENSFIL) TITLE ('BH885PEN');                        
         OPEN FILE (ESRFIL)  TITLE ('BH885ESR');                        
         OPEN FILE (BH885PRM) TITLE ('BH885NYP');                       
                                                                        
         EOF_TOTLIST = NEJ;                                             
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH88500L');             
            END;                                                        
                                                                        
         ON ENDFILE (TOTLIST)                                           
         BEGIN;                                                         
            EOF_TOTLIST = JA;                                           
         END;                                                           
                                                                        
         ON ENDFILE (BH885PRM)                                          
         BEGIN;                                                         
            EOF_BH885PRM = JA;                                          
         END;                                                           
                                                                        
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
         CALL ZZ20390('*OPEN','BH88501Y');       /* ÅBEN KONTROLLISTE */
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.ÅR   = GLSLUTÅR - 1;                                  
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
         READ FILE (BH885PRM) INTO (BH885_PARM);                        
                                                                        
         IF EOF_BH885PRM = NEJ                                          
         THEN DO;                                                       
            SELECT (BH885_PARM.FS_MRK);                                 
               WHEN ('F')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH885_PARM.FORSKUD_TXT;      
                     WS_TIL_EVS_AAR = BH885_PARM.AAR;                   
                     OVERLIN1.TEKST2 = 'TESTUDTRÆK FORSKUD';            
                  END;                                                  
               WHEN ('S')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH885_PARM.SLUT_TXT;         
                     WS_TIL_EVS_AAR = BH885_PARM.AAR;                   
                     OVERLIN1.TEKST2 = 'TESTUDTRÆK SLUT';               
                  END;                                                  
               WHEN ('U')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH885_PARM.SUPPL_TXT;        
                     WS_TIL_EVS_AAR = BH885_PARM.AAR;                   
                     OVERLIN1.TEKST2 = 'TESTUDTRÆK SUPPLEMENT';         
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     ABENDTXT = '101 PARAMETERFEJL';                    
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
         END;                                                           
                                                                        
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
                                                                        
         /* OPBYG INTERN TABEL MED ØNSKEDE UDTRÆKSKRITERIER */          
                                                                        
         READ FILE (TOTLIST) INTO (TOT_AR);                             
                                                                        
         do while (^EOF_TOTLIST);                                       
            SELECT (TOT_DET.NOEGLE);                                    
               WHEN ('MANY')                                            
                  DO;                                                   
                     MANY_DET = TOT_DET_MANY, by name;                  
                     if ant_skriv_many < 3                              
                     then do;                                           
                        put skip list ('many_det.dcprn: ' !!            
                            many_det.dcprn);                            
                     end;                                               
                     write file (MANYFIL) from (MANY_DET);              
                     ANT_SKRIV_MANY = ANT_SKRIV_MANY + 1;               
                  END;                                                  
               WHEN ('MAGL')                                            
                  DO;                                                   
                     MAGL_DET = TOT_DET_MAGL, by name;                  
                     if ant_skriv_magl < 3                              
                     then do;                                           
                        put skip list ('magl_det.dcprn: ' !!            
                            magl_det.dcprn);                            
                     end;                                               
                     write file (MAGLFIL) from (MAGL_DET);              
                     ANT_SKRIV_MAGL = ANT_SKRIV_MAGL + 1;               
                  END;                                                  
               WHEN ('PENS')                                            
                  DO;                                                   
                     PENS_DET = TOT_DET_PENS, by name;                  
                     if ant_skriv_pens < 3                              
                     then do;                                           
                        put skip list ('pens_det.dcprn: ' !!            
                            pens_det.dcprn);                            
                     end;                                               
                     write file (PENSFIL) from (PENS_DET);              
                     ANT_SKRIV_PENS = ANT_SKRIV_PENS + 1;               
                  END;                                                  
               WHEN ('ESR ')                                            
                  DO;                                                   
                     ESR_DET = TOT_DET_ESR, by name;                    
                     if ant_skriv_esr < 3                               
                     then do;                                           
                        put skip list ('esr_det.dcprcir: ' !!           
                            esr_det.dcprcir);                           
                     end;                                               
                     write file (ESRFIL) from (ESR_DET);                
                     ANT_SKRIV_ESR = ANT_SKRIV_ESR + 1;                 
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     ABENDTXT = '101 TYPE ' !! TOT_MANY.NOEGLE;         
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
            READ FILE (TOTLIST) INTO (TOT_AR);                          
         end;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
                                                                        
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
                                                                        
         DETLIN1.TEKST =                                                
         'LÆSTE PÅ TOTALLISTE ';                                        
         DETLIN1.ANTAL = ANT_LÆS_TOT;                                   
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'UDTRUKKET PÅ MANDTAL Forskud 2017 ';                          
         DETLIN1.ANTAL = ANT_SKRIV_MANY;                                
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'UDTRUKKET PÅ MANDTAL Slut 2016 ';                             
         DETLIN1.ANTAL = ANT_SKRIV_MAGL;                                
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'UDTRUKKET PÅ Pension ';                                       
         DETLIN1.ANTAL = ANT_SKRIV_PENS;                                
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'UDTRUKKET PÅ ESR ';                                           
         DETLIN1.ANTAL = ANT_SKRIV_ESR;                                 
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL KONTROLLISTE;                                             
         CLOSE FILE (TOTLIST);                                          
         CLOSE FILE (BH885PRM);                                         
         CLOSE FILE (MANYFIL);                                          
         CLOSE FILE (MAGLFIL);                                          
         CLOSE FILE (PENSFIL);                                          
         CLOSE FILE (ESRFIL);                                           
                                                                        
         CALL ZZ20300(' ','99');                                        
                                                                        
         PUT SKIP LIST ('Programmet afsluttet');                        
                                                                        
 END AFSLUT;                                                            
 END BH88500;                                                           
