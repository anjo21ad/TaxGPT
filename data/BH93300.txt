 PROCESS OPT(TIME);                                                     
 BH93300:/* BEHANDL SOMMERHUSE A.H.T. OPSÆT KODE FOR HELÅRSBEBOELSE */  
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /********************************************************************/ 
         DCL COPYRIGHT   CHAR      (30) STATIC                          
         INIT ('COPYRIGHT KMD A/S 2004');                               
 /********************************************************************* 
 *                                                                    * 
 * FORMÅL : PROGRAMMETS FORMÅL ER AT OPSÆTTE KODE FOR HELÅRSBEBOELSE  * 
 *          HVIS AKTUEL EJER ER TILMELDT EJENDOMMENS ADRESSE JVF.     * 
 *          P-DATA OG  E N T E N  HAR DISPENSATION TIL HELÅRSBEBOELSE * 
 *          IFLG. BBR-REGISTRET  E L L E R  ER PENSIONIST, EFTERLØNS- * 
 *          MODTAGER, FØRTIDS-/INVALIDE-PENSIONIST, ENKE EFTER EN AF  * 
 *          DISSE SAMT HAR EJET EJENDOMMEN I MINDST 8 ÅR JÆVNFØR      * 
 *          DATO0107 PR. 31/12 I AKTUELT ÅR FOR SLUT- ELLER FORSKUDS- * 
 *          BEREGNINGEN AF EVS.                                       * 
 *                                                                    * 
 * INPUT  : B.H94110D  UDTRÆK (KUN BK 08,28 ELLER 48)                 * 
 *                                                                    * 
 * OUTPUT : B.H94200D  BEHANDLET ESR-UDTRÆK                           * 
 *          BH93301Y   KONTROLLISTE                                   * 
 *          BH93302Y   FEJLLISTE FRA MASKINEL AFSTEMNING              * 
 *                                                                    * 
 *    OBS.  ÅRSRULLES KUN I ULIGE ÅR.                    SØG 'RUL'    * 
 *                                                                    * 
 * PROGRAMMØR:    AAGE RASMUSSEN   AUG  1999                          * 
 * SLUT06         BO SCHMIDT                        04-10-2006        * 
 *                TILRETTET SLUT 2006                                 * 
 * FORSKUD08      BO SCHMIDT                        23-05-2007        * 
 *                TILRETTET FORSKUD 2008                              * 
 * SLUT07         BO SCHMIDT                        19-11-2007        * 
 *                TILRETTET SLUT 2007                                 * 
 * FORSKUD09      BO SCHMIDT                        23-06-2008        * 
 *                TILRETTET FORSKUD 2009                              * 
 * SLUT08         BO SCHMIDT                        19-11-2008        * 
 *                TILRETTET SLUT 2008                                 * 
 * FORSKUD10      GERD HOLM-PEDERSEN                20-04-2009        * 
 *                TILRETTET FORSKUD 2010, RUL                         * 
 * SLUT09         GERD HOLM-PEDERSEN                13-10-2009        * 
 *                TILRETTET SLUT 2009                                 * 
 * FORSKUD11      GERD HOLM-PEDERSEN                08-05-2009        * 
 *                TILRETTET FORSKUD 2011, RUL                         * 
 * SLUT10         GERD HOLM-PEDERSEN                08-10-2010        * 
 *                TILRETTET SLUT 2010                                 * 
 * FORSKUD12      GERD HOLM-PEDERSEN                08-04-2011        * 
 *                TILRETTET FORSKUD 2012, RUL                         * 
 *                SAMT ÆNDRET TÆLLERNE FKUN OG NOGET TEKST TIL AT     * 
 *                VÆRE ÅRSUAFHÆNGIGE.                                 * 
 *                FKUN2011 -> FKUN_SIDSTE_HELÅR                       * 
 *                FKUN2012 -> FKUN_AKT_HELÅR                          * 
 * SLUT11         GERD HOLM-PEDERSEN                22-09-2011        * 
 *                TILRETTET SLUT 2011                                 * 
 * FORSKUD13      LARS KAAE HARRITSHØJ              30-05-2012        * 
 *                TILRETTET FORSKUD 2013, RUL                         * 
 *                BH90000N => BH90100N                                * 
 *                ÆNDRINGER FORETAGET I BH90100N                      * 
 * SLUT12         LARS KAAE HARRITSHØJ                SEP 2012        * 
 *                TILRETTET SLUT 2012                                 * 
 *                BH90100N => BH90000N                                * 
 * FORSKUD14      LARS KAAE HARRITSHØJ                MAJ 2013        * 
 *                TILRETTET FORSKUD 2014, RUL                         * 
 *                BH90000N => BH90100N                                * 
 * SLUT13         JØRGEN SAUGMANN                     NOV 2013        * 
 *                TILRETTET SLUT 2013                                 * 
 *                BH90100N => BH90000N                                * 
 * Årsrullet :    JNK                             #SLUT-15  Dec. 2015 * 
 *********************************************************************/ 
                                                                        
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
                                                                        
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
                                                                        
 DCL     EXECPARM        CHAR      (6) VAR;                             
                                                                        
 DCL     EX_PARM         CHAR      (4);                                 
                                                                        
         EX_PARM = EXECPARM;                                            
                                                                        
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
                                                                        
 DCL     BH931I  FILE RECORD INPUT;          /* EJENDOMSUDTRÆK       */ 
                                                                        
 DCL     BH933O  FILE RECORD OUTPUT;         /* ESR-UDTRÆK BEHANDLET */ 
                                                                        
 /********************************************************************* 
 *       DECLARE AF UDTRÆK                                            * 
 *********************************************************************/ 
                                                                        
 DCL     BH931_AR        CHAR      (6000) VAR;                          
 DCL     1 BH931         BASED     (ADDR(BH931_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
                                                                        
 /*RUL*/ %INCLUDE BH90000N;;    /* DENNE STRUKTUR NÅR KØRSEL=SLUT15  */ 
 /*RUL*//*  %INCLUDE BH90100N;;    DENNE STRUKTUR NÅR KØRSEL=FORSKUD */ 
                                                                        
 DCL     1 BH93199       BASED     (ADDR(BH931_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;                                           
                                                                        
 DCL     1 BH93399,                                                     
           %INCLUDE BH90099N;                                           
                                                                        
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
                                                                        
 DCL     1 TV,                   /* TÆLLEVÆRKER TIL TOTALLISTEN */      
           2 LÆS_BH931   FIXED DEC (9),                                 
           2 SKREVNE     FIXED DEC (9),                                 
           2 FL_EJER     FIXED DEC (9),                                 
           2 FKUN_SIDSTE_ÅR FIXED DEC (9),                              
           2 FKUN_AKT_ÅR FIXED DEC (9),                                 
           2 FBEGGE      FIXED DEC (9);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. FÆLLESMACROER (EVS SLUT/FORSKUD)     * 
 *********************************************************************/ 
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH90700M;                                           
                                                                        
 /********************************************************************* 
 *       DECLARE AF HJÆLPEFELTER TIL MASKINEL AFSTEMNING              * 
 *********************************************************************/ 
                                                                        
 DCL     OPTALT          FIXED     (7)       INIT      (0);             
 DCL     SWFEJL          CHAR      (1)       INIT      (' ');           
                                                                        
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
                                                                        
 DCL     EOF_BH931       BIT       (1);                                 
                                                                        
 DCL     CHELÅRBBR       CHAR      (1);   /* HELÅRSSOMMERHUS JVF. BBR?*/
 DCL     CHELÅRESR       CHAR      (1);   /* HELÅRSSOMMERHUS JVF. ESR?*/
                                                                        
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DAG         CHAR      (02);                                
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
                                                                        
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
                                                                        
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
                                                                        
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /********************************************************************/ 
 /*      DECLARE AF PRINTLINIER                                      */ 
 /********************************************************************/ 
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH93300L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (                
           'BEHANDLING AF EVT. HELÅRSBEBOEDE SOMMERHUSE   '),           
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO,                                                      
             3 DAG         CHAR      (02),                              
             3 F1          CHAR      (01) INIT ('.'),                   
             3 MD          CHAR      (02),                              
             3 F2          CHAR      (01) INIT ('.'),                   
             3 ÅR          CHAR      (04);                              
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN0,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (41),                                
           2 CPUÅR       PIC       '99',                                
           2 TEKST2      CHAR      (11),                                
           2 START       PIC       '999999',                            
           2 TEKST3      CHAR      (9),                                 
           2 SLUT        PIC       '999999',                            
           2 TEKST4      CHAR      (57);                                
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         EOF_BH931 = '0'B;                                              
                                                                        
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
                                                                        
         OPEN FILE (BH931I)  TITLE ('BH93110I');                        
         OPEN FILE (BH933O)  TITLE ('BH93300O');                        
                                                                        
         ON ENDFILE (BH931I)                                            
            EOF_BH931 = '1'B;                                           
                                                                        
         CALL ZZ20390 ('*OPEN','BH93301Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         OVERLIN1.DATO = DATO2, BY NAME;                                
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
         TV = 0;                                                        
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH931;                                                
                                                                        
         DO WHILE ( ^EOF_BH931 );                                       
            CALL BEHANDL;                                               
            CALL LÆS_BH931;                                             
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS UDTRÆK FRA ESR                                           * 
 *********************************************************************/ 
                                                                        
 LÆS_BH931:                                                             
         PROC;                                                          
                                                                        
         READ FILE (BH931I) INTO (BH931_AR);                            
                                                                        
         IF BH93199.SORT_CPR = 99999999999    /* OPTÆLLINGSINDIVID */   
         THEN                                                           
            DO;                                                         
               OPTALT = BH93199.ANT_INDV;                               
               READ FILE (BH931I) INTO (BH931_AR);                      
            END;                                                        
                                                                        
         IF ^EOF_BH931 THEN                                             
         DO;                                                            
            CALL FIND_KØRSELSTYPE(LENGTH(BH931_AR));                    
            TV.LÆS_BH931 = TV.LÆS_BH931 + 1;                            
         END;                                                           
                                                                        
 END LÆS_BH931;                                                         
 /********************************************************************* 
 *       PERSONER SOM EVT. SKAL BEHANDLES/BEREGNES                    * 
 *********************************************************************/ 
 BEHANDL:                                                               
         PROC;                                                          
                                                                        
         IF BH931.AKT_VURD.CBENYT = '08' !                              
            BH931.AKT_VURD.CBENYT = '28' !                              
            BH931.AKT_VURD.CBENYT = '48' !                              
           (BH931.AKT_VURD.CBENYT = '25' &                              
           (BH931.ESR.CEANV = '510' !                                   
            BH931.ESR.CEANV = '520'))                                   
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               /* FORKERT INPUT TIL KØRSLEN !!! */                      
               CALL ZS30101(4,'BH93300 ** 251 ' !!                      
               'FORKERT INPUT BENYTTET (=B.H94110D?)',2);               
            END;                                                        
                                                                        
         BH931.C_SÆLGER   = '0';                                        
         BH931.CHELÅR     = '0';                                        
         BH931.CHELÅR_FOR = '0';                                        
         BH931.CHELÅR_ORD = '0';                                        
         CHELÅRBBR        = '0';                                        
         CHELÅRESR        = '0';                                        
                                                                        
    /*         SALG FØR INDKOMSTÅRET SPRINGES OVER.    Begynd        */ 
         IF (BH931.AKT_EJER.DAFSTÅ > 0 &                                
             BH931.AKT_EJER.DAFSTÅ < BH931.AKT_EJER.DRGNÅRFRA)          
         THEN                                                           
            DO;                                                         
               CALL SKRIV_UDTRÆK;                                       
               RETURN;                                                  
            END;                                                        
                                                                        
         IF (BH931.TIL_EVS.DAFSTAA > 0 &                                
             BH931.TIL_EVS.DAFSTAA < BH931.MT.DRGNÅRFRA)                
         THEN                                                           
            DO;                                                         
               CALL SKRIV_UDTRÆK;                                       
               RETURN;                                                  
            END;  /*                                     Slut        */ 
                                                                        
         IF BH931.AKT_EJER.BOPÆLSKODE > '0'                             
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
    /* EJEREN BOR IKKE PÅ ADRESSEN JVF. P-DATA.   INGEN BEHANDLING!  */ 
               CALL SKRIV_UDTRÆK;                                       
               RETURN;                                                  
            END;                                                        
                                                                        
         /* INITIER HJÆLPEFELTER */                                     
                                                                        
         /*************************************************************/
         /* HEREFTER BLIVER FØLGENDE 4 KODER EVT. OPSAT.              */
         /*                                                           */
         /* C_SÆLGER : 0 BETYDER PERSONEN ER IKKE EN SÆLGER           */
         /*          : 1 BETYDER PERSONEN ER EN SÆLGER                */
         /*                                                           */
         /* CHELÅR:                                                   */
         /* 0 BETYDER OPFYLDER IKKE KRAV TIL 2% BEREGNING             */
         /* 1 KUN SIDSTE_ÅR OPFYLDER KRAV MEN IKKE JVF. BBR           */
         /* 2 KUN SIDSTE_ÅR OPFYLDER KRAV MEN KUN JVF. BBR            */
         /* 3 KUN SIDSTE_ÅR OPFYLDER KRAV BÅDE JVF. BBR OG 8 ÅRS      */
         /* 4 KUN AKT_ÅR OPFYLDER KRAV MEN IKKE JVF. BBR              */
         /* 5 KUN AKT_ÅR OPFYLDER KRAV MEN KUN JVF. BBR               */
         /* 6 KUN AKT_ÅR OPFYLDER KRAV BÅDE JVF. BBR OG 8 ÅRS         */
         /* 7 SIDSTE_ÅR+AKT_ÅR OPFYLDER KRAV MEN IKKE JVF. BBR        */
         /* 8 SIDSTE_ÅR+AKT_ÅR OPFYLDER KRAV MEN KUN JVF. BBR         */
         /* 9 SIDSTE_ÅR+AKT_ÅR OPFYLDER KRAV JVF BBR OG/ELLER 8 ÅRS   */
         /*                                                           */
         /* CHELÅR_ORD                                                */
         /* OG                                                        */
         /* CHELÅR_FOR : 0 BETYDER OPFYLDER IKKE EFTERFØLGENDE KRAV:  */
         /* 0 BETYDER OPFYLDER IKKE EFTERFØLGENDE KRAV:               */
         /* 1 OPFYLDER MINDST 1 AF BETINGELSERNE 67 ÅR                */
         /* 2 OPFYLDER MINDST 1 AF BETINGELSERNE 60-66 ÅR             */
         /* 3 OPFYLDER MINDST 1 AF BETINGELSERNE < 60 ÅR              */
         /* 4 DISPENSATION OVERFØRT FRA ANDEN EJER (OPSÆT-            */
         /*   TES EVT. I BH64400-SAMKØRING AF ALLE EJERE).            */
         /*                                                           */
         /*************************************************************/
                                                                        
         CALL TEST_BBROPLYS;                                            
                                                                        
         CALL OPSÆT_CHELÅR; /* OPFYLDES BBR OG/ELLER 8 ÅR'S REGEL */    
                                                                        
         CALL SKRIV_UDTRÆK;                                             
                                                                        
 END BEHANDL;                                                           
 /*********************************************************************/
 /*      OPSÆT EVT. 2 PCT BEREGNINGSKODE P.G.A. BBRF379 OG BBRF380    */
 /*********************************************************************/
 TEST_BBROPLYS:                                                         
         PROC;                                                          
                                                                        
         IF BH931.BBRF379 < 'A'         /* HVIS INGEN DISPENSATION    */
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BH931.BBRF380 > '0000' &    /* HVIS UDLØBET DISPENSATION  */
            BH931.BBRF380 < INDKÅR1     /* FØR AKTUELT INDKOMSTÅR     */
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BH931.BBRF380 = INDKÅR1     /* HVIS GÆLDER KUN TIL OG     */
         THEN                           /* MED AKTUELT INDKOMSTÅR     */
            CHELÅRBBR = '1';                                            
                                                                        
         IF BH931.BBRF380 <= '0000' !   /* HVIS GÆLDER 'BEGGE' ÅR     */
            BH931.BBRF380 > INDKÅR1                                     
         THEN                                                           
            CHELÅRBBR = '3';                                            
                                                                        
 END TEST_BBROPLYS;                                                     
                                                                        
 /**********************************************************************
 *   FØRST OPSÆTTES CHELÅR_ORD OG CHELÅR_FOR OG DEREFTER               *
 *   UNDERSØGES OM PERSONEN HAR EJET EJENDOMMEN I MINDST 8 ÅR          *
 *   PR. 31.12 I AKTUELT INDKOMSTÅR OG SAMTIDIG OPFYLDER EN            *
 *   AF BETINGELSERNE FOR HELÅRSBEBOELSE.                              *
 *   CHELÅR OPSÆTTES UDFRA 8 ÅRS REGLEN OG/ELLER DISP. JVF. BBR.       *
 **********************************************************************/
 OPSÆT_CHELÅR:                                                          
         PROC;                                                          
                                                                        
    /*          FØRST LAVES OPSÆTNING FOR AKTUELT SLUTÅR       */       
         IF BH931.TIL_EVS.CPTYPSLUT = '3'                               
         THEN                                                           
            CHELÅR_ORD = '1';                            /* 67-ÅRIGE */ 
         ELSE                                                           
            IF BH931.TIL_EVS.CPTYPSLUT = '2'                            
            THEN                                                        
               CHELÅR_ORD = '2';             /* FØRTIDSPENS/EFTERLØN */ 
                                                                        
    /*      SÅ LAVES EVT. OPSÆTNING FOR AKTUELT FORSKUDSÅR     */       
         CHELÅR_FOR = '0';                                              
                                                                        
         IF BH931.TIL_EVS.CPTYPFOR = '3'                                
         THEN                                                           
            CHELÅR_FOR = '1';                            /* 67-ÅRIGE */ 
         ELSE                                                           
            IF BH931.TIL_EVS.CPTYPFOR = '2'                             
            THEN                                                        
               CHELÅR_FOR = '2';             /* FØRTIDSPENS/EFTERLØN */ 
                                                                        
         /* FØRST UNDERSØGES AKTUELT INDKOMSTÅR */                      
                                                                        
         IF TEST_EJET_8ÅR_OK(BH931.TIL_EVS.DATO0107,HJ_DATO1) &         
            BH931.CHELÅR_ORD > '0'                                      
         THEN                                                           
  /*???*/   CHELÅRESR = '1';               /*RUL*/  /* 8 ÅR OK I 2015 */
         ELSE                                                           
  /*???*/   CHELÅRESR = '0';           /*RUL*/ /* 8 ÅR IKKE OK I 2015 */
                                                                        
         /* SÅ UNDERSØGES AKTUELT FORSKUDSÅR */                         
                                                                        
         IF TEST_EJET_8ÅR_OK(BH931.TIL_EVS.DATO0107,HJ_DATO2) &         
            BH931.CHELÅR_FOR > '0'                                      
         THEN                                                           
            DO;                                                         
              IF CHELÅRESR = '0'                                        
              THEN                                                      
  /*???*/        CHELÅRESR = '2';        /*RUL*//* 8 ÅR OK KUN I 2016 */
              ELSE                                                      
  /*???*/        CHELÅRESR = '3';   /*RUL*//* 8 ÅR OK BÅDE I 2015+2016*/
            END;                                                        
                                                                        
      /*       SÅ OPSÆTTES BH931.CHELÅR P.G.A. AF OM BBR       */       
      /*       OG/ELLER 8 ÅRS REGEL GIVER DISPENSATION.        */       
         SELECT;                                                        
           WHEN (CHELÅRBBR = '0' & CHELÅRESR = '1')                     
             DO;                                                        
               BH931.CHELÅR= '1';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '0' & CHELÅRESR = '2')                     
             DO;                                                        
               BH931.CHELÅR= '4';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '0' & CHELÅRESR = '3')                     
             DO;                                                        
               BH931.CHELÅR= '7';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '1' & CHELÅRESR = '0')                     
             DO;                                                        
               BH931.CHELÅR= '2';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '1' & CHELÅRESR = '1')                     
             DO;                                                        
               BH931.CHELÅR= '3';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '1' & CHELÅRESR = '2')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '1' & CHELÅRESR = '3')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '2' & CHELÅRESR = '0')                     
             DO;                                                        
               BH931.CHELÅR= '5';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '2' & CHELÅRESR = '1')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '2' & CHELÅRESR = '2')                     
             DO;                                                        
               BH931.CHELÅR= '6';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '2' & CHELÅRESR = '3')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '3' & CHELÅRESR = '0')                     
             DO;                                                        
               BH931.CHELÅR= '8';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '3' & CHELÅRESR = '1')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '3' & CHELÅRESR = '2')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           WHEN (CHELÅRBBR = '3' & CHELÅRESR = '3')                     
             DO;                                                        
               BH931.CHELÅR= '9';                                       
             END;                                                       
           OTHERWISE                                                    
               BH931.CHELÅR= '0';                                       
         END;                                                           
                                                                        
         IF BH931.CHELÅR > '0' &                                        
            BH931.AKT_EJER.DAFSTÅ > 0 &                                 
           (BH931.AKT_EJER.DAFSTÅ >= BH931.AKT_EJER.DRGNÅRFRA &         
            BH931.AKT_EJER.DAFSTÅ <= BH931.AKT_EJER.DRGNÅRTIL)          
         THEN                                                           
         /*     HVIS EJEREN HAR SOLGT EJENDOMMEN I INDKOMSTÅRET    */   
            BH931.C_SÆLGER = '1';                                       
         ELSE                                                           
            BH931.C_SÆLGER = '0';                                       
                                                                        
 END OPSÆT_CHELÅR;                                                      
                                                                        
 /********************************************************************* 
 *       HER SKRIVES UDTRÆK EFTER EVT. BEHANDLING.                    * 
 *       A L L E  LÆSTE INDIVIDER SKRIVES IDET EVT. AFLUSNING AF IKKE * 
 *       BEHANDLEDE INDIVIDER UDELUKKENDE SKAL SKE I FORBINDELSE MED  * 
 *       BEREGNING AF EVS TIL SLUT ELLER FORSKUD.                     * 
 *********************************************************************/ 
 SKRIV_UDTRÆK:                                                          
         PROC;                                                          
                                                                        
         IF FIND_KØRSELSTYPE(0) = 'EVSSLUT'                             
         THEN                                                           
            CALL TILPAS_SLUT_CHELÅR;                                    
                                                                        
         IF BH931.CHELÅR > '0' &                                        
            BH931.AKT_EJER.FEJER > 1                                    
         THEN                                                           
            TV.FL_EJER = TV.FL_EJER + 1;                                
                                                                        
         IF BH931.CHELÅR >= '1' &                                       
            BH931.CHELÅR <= '3'                                         
         THEN                                                           
            TV.FKUN_SIDSTE_ÅR = TV.FKUN_SIDSTE_ÅR + 1;                  
                                                                        
         IF BH931.CHELÅR >= '4' &                                       
            BH931.CHELÅR <= '6'                                         
         THEN                                                           
            TV.FKUN_AKT_ÅR = TV.FKUN_AKT_ÅR + 1;                        
                                                                        
         IF BH931.CHELÅR >= '7' &                                       
            BH931.CHELÅR <= '9'                                         
         THEN                                                           
            TV.FBEGGE = TV.FBEGGE + 1;                                  
                                                                        
         BH931.SORT_SALG  = BH931.C_SÆLGER;                             
         BH931.SORT_HELÅR = BH931.CHELÅR;                               
                                                                        
         WRITE FILE (BH933O) FROM (BH931_AR);                           
                                                                        
         TV.SKREVNE = TV.SKREVNE + 1;                                   
                                                                        
 END SKRIV_UDTRÆK;                                                      
 /********************************************************************* 
 *       KØRSLEN VEDR. BEREGNING TIL SLUT                             * 
 *       DERFOR TILPASSES CHELÅR-KODEN.                               * 
 *********************************************************************/ 
 TILPAS_SLUT_CHELÅR:                                                    
         PROC;                                                          
                                                                        
         SELECT (BH931.CHELÅR);                                         
           WHEN ('4','5','6')                                           
             DO;                                                        
                BH931.CHELÅR = '0';                                     
             END;                                                       
           WHEN ('7')                                                   
             DO;                                                        
                BH931.CHELÅR = '1';                                     
             END;                                                       
           WHEN ('8')                                                   
             DO;                                                        
                BH931.CHELÅR = '2';                                     
             END;                                                       
           WHEN ('9')                                                   
             DO;                                                        
                BH931.CHELÅR = '3';                                     
             END;                                                       
           OTHERWISE;                                                   
         END;                                                           
                                                                        
 END TILPAS_SLUT_CHELÅR;                                                
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN0.CCA = ZZWS2;                                           
         DETLIN0.TEKST1 = 'DENNE BEREGNING ER UDFØRT FOR INDKOMSTÅR=';  
         DETLIN0.CPUÅR = BH65300M.SLUTÅR;                               
         DETLIN0.TEKST2 = ' MED START=';                                
         DETLIN0.START = ÅR0101;                                        
         DETLIN0.TEKST3 = ' OG SLUT=';                                  
         DETLIN0.SLUT  = ÅR1231;                                        
         DETLIN0.TEKST4 = ' ';                                          
         CALL ZZ20300 (DETLIN0,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BH931;                                  
         DETLIN1.TEKST = 'ANTAL LÆSTE PÅ UDTRÆK FRA ESR     B.H94110D'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKREVNE;                                    
         DETLIN1.TEKST = 'ANTAL SKREVNE/BEHANDLET PÅ        B.H94200D'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.FL_EJER;                                    
         DETLIN1.TEKST = 'HERAF 2%-BEREGNING MED FLERE END 1 EJER    '; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FKUN_SIDSTE_ÅR;                             
  /*RUL*/DETLIN1.TEKST = 'BEREGNET MED 2% KUN I 2015           ANTAL:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FKUN_AKT_ÅR;                                
         DETLIN1.TEKST = 'BEREGNET MED 2% KUN I 2016           ANTAL:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FBEGGE;                                     
  /*RUL*/DETLIN1.TEKST = 'BEREGNET MED 2% BÅDE I 2015 OG 2016  ANTAL:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
  END TOTALLISTE;                                                       
 /********************************************************************* 
 *       MASKINEL AFSTEMNING                                          * 
 *********************************************************************/ 
 MASK_AFSTEMNING:                                                       
         PROC;                                                          
         IF BH93199.SORT_KOMNR ^= 999                                   
         THEN                                                           
            CALL FEJLLISTE('1');                                        
         IF OPTALT ^= TV.LÆS_BH931 &                                    
            EX_PARM ^= 'TEST'                                           
         THEN                                                           
            CALL FEJLLISTE('2');                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      UDSKRIV FEJLLISTE VEDR. KONSTATERET AFSTEMNINGFEJL PÅ ???    */
 /*********************************************************************/
 FEJLLISTE:                                                             
         PROC(FEJL);                                                    
 DCL     FEJL            CHAR      (01);                                
         CALL ZZ20390 ('*OPEN','BH93302Y');                             
         CALL ZZ20300(SIDELIN,'02');                                    
         CALL ZZ20300(OVERLIN1,'02');                                   
         OVERLIN2.CCA = ZZWS3;                                          
         OVERLIN2.TEKST1 = 'FEJLLISTE VEDR. MASKINEL AFSTEMNING';       
         CALL ZZ20300(OVERLIN2,'02');                                   
         IF FEJL = '1'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST =                                          
               'AFSTEMNINGSINDIVID MANGLER PÅ B.H94110D';               
               DETLIN1.ANTAL = '';                                      
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
         IF FEJL = '2'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST =                                          
               'ANTAL LÆSTE INDIVIDER PÅ B.H94110D';                    
               DETLIN1.ANTAL = TV.LÆS_BH931;                            
               CALL ZZ20300(DETLIN1,'02');                              
               DETLIN1.TEKST  =                                         
               'ANTAL INDIVIDER IFLG. AFSTEMNINGSINDIVID IALT  :';      
               DETLIN1.ANTAL = OPTALT;                                  
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
            SWFEJL = '1';                                               
 END FEJLLISTE;                                                         
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT:                                                                
         PROC;                                                          
         BH93399 = '';                                                  
         BH93399.SORT_CPR   = 99999999999;                              
         BH93399.SORT_KOMNR = 999;                                      
         BH93399.SORT_EJDNR = 9999999;                                  
         BH93399.ANT_INDV = TV.SKREVNE;                                 
         WRITE FILE (BH933O) FROM (BH93399);                            
         CALL TOTALLISTE;                                               
         CALL MASK_AFSTEMNING;                                          
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH931I),                                           
               FILE (BH933O);                                           
         IF SWFEJL = '1'                                                
         THEN                                                           
            CALL ZS30101(4,'BH93300 ** 252 ' !!                         
                 'MASKINEL KONSTATERET AFSTEMNINGSFEJL',2);             
 END AFSLUT;                                                            
                                                                        
 %INCLUDE BH94200M;     /* UNDERSØG OM EJENDOM EJET I MINDST 8 ÅR */    
 %INCLUDE BH94201M;     /* FIND OM DET ER FORSKUD ELLER EVSSLUT   */    
                                                                        
 END  BH93300;                                                          
