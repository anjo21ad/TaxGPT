 /**********************************************************************
 * MACRO  BE24907M  REF. AKTIVITETSBESKRIVELSE EVS SLUT                *
 *                                                                     *
 * MACROEN BENYTTES I FORBINDELSE MED BEREGNING AF KMD DEL AF          *
 * EJENDOMSVÆRDISKAT TIL SLUT                                          *
 *                                                                     *
 * NB.: E F T E R  KALD AF MACRO BQ11905M.                             *
 * SLUT06     BO SCHMIDT                                  06-10-2009   *
 *            TILRETNING TIL SLUT 2009                                 *
 * SLUT10     GERD HOLM-PEDERSEN                          10-10-2010   *
 *            TILRETNING TIL SLUT 2010                                 *
 * SLUT11     GERD HOLM-PEDERSEN                          20-10-2011   *
 *            TILRETNING TIL SLUT 2011                                 *
 * SLUT12     LARS KAAE HARRITSHØJ                        SEP 2012     *
 *            TILRETNING TIL SLUT 2012                                 *
 * SLUT13     JØRGEN SAUGMANN                             OKT 2013     *
 *            TILRETNING TIL SLUT 2013                                 *
 *            JØRGEN SAUGMANN                           JAN 2014       *
 *            INDLÆGGELSE AF EU LOVGIVNING VEDR. BEGRÆNSET SKATTEPLIGT *
 *            OG DOBBELTDOMICILERING FOR PENSIONSTER    / SØG #01      *
 * SLUT14     JØRGEN SAUGMANN                             OKT 2014     *
 *            TILRETNING TIL SLUT 2014                                 *
 * Årsrullet: Kopi af BQ14907M (søg   *RUL*)             JNK Okt. 2015 *
 * Årsrullet: Kopi af BQ15907M (søg   *RUL*)             QLE Juni 2016 *
 * Årsrullet: Kopi af BQ16907M (søg   *RUL*)             QEB Sep  2017 *
 * Årsrullet: Kopi af BQ17907M (søg   *RUL*)             QLE Juni 2018 *
 * Årsrullet: Kopi af BQ18907M (søg   *RUL*)             EBD Juni 2018 *
 *            Nye felter pga af 2familiehuse                           *
 * Årsrullet: Kopi af BQ19907M (søg   *RUL*)             PBR Okt. 2020 *
 * Årsrullet: Kopi af BQ20907M (søg   *RUL*)             PBR Okt. 2021 *
 * Årsrullet: Kopi af BQ21907M (søg   *RUL*)             PBR Okt. 2022 *
 * Årsrullet: Kopi af BQ22907M BQ --> BE                 HNK Okt. 2024 *
 * SLUT 2024: -Fjernet Stigningsbegrænsning              TIC Okt. 2023 *
 *            -Nye Satser for 2024                                     *
 *            -Beregning af Taksation                                  *
 **********************************************************************/
 BEREGN_EVS_KMD_DEL: PROC (INPSTRUK);                                   
                                                                        
 /********************************************************************* 
 *       DECLARE AF DIVERSE                                           * 
 *********************************************************************/ 
 DCL     1 INPSTRUK,                                                    
           %INCLUDE BE24907N;;                                  /*RUL*/ 
                                                                        
 DCL     HJVÆRDI1        FIXED     (13,3);                              
 DCL     HJ_PCT          FIXED     (5,4);                               
 DCL     MULTIPLY        BUILTIN;                                       
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
                                                                        
         /* INITIER DIVERSE FELTER INDEN BEREGNING */                   
         INPSTRUK.FRA.BGRPROM1 = 0;                                     
         INPSTRUK.FRA.BGRPROM1_SW = -1;                                 
         INPSTRUK.FRA.BGRPROM2 = 0;                                     
         INPSTRUK.FRA.BGRPROM2_SW = -1;                                 
         INPSTRUK.FRA.BPROM1 = 0;                                       
         INPSTRUK.FRA.BPROM1_SW = -1;                                   
         INPSTRUK.FRA.BPROM2 = 0;                                       
         INPSTRUK.FRA.BPROM2_SW = -1;                                   
         INPSTRUK.FRA.BNEDSL1 = 0;                                      
         INPSTRUK.FRA.BNEDSL1_SW = -1;                                  
         INPSTRUK.FRA.CNEDSL2 = ' ';                                    
         INPSTRUK.FRA.BNEDSL2 = 0;                                      
         INPSTRUK.FRA.BNEDSL2_SW = -1;                                  
         INPSTRUK.FRA.BPENSNED = 0;                                     
         INPSTRUK.FRA.BPENSNED_SW = -1;                                 
         INPSTRUK.FRA.BPENSNÆ = 0;                                      
         INPSTRUK.FRA.BPENSNÆ_SW = -1;                                  
         INPSTRUK.FRA.BSUM1 = 0;                                        
         INPSTRUK.FRA.BSUM1_SW = -1;                                    
         INPSTRUK.FRA.BSUM2 = 0;                                        
         INPSTRUK.FRA.BSUM2_SW = -1;                                    
         INPSTRUK.FRA.BSUM3 = 0;                                        
         INPSTRUK.FRA.BSUM3_SW = -1;                                    
         INPSTRUK.FRA.BNETTO = 0;                                       
         INPSTRUK.FRA.BNETTO_SW = -1;                                   
         INPSTRUK.FRA.BTAKS         = 0;                                
         INPSTRUK.FRA.BTAKSRABAT    = 0;                                
         INPSTRUK.FRA.BTAKSEFTRABAT = 0;                                
                                                                        
         IF INPSTRUK.TIL.CNEDSL1 >= '10' &                              
            INPSTRUK.TIL.CNEDSL1 <= '29'                                
         THEN                                                           
            DO;                                                         
               /* Opsæt kode for 4 promille 1/7-nedslag */              
               SELECT;                                                  
                 WHEN (INPSTRUK.TIL.FELT760      = '1')                 
                   INPSTRUK.FRA.CNEDSL2      = '3';                     
                                                                        
                 /* Udvidet behandling til 2 familiehuse */             
                 WHEN (INPSTRUK.TIL.CEJDTYPE_AKT = '4')                 
                   DO;                                                  
                     IF  INPSTRUK.TIL.CEJBEVS = '0' !                   
                         INPSTRUK.TIL.CEJBEVS = '4'     /* ?*/          
                     THEN                                               
                       DO;                                              
                          IF INPSTRUK.TIL.CEJBEVS_ANDEN_ENHED = 'C'     
                          THEN                                          
                            INPSTRUK.FRA.CNEDSL2      = '4';            
                          ELSE                                          
                            INPSTRUK.FRA.CNEDSL2      = '5';            
                       END;                                             
                     IF  INPSTRUK.TIL.CEJBEVS = 'B' !                   
                         INPSTRUK.TIL.CEJBEVS = '7'                     
                     THEN                                               
                       INPSTRUK.FRA.CNEDSL2      = '0';                 
                   END;                                                 
                                                                        
                 WHEN (INPSTRUK.TIL.CEJDTYPE_AKT = '5')                 
                   INPSTRUK.FRA.CNEDSL2      = '4';                     
                                                                        
                 WHEN (INPSTRUK.TIL.CEJDTYPE_AKT = '7' !                
                       INPSTRUK.TIL.CEJDTYPE_AKT = '8')                 
                   INPSTRUK.FRA.CNEDSL2      = '2';                     
                                                                        
                 OTHERWISE                                              
                   INPSTRUK.FRA.CNEDSL2 = '1';                          
               END;                                                     
            END;                                                        
                                                                        
         IF INPSTRUK.TIL.BBERGRL > 9200000                              
         THEN                                                           
            DO;                                                         
              INPSTRUK.FRA.BGRPROM1    = 9200000;                       
              INPSTRUK.FRA.BGRPROM1_SW = 0;                             
              INPSTRUK.FRA.BGRPROM2    = INPSTRUK.TIL.BBERGRL - 9200000;
              INPSTRUK.FRA.BGRPROM2_SW = 0;                             
            END;                                                        
         ELSE                                                           
            DO;                                                         
              INPSTRUK.FRA.BGRPROM1    = INPSTRUK.TIL.BBERGRL;          
              INPSTRUK.FRA.BGRPROM1_SW = 0;                             
            END;                                                        
                                                                        
         /* Beregn Ejendomsværdiskat 5,1 promille */                    
         INPSTRUK.FRA.BPROM1 =                                          
                  MULTIPLY(INPSTRUK.FRA.BGRPROM1,0.0051,11,2); /*D882*/ 
         INPSTRUK.FRA.BPROM1_SW = 0;                                    
                                                                        
         /* Beregn Ejendomsværdiskat 14 promille */                     
         IF INPSTRUK.FRA.BGRPROM2_SW = 0                                
         THEN                                                           
           DO;                                                          
            INPSTRUK.FRA.BPROM2    =                                    
                     MULTIPLY(INPSTRUK.FRA.BGRPROM2,0.014,11,2);        
            INPSTRUK.FRA.BPROM2_SW = 0;                                 
           END;                                                         
                                                                        
         IF INPSTRUK.TIL.CNEDSL1 >= '10' &                              
            INPSTRUK.TIL.CNEDSL1 <= '29'                                
         THEN                                                           
            DO;                                                         
               /* Beregn 1 promille nedslag - ejendom ejet 1/7-98 */    
               HJVÆRDI1 =                                               
               (MULTIPLY(INPSTRUK.TIL.BBERGRL,0.0010,13,3)) + 0.005;    
                                                            /*D882*/    
               INPSTRUK.FRA.BNEDSL1 = HJVÆRDI1;                         
               INPSTRUK.FRA.BNEDSL1_SW = 0;                             
                                                                        
               /* Beregn 2,1 promille nedslag - ejendom ejet 1/7-98 */  
               SELECT (INPSTRUK.FRA.CNEDSL2);                           
                 WHEN ('1')                                             
                   DO;                                                  
                      /* Beregn 2,1 promille nedslag - 1-fam-hus  */    
                      HJVÆRDI1 = (MULTIPLY                              
                      (INPSTRUK.TIL.BBERGRL,0.0021,13,3)) + 0.005;      
                                                          /*D882*/      
                      /* Nedslaget kan max. være 1200,00 kr. */         
                      IF HJVÆRDI1 < 1200.000                            
                      THEN                                              
                         INPSTRUK.FRA.BNEDSL2 = HJVÆRDI1;               
                      ELSE                                              
                         INPSTRUK.FRA.BNEDSL2 = 1200.00;                
                      INPSTRUK.FRA.BNEDSL2_SW = 0;                      
                   END;                                                 
                 WHEN ('4') /* OK  med forskud 2fam krav */             
                   DO;                                                  
                      /* BEREGN 2,1 PROMILLE NEDSLAG - 2-FAM-HUS */     
                      /* MED KUN EEN EJERBOLIGVÆRDI            */       
                      HJVÆRDI1 = (MULTIPLY                              
                      (INPSTRUK.TIL.BBER2FAM,0.0021,13,3)) + 0.005;     
                                                           /*D882*/     
                      /* NEDSLAGET KAN MAX. VÆRE 2400,00 KR. */         
                      IF HJVÆRDI1 < 2400.000                            
                      THEN                                              
                         INPSTRUK.FRA.BNEDSL2 = HJVÆRDI1;               
                      ELSE                                              
                         INPSTRUK.FRA.BNEDSL2 = 2400.00;                
                      INPSTRUK.FRA.BNEDSL2_SW = 0;                      
                   END;                                                 
                 WHEN ('5') /* OK med forskud 2fam krav */              
                   DO;                                                  
                      /* Beregn 2,1 promille nedslag - 2-fam-hus */     
                      /* med 2 ejerboligværdier                */       
                      HJVÆRDI1 = (MULTIPLY                              
                      (INPSTRUK.TIL.BBER2FAM,0.0021,13,3)) + 0.005;     
                                                           /*D882*/     
                      /* Nedslaget kan max. være 1200,00 kr. */         
                      IF HJVÆRDI1 < 1200.000                            
                      THEN                                              
                         INPSTRUK.FRA.BNEDSL2 = HJVÆRDI1;               
                      ELSE                                              
                         INPSTRUK.FRA.BNEDSL2 = 1200.00;                
                      INPSTRUK.FRA.BNEDSL2_SW = 0;                      
                   END;                                                 
                 OTHERWISE; /* INTET NEDSLAG */                         
               END;                                                     
            END;                                                        
                                                                        
         /* Beregn nedslag for 67-årige som  */                         
         /* ikke er begrænset skattepligtige */                         
                                                                        
         IF INPSTRUK.TIL.CPTYPSLUT = '3' !                              
            INPSTRUK.TIL.CPTYPSLUT = '4'                                
         THEN                                                           
           DO;                                                          
              SELECT (INPSTRUK.TIL.FELT759);                            
                                                                        
                WHEN ('1')                                              
                 DO;                                                    
                    INPSTRUK.FRA.BPENSNED    = 6000.00;                 
                    INPSTRUK.FRA.BPENSNED_SW = 0;                       
                                                                        
                 /*  Helårsbolig *                                      
                    HJVÆRDI1 = (MULTIPLY                                
                    (INPSTRUK.TIL.BBERGRL,0.0021,13,3)) + 0.005;        
                                                            *D882*      
                     * Nedslaget kan max. være 6000,00 kr. *            
                    IF HJVÆRDI1 < 6000.000                              
                    THEN                                                
                       INPSTRUK.FRA.BPENSNED = HJVÆRDI1;                
                    ELSE                                                
                       INPSTRUK.FRA.BPENSNED = 6000.00;                 
                    INPSTRUK.FRA.BPENSNED_SW = 0;                       
                 */                                                     
                 END;                                                   
                                                                        
                WHEN ('2')                                              
                 DO;                                                    
                    INPSTRUK.FRA.BPENSNED    = 2000.00;                 
                    INPSTRUK.FRA.BPENSNED_SW = 0;                       
                                                                        
                 /*  Fritidsbolig *                                     
                    HJVÆRDI1 = (MULTIPLY                                
                    (INPSTRUK.TIL.BBERGRL,0.0037,13,3)) + 0.005;        
                                                            *D882*      
                     * Nedslaget kan max. være 2000,00 kr. *            
                    IF HJVÆRDI1 < 2000.000                              
                    THEN                                                
                       INPSTRUK.FRA.BPENSNED = HJVÆRDI1;                
                    ELSE                                                
                       INPSTRUK.FRA.BPENSNED = 2000.00;                 
                    INPSTRUK.FRA.BPENSNED_SW = 0;                       
                 */                                                     
                 END;                                                   
                                                                        
                OTHERWISE                                               
                 DO;                                                    
                    /* INTET NEDSLAG */                                 
                    INPSTRUK.FRA.BPENSNED = 0;                          
                    INPSTRUK.FRA.BPENSNED_SW = -1;                      
                 END;                                                   
                                                                        
              END; /* SELECT */                                         
           END;                                                         
                                                                        
         /* Beregn sum af ejendomsværdiskat */                          
         /* i n d e n  pensionistnedslag    */                          
                                                                        
         INPSTRUK.FRA.BSUM2 =                                           
                 (INPSTRUK.FRA.BPROM1 + INPSTRUK.FRA.BPROM2) -          
                 (INPSTRUK.FRA.BNEDSL1 + INPSTRUK.FRA.BNEDSL2);         
         INPSTRUK.FRA.BSUM2_SW = 0;                                     
                                                                        
         /* Reguler evt. bnedsl2 så bsum2 ikke bliver negativ */        
         IF INPSTRUK.FRA.BSUM2 < 0                                      
         THEN                                                           
           DO;                                                          
             INPSTRUK.FRA.BNEDSL2 =                                     
                      INPSTRUK.FRA.BNEDSL2 + INPSTRUK.FRA.BSUM2;        
             INPSTRUK.FRA.BSUM2 = 0;                                    
           END;                                                         
                                                                        
         IF INPSTRUK.FRA.BPENSNED > INPSTRUK.FRA.BSUM2                  
         THEN                                                           
            INPSTRUK.FRA.BPENSNED = INPSTRUK.FRA.BSUM2;                 
                                                                        
         IF INPSTRUK.FRA.BPENSNED_SW = 0                                
         THEN                                                           
           DO;                                                          
            INPSTRUK.FRA.BPENSNÆ    = INPSTRUK.FRA.BPENSNED;            
            INPSTRUK.FRA.BPENSNÆ_SW = 0;                                
           END;                                                         
         ELSE                                                           
            IF INPSTRUK.CEJDTYPE_AKT = '3' !                            
               INPSTRUK.CEJDTYPE_AKT = '8'                              
            THEN                                                        
               DO;                                                      
                  /* FRITIDSBOLIG */                                    
                  INPSTRUK.FRA.BPENSNÆ    = 2000.00;                    
                  INPSTRUK.FRA.BPENSNÆ_SW = 0;                          
                                                                        
               /* HJVÆRDI1 =                                            
                  (MULTIPLY(INPSTRUK.TIL.BBERGRL,0.0021,13,3)) + 0.005; 
                                                               *D882*   
                   * NEDSLAGET KAN MAX. VÆRE 2000,00 KR. *              
                  IF HJVÆRDI1 < 2000.000                                
                  THEN                                                  
                     INPSTRUK.FRA.BPENSNÆ = HJVÆRDI1;                   
                  ELSE                                                  
                     INPSTRUK.FRA.BPENSNÆ = 2000.00;                    
                  INPSTRUK.FRA.BPENSNÆ_SW = 0;                          
               */                                                       
               END;                                                     
            ELSE                                                        
               DO;                                                      
                  /* HELÅRSBOLIG */                                     
                  INPSTRUK.FRA.BPENSNÆ    = 6000.00;                    
                  INPSTRUK.FRA.BPENSNÆ_SW = 0;                          
                                                                        
               /* HJVÆRDI1 =                                            
                  (MULTIPLY(INPSTRUK.TIL.BBERGRL,0.0021,13,3)) + 0.005; 
                                                               *D882*   
                   * NEDSLAGET KAN MAX. VÆRE 6000,00 KR. *              
                  IF HJVÆRDI1 < 6000.000                                
                  THEN                                                  
                     INPSTRUK.FRA.BPENSNÆ = HJVÆRDI1;                   
                  ELSE                                                  
                     INPSTRUK.FRA.BPENSNÆ = 6000.00;                    
                  INPSTRUK.FRA.BPENSNÆ_SW = 0;                          
               */                                                       
               END;                                                     
                                                                        
         INPSTRUK.FRA.BSUM1 =                                           
                 (INPSTRUK.FRA.BPROM1 + INPSTRUK.FRA.BPROM2) -          
                 (INPSTRUK.FRA.BNEDSL1 +                                
                  INPSTRUK.FRA.BNEDSL2 +                                
                  INPSTRUK.FRA.BPENSNED);                               
         INPSTRUK.FRA.BSUM1_SW = 0;                                     
                                                                        
         /* Beregn sum af ejendomsværdiskat */                          
         INPSTRUK.FRA.BSUM3 = INPSTRUK.FRA.BSUM1;                       
         INPSTRUK.FRA.BSUM3_SW = 0;                                     
                                                                        
         IF INPSTRUK.FRA.BSUM1 < 0                                      
         THEN                                                           
            INPSTRUK.FRA.BSUM1 = 0;                                     
                                                                        
         /* Beregn ejendomsværdiskat        */                          
         INPSTRUK.FRA.BNETTO = INPSTRUK.FRA.BSUM1;                      
                                                                        
         INPSTRUK.FRA.BNETTO_SW = 0;                                    
                                                                        
         IF INPSTRUK.FRA.BNETTO < 0                                     
         THEN                                                           
            INPSTRUK.FRA.BNETTO = 0;                                    
                                                                        
         /* EVS Rabat felter                                          */
         HJ_PCT   = INPSTRUK.TIL.GPCTEJER/100;                          
                                                                        
         /* BNETTO reduceret for Ejerandel                            */
         HJVÆRDI1 = (MULTIPLY                                           
                    (INPSTRUK.FRA.BNETTO,HJ_PCT,13,3)) + 0.005;         
         INPSTRUK.FRA.BTAKS = HJVÆRDI1;                                 
                                                                        
         /* EVS Rabat reduceret for Ejerandel                         */
         HJVÆRDI1 = (MULTIPLY                                           
                    (INPSTRUK.TIL.BASISRABAT,HJ_PCT,13,3)) + 0.005;     
         INPSTRUK.FRA.BTAKSRABAT = HJVÆRDI1;                            
                                                                        
         IF INPSTRUK.FRA.BTAKSRABAT > INPSTRUK.FRA.BTAKS                
         THEN                                                           
            INPSTRUK.FRA.BTAKSRABAT = INPSTRUK.FRA.BTAKS;               
                                                                        
         INPSTRUK.FRA.BTAKSEFTRABAT = INPSTRUK.FRA.BTAKS -              
                                      INPSTRUK.FRA.BTAKSRABAT;          
                                                                        
 END BEREGN_EVS_KMD_DEL;                                                
