 BH60700:/***  DAN OG OPSPLIT TRANS'ER TIL ESR FRA EVS SLUT         ***/
         PROC OPTIONS (MAIN) ;                                          
 /*********************************************************************/
 /*                                                                   */
 /*      NAVN       :  DAN OG OPSPLIT TRANS'ER TIL ESR FRA EVS        */
 /*                                                                   */
 /*      FORMÅL     :  AT DANNE OG OPSPLITTE TRANSAKTIONER MED PENSI- */
 /*                    ONISTKODE PR. REGIONAL CENTRAL.                */
 /*                                                                   */
 /*      INDDATA    :  BH96011D                                       */
 /*                                                                   */
 /*      UDDATA     :  BH607X0D (PENSIONISTKODE)                      */
 /*                    (X=KODE FOR REGIONAL CENTRAL)                  */
 /*                    BH60701Y (TOTALLISTE)                          */
 /*                                                                   */
 /*      PROGRAMMØR :  JESPER OLSEN     UC-P   SEPT. 1987             */
 /*      RETTET     :  AAGE RASMUSSEN   DIV-SKAT OKT. 1990            */
 /*                 :  AAGE RASMUSSEN   DIV-SKAT DEC. 1990            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT JAN. 1997            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT JAN. 1998            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT NOV. 1999            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT DEC. 2000            */
 /*                 :  AAGE RASMUSSEN   LSU      DEC. 2002            */
 /*********************************************************************/
         DEFAULT RANGE(*) STATIC UNALIGNED;                             
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
 DCL     BH960IN         FILE RECORD INPUT;                             
 DCL     BH607X0         FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      DECLARE AF INPUT                                             */
 /*********************************************************************/
 DCL     1 BH960,                                                       
           %INCLUDE BH96011N;                                           
 DCL     BH960_ID        CHAR      (14) BASED(ADDR(BH960));             
 /*********************************************************************/
 /*      DECLARE AF OUTPUT                                            */
 /*********************************************************************/
 DCL     BH607           CHAR      (80);                                
 DCL     1     Z300      BASED(ADDR(BH607)),                            
          2    CTRANS    CHAR      (4),                                 
          2    HDISP     CHAR      (1),                                 
          2    EBRUGER   PIC       '9999',                              
          2    CMEDIE    CHAR      (1),                                 
          2    EVOLBR    CHAR      (6),                                 
          2    DREGNR    CHAR      (3),                                 
          2    CBLAND    CHAR      (1),                                 
          2    EPLADS    CHAR      (3),                                 
          2    EOPGOM    CHAR      (1),                                 
          2    EDATNR    CHAR      (2),                                 
          2    HDISP1    CHAR      (54);                                
 DCL     1     SLUTD     BASED(ADDR(BH607)),                            
          2    CTRANS    CHAR    (5),                                   
          2    EANTAL    PIC    '(5)9',                                 
          2    HDISP     CHAR    (70);                                  
 DCL     1 BH60702       BASED(ADDR(BH607)),                            
           %INCLUDE BH60702N;                                           
 /*********************************************************************/
 /*      DECLARE AF ENTRY OG BUILTIN                                  */
 /*********************************************************************/
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ZS61900         ENTRY;                                         
 %INCLUDE ZS38000N;                /* DATORUTINE - SKAF D.D. + DAGNR. */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH60700L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (44)      INIT      (                
           'OPSPLITNING AF PENSIONISTTRANS PR. CENTRAL'),               
           2 F2          CHAR      (24)      INIT      (' '),           
           2 TEKST3      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (48),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (73)      INIT      (' ');           
 % INCLUDE ZZ20301M;                                                    
 /*********************************************************************/
 /*      DECLARE AF DIVERSE HJÆLPEFELTER                              */
 /*********************************************************************/
 DCL     INDEX           DEC FIXED (1);                                 
 DCL     GEM_ID          CHAR      (14);                                
 DCL     GEM_RC          PIC       '9';                                 
 DCL     GEM_KOM         PIC       '999';                               
 DCL     EOF_BH960       BIT       (01);                                
 DCL     I               PIC       '9';                                 
 DCL     HEXE0           CHAR      (1)       INIT      ('\');           
                                                                        
 %INCLUDE ZS38002N;                /* DATORUTINE - DIVERSE DECLARE    */
                                                                        
 DCL     HJ_DAGS_DATO    CHAR      (08);                                
 DCL     1 HJ_DAGS_DATO1 DEF HJ_DAGS_DATO,                              
           2 ÅR          CHAR      (04),                                
           2 MD          CHAR      (02),                                
           2 DAG         CHAR      (02);                                
                                                                        
 DCL     DAGNR           CHAR      (3);                                 
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     ANT_LÆS         FIXED     (7);       /* ANTAL LÆSTE INDV.    */
 DCL     ANT_RC0(6)      FIXED     (7);       /* SKREVNE INDV. PR. RC */
 DCL     ANT_SKR         FIXED     (7);       /* SKREVNE INDV. IALT   */
 DCL     ANT_TR0         FIXED     (7);       /* ANTAL SKREVNE TRANS. */
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
         CALL ZS61900;                            /* UDSKRIV SSI-DATO */
         ON ERROR                                                       
            CALL PLIDUMP('SNHOB');                                      
         ON ENDFILE (BH960IN)                                           
            EOF_BH960 = '1'B;                                           
         OPEN FILE (BH960IN) TITLE ('BH96011I');                        
         READ FILE (BH960IN) INTO (BH960);                              
         GEM_ID = '';                                                   
         GEM_RC = BH960.CRCKO;                                          
         GEM_KOM = 0;                                                   
         INDEX = GEM_RC;                                                
         OPEN FILE (BH607X0) TITLE ('BH607'!!BH960.CRCKO!!'0O');        
         CALL ZZ20390 ('*OPEN','BH60701Y');                             
         /* SKAF DAGS DATO SAMT DAGNR */                                
         ZS380PARM.FUNKTION   = 'DAGSDATO';                             
         ZS380PARM.VALIDERING = 'N';                                    
         CALL ZS38000 (ZS380PARM.FUNKTION,                              
                       ZS380PARM.VALIDERING,                            
                       ZS380PARM.PARM,                                  
                       ZS380PARM.RETURKODE);                            
         DAGNR = SUBSTR(ZS380PARM3.DATO2,6,3);                          
         HJ_DAGS_DATO  = ZS380PARM3.DATO1;                              
         OVERLIN1.DATO = HJ_DAGS_DATO1.DAG  !! '/' !!                   
                         HJ_DAGS_DATO1.MD   !! '-' !!                   
                         HJ_DAGS_DATO1.ÅR;                              
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         ANT_LÆS    = 0;                                                
         ANT_SKR    = 0;                                                
         ANT_RC0(*) = 0;                                                
         ANT_TR0    = 0;                                                
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
         DO WHILE (^EOF_BH960);                                         
            ANT_LÆS = ANT_LÆS + 1;                                      
            IF BH960.CRCKO ^= GEM_RC                                    
            THEN                                                        
               DO;                                                      
                  CALL SLUT_INDIVID;                                    
                  CALL LUK_ÅBN;                                         
               END;                                                     
            IF GEM_ID ^= BH960_ID                                       
            THEN                                                        
               CALL SKRIV;                                              
            READ FILE (BH960IN) INTO (BH960);                           
         END;  /* DO WHILE */                                           
         CALL SLUT_INDIVID;                                             
         CALL KONTROLLISTE;                                             
         CALL AFSLUT;                                                   
 /*********************************************************************/
 /*      SKRIV PENSIONISTTRANS                                        */
 /*********************************************************************/
 SKRIV:                                                                 
         PROC;                                                          
         IF BH960.EKOMNR ^= GEM_KOM                                     
         THEN                                                           
            DO;                                                         
               IF GEM_KOM ^= 0                                          
               THEN                                                     
                  CALL SLUT_INDIVID;                                    
               BH607 = ' ';                                             
               Z300.CTRANS = 'Z300';                                    
               Z300.EBRUGER = BH960.EKOMNR;                             
               Z300.CMEDIE = '4';                                       
               Z300.DREGNR = DAGNR;                                     
               Z300.CBLAND = '0';                                       
               Z300.EOPGOM = 'C';                                       
               Z300.EDATNR = '01';                                      
               WRITE FILE (BH607X0) FROM (BH607);                       
               GEM_KOM = BH960.EKOMNR;                                  
            END;                                                        
         BH607 = ' ';                                                   
         CALL SKRIV_TYP10;                                              
         GEM_ID = BH960_ID;                                             
 END SKRIV;                                                             
 /*********************************************************************/
 /*                      SKRIV TYPE 10                                */
 /*********************************************************************/
 SKRIV_TYP10:                                                           
         PROC;                                                          
         BH60702.CNART   = 'CN';                                        
         BH60702.EKOMNR  = BH960.EKOMNR;                                
         BH60702.EEJDNR  = BH960.EEJDNR;                                
         BH60702.CTRTYP  = 10;                                          
         BH60702.CRET    = 2;                                           
         BH60702.FELT1   = '&261';                                      
         BH60702.DCPRCIR = BH960.DCPRCIR;                               
         BH60702.FELT2   = '&296';                                      
         BH60702.CÅRSAG  = '1';                                         
         BH60702.FELT3   = '&297';                                      
         BH60702.CMYND   = 'K';                                         
         BH60702.FELT4   = '&282';                                      
         BH60702.CPENS   = BH960.CPENS;                                 
         BH60702.CSLUT   = HEXE0;                                       
         WRITE FILE (BH607X0) FROM (BH607);                             
         ANT_TR0 = ANT_TR0 + 1;                                         
         ANT_RC0(INDEX) = ANT_RC0(INDEX) + 1;                           
         ANT_SKR = ANT_SKR + 1;                                         
 END SKRIV_TYP10;                                                       
 /*********************************************************************/
 /*      SKRIV SLUT-KOMMUNEINDIVID                                    */
 /*********************************************************************/
 SLUT_INDIVID:                                                          
         PROC;                                                          
         BH607 = ' ';                                                   
         SLUTD.CTRANS = 'SLUTD';                                        
         SLUTD.EANTAL = ANT_TR0;                                        
         WRITE FILE (BH607X0) FROM (BH607);                             
         ANT_TR0 = 0;                                                   
 END SLUT_INDIVID;                                                      
 /*********************************************************************/
 /*      LUKNING OG ÅBNING AF FILER TIL REGIONALE CENTRALER           */
 /*********************************************************************/
 LUK_ÅBN:                                                               
         PROC;                                                          
         CLOSE FILE (BH607X0);                                          
         OPEN FILE (BH607X0) TITLE ('BH607'!!BH960.CRCKO!!'0O');        
         GEM_RC = BH960.CRCKO;                                          
         INDEX = GEM_RC;                                                
         GEM_KOM = 0;                                                   
 END LUK_ÅBN;                                                           
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS3;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE RECORDS PÅ B.H96011D';            
         DETLIN1.ANTAL = ANT_LÆS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DO I = 1 TO 6;                                                 
            IF ANT_RC0(I) ^= 0                                          
            THEN                                                        
               DO;                                                      
                  DETLIN1.CCA = ZZWS3;                                  
                  DETLIN1.TEKST =                                       
                  'ANTAL SKREVNE TRANSAKTIONER PÅ B.H607' !! I !! '0D'; 
                  DETLIN1.ANTAL = ANT_RC0(I);                           
                  CALL ZZ20300(DETLIN1,'01');                           
               END;                                                     
         END;                                                           
         DETLIN1.CCA   = ZZWS3;                                         
         DETLIN1.TEKST = 'ANTAL SKREVNE TRANSAKTIONER I ALT ';          
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
         CALL ZZ20300(' ','99');                                        
         CLOSE FILE (BH607X0),                                          
               FILE (BH960IN);                                          
 END AFSLUT;                                                            
 END BH60700;                                                           
