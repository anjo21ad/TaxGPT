  PROCESS OPT(TIME);                                                    
 BH60110: /* BEREGNING AF LEJEVÆRDI FOR INDKOMSTÅR 1999 */              
 PROCEDURE (BH601_AR,BH60101M,BEREGN_TIL) REORDER;                      
 /*********************************************************************/
                                                                        
 DCL COPYRIGHT CHAR (30) STATIC INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');
                                                                        
 /**********************************************************************
 *                                                                     *
 * DETTE MODUL HAR TIL FORMÅL AT UDFØRE BEREGNING AF LEJEVÆRDI P.G.A.  *
 * OPLYSNINGERNE I BEREGNINGSBÅNDET HENTET FRA HENHOLDSVIS ESR-UDTRÆK, *
 * OVERGANGSORDNINGSBÅND FRA BEREGNINGEN FOR INDKOMSTÅR 1998 SAMT VED  *
 * BEREGNING TIL ORDINÆR ESKEMA DESUDEN MINI-MTFIL FRA FORSKUD 1999.   *
 *                                                                     *
 * MODULET ER FÆLLES FOR ORDINÆR ESKEMA OG BOY.                        *
 *                                                                     *
 * NB.: BEREGNING AF LEJEVÆRDI FOR INDKOMSTÅR 1999 TIL FORSKUD         *
 *      FORETAGES VIA MODUL BH95010.                                   *
 *                                                                     *
 * NB.: BEREGNING AF LEJEVÆRDI FOR INDKOMSTÅR 2000 TIL BOY             *
 *      FORETAGES VIA MODUL BH80110.                                   *
 *                                                                     *
 *  PROGRAMMERET AF AAGE RASMUSSEN   PFB SKAT   AUGUST 1996            *
 *  ÅRSTILRETTET AF AAGE RASMUSSEN   PFB SKAT   AUGUST 1997            *
 *  ÅRSTILRETTET AF AAGE RASMUSSEN   PFB SKAT   JANUAR 1998            *
 *  ÅRSTILRETTET AF AAGE RASMUSSEN   PFB SKAT   JULI   1998            *
 *  ÅRSTILRETTET AF AAGE RASMUSSEN   PFB SKAT   JANUAR 2000            *
 **********************************************************************/
                                                                        
         DEFAULT RANGE (*) STATIC CONN UNALIGNED;                       
 /*********************************************************************/
 /*      DECLARE AF DIVERSE PARAMETRE TIL DETTE MODUL                 */
 /*********************************************************************/
                                                                        
 DCL     BH601_AR   CHAR      (4000) VAR;   /* BEREGNINGSBÅND         */
 DCL     1 BH601                          BASED(ADDR(BH601_AR)),        
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60100N;                                           
                                                                        
 DCL     1 BH60101M ALIGNED,                                            
         %INCLUDE BH60101M;                                             
                                                                        
 DCL     BEREGN_TIL      CHAR      (3);                                 
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
                                                                        
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
                                                                        
 /********************************************************************* 
 *       DECLARE AF DIV. HJÆLPEFELTER                                 * 
 *********************************************************************/ 
 DCL     JA              BIT       (1)       INIT ('1'B);               
 DCL     NEJ             BIT       (1)       INIT ('0'B);               
 DCL     GRÆNSEVÆRDI     FIXED     (7);                                 
 DCL     PENSPCT         FIXED     (5,4);                               
 DCL     CREDUKT         PIC       '9';  /* KODE FOR REDUKTIONSBEREGN.*/
 DCL     CREDUKTTYP      PIC       '9';  /* KODE FOR REDUKTIONSTYPE   */
                                                                        
 DCL     1 BH60100M,                                                    
         %INCLUDE BH60100M;                                             
                                                                        
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
                                                                        
         /* INITIER/NULSTIL DIVERSE FELTER INDEN BEREGNING */           
                                                                        
         CALL SKAF_BEREGN_GRUNDLAG_1999;                                
                                                                        
         CALL NULSTIL_LEJEVÆRDI_FELTER;                                 
                                                                        
         CALL UNDTAGELSER_LEJEVÆRDI_EJENDOM;                            
                                                                        
         IF BEREGN = JA                /* DER SKAL BEREGNES LEJEVÆRDI */
         THEN                          /* FOR EJENDOMMEN              */
            DO;                                                         
               GRÆNSEVÆRDI = 2393000;  /* VÆRDI FOR 1999 */             
               PENSPCT     = 0.0100;   /* VÆRDI FOR 1999 */             
               /* FØRST BEREGNES UDEN HENSYN TIL OM DER ER TALE OM */   
               /* ET 'HELÅRSBEBOET' SOMMERHUS. DETTE GØRES AF      */   
               /* HENSYN TIL OVERFØRSEL AF DE 3 'FASTE' EJENDOMS-  */   
               /* LEJEVÆRDIER TIL ESR SAMT EVT. BEREGNING FOR 2000 */   
               CALL LEJEVÆRDI_EJENDOM(BH601.EJD_1999,PENSPCT,'1');      
               CALL UNDTAGELSER_REDUKTION;                              
               IF BH601.EJD_1999(1).BLEJE ^= 0 &                        
                  BEREGN = JA          /* BLEV BEREGNET SIDSTE ÅR OG  */
               THEN                    /* ER IKKE UNDTAGET REDUKTION  */
                  CALL REDUCER_EJENDOM(BH601.EJD_1999);                 
               CALL SKAF_NYE_EJD_LEJEVÆRDIER                            
                    (BH601.EJD_1999,BH601.EJD_2000);                    
               /* SÅ BEREGNES MEN  K U N                             */ 
               /* HVIS 'HELÅRSBEBOET' SOMMERHUS  M E D  DISPENSATION */ 
               /* AF HENSYN TIL VALG AF PERSONLIG LEJEVÆRDI.         */ 
               IF (BH601.CBENYT = '08' !                                
                   BH601.CBENYT = '28' !                                
                   BH601.CBENYT = '48') &                               
                  (BH601.C_2PCT = '1' !                                 
                   BH601.C_2PCT = '2' !                                 
                   BH601.C_2PCT = '3' !                                 
                   BH601.C_2PCT = '7' !                                 
                   BH601.C_2PCT = '8' !                                 
                   BH601.C_2PCT = '9')                                  
               THEN                                                     
                 DO;                                                    
                    CALL SKAF_GL_LEJEVÆRDIER;                           
                    CALL LEJEVÆRDI_EJENDOM(BH601.SOM_1999,PENSPCT,'2'); 
                    CALL UNDTAGELSER_REDUKTION;                         
                    IF BH601.SOM_1999(1).BLEJE ^= 0 &                   
                       BEREGN = JA                                      
                    THEN                                                
                       CALL REDUCER_EJENDOM(BH601.SOM_1999);            
                                                                        
                    /* OPSÆT OVERGANGSORDNINGSKODE FOR 1999 SAMT */     
                    /* GEM DE TRE 'NYE' EJENDOMSLEJEVÆRDIER I    */     
                    /* BEREGNINGSBÅNDET A.H.T. KORREKT BEREGNING */     
                    /* AF NÆSTE ÅRS LEJEVÆRDIER HVIS PERSONEN    */     
                    /* IGEN HAR 'HELÅRSSTATUS' I SOMMERHUS.      */     
                                                                        
                    /* OPLYSNINGERNE SKRIVES DESUDEN PÅ NYT  */         
                    /* OVERGANGSORDINGSBÅND I ORDINÆR ESKEMA */         
                    /* (SKER I PROGRAM BH60100).             */         
                                                                        
                    CALL SKAF_NYE_EJD_LEJEVÆRDIER                       
                         (BH601.SOM_1999,BH601.OVG_1999);               
                    BH601.COVG_1999 = '2';                              
                                                                        
                    CALL UNDTAGELSER_LEJEVÆRDI_PERSON;                  
                    IF BEREGN = JA     /* DER SKAL BEREGNES LEJEVÆRDI */
                    THEN               /* FOR PERSONEN                */
                      DO;                                               
                         CALL PERSONLIG_EJD_LEJEVÆRDI(BH601.SOM_1999);  
                         CALL LEJEVÆRDI_PERSON(BH601.SOM_1999);         
                      END;                                              
                 END;                                                   
               ELSE                                                     
                 DO;                                                    
                    CALL UNDTAGELSER_LEJEVÆRDI_PERSON;                  
                    IF BEREGN = JA     /* DER SKAL BEREGNES LEJEVÆRDI */
                    THEN               /* FOR PERSONEN                */
                      DO;                                               
                         CALL PERSONLIG_EJD_LEJEVÆRDI(BH601.EJD_1999);  
                         CALL LEJEVÆRDI_PERSON(BH601.EJD_1999);         
                      END;                                              
                 END;                                                   
            END;                                                        
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /* FORETAG OPSÆTNING AF BEREGNINGSGRUNDLAGET SOM SKAL BRUGES VED BE- */
 /* REGNING AF LEJEVÆRDI M.V. FOR 1999 P.G.A. EJENDOMSVÆRDI, EJERBO-  */
 /* LIG ELLER STUEHUSVÆRDI(ER) PR. 1/1-1999.                          */
 /*                                                                   */
 /*********************************************************************/
 SKAF_BEREGN_GRUNDLAG_1999:                                             
         PROC;                                                          
                                                                        
         SELECT;                                                        
                                                                        
            WHEN (BH601.ANT_FOR_1 > 0)                                  
               DO;                                                      
                  /* MINDRE UDLEJNINGSEJENDOM */                        
               END;                                                     
                                                                        
            WHEN (BH601.ANT_FOR_2 > 0)                                  
               DO;                                                      
                  /* BOFÆLLESSKAB           */                          
               END;                                                     
                                                                        
            WHEN (BH601.ANT_FOR_4 = 0 &                                 
                  BH601.ANT_FOR_5 = 0 &                                 
                  BH601.ANT_FOR_6 = 0)                                  
               DO;                                                      
                  /* INGEN EJERBOLIG- ELLER STUEHUSVÆRDI */             
                  /* ELLER MINDRE UDLEJN ELLER BOFÆLLES  */             
                  /* SÅ BENYT EJENDOMSVÆRDI              */             
                  HJVÆRDI = BH601.BEJDVÆR;                              
               END;                                                     
                                                                        
            WHEN (BH601.ANT_FOR_4 +                                     
                  BH601.ANT_FOR_5 +                                     
                  BH601.ANT_FOR_6 = 1)                                  
               DO;                                                      
                  /* DER FINDES KUN 1 EJERBOLIG- ELLER   */             
                  /* STUEHUSVÆRDI, SOM DERFOR BENYTTES I */             
                  /* STEDET FOR EJENDOMSVÆRDIEN          */             
                  HJVÆRDI = BH601.EJEBOL(1).BEJEBOL;                    
               END;                                                     
                                                                        
            OTHERWISE;                                                  
                                                                        
         END;                                                           
                                                                        
         BH601.BERGRL1 = HJVÆRDI;                                       
                                                                        
         IF BH601.CBENYT ^= '09' &                                      
            BEREGN_TIL = 'SAM'                                          
         THEN                                                           
            DO;                                                         
               /* OPSÆT TIL SAMBESKATNING */                            
               BH601.BERGRL3 = HJVÆRDI + BH601.BERGRL2;                 
               HJVÆRDI = BH601.BERGRL3;                                 
            END;                                                        
                                                                        
 END SKAF_BEREGN_GRUNDLAG_1999;                                         
                                                                        
 /********************************************************************* 
 *      MÅ DER BEREGNES REDUKTION (GÆLDER BÅDE EJENDOMMENS OG PERSO-  * 
 *      NENS LEJEVÆRDIER).                                            * 
 *                                                                    * 
 *      UDFØRES  I K K E  FOR DET NYE ÅR I BH80110 (BOY)              * 
 *********************************************************************/ 
 UNDTAGELSER_REDUKTION:                                                 
 PROC;                                                                  
                                                                        
        BEREGN = NEJ;                                                   
                                                                        
        IF BH601.CFORSKEL = '2'                                         
        THEN                                                            
           RETURN;                                                      
                                                                        
        IF (BH601.CBENYT = '21' !                                       
            BH601.CBENYT = '22' !                                       
            BH601.CBENYT = '26' !                                       
            BH601.CBENYT = '27' !                                       
            BH601.CBENYT = '28' !                                       
            BH601.CBENYT = '29') &                                      
           (BH601.CEJERL_GL = '1' &                                     
            BH601.CEJERL ^= '1')                                        
        THEN                                                            
           RETURN;                                                      
                                                                        
        BEREGN = JA;                                                    
                                                                        
 END UNDTAGELSER_REDUKTION;                                             
                                                                        
 /****************************************************************/     
 /*                                                              */     
 /*      FOR PERSONER SOM EJER SOMMERHUSE MED 'HELÅRSSTATUS'     */     
 /*      SKAL DER VED BEREGNING AF LEJEVÆRDIEN FOR INDKOMSTÅR    */     
 /*      1999 TAGES UDGANGSPUNKT I                               */     
 /*      E N T E N                                               */     
 /*      DE 3 1998-EJENDOMS-LEJEVÆRDIER HENTET FRA ESR (HVIS     */     
 /*      PERSONEN  I K K E  BLEV BEREGNET MED 'HELÅRSSTATUS'     */     
 /*      ÅRET FØR)                                               */     
 /*      E L L E R                                               */     
 /*      DE 3 1998-HELÅRSSOMMERHUS-LEJEVÆRDIER HENTET FRA OVER-  */     
 /*      ORDNINGSBÅNDET (HVIS PERSONEN  B L E V  BEREGNET MED    */     
 /*      'HELÅRSSTATUS' ÅRET FØR).                               */     
 /*                                                              */     
 /****************************************************************/     
 SKAF_GL_LEJEVÆRDIER:                                                   
 PROC;                                                                  
                                                                        
         IF BH601.COVG_1998 = '2'     /* HAVDE 'HELÅRSSTATUS' I 1998 */ 
         THEN                                                           
            DO;                                                         
               /* BENYT LEJEVÆRDIER FRA */                              
               /* OVERGANGSORDNINGSBÅND */                              
               BH601.SOM_1999(1).BLEJE   = BH601.OVG_1998.BLEJE;        
               BH601.SOM_1999(1).BLEJEFP = BH601.OVG_1998.BLEJEFP;      
               BH601.SOM_1999(1).BLEJE67 = BH601.OVG_1998.BLEJE67;      
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* BENYT LEJEVÆRDIER FRA ESR */                          
               BH601.SOM_1999(1).BLEJE   = BH601.EJD_1999(1).BLEJE;     
               BH601.SOM_1999(1).BLEJEFP = BH601.EJD_1999(1).BLEJEFP;   
               BH601.SOM_1999(1).BLEJE67 = BH601.EJD_1999(1).BLEJE67;   
            END;                                                        
                                                                        
 END SKAF_GL_LEJEVÆRDIER;                                               
 /*********************************************************************/
 /*      SKAF PERSONENS GL. LEJEVÆRDI VIA DEN AKTUELLE AF EJENDOMMENS */
 /*      3 GL. LEJEVÆRDIER (P.G.A. AKTUEL CPER_ORD) SAMT PERSONENS    */
 /*      NYE BEREGNEDE LEJEVÆRDI. DISSE 2 VÆRDIER INDGÅR I OPSÆTNING  */
 /*      AF KODER FOR LEJEVÆRDISTIGNING TIL SLS.                      */
 /*********************************************************************/
 SKAF_PERSONLIG_GL_LEJEVÆRDI:                                           
 PROC (GLBLEJE,NYBLEJE);                                                
 DCL     GLBLEJE        FIXED     (11,0);                               
 DCL     NYBLEJE        FIXED     (11,0);                               
         IF BH601.CPER_ORD = '1' !                                      
            BH601.CPER_ORD = '4'                                        
         THEN                                                           
            /* IKKE PENSIONIST ELL. FØRTIDSPENS/EFTERLØN */             
            GLBLEJE = BH601.EJD_1999(1).BLEJE;                          
         ELSE                                                           
            IF BH601.CPER_ORD = '2'                                     
            THEN                                                        
               /* FØRTIDSPENS ELL. EFTERLØN */                          
               GLBLEJE = BH601.EJD_1999(1).BLEJEFP;                     
            ELSE                                                        
               /* PENSIONIST */                                         
               GLBLEJE = BH601.EJD_1999(1).BLEJE67;                     
         NYBLEJE = BH601.PER_LEJEV(1).BELØB;                            
 END SKAF_PERSONLIG_GL_LEJEVÆRDI;                                       
                                                                        
 /*********************************************************************/
 /*       BEREGNING AF LEJEVÆRDI FOR PERSONEN EVT. REDUCERET HVIS     */
 /*       PERSONEN ER OMFATTET AF DEN GL. OVERGANGSORDNING.           */
 /* OBS.: UDFØRES HVIS DER ER BEREGNET LEJEVÆRDIER FOR EJENDOMMEN     */
 /*       OG PERSONEN IKKE ER UNDTAGET FOR BEREGNING.                 */
 /*       OPSÆTNINGEN AF CLEJVBEKOD HAR  K U N  BETYDNING FOR         */
 /*       ORDINÆR ESKEMA.                                             */
 /*********************************************************************/
 LEJEVÆRDI_PERSON:                                                      
       PROC(TAB_LEJEV);                                                 
                                                                        
 DCL   1 TAB_LEJEV(3),                  /* EJENDOMMENS 3 LEJEVÆRDIER  */
         2 BLEJE      FIXED  (11),      /* LEJEV. NORMAL              */
         2 BLEJEFP    FIXED  (11),      /* LEJEV. FØRTIDSPENS./EFTERL.*/
         2 BLEJE67    FIXED  (11);      /* LEJEVÆRDI PENSIONIST       */
                                        /* 1=SIDSTE ÅRS VÆRDIER       */
                                        /* 2=NYT ÅRS VÆRDIER UREDUC.  */
                                        /* 3=NYT ÅRS VÆRDIER REDUC.   */
                                                                        
 DCL     GLBLEJE        FIXED     (11,0)     INIT (0);                  
 DCL     NYBLEJE        FIXED     (11,0)     INIT (0);                  
                                                                        
         BH601.CLIN03 = 1;                                              
                                                                        
         /* HEREFTER UNDERSØGES OM DER SKAL DER FORETAGES BEREGNING   */
         /* AF REDUCERET LEJEVÆRDI FOR PERSONEN. DETTE ER KUN AKTUELT */
         /* HVIS DENNE ER OMFATTET AF DEN GL. OVERGANGSORDNING.       */
                                                                        
         /* PERSONER, SOM ENTEN HAR KØBT ELLER SOLGT EJERANDELE I */    
         /* INDKOMSTÅRET, ER OGSÅ OMFATTET AF OVENNÆVNTE REGLER   */    
                                                                        
         /* DET ER ALTID DEN HELÅRLIGE LEJEVÆRDI, DER ER GEMT PÅ  */    
         /* OVERGANGSORDNINGSBÅNDET.                              */    
                                                                        
         CALL UNDTAGELSER_REDUKTION;                                    
                                                                        
         IF BH601.COVG_1998 ^= '1' !                                    
            BEREGN = NEJ                                                
         THEN                                                           
            DO;                                                         
               /* UNDTAGELSE HVIS PERSONEN IKKE ER OMFATTET AF */       
               /* GL. OVERGANGSORDNING                         */       
               /* ELLER                                        */       
               /* HVIS EJENDOMMEN ER UNDTAGET FOR REDUKTION    */       
                                                                        
               CALL SKAF_PERSONLIG_GL_LEJEVÆRDI (GLBLEJE,NYBLEJE);      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
               CALL INDSÆT_LEJEVÆRDI;                                   
               RETURN;                                                  
                                                                        
            END;                                                        
                                                                        
         GLBLEJE = BH601.OVLEJ_1998;                                    
                                                                        
         IF BH601.CPER_ORD = '1' !                                      
            BH601.CPER_ORD = '4'                                        
         THEN                                                           
            DO;                                                         
               /* IKKE PENSIONIST ELL. FØRTIDSPENS/EFTERLØN */          
               CREDUKT = 1;                                             
               NYBLEJE = TAB_LEJEV(2).BLEJE;                            
            END;                                                        
         ELSE                                                           
            IF BH601.CPER_ORD = '2'                                     
            THEN                                                        
               DO;                                                      
                  /* FØRTIDSPENS ELL. EFTERLØN */                       
                  CREDUKT = 2;                                          
                  NYBLEJE = TAB_LEJEV(2).BLEJEFP;                       
               END;                                                     
               ELSE                                                     
                  DO;                                                   
                     /* PENSIONIST */                                   
                     CREDUKT = 2;                                       
                     NYBLEJE = TAB_LEJEV(2).BLEJE67;                    
                  END;                                                  
                                                                        
         CALL REDUKTION (GLBLEJE,NYBLEJE,BH601.PER_LEJEV(2).BELØB,      
                         CREDUKT,CREDUKTTYP);                           
                                                                        
         IF BH601.PER_LEJEV(2).BELØB ^= 0 &                             
            BH601.PER_LEJEV(2).BELØB < BH601.PER_LEJEV(1).BELØB         
         THEN                                                           
            DO;                                                         
                                                                        
               /* DENNE PERSON ER STADIG OMFATTET AF */                 
               /* DEN GL. OVERGANGSORDNING           */                 
                                                                        
               BH601.CP20_3500 = CREDUKTTYP;                            
               BH601.CLIN06 = 1;                                        
               PER_LEJEV(2).CREDTYP = CREDUKTTYP;                       
                                                                        
               NYBLEJE = BH601.PER_LEJEV(2).BELØB;                      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
                                                                        
               BH601.BLEJE2 = (BH601.PER_LEJEV(2).BELØB *               
                               TÆLLER) / NÆVNER;                        
                                                                        
               /* GEM LEJEVÆRDI FRA OVERGANGSORDNING 1999 */            
               /* I BEREGNINGSBÅNDET A.H.T. TEST/KONTROL  */            
               /* SAMT EVT. BEREGNING FOR 2000 I BH80110. */            
                                                                        
               BH601.OVLEJ_1999 = BH601.PER_LEJEV(2).BELØB;             
               BH601.COVG_1999 = '1';                                   
                                                                        
               SELECT (BH601.CPER_ORD);                                 
                                                                        
                  WHEN ('1','4')                                        
                    DO;                                                 
                       IF (BH601.CBENYT = '08' !                        
                           BH601.CBENYT = '28' !                        
                           BH601.CBENYT = '48')                         
                       THEN                                             
                         DO;                                            
                            IF (BH601.C_2PCT = '1' !                    
                                BH601.C_2PCT = '2' !                    
                                BH601.C_2PCT = '3')                     
                            THEN                                        
                               BH601.CLEJVBEKOD = 43;                   
                            ELSE                                        
                               BH601.CLEJVBEKOD = 33;                   
                         END;                                           
                       ELSE                                             
                          BH601.CLEJVBEKOD = 03;                        
                    END;                                                
                                                                        
                  WHEN ('2')                                            
                    DO;                                                 
                       IF (BH601.CBENYT = '08' !                        
                           BH601.CBENYT = '28' !                        
                           BH601.CBENYT = '48')                         
                       THEN                                             
                         DO;                                            
                            IF (BH601.C_2PCT = '1' !                    
                                BH601.C_2PCT = '2' !                    
                                BH601.C_2PCT = '3')                     
                            THEN                                        
                               BH601.CLEJVBEKOD = 63;                   
                            ELSE                                        
                               BH601.CLEJVBEKOD = 53;                   
                         END;                                           
                       ELSE                                             
                          BH601.CLEJVBEKOD = 13;                        
                    END;                                                
                                                                        
                  WHEN ('3')                                            
                    BH601.CLEJVBEKOD = 23;                              
                                                                        
                  OTHERWISE;                                            
                                                                        
               END;                                                     
                                                                        
            END;                                                        
         ELSE                                                           
            DO;                                                         
                                                                        
               /* PERSONEN ER IKKE LÆNGERE OMFATTET */                  
               /* AF OVERGANGSORDNINGERNE           */                  
                                                                        
               CALL SKAF_PERSONLIG_GL_LEJEVÆRDI (GLBLEJE,NYBLEJE);      
               CALL OPSÆT_CDIFLEJE (GLBLEJE,NYBLEJE);                   
               CALL INDSÆT_LEJEVÆRDI;                                   
                                                                        
            END;                                                        
                                                                        
 END LEJEVÆRDI_PERSON;                                                  
 /*********************************************************************/
 /*      OPSÆT KODER FOR LEJEVÆRDISTIGNING TIL SLS                    */
 /*      (VEDRØRER KUN DEN PERSONLIGE LEJEVÆRDI)                      */
 /* OBS: I N D E N  KALD AF PROCEDUREN SKAL                           */
 /*      GLBLEJE OG NYBLEJE SÆTTES LIG PERSONENS GL. LEJEVÆRDI OG     */
 /*      PERSONENS NY LEJEVÆRDI.                                      */
 /*********************************************************************/
 OPSÆT_CDIFLEJE:                                                        
 PROC(GLBLEJE,NYBLEJE);                                                 
 DCL     GLBLEJE        FIXED     (11,0);                               
 DCL     NYBLEJE        FIXED     (11,0);                               
 DCL     GLBLEJE20      FIXED     (15,2);                               
         GLBLEJE20  = GLBLEJE *  0.20;                                  
         IF GLBLEJE > 0                                                 
         THEN                                                           
            DO;                                                         
               IF NYBLEJE > GLBLEJE + 1000                              
               THEN                                                     
                  BH601.CDIFLEJE = 1;                                   
               IF NYBLEJE > ( GLBLEJE + GLBLEJE20 - 2500 )              
               THEN                                                     
                  BH601.CDIFLEJE2 = 2;                                  
            END;                                                        
 END OPSÆT_CDIFLEJE;                                                    
                                                                        
 /*********************************************************************/
 /*      SKAF PERSONENS ENDELIGE LEJEVÆRDI FOR 1999, HVIS HAN IKKE ER */
 /*      OMFATTET AF GL. OVERGANGSORDNING.                            */
 /*********************************************************************/
 INDSÆT_LEJEVÆRDI:                                                      
 PROC;                                                                  
                                                                        
         IF BH601.PER_LEJEV(1).CREDTYP = 0                              
         THEN                                                           
            BH601.BLEJE1 = (BH601.PER_LEJEV(1).BELØB *                  
                            TÆLLER) / NÆVNER;                           
                            /* IKKE REDUCERET */                        
         ELSE                                                           
            DO;                                                         
               BH601.BLEJE2 = (BH601.PER_LEJEV(1).BELØB *               
                               TÆLLER) / NÆVNER;                        
                              /* REDUCERET */                           
               BH601.CP20_3500 = BH601.PER_LEJEV(1).CREDTYP;            
               BH601.CLIN06 = 1;                                        
            END;                                                        
                                                                        
 END INDSÆT_LEJEVÆRDI;                                                  
                                                                        
         %INCLUDE BH60111M; /* NULSTIL LEJEVÆRDI FELTER               */
         %INCLUDE BH60150M; /* TEST UNDTAGELSER LEJEVÆRDI EJENDOM     */
         %INCLUDE BH60155M; /* BEREGN LEJEVÆRDI EJENDOM               */
         %INCLUDE BH60156M; /* BEREGN EJENDOMS LEJEVÆRDIER REDUCERET  */
         %INCLUDE BH60160M; /* BEREGNING AF REDUKTION                 */
         %INCLUDE BH60158M; /* SKAF ENDELIGE EJENDOMS LEJEVÆRDIER     */
         %INCLUDE BH60151M; /* TEST UNDTAGELSER LEJEVÆRDI PERSON      */
         %INCLUDE BH60149M; /* SKAF PERSONLIG EJD-LEJEVÆRDI           */
                                                                        
 END BH60110;                                                           
