  PROCESS OPT(TIME);                                                    
 BH31900: /* Udvid BH31700N med BFENR og VUREJENDOMID   */              
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /********************************************************************/ 
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KMD A/S 2020');               
 /********************************************************************* 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH3170I FILE RECORD INPUT;     /* ESR UDTRÆK */                
 DCL     BH3190O FILE RECORD OUTPUT;    /* ESR UDTRÆK MED CEJFOER0107*/ 
 DCL     SYSPRINT FILE OUTPUT;                                          
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     BH317_IN        CHAR      (6000) VAR;                          
 DCL     1 BH317I         BASED     (ADDR(BH317_IN)),                   
           4 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH31700N;;                                          
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK OUTPUT                                 * 
 *********************************************************************/ 
 DCL     BH319_OUT       CHAR      (6000) VAR;                          
 DCL     1 BH319O         BASED     (ADDR(BH319_OUT)),                  
           4 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH31900N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                        /* TÆLLEVÆRKER TIL TOTALLISTEN */ 
           2 LÆS_BH317   FIXED DEC (7);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     EOF_BH317       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     HJ_DATOEN       PIC  '(8)9';                                   
 DCL     1 HJ_DATO       BASED(ADDR(HJ_DATOEN)),                        
           2 AAR         PIC  '9999',                                   
           2 MD          PIC  '99',                                     
           2 DG          PIC  '99';                                     
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     MOD             BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH31900L'),                                        
           2 F1          CHAR      (21)      INIT      (' '),           
           2 TEKST2      CHAR      (52)      INIT      (                
           'Udvid BH31700N med BFENR og VUREJENDOMID'),                 
           2 F2          CHAR      (21)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (63)      INIT      ((63)' ');       
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH317 = NEJ;                                               
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH87100L');             
            END;                                                        
                                                                        
         OPEN FILE (BH3170I)  TITLE ('BH31700I');                       
         OPEN FILE (BH3190O) TITLE ('BH31900O');                        
         ON ENDFILE (BH3170I)                                           
         BEGIN;                                                         
            EOF_BH317 = JA;                                             
         END;                                                           
         CALL ZZ20390 ('*OPEN','BH31901Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         TV = '';                                                       
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH317;                                                
                                                                        
         DO WHILE (EOF_BH317 = NEJ);                                    
            BH319O = BH317I, BY NAME;                                   
            BH319O.BFENR        = 0;                                    
            BH319O.VUREJENDOMID = 0;                                    
            BH319O.LÆNGDE = BH317I.LÆNGDE + 14;                         
                                                                        
            WRITE FILE (BH3190O) FROM (BH319_OUT);                      
            CALL LÆS_BH317;                                             
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS ESR UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_BH317:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH3170I) INTO (BH317_IN);                           
                                                                        
         IF EOF_BH317 = NEJ                                             
         THEN                                                           
            TV.LÆS_BH317 = TV.LÆS_BH317 + 1;                            
                                                                        
 END LÆS_BH317;                                                         
                                                                        
                                                                        
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
                                                                        
         CLOSE FILE (BH3170I),                                          
               FILE (BH3190O);                                          
                                                                        
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.LÆS_BH317;                                  
         DETLIN1.TEKST = 'LÆSTE INDIVIDER PÅ B.H31700D';                
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
 END TOTALLISTE;                                                        
                                                                        
 END  BH31900;                                                          
