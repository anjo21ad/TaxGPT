 BH65500:/*** AJOURFØR ÆGTEFÆLLLER TIL FØRTIDSPENS. M.FL.     ***/      
         PROC OPTIONS (MAIN);                                           
 /*********************************************************************/
 /*                                                                   */
 /*      NAVN            AJOURFØR CFØREFT KODER PÅ ÆGTEFÆLLER         */
 /*                                                                   */
 /*      FUNKTION        AT AJOURFØRE MT TIL FORSKUD OG SLUT EVS      */
 /*                      SÅLEDES AT ÆGTEFÆLLER TIL                    */
 /*                      FØRTIDSPENSIONISTER OG EFTERLØNSMODTAGERE    */
 /*                      M.V. OGSÅ BLIVER REDUCERET SOM DISSE.        */
 /*                      DERFOR OVERFØRES ÆGTEFÆLLENS EVT. KODE       */
 /*                      (CFØREFT).                                   */
 /*                                                                   */
 /*      INDDATA         B.H65301D    MINI MT TIL FORSKUD/SLUT        */
 /*                                   HOP. ORDEN                      */
 /*                                                                   */
 /*      UDDATA          B.H65500D    AJOURFØRT MINI-MT               */
 /*                      BH65501Y     KØRSELSKONTROLLISTE             */
 /*                      BH65502Y     FEJLLISTE                       */
 /*                                                                   */
 /*      PROGRAMMØR      AAGE RASMUSSEN    PUS NOV 1994               */
 /*                      (LAVET P.G.A. BH95500)                       */
 /*                                                                   */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PUV PUSLPS  JULI 1995      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    JULI 1996      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    JAN  1997      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    AUG  1997      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    JULI 1998      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    AUG  1999      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    JUNI 2000      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    PFB SKAT    DEC. 2001      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    LSU T&S     AUG. 2001      */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN    LSU T&S     NOV. 2001      */
 /*      ÅRSTILRETTET    OLUF MØRK         LSU T&S     JUNI 2002      */
 /*      ÅRSTILRETTET    BO SCHMIDT        LSU T&S     JULI 2003      */
 /*      ÅRSTILRETTET    BO SCHMIDT        LSU T&S     JUN  2004      */
 /* FORSKUD06    SUSANNE KROGH VILLUMSEN               26/05-2005     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD07    BO SCHMIDT                            14/05-2006     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD08    BO SCHMIDT                            23/05-2007     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD09    BO SCHMIDT                            23/06-2008     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD10    GERD HOLM-PEDERSEN                    16/04-2009     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD11    GERD HOLM-PEDERSEN                    03/06-2010     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD12    GERD HOLM-PEDERSEN                    JUNI  2011     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD13    ESBEN BRODERSEN                       MAJ   2012     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD14    ESBEN BRODERSEN                       MAJ   2013     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD15    JØRGEN SAUGMANN                       JUNI  2014     */
 /*      ÅRSTILRETTET                                                 */
 /* FORSKUD16    Z6JNK.  ÅRSRULLET                        08.06.2015  */
 /* SLUT15 QEB ESBEN BRODERSEN                         MARTS 2016     */
 /*        UDVIDET BRUG BH65300DATO MACRO #01                         */
 /* FORSKUD17   ESBEN BRODERSEN  QEB                   JUNI  2016     */
 /*      KOMPILERET TIL FORSKUD 2017                                  */
 /* FORSKUD18   ESBEN BRODERSEN  QEB                   JUNI  2017     */
 /*      KOMPILERET TIL FORSKUD 2018                                  */
 /* FORSKUD19   ESBEN BRODERSEN  QEB                   MAJ   2018     */
 /*      KOMPILERET TIL FORSKUD 2019                                  */
 /* FORSKUD21   PER BRUNK        PBR                   MARTS 2020     */
 /*      KOMPILERET TIL FORSKUD 2021                                  */
 /* SLUT 2021   Elena Flygenring EFL                   Marts 2021     */
 /*             - BH65300M erstattes med BH55300M                     */
 /*             - BH65300N erstattes med BH65321N                     */
 /* FORSKUD23    Z6PBR  ÅRSRULLET mgl. for BH6532*N      12.10.2022   */
 /* SLUT24       Z6EBD  ÅRSRULLET                        17.09.2024   */
 /*              BH55600M erstatter BH55300M                          */
 /*********************************************************************/
         DEFAULT RANGE(*) STATIC UNALIGNED CONN;                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF FILER                                             */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH653I          FILE RECORD INPUT;                             
 DCL     BH655O          FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF INPUT FRA MT                                      */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 PERS1,                                                       
         % INCLUDE BH65325N;;  /* RUL */                                
 DCL     1 PERS2,                                                       
         % INCLUDE BH65325N;;  /* RUL */                                
 DCL     1 BH65399       BASED     (ADDR(PERS1)),                       
         % INCLUDE BH65399N;                                            
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ENTRY OG BUILTIN                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;              /* PROGRAM-KOMMUNIKATION */
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PROCNAME        BUILTIN;                                       
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 TV,                                                          
           2 ANT_LÆS         FIXED     (7),  /* ANTAL LÆSTE MT        */
           2 ANT_DUB         FIXED     (7),  /* ANTAL LÆSTE DUBL.DØDE */
           2 ANT_ÆGTF1_OK    FIXED     (7),  /* ANTAL DUB1 MED ÆGTF   */
           2 ANT_ÆGTF2_OK    FIXED     (7),  /* ANTAL DUB2 MED ÆGTF   */
           2 ANT_ÆGTF1_UD    FIXED     (7),  /* ANTAL DUB1 UDEN ÆGTF  */
           2 ANT_ÆGTF2_UD    FIXED     (7),  /* ANTAL DUB2 UDEN ÆGTF  */
                                                                        
           2 ANT_PENS_FORRIGE_ÅR FIXED (7),  /* ANTAL LÆST PENSIONIST */
                                                                        
           2 ANT_PENS_AKT_ÅR FIXED     (7),  /* ANTAL LÆST PENSIONIST */
                                                                        
           2 ANT_AJOUR_FORRIGE_ÅR FIXED (7), /* ANTAL AJOURFØRTE      */
                                                                        
           2 ANT_AJOUR_AKT_ÅR FIXED    (7),  /* ANTAL AJOURFØRTE      */
           2 ANT_SKR         FIXED     (7);  /* ANTAL SKREVNE MT      */
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*                                                                   */
 /*********************************************************************/
 DCL     EOF_BH653       BIT       (01)      INIT      ('0'B);          
 DCL     GL_NØGLE        DEC FIXED (11)      INIT      (0);             
 DCL     NY_NØGLE        DEC FIXED (11)      INIT      (0);             
 DCL     HJ_CPR          PIC       '(10)9';                             
 DCL     1 HJ_CPR1       BASED(ADDR(HJ_CPR)),                           
           2 F1          CHAR      (04),                                
           2 ÅR          PIC       '99',                                
           2 LBNR        PIC       '9',                                 
           2 F2          CHAR      (2),                                 
           2 CHKCIF      PIC       '9';                                 
 DCL     C67_ÅRIG        CHAR      (01);                                
 DCL     1 BH55600M,     /* EVS STD. MACRO */                           
           %INCLUDE BH55600M; /* OPSÆT AKTUELT INDKOMSTÅR M.V. */       
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*                                                                   */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH65500L'),                                        
           2 F1          CHAR      (18)      INIT      (' '),           
           2 TEKST2      CHAR      (55)      INIT      (                
           'AJOURFØRING AF ÆGTEFÆLLER TIL FØRTIDSPENSIONISTER M.FL.'),  
           2 F2          CHAR      (18)      INIT      (' '),           
           2 TEKST3      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (43)      INIT      (' '),           
           2 TEKST1      CHAR      (89)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F1          CHAR      (68)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /*********************************************************************/
 /*                                                                   */
 /*      HOUSEKEEPING                                                 */
 /*                                                                   */
 /*********************************************************************/
         CALL ZS61900;                            /* UDSKRIV SSI-DATO */
         ON ERROR                                                       
            CALL PLIDUMP('SNHOB');                                      
         ON ENDFILE (BH653I)                                            
            EOF_BH653 = '1'B;                                           
         OPEN FILE (BH653I) TITLE ('BH65301I');                         
         OPEN FILE (BH655O) TITLE ('BH65500O');                         
         CALL ZZ20390 ('*OPEN','BH65501Y');                             
         CALL ZZ20390 ('*OPEN','BH65502Y');                             
         TV = '';                                                       
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
 /*********************************************************************/
 /*                                                                   */
 /*      PROCESS                                                      */
 /*                                                                   */
 /*********************************************************************/
         CALL LÆS_MT ('1');                                             
         CALL LÆS_MT ('2');                                             
         DO WHILE (^EOF_BH653);                                         
            IF PERS1.DSORP = PERS2.DSORP                                
            THEN                                                        
               DO;                                                      
                  CALL OPSÆT_KODE;                                      
                  IF PERS1.CCIVS = 'D' !                                
                     PERS2.CCIVS = 'D'                                  
                  THEN                                                  
                     CALL KODE_DØD_ÆGTF;                                
                  CALL SKRIV_PERSON1;                                   
                  CALL SKRIV_PERSON2;                                   
                  CALL LÆS_MT ('1');                                    
                  CALL LÆS_MT ('2');                                    
               END;                                                     
            ELSE                                                        
               DO;                                                      
                  /* SKRIV EN ENLIG */                                  
                  CALL SKRIV_PERSON1;                                   
                  PERS1 = PERS2;                                        
                  CALL LÆS_MT ('2');                                    
               END;                                                     
         END;                                                           
         IF ANT_LÆS > (ANT_SKR + ANT_ÆGTF1_UD + ANT_ÆGTF2_UD) &         
            PERS1.DCPRN < 99999999999                                   
         THEN                                                           
            DO;                                                         
               /* PERSON 1 IKKE SKREVET */                              
               WRITE FILE (BH655O) FROM (PERS1);                        
               ANT_SKR = ANT_SKR + 1;                                   
            END;                                                        
         CALL KONTROLLISTE;                                             
         CALL MASK_AFSTEMNING;                                          
 /*********************************************************************/
 /*                                                                   */
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*                                                                   */
 /*********************************************************************/
         CALL ZZ20300(' ','99');                                        
         CLOSE FILE (BH653I);                                           
         CLOSE FILE (BH655O);                                           
 /*********************************************************************/
 /*                                                                   */
 /*      LÆSNING AF MT                                                */
 /*                                                                   */
 /*********************************************************************/
 LÆS_MT:                                                                
         PROC (CLÆS);                                                   
 DCL     CLÆS            CHAR      (01);                                
         IF CLÆS = '1'                                                  
         THEN                                                           
            DO;                                                         
               READ FILE (BH653I) INTO (PERS1);                         
               NY_NØGLE = PERS1.DSORP;                                  
               IF PERS1.CFØREFT(BH55600M.SLUTÅR) > 0 /*#01 fra 2015*/   
               THEN                                                     
                  ANT_PENS_FORRIGE_ÅR = ANT_PENS_FORRIGE_ÅR + 1;        
               IF PERS1.CFØREFT(BH55600M.FORSKÅR) > 0 /*#01 fra 2016 */ 
               THEN                                                     
                  ANT_PENS_AKT_ÅR = ANT_PENS_AKT_ÅR + 1;                
               IF PERS1.CDUBLET = '2'                                   
               THEN                                                     
                  ANT_DUB = ANT_DUB + 1;                                
            END;                                                        
         ELSE                                                           
            DO;                                                         
               READ FILE (BH653I) INTO (PERS2);                         
               NY_NØGLE = PERS2.DSORP;                                  
               IF PERS2.CFØREFT(BH55600M.SLUTÅR) > 0  /*#01 fra 2015*/  
               THEN                                                     
                  ANT_PENS_FORRIGE_ÅR = ANT_PENS_FORRIGE_ÅR + 1;        
               IF PERS2.CFØREFT(BH55600M.FORSKÅR) > 0 /*#01 fra 2016*/  
               THEN                                                     
                  ANT_PENS_AKT_ÅR = ANT_PENS_AKT_ÅR + 1;                
               IF PERS2.CDUBLET = '2'                                   
               THEN                                                     
                  ANT_DUB = ANT_DUB + 1;                                
            END;                                                        
         IF ^EOF_BH653 & NY_NØGLE ^= 99999999999                        
         THEN                                                           
            ANT_LÆS = ANT_LÆS + 1;                                      
         IF NY_NØGLE < GL_NØGLE & ^EOF_BH653                            
         THEN DO;                                                       
            PUT SKIP LIST('GL = '!!GL_NØGLE!!'   NY = '!!NY_NØGLE);     
            PUT SKIP LIST('                                      ');    
            PUT SKIP LIST('                                      ');    
            PUT SKIP LIST('                                      ');    
            PUT SKIP LIST('                                      ');    
            PUT SKIP LIST('                                      ');    
            PUT SKIP LIST('                                      ');    
            CALL ZS30101(5,'BH65500 ** 250 SEKVENSFEJL PÅ B.H65301D',2);
         END;                                                           
         GL_NØGLE = NY_NØGLE;                                           
 END LÆS_MT;                                                            
 /*********************************************************************/
 /*                                                                   */
 /*      OPSÆTNING AF KODE                                            */
 /*                                                                   */
 /*********************************************************************/
 OPSÆT_KODE:                                                            
        PROC;                                                           
        /* OPSÆT KODE FOR OM DUBLERET DØD PERSON */                     
        /* HAR FUNDET EN MAKKER. INDIVIDET SKRI- */                     
        /* VES KUN HVIS MAKKEREN IKKE ER FUNDET  */                     
        IF PERS1.CDUBLET > '0'                                          
        THEN                                                            
           PERS1.CÆGTF_OK = '1';                                        
        IF PERS2.CDUBLET > '0'                                          
        THEN                                                            
           PERS2.CÆGTF_OK = '1';                                        
        /* EFTERFØLGENDE OPSÆTTES KUN HVIS PERSONERNE ER */             
        /* SAMBESKATTET I SLUT-ÅRET                      */             
        /* #01*/ /*#02 DER SPØRGES KUN PÅ SLUTÅR SOM I TIDLIGERE  */    
        /* VERSIONER                                              */    
        IF (PERS1.CSBSK(BH55600M.SLUTÅR) = '2' !                        
            PERS1.CSBSK(BH55600M.SLUTÅR) = '3' !                        
            PERS1.CSBSK(BH55600M.SLUTÅR) = '5' !                        
            PERS1.CSBSK(BH55600M.SLUTÅR) = '6') &                       
           (PERS2.CSBSK(BH55600M.SLUTÅR) = '2' !                        
            PERS2.CSBSK(BH55600M.SLUTÅR) = '3' !                        
            PERS2.CSBSK(BH55600M.SLUTÅR) = '5' !                        
            PERS2.CSBSK(BH55600M.SLUTÅR) = '6')                         
        THEN;                                                           
        ELSE                                                            
           RETURN; /*EJ SAMBESKATTET I SLUTÅRET */                      
        /* OPSÆT KODE FOR EFTERLØN/FØRTIDSPENSION */                    
        SELECT; /*#01*/                                                 
           WHEN (PERS1.CFØREFT(BH55600M.SLUTÅR)  > 0 &                  
                 PERS2.CFØREFT(BH55600M.SLUTÅR) = 0)                    
              DO;                                                       
                 PERS2.CFØREFT(BH55600M.SLUTÅR)  =                      
                     PERS1.CFØREFT(BH55600M.SLUTÅR) + 2;                
                 ANT_AJOUR_FORRIGE_ÅR = ANT_AJOUR_FORRIGE_ÅR + 1;       
              END;                                                      
           WHEN (PERS2.CFØREFT(BH55600M.SLUTÅR)  > 0 &                  
                 PERS1.CFØREFT(BH55600M.SLUTÅR) = 0)                    
              DO;                                                       
                 PERS1.CFØREFT(BH55600M.SLUTÅR)  =                      
                     PERS2.CFØREFT(BH55600M.SLUTÅR) + 2;                
                 ANT_AJOUR_FORRIGE_ÅR = ANT_AJOUR_FORRIGE_ÅR + 1;       
              END;                                                      
           OTHERWISE;                                                   
        END;                                                            
        /* EFTERFØLGENDE OPSÆTTES KUN HVIS PERSONERNE ER */             
        /* SAMBESKATTET I FORSKUDS-ÅRET                  */             
        /* #01 */                                                       
        IF (PERS1.CSBSK(BH55600M.FORSKÅR) = '2' !                       
            PERS1.CSBSK(BH55600M.FORSKÅR) = '3' !                       
            PERS1.CSBSK(BH55600M.FORSKÅR) = '5') &                      
           (PERS2.CSBSK(BH55600M.FORSKÅR) = '2' !                       
            PERS2.CSBSK(BH55600M.FORSKÅR) = '3' !                       
            PERS2.CSBSK(BH55600M.FORSKÅR) = '5')                        
        THEN;                                                           
        ELSE                                                            
           RETURN;                                                      
        SELECT;  /* #01 */                                              
           WHEN (PERS1.CFØREFT(BH55600M.FORSKÅR) > 0 &                  
                 PERS2.CFØREFT(BH55600M.FORSKÅR) = 0)                   
              DO;                                                       
                 PERS2.CFØREFT(BH55600M.FORSKÅR) =                      
                       PERS1.CFØREFT(BH55600M.FORSKÅR) + 2;             
                 ANT_AJOUR_AKT_ÅR    = ANT_AJOUR_AKT_ÅR + 1;            
              END;                                                      
           WHEN (PERS2.CFØREFT(BH55600M.FORSKÅR) > 0 &                  
                 PERS1.CFØREFT(BH55600M.FORSKÅR) = 0)                   
              DO;                                                       
                 PERS1.CFØREFT(BH55600M.FORSKÅR) =                      
                    PERS2.CFØREFT(BH55600M.FORSKÅR) + 2;                
                 ANT_AJOUR_AKT_ÅR    = ANT_AJOUR_AKT_ÅR + 1;            
              END;                                                      
           OTHERWISE;                                                   
        END;                                                            
 END OPSÆT_KODE;                                                        
 /*********************************************************************/
 /*      OPSÆT KODE FOR ÆGTEFÆLLE DØD I 2007 ELLER 2009 AF HENSYN TIL */
 /*      OPSÆTNINGEN AF KODE FOR ENKE EFTER (CENKE) 3 ELLER 4 SENERE  */
 /*      I PROGRAM BH94300.                                           */
 /*      DOG  I K K E  HVIS ÆGTEFÆLLERNE VAR SEPARERET.               */
 /*********************************************************************/
 KODE_DØD_ÆGTF:                                                         
                                                                        
        PROC;                                                           
        IF PERS1.CSBSK(BH55600M.GLSLUTÅR) = '4' !    /*#01*/            
           PERS1.CSBSK(BH55600M.SLUTÅR)   = '4' !    /*#01*/            
           PERS2.CSBSK(BH55600M.GLSLUTÅR) = '4' !    /*#01*/            
           PERS2.CSBSK(BH55600M.SLUTÅR)   = '4'      /*#01*/            
        THEN                                                            
           RETURN; /* HVIS SEPARERET */                                 
        SELECT;                                                         
           WHEN (PERS1.CCIVS = 'E' ! PERS1.CCIVS = 'L')                 
              DO;                                                       
                IF PERS2.CCIVS = 'D' &                                  
   /*#01*/         PERS2.DCIÆN(BH55600M.SLUTÅR) <= PERS1.DRGNÅRTIL      
                 THEN                                                   
                    DO;                                                 
                       HJ_CPR = PERS2.DCPRN;                            
                       /* HVIS DØD SKAL ALDER */                        
                       /* VÆRE PÅ DØDSDAGEN   */                        
   /*#01*/             IF ER_ALDERS_PENSIONIST                          
                            (PERS2.DCIÆN(BH55600M.SLUTÅR),HJ_CPR)       
                       THEN                                             
                          C67_ÅRIG = '1';                               
                       ELSE                                             
                          C67_ÅRIG = '0';                               
                                                                        
    /*#01*/            IF PERS2.CFØREFT(BH55600M.GLSLUTÅR) > 0 !        
    /*#01*/               PERS2.CFØREFT(BH55600M.SLUTÅR) > 0            
                       THEN                                             
                          PERS1.CDØDÆGTF = '2' ;  /* ENKE EFTER FØRTP.*/
                       IF C67_ÅRIG = '1'                                
                       THEN                                             
                          PERS1.CDØDÆGTF = '1' ;  /* ENKE EFTER 67-ÅR */
                    END;                                                
              END;                                                      
           WHEN (PERS2.CCIVS = 'E' ! PERS2.CCIVS = 'L')                 
              DO;                                                       
                 IF PERS1.CCIVS = 'D' &                                 
                    PERS1.DCIÆN(BH55600M.SLUTÅR) <= PERS2.DRGNÅRTIL     
                 THEN                                                   
                    DO;                                                 
                       HJ_CPR = PERS1.DCPRN;                            
                       IF ER_ALDERS_PENSIONIST                          
                          (PERS1.DCIÆN(BH55600M.SLUTÅR),HJ_CPR)         
                       THEN                                             
                          C67_ÅRIG = '1';                               
                       ELSE                                             
                          C67_ÅRIG = '0';                               
                                                                        
                       IF PERS1.CFØREFT(BH55600M.GLSLUTÅR) > 0 !        
                          PERS1.CFØREFT(BH55600M.SLUTÅR) > 0            
                        THEN                                            
                          PERS2.CDØDÆGTF = '2' ;  /* ENKE EFTER FØRTP.*/
                       IF C67_ÅRIG = '1'                                
                       THEN                                             
                          PERS2.CDØDÆGTF = '1' ;  /* ENKE EFTER 67-ÅR */
                    END;                                                
              END;                                                      
           OTHERWISE;                                                   
        END;            /* SELECT */                                    
 END KODE_DØD_ÆGTF;                                                     
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      SKRIV PERSON1                                                */
 /*********************************************************************/
 SKRIV_PERSON1:                                                         
         PROC;                                                          
         IF PERS1.CDUBLET < '1'                                         
         THEN                                                           
            DO;                                                         
               /* PERSON1 ER IKKE EN DUBLET */                          
               WRITE FILE (BH655O) FROM (PERS1);                        
               ANT_SKR = ANT_SKR + 1;                                   
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* PERSON1 ER EN DUBLET */                               
               IF PERS1.CÆGTF_OK = '1'                                  
               THEN                                                     
                  DO;                                                   
                     /* PERSON1 FANDT SIN MAKKER */                     
                     WRITE FILE (BH655O) FROM (PERS1);                  
                     ANT_SKR = ANT_SKR + 1;                             
                     IF PERS1.CDUBLET = '1'                             
                     THEN                                               
                        ANT_ÆGTF1_OK = ANT_ÆGTF1_OK + 1;                
                     ELSE                                               
                        ANT_ÆGTF2_OK = ANT_ÆGTF2_OK + 1;                
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                     /* PERSON1 FANDT IKKE SIN MAKKER */                
                     IF PERS1.CDUBLET = '1'                             
                     THEN                                               
                        ANT_ÆGTF1_UD = ANT_ÆGTF1_UD + 1;                
                     ELSE                                               
                        ANT_ÆGTF2_UD = ANT_ÆGTF2_UD + 1;                
                  END;                                                  
            END;                                                        
 END SKRIV_PERSON1;                                                     
 /*********************************************************************/
 /*                                                                   */
 /*      SKRIV PERSON2                                                */
 /*********************************************************************/
 SKRIV_PERSON2:                                                         
         PROC;                                                          
         IF PERS2.CDUBLET < '1'                                         
         THEN                                                           
            DO;                                                         
               /* PERSON2 ER IKKE EN DUBLET */                          
               WRITE FILE (BH655O) FROM (PERS2);                        
               ANT_SKR = ANT_SKR + 1;                                   
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* PERSON2 ER EN DUBLET */                               
               IF PERS2.CÆGTF_OK = '1'                                  
               THEN                                                     
                  DO;                                                   
                     /* PERSON2 FANDT SIN MAKKER */                     
                     WRITE FILE (BH655O) FROM (PERS2);                  
                     ANT_SKR = ANT_SKR + 1;                             
                     IF PERS2.CDUBLET = '1'                             
                     THEN                                               
                        ANT_ÆGTF1_OK = ANT_ÆGTF1_OK + 1;                
                     ELSE                                               
                        ANT_ÆGTF2_OK = ANT_ÆGTF2_OK + 1;                
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                     /* PERSON2 FANDT IKKE SIN MAKKER */                
                     IF PERS2.CDUBLET = '1'                             
                     THEN                                               
                        ANT_ÆGTF1_UD = ANT_ÆGTF1_UD + 1;                
                     ELSE                                               
                        ANT_ÆGTF2_UD = ANT_ÆGTF2_UD + 1;                
                  END;                                                  
            END;                                                        
 END SKRIV_PERSON2;                                                     
 /*********************************************************************/
 /*                                                                   */
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*                                                                   */
 /*********************************************************************/
 KONTROLLISTE:                                                          
         PROC;                                                          
         SIDELIN.CCA    = ZZIK1;                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         OVERLIN1.CCA   = ZZWS2;                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         OVERLIN2.CCA   = ZZWS3;                                        
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST  = 'ANTAL LÆSTE PERSONER PÅ  B.H65301D';         
         DETLIN1.ANTAL = ANT_LÆS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST  = 'HERAF DUBLEREDE DØDE                       ';
         DETLIN1.ANTAL = ANT_DUB;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
                                                                        
         DETLIN1.TEKST  = 'ANTAL LÆSTE FØRTIDSPENSIONISTER ' !!         
         /*#01*/          'M.V. I ' !! BH55600M.INDKÅR1;                
         DETLIN1.ANTAL = ANT_PENS_FORRIGE_ÅR;                           
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
                                                                        
         DETLIN1.TEKST  = 'ANTAL LÆSTE FØRTIDSPENSIONISTER ' !!         
         /*#01*/          'M.V. I ' !! BH55600M.INDKÅR2;                
         DETLIN1.ANTAL = ANT_PENS_AKT_ÅR;                               
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS1;                                         
                                                                        
         DETLIN1.TEKST  = 'ANTAL ÆGTEFÆLLER AJOURFØRTE VEDR. ' !!       
         /*#01*/          BH55600M.INDKÅR1;                             
         DETLIN1.ANTAL  = ANT_AJOUR_FORRIGE_ÅR;                         
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
                                                                        
         DETLIN1.TEKST  = 'ANTAL ÆGTEFÆLLER AJOURFØRTE VEDR. ' !!       
         /*#01*/          BH55600M.INDKÅR2;                             
         DETLIN1.ANTAL  = ANT_AJOUR_AKT_ÅR;                             
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST  = 'ANTAL EJ SKREVNE DUBLET DSORP=DCPRN   ';     
         DETLIN1.ANTAL = ANT_ÆGTF1_UD;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST  = 'ANTAL EJ SKREVNE DUBLET DSORP=DHENVN  ';     
         DETLIN1.ANTAL = ANT_ÆGTF2_UD;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST  = 'ANTAL SKREVNE PERSONER PÅ B.H65500D';        
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST  = 'HERAF DUBLET MED DSORP=DCPRN       ';        
         DETLIN1.ANTAL = ANT_ÆGTF1_OK;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST  = 'HERAF DUBLET MED DSORP=DHENVN      ';        
         DETLIN1.ANTAL = ANT_ÆGTF2_OK;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      UDSKRIV FEJLLISTE VEDR. KONSTATERET AFSTEMNINGFEJL PÅ        */
 /*                                                                   */
 /*********************************************************************/
 FEJLLISTE:                                                             
         PROC(FEJL);                                                    
 DCL     FEJL            CHAR      (01);                                
         CALL ZZ20300(SIDELIN,'02');                                    
         CALL ZZ20300(OVERLIN1,'02');                                   
         OVERLIN2.TEKST1 = 'FEJLLISTE VEDR. MASKINEL AFSTEMNING';       
         CALL ZZ20300(OVERLIN2,'02');                                   
         IF FEJL = '1'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST  = 'ANTAL LÆSTE PERSONER PÅ  B.H65301D';   
               DETLIN1.ANTAL = ANT_LÆS;                                 
               CALL ZZ20300(DETLIN1,'02');                              
               DETLIN1.TEKST  =                                         
               'ANTAL PERSONER PÅ B.H65301D IFØLGE AFSTEMNINGSINDIVID'; 
               DETLIN1.ANTAL = BH65399.ANT_INDV;                        
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
         IF FEJL = '2'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST  =                                         
               'AFSTEMNINGSINDIVID MANGLER PÅ B.H65301D';               
               DETLIN1.ANTAL = '';                                      
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
 END FEJLLISTE;                                                         
 /*********************************************************************/
 /*                                                                   */
 /*      MASKINEL AFSTEMNING                                          */
 /*                                                                   */
 /*********************************************************************/
 MASK_AFSTEMNING:                                                       
         PROC;                                                          
         IF BH65399.DCPRN ^= 99999999999                                
         THEN                                                           
            DO;                                                         
               CALL FEJLLISTE('2');                                     
               CALL ZS30101(5,'BH65500 ** 253 ' !!                      
                    'MASKINEL KONSTATERET AFSTEMNINGSFEJL=2   ',2);     
            END;                                                        
         IF ANT_LÆS ^= BH65399.ANT_INDV                                 
         THEN                                                           
            DO;                                                         
               CALL FEJLLISTE('1');                                     
               CALL ZS30101(5,'BH65500 ** 253 ' !!                      
                    'MASKINEL KONSTATERET AFSTEMNINGSFEJL=1   ',2);     
            END;                                                        
         BH65399.ANT_INDV = ANT_SKR;                                    
         WRITE FILE (BH655O) FROM (PERS1);                              
 END MASK_AFSTEMNING;                                                   
 /* %INCLUDE BH65500M;     /* Udgået */                                 
 /*%INCLUDE BH65501M;     /* BEREGN/UNDERSØG EN PERSON'S ALDER */       
 %INCLUDE BH65502M;       /* BEREGN/UNDERSØG EN PERSON'S ALDER */       
 END BH65500;                                                           
