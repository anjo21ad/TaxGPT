 /*********************************************************************/
 /*      BH86072M:                                                   */ 
 /*      OPSÆT CEJBEVS (BEREGNINGSKODE) OG FEJLNR M.V. TIL EVS SLUT  */ 
 /*      VEDR. EJERENS PERSONLIGE FORHOLD                            */ 
 /*  SLUT05          SUSANNE KROGH VILLUMSEN            11/11-2005   */ 
 /*          TILRETTET SLUT 2005                                     */ 
 /*  SLUT06          BO SCHMIDT                         04/10-2006   */ 
 /*          TILRETTET SLUT 2006                                     */ 
 /*  SLUT07          BO SCHMIDT                         20/11-2007   */ 
 /*          TILRETTET SLUT 2007                                     */ 
 /*  SLUT08          BO SCHMIDT                         15/11-2008   */ 
 /*          TILRETTET SLUT 2008                                     */ 
 /*  SLUT12  LARS KAAE HARRITSHØJ                       06/11-2012   */ 
 /*          TILRETTET SLUT 2012                                     */ 
 /*  SLUT13  JØRGEN SAUGMANN                            08/10-2013   */ 
 /*          TILRETTET SLUT 2013                                     */ 
 /*                KRAV 18-02: FEJLKODE 5017 UDGåR UNDTAGEN HVIS     */ 
 /*                            EJERPCT. ER 0,00%                     */ 
 /*  Defect 90 Anders Pedersen                          06/12/2024   */ 
 /*------------------------------------------------------------------*/ 
 /* EVS SLUT-15.  JNK :  Ingen ændringer                             */ 
 /* EVS SLUT-16   QEB :  Indlagt fejl 5164 fra Forskud 2017 KS30     */ 
 /* EVS SLUT-17   QEB :  Rettet fejl 5059 da der ikke kan spørges    */ 
 /*                      MT.DCPRN = 0 DB2 løsningen                  */ 
 /* EVS SLUT-18   EBD :  Nyt fejlnr 5184 til defect 116              */ 
 /*                      Ændring til fejlnr 5017 defect 209          */ 
 /*                      så ejerprocent ikke hentes fra slut         */ 
 /*                      for 2 familiehuse                           */ 
 /* EVS SLUT-19   EBD : Ændring til fejlnummer 5017 nu må ejerandel  */ 
 /*                     gerne tages fra fra tidligere slut år for    */ 
 /*                     for at opsætte ejerprocent, men det bør      */ 
 /*                     allerede være sket i BH84300L for 2fam       */ 
 /* Slut2020    Ole Nielsen ONE KS02-34                  Nov 2020     */
 /*             Indsat fejlkode 5200                                  */
 /* Slut2021    Per Brunk PBR                            OKT 2021     */
 /*              D528/D530 fejlnummer 5200 nu også 158=7              */
 /*              Og åbnet op for at udenlandsk ejendomme              */
 /*              kan få fejlnummer 5200                               */
 /* Slut2022    Jan Jensen JJJ                           OKT 2022     */
 /*              D90 Ejendom ejet af forening fraflyttes              */
 /* Slut2022    Esben Brodersen EBD                      OKT 2022     */
 /*             D1060 fejl 5017 må ikke opsættes for 2familiehuse     */
 /* Slut2022    Jan Jensen JJJ                           OKT 2022     */
 /*              D1291 Negativt ejerskab                              */
 /* SLUT2024    Henrik Knigge HNK                        OKT 2023     */
 /*             Årsrul BQ --> BE                                      */
 /*********************************************************************/
 OPSÆT_PERSONLIGE_FEJLNR: PROC;                                         
                                                                        
         DCL  1 HJ_ADRFRADTO,                                           
                2 HJ_TIL_EVS_AAR CHAR (4) INIT ('0000'),                
                2 HJ_FILLER      CHAR (4) INIT ('0000');                
                                                                        
         SELECT;                                                        
                                                                        
           WHEN ( BE78_AREA.TILEVS_CINVPNR ^= '0' )                     
             DO;                                                        
                BE78_AREA.FEJLNR = 5020;                                
                BE78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           /* Defect 116 nyt fejlnummer til døde som har */             
           /* udlejnings ejendommme så disse ikke sendes til DXC */     
           WHEN ( BE76_AREA.CCIVS = 'D' &            /* EJER DØD FØR/ */
                 (BE76_AREA.HIST_OPD_DETTE_DCIÆN <= /* ELLER I INDKÅR*/ 
                  BE76_AREA.DRGNÅRTIL)  &                               
                  BE78_AREA.FEJLNR = 5024 )                             
             DO;                                                        
                BE78_AREA.FEJLNR = 5184;                                
                BE78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           WHEN ( BE76_AREA.CCIVS = 'D' &          /* EJER DØD FØR/ */  
                 (BE76_AREA.HIST_OPD_DETTE_DCIÆN <= /* ELLER I INDKÅR*/ 
                  BE76_AREA.DRGNÅRTIL)  &                               
                  BE78_AREA.FEJLNR ^= 5001 )                            
             DO;                                                        
                BE78_AREA.FEJLNR = 5004;                                
                                                                        
                IF (BE76_AREA.HIST_OPD_SIDSTE_CSBSK = '2' !             
                    BE76_AREA.HIST_OPD_SIDSTE_CSBSK = '4' !             
                    BE76_AREA.HIST_OPD_SIDSTE_CSBSK = '6')        &     
                   BE76_AREA.HIST_OPD_DETTE_DCIÆN >=                    
                                              BE76_AREA.DRGNÅRFRA  &    
                   BE76_AREA.HIST_OPD_DETTE_DCIÆN <=                    
                                              BE76_AREA.DRGNÅRTIL  &    
                   BE77_AREA.EVSSEJ_DCPRN > 0                           
                THEN                                                    
                  DO;                                                   
                    IF BE78_AREA.TILEVS_DOVTG >= BE76_AREA.DRGNÅRFRA    
                    THEN                                                
                       BE78_AREA.TILEVS_CEJBEVS = '0';                  
                     ELSE                                               
                       DO;                                              
                         IF BE77_AREA.EVSSEJ_SID_CEJBEVS = '4' !        
                            BE77_AREA.EVSSEJ_SID_CEJBEVS = '5' !        
                            BE77_AREA.EVSSEJ_SID_CEJBEVS = '6' !        
                            BE77_AREA.EVSSEJ_SID_CEJBEVS = '9'          
                         THEN                                           
                           BE78_AREA.TILEVS_CEJBEVS = '0';              
                         ELSE                                           
                           BE78_AREA.TILEVS_CEJBEVS =                   
                                 BE77_AREA.EVSSEJ_SID_CEJBEVS;          
                       END;                                             
                  END;                                                  
                ELSE                                                    
                  DO; /* CHECK MED SKAT */                              
                    BE78_AREA.TILEVS_CEJBEVS = '1';                     
                  END;                                                  
             END;                                                       
                                                                        
           WHEN ( BE78_AREA.TILEVS_A_EJ_CEJFORH = '50' !                
                  BE78_AREA.TILEVS_A_EJ_CEJFORH = '60' !                
                  BE78_AREA.TILEVS_A_EJ_CEJFORH = '70' !                
                  BE78_AREA.TILEVS_A_EJ_CEJFORH = '80' )                
                     /* Ejeren er offentlig dvs. komm./amt/stat */      
             DO;                                                        
                BE78_AREA.FEJLNR = 5020;                                
                BE78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           /*QEB ret test på om person findes på MT */                  
           /* BE76_AREA.DCPRN = 0 */                                    
           /*SLUT05  FLYTTET HERTIL*/                                   
           /*Der skal ikke spørges på X da de er ville indeholde */     
           /* DCPRN i fil systemmet                              */     
                                                                        
           WHEN (BE76_AREA.DB2FUNKTION = 'F' &  /* fiktiv række */      
                 BE78_AREA.EKOMNR ^= 881 &    /* EJER EJ PÅ MT + */     
                 BE78_AREA.TILEVS_A_EJ_DCPRCIR > 0 & /* EJER JVF.ESR */ 
                 BE78_AREA.FEJLNR ^= 5001 )       /* ELLER OPRETTET  */ 
             DO;                                  /* P.G.A. GL. SLUT */ 
                BE78_AREA.FEJLNR = 5059;                                
                BE78_AREA.TILEVS_CEJBEVS = '2';                         
             END;                                                       
                                                                        
           /* KS30 Forskud 2017 afvisning 5164 */                       
           WHEN (BE78_AREA.TILEVS_CENKE = '5'  &                        
                 BE78_AREA.TILEVS_HJ_67 ^= 'J'  &                       
                 ER_BEGRÆNSET_SKATTEPLIGTIG = NEJ)                      
             DO;                                                        
                BE78_AREA.FEJLNR = 5164;                                
                BE78_AREA.TILEVS_CEJBEVS = '2';                         
             END;                                                       
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BE78_AREA.FEJLNR ^= 0                                       
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BE78_AREA.TILEVS_C5060 = '5060' &                           
           (BE77_AREA.EVSSEJ_SID_GPCTEJER = 0 !                         
            BE77_AREA.EVSSEJ_SID_CNEDSL1 = '  ' !                       
            BE77_AREA.EVSSEJ_SID_CPTYPSLUT = ' ')                       
         THEN                                                           
           DO;                                                          
             /*    EJER OPRETTET MANGELFULDT P.G.A. GL. SLUT    */      
             BE78_AREA.FEJLNR = 5059;                                   
             BE78_AREA.TILEVS_CEJBEVS = '2';                            
             RETURN;                                                    
           END;                                                         
                                                                        
         /* Fejlnummer 5017 ejer procent fra ESR       */               
         /* regler omlagt til Slut 2020 EBD 12-11-2020 */               
         IF BE78_AREA.TILEVS_A_EJ_GANE > 10000                          
         THEN                                                           
           DO;                                                          
             BE78_AREA.TILEVS_A_EJ_GANE = 10000;                        
           END;                                                         
                                                                        
         IF BE78_AREA.TILEVS_A_EJ_GANE = 0  &                           
            BE78_AREA.EENHEDLBNR = 0 &                                  
            BE78_AREA.TILEVS_CEJBEVS  ^= 'B'                            
         THEN                                                           
           DO;                                                          
             BE78_AREA.FEJLNR = 5017;                                   
           END;                                                         
                                                                        
         IF BE78_AREA.FEJLNR = 5017 &                                   
            BE77_AREA.EVSSEJ_DCPRN > 0 &                                
          ((BE77_AREA.EVSSEJ_SID_CEJBEVS = '0' !                        
            BE77_AREA.EVSSEJ_SID_CEJBEVS = '4' !                        
            BE77_AREA.EVSSEJ_SID_CEJBEVS = '5' !                        
            BE77_AREA.EVSSEJ_SID_CEJBEVS = '6') &                       
            BE77_AREA.EVSSEJ_SID_CSLET < '1') &                         
            BE78_AREA.EJERSKAB_BBERGRL_SW = 0 &                         
            BE78_AREA.TILEVS_DOVTG < BE76_AREA.DRGNÅRFRA &              
            BE77_AREA.EVSSEJ_SID_GPCTEJER_SW = 0 &                      
           (BE78_AREA.TILEVS_GPCTEJER_SW = -1 !                         
           (BE78_AREA.TILEVS_GPCTEJER ^=                                
            BE77_AREA.EVSSEJ_SID_GPCTEJER))                             
         THEN                                                           
            DO;                                                         
               BE78_AREA.TILEVS_GPCTEJER =                              
               BE77_AREA.EVSSEJ_SID_GPCTEJER;                           
               BE78_AREA.TILEVS_GPCTEJER_SW = 0;                        
               BE78_AREA.TILFORSK_COPR_GPCTEJER = 'S';                  
               BE78_AREA.TILEVS_CDATAFRA = '3';                         
               BE78_AREA.FEJLNR = 0;                                    
            END;                                                        
                                                                        
         /* Defect 203                                              */  
         /* Hvis felt 158(CEJBEVS) er 0/4/C og 735 er 0 /NULL/blank */  
         /* skal der opsættes et fejlnummer 5200         ej n       */  
         IF ((BE78_AREA.TILEVS_CEJBEVS = '0' !                          
              BE78_AREA.TILEVS_CEJBEVS = '4' !                          
              BE78_AREA.TILEVS_CEJBEVS = '7' !      // D528/530         
              BE78_AREA.TILEVS_CEJBEVS = 'A' !      // D960             
              BE78_AREA.TILEVS_CEJBEVS = 'C'   ) &                      
             (BE78_AREA.TILEVS_GPCTEJER = 0  !                          
              BE78_AREA.TILEVS_GPCTEJER_SW = -1)   )                    
         THEN                                                           
           DO;                                                          
             BE78_AREA.FEJLNR = 5200;                                   
             BE78_AREA.TILEVS_CEJBEVS = '2';                            
             RETURN;                                                    
           END;                                                         
                                                                        
         /* Defect 90                                               */  
         IF BE78_AREA.TILEVS_DAFSTAA = 0 &                              
            BE78_AREA.DCPRN > 99999999 THEN                             
           DO;                                                          
             CALL HENT_ADRESSE;                                         
             HJ_VEJKOD_CH = DCLBE70000T.VEJKOD;                         
                                                                        
             SELECT;                                                    
                WHEN (ADRESSE_FUNDET = NEJ);                            
                /* Adresse fundet - enhedløbenr = 0 */                  
                WHEN (BE78_AREA.EENHEDLBNR = 0 &                        
                      BE78_AREA.EJENDOM_VEJK_EJD = HJ_VEJKOD_NUM &      
                      UPPERCASE(BE78_AREA.EJENDOM_HUSNR_EJD) =          
                                (UPPERCASE(DCLBE70000T.HUSNR) ||        
                                 UPPERCASE(DCLBE70000T.HUSBOGSTAADR)) & 
                      UPPERCASE(BE78_AREA.EJENDOM_ETAGE_EJD) =          
                                 UPPERCASE(DCLBE70000T.ETAGENR) &       
                      UPPERCASE(BE78_AREA.EJENDOM_SDØR_EJD)  =          
                                 UPPERCASE(DCLBE70000T.SIDEDOERNR));    
                /* Adresse fundet - enhedløbenr større end 0 */         
                WHEN (BE78_AREA.EENHEDLBNR > 0 &                        
                      BE78_AREA.EJENDOM_VEJK_ENHED = HJ_VEJKOD_NUM &    
                      UPPERCASE(BE78_AREA.EJENDOM_HUSNR_ENHED) =        
                                (UPPERCASE(DCLBE70000T.HUSNR) ||        
                                 UPPERCASE(DCLBE70000T.HUSBOGSTAADR)) & 
                      UPPERCASE(BE78_AREA.EJENDOM_ETAGE_ENHED) =        
                                 UPPERCASE(DCLBE70000T.ETAGENR) &       
                      UPPERCASE(BE78_AREA.EJENDOM_SDØR_ENHED)  =        
                                 UPPERCASE(DCLBE70000T.SIDEDOERNR));    
                /* Adresse fundet men ej match = fraflyttet */          
                OTHERWISE DO;                                           
                   HJ_ADRFRADTO = DCLBE70000T.ADRFRADTO;                
                   IF HJ_TIL_EVS_AAR = DCLBE70000T.TIL_EVS_AAR          
                   THEN                                                 
                      DO;                                               
                        BE78_AREA.FEJLNR = 5222;                        
                        BE78_AREA.TILEVS_CEJBEVS = '1';                 
                        RETURN;                                         
                      END;                                              
                   END;                                                 
                END; /* select */                                       
           END;                                                         
                                                                        
         /* Defect 1291 */                                              
         IF BE78_AREA.TILEVS_DAFSTAA < BE78_AREA.TILEVS_DOVTG &         
            BE78_AREA.TILEVS_DAFSTAA > 0 &                              
            BE78_AREA.TILEVS_DOVTG > 0                                  
         THEN                                                           
          DO;                                                           
            BE78_AREA.FEJLNR = 5224;                                    
            BE78_AREA.TILEVS_CEJBEVS = '1';                             
            RETURN;                                                     
          END;                                                          
                                                                        
 END OPSÆT_PERSONLIGE_FEJLNR;                                           
  /********************************************************************/
 /* UNDERSØG OM EN BORGER ER BEGRÆNSET SKATTEPLIGTIG. FINDES MT DATA */ 
 /* ANVENDES DE ELLERS UNDERSØGES SIDSTE EVS SLUT ÅR                 */ 
 /********************************************************************/ 
 ER_BEGRÆNSET_SKATTEPLIGTIG:PROC RETURNS(BIT(1));                       
                                                                        
   DCL HJBEGRÆNSET_BIT BIT(1);                                          
                                                                        
   HJBEGRÆNSET_BIT = NEJ;                                               
   HJBEGRÆNSET_TP = '0';                                                
                                                                        
   IF BE76_AREA.DCPRN > 0 &                                             
      BE76_AREA.HIST_OPD_DETTE_CSKPLOM > '0'                            
   THEN                                                                 
     DO;                                                                
       IF BE76_AREA.HIST_OPD_DETTE_CSKPLOM = '1' !                      
          BE76_AREA.HIST_OPD_DETTE_CSKPLOM = '3' !                      
          BE76_AREA.HIST_OPD_DETTE_CSKPLOM = '8'                        
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP = BE76_AREA.HIST_OPD_DETTE_CSKPLOM;           
         END;                                                           
     END;                                                               
   ELSE                                                                 
     DO;                                                                
       IF BE77_AREA.EVSSEJ_SID_CSKPLOMF = '1' !                         
          BE77_AREA.EVSSEJ_SID_CSKPLOMF = '3' !                         
          BE77_AREA.EVSSEJ_SID_CSKPLOMF = '8'                           
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP =                                             
                BE77_AREA.EVSSEJ_SID_CSKPLOMF;                          
         END;                                                           
     END;                                                               
                                                                        
   RETURN(HJBEGRÆNSET_BIT);                                             
                                                                        
 END ER_BEGRÆNSET_SKATTEPLIGTIG;                                        
