 BH65300:/*** AJOURFØR MINI MT-FIL TIL EVS FORSKUD ELLER EVS SLUT ***/  
         PROCEDURE (EXECPARM) OPTIONS (MAIN);                           
 /*********************************************************************/
 /*                                                                  **/
 /* NB.: ÅRSTILRETNING SKER ALMINDELIGVIS KUN I.F.M. EVS TIL FORSKUD  */
 /*      OG KRÆVER KUN OMKOMPILERING EFTER AT DIVERSE OPSÆTNINGER AF  */
 /*      KONSTANTER ER 'ÅRSRULLET' I MAKRO BH55300M SAMT AT STRUKTUR  */
 /*      BH65300N ER 'ÅRSRULLET' OG AT STRUKTUR BH65501N ER 'ÅRSRUL-  */
 /*      LET' D.V.S. ER SAT LIG MED BH65300N SOM DENNE SÅ UD VED      */
 /*      'GRUNDLEVERINGEN' FOR GL. SLUTÅR.                            */
 /*                                                                   */
 /*      ØNSKER OM ÆNDRET FUNKTIONALITET KRÆVER SELVFØLGELIG SPECIEL  */
 /*      TILRETTELSE.                                                 */
 /*                                                                   */
 /*      NAVN            AJOURFØR MINI MT-FIL TIL EVS FORSKUD ELLER   */
 /*                      EVS SLUT.                                    */
 /*                                                                   */
 /*      FUNKTION        AT AJOURFØRE MT TIL FORSKUD/SLUT MED OPLYS-  */
 /*                      NINGER FRA UDTRÆK FRA COR OG SOC. PENSIONER  */
 /*                      VEDRØRENDE FØRTIDSPENSIONISTER M.V.          */
 /*                      DISSE PERSONER FÅR OPSAT KODER HERFOR A.H.T. */
 /*                      BEREGNINGEN AF EJENDOMSVÆRDISKAT SAMT        */
 /*                      VED BESTEMMELSEN AF HVILKE PERSONER(EJERE)   */
 /*                      DER OPFYLDER BETINGELSERNE FOR AT BLIVE      */
 /*                      BEREGNET SOM 'HELÅRSBEBOER' AF SOMMERHUS.    */
 /*                                                                   */
 /*      INDDATA         B.H68300D    EVS UDTRÆK FRA FORSKUD IR GL. ÅR*/
 /*                                   EFTER REPARATION AF DHENVN M.V. */
 /*                      B.H64900D    UDTRÆK COR                      */
 /*                      B.H65000D    UDTRÆK FRA SLUT                 */
 /*                      B.H65201D    UDTRÆK PENS. SYSTEMET           */
 /*                      B.H65501D    MT-FIL FRA EVS SLUT SIDSTE ÅR   */
 /*                                                                   */
 /*      UDDATA          B.H65300D    AJOURFØRT MINI MT               */
 /*                      BH65301Y     KØRSELSKONTROLLISTE             */
 /*                      BH65302Y     FEJLLISTE VEDR. AFSTEMNING      */
 /*                                                                   */
 /*      PROGRAMMØR      AAGE RASMUSSEN  PUS      NOV  1994           */
 /*                                                                   */
 /*      ÅRSTILRETTET    AAGE RASMUSSEN  PUS SKAT    JUNI 2000        */
 /*            RETTET    AAGE RASMUSSEN  PUS SKAT    DEC. 2000        */
 /*            RETTET    AAGE RASMUSSEN  LSU T&S     AUG. 2001        */
 /*            RETTET    AAGE RASMUSSEN  LSU T&S     NOV. 2001        */
 /*            RETTET    OLUF MØRK       LSU T&S     JUNI 2002        */
 /*            RETTET    BO SCHMIDT      LSU T&S     JUNI 2004        */
 /*  FORSKUD06    SUSANNE KROGH VILLUMSEN                26/05-2005   */
 /*            TILRETTET FORSKUD 2006                                 */
 /*  SLUT05       SUSANNE KROGH VILLUMSEN                14/11-2005   */
 /*            TILRETTET SLUT 2005                                    */
 /*            MT-FILENS TABELLER HEDDER NU BG* ISTEDET FOR BW*       */
 /*  FORSKUD07    BO SCHMIDT                             14/05-2006   */
 /*            TILRETTET FORSKUD 2007                                 */
 /*  SLUT06       BO SCHMIDT                             04/10-2006   */
 /*            TILRETTET SLUT 2006                                    */
 /*            MT-FILEN ERSTATES MED UDTRÆK FRA SLUT.                 */
 /*  FORSKUD08    BO SCHMIDT                             23/05-2007   */
 /*            TILRETTET FORSKUD 2008                                 */
 /*  FORSKUD09    BO SCHMIDT                             23/06-2008   */
 /*            TILRETTET FORSKUD 2009                                 */
 /*  FORSKUD10    GERD HOLM-PEDERSN                      14/04-2009   */
 /*            TILRETTET FORSKUD 2010                                 */
 /*  SLUT09       GERD HOLM-PEDERSN                      10/11-2009   */
 /*               DRGNÅRTIL = 20090631; VAR DESVÆRRE BLEVET STJERNET  */
 /*               PGA DEN FORRIGES LINJE KOMMENTAR FORTSATTE          */
 /*  SLUT09       GERD HOLM-PEDERSEN        21-01-2010                */
 /*               RETTELSE IFB KRAV #18-03-1                          */
 /*               NU BRUGES DRGNÅRTIL TIL AT LÆSE P-DATA I BH94100    */
 /*               OG SKAL DERFOR VÆRE VALIDE DATOER (IKKE ALTID DD=31)*/
 /*  FORSKUD11    GERD HOLM-PEDERSN                      03/06-2010   */
 /*            TILRETTET FORSKUD 2011                                 */
 /*  FORSKUD12    GERD HOLM-PEDERSN                      24/06-2011   */
 /*            TILRETTET FORSKUD 2012                                 */
 /*  FORSKUD13    ESBEN BRODERSEN                        23/05-2012   */
 /*            TILRETTET FORSKUD 2013                                 */
 /*  FORSKUD14    ESBEN BRODERSEN                        21/05-2012   */
 /*            TILRETTET FORSKUD 2014                                 */
 /*  FORSKUD15    JØRGEN SAUGMANN                        27/06-2014   */
 /*            TILRETTET FORSKUD 2015                                 */
 /*            BR3 -> BR4                                             */
 /* FORSKUD16    Z6JNK.  ÅRSRULLET BR4 -> BR5            08.06.2015   */
 /* SLUT 2015  ESBEN BRODERSEN  Z8QEB                    09/03-2015   */
 /*            FEJLRETTELSE OG UDVIDET BRUG AF INCLUDE BH55300M #01   */
 /* FORSKUD17    Z8QEB  ÅRSRULLET BR5 -> BR6             12.06.2016   */
 /* FORSKUD18    Z8QEB  ÅRSRULLET BR6 -> BR7             05.06.2017   */
 /* FORSKUD19    Z8QEB  ÅRSRULLET BR7 -> BR8             23.08.2018   */
 /* Ny rutine BH65501M til stigene pensionsalder                      */
 /* FORSKUD21    Z6PBR  ÅRSRULLET                        23.03.2020   */
 /*            Rettet vedr. D462 Per Brunk               23.09.2020   */
 /*            Opsætning af DSORP ved CCIVS = 'G'                     */
 /* FORSKUD22    Z6PBR  ÅRSRULLET                        24.02.2021   */
 /* SLUT 2021    Z6EFL                                   Marts 2021   */
 /*              - BH65300M erstattes med BH55300M                    */
 /*              - BH65300N erstattes med BH65321N                    */
 /* FORSKUD23    Z6PBR  ÅRSRULLET                        08.06.2022   */
 /* FORSKUD23    Z6PBR  ÅRSRULLET mgl. for BH6532*N      11.10.2022   */
 /*              - D1173                                 28.11.2022   */
 /*                I OVERF_HISTOPL overføres DHENVN ikke hvis         */
 /*                sambeskat = 1 og civilstand ^= D                   */
 /* EVS 2024   Z6PQN                                     22.09.2021   */
 /*              - Forskudt regnskabsår fjernet fra 2024              */
 /*                Se Task FIXME for uklarhed - ok                    */
 /* EVS 2024   Z6EBD                                                  */
 /*              - Div årsrulnings rettelser                          */
 /*              BH65321N -> BH65324N                                 */
 /*              BH66501N -> BH65323N                                 */
 /*********************************************************************/
         DEFAULT RANGE(*) STATIC UNALIGNED CONN;                        
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF FILER                                             */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH683I          FILE RECORD INPUT;                             
 DCL     BH649I          FILE RECORD INPUT;                             
 DCL     BH650I          FILE RECORD INPUT;                             
 DCL     BH652I          FILE RECORD INPUT;                             
 DCL     BH655I          FILE RECORD INPUT;                             
 DCL     BH653O          FILE RECORD OUTPUT;                            
 DCL     SYSPRINT        EXTERNAL FILE PRINT;                           
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF INPUT                                             */
 /*      'NY' MT-FIL DANNET VED UDTRÆK FRA SENESTE FORSKUD IR         */
 /*      EFTER FORBEHANDLING VIA BH68000L OG BH68200L M.V.            */
 /*********************************************************************/
 DCL     1 BH683,                                                       
         %INCLUDE BH68001N;                                             
 DCL     1 BH68399       BASED     (ADDR(BH683)),                       
         %INCLUDE BR34259N;                          /*RUL FORSKUD23  */
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF GL. MT-FIL FRA GL. EVS SLUT INDKOMSTÅR            */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH655_AR CHAR (200) VAR;                                       
 DCL     1 BH655         BASED     (ADDR(BH655_AR)),                    
         3 LÆNGDE BIN FIXED (15),                                       
         %INCLUDE BH65323N;;      /*RUL FORSKUD 2024 */                 
 DCL     1 BH65599       BASED     (ADDR(BH655_AR)),                    
         2 LÆNGDE BIN FIXED (15),                                       
         %INCLUDE BH65599N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF INPUT FRA SENESTE COR INDKOMSTÅR                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH649,                                                       
         %INCLUDE BH64900N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF INPUT FRA SLUT                                    */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH650,                                                       
         %INCLUDE BH65000N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF INPUT FRA PENS. SYSTEM                            */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH652,                                                       
         %INCLUDE BH65200N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF AJOURFØRT MT-FIL                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH653,                                                       
         %INCLUDE BH65324N;;  /*RUL FORSKUD 2024 */                     
 DCL     1 BH65399,                                                     
         %INCLUDE BH65399N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ENTRY OG BUILTIN                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;              /* PROGRAM-KOMMUNIKATION */
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     VERIFY          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 TV,                                                          
           2 SCFRÅR      FIXED DEC (7),                                 
           2 FCFRÅR      FIXED DEC (7);                                 
 DCL     ANT_MT          FIXED     (7);       /* ANTAL LÆSTE MT       */
 DCL     ANT_GLMT        FIXED     (7);       /* ANTAL LÆSTE GL MT    */
 DCL     ANT_COR         FIXED     (7);       /* ANTAL LÆSTE COR      */
 DCL     ANT_SLUT        FIXED     (7);       /* ANTAL LÆSTE SLUT     */
 DCL     ANT_PENS        FIXED     (7);       /* ANTAL LÆSTE PENS     */
 DCL     ANT_SKR         FIXED     (7);       /* ANTAL SKREVNE MT     */
 DCL     ANT_DUB         FIXED     (7);       /* ANTAL SKREVNE DUBLET.*/
 DCL     ANT_BLOT_DØDE   FIXED     (7);       /* ANTAL SKREVNE DUBLET.*/
 DCL     ANT_AJOUR       FIXED     (7);       /* ANTAL AJOURFØRTE     */
 DCL     ANT_NYGIFTE     FIXED     (7);       /* ANTAL NYGIFTE        */
 DCL     ANT_TIDL_SAMB   FIXED     (7);       /* ANTAL TIDL. SAMBESKAT*/
 DCL     ANT_SKPL(0:9)   FIXED     (7);       /* ANTAL SKATTEPL.OMFANG*/
 DCL     MT_OPTALT       FIXED     (7);       /* ANTAL INDV. FRA MT   */
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*                                                                   */
 /*********************************************************************/
 DCL     NY_MT           PIC '(11)9';                                   
 DCL     GL_MT           PIC '(11)9'         INIT      (0);             
 DCL     NY_COR          PIC '(11)9';                                   
 DCL     GL_COR          PIC '(11)9'         INIT      (0);             
 DCL     NY_SLUT         PIC '(11)9';                                   
 DCL     GL_SLUT         PIC '(11)9'         INIT      (0);             
 DCL     NY_PENS         PIC '(11)9';                                   
 DCL     GL_PENS         PIC '(11)9'         INIT      (0);             
 DCL     GL_BH653        PIC '(11)9'         INIT      (0);             
 DCL     NY_BH655        PIC '(11)9';                                   
 DCL     GL_BH655        PIC '(11)9'         INIT      (0);             
 DCL     EOF_BH683       BIT       (01)      INIT      ('0'B);          
 DCL     EOF_BH649       BIT       (01)      INIT      ('0'B);          
 DCL     EOF_BH650       BIT       (01)      INIT      ('0'B);          
 DCL     EOF_BH652       BIT       (01)      INIT      ('0'B);          
 DCL     EOF_BH655       BIT       (01)      INIT      ('0'B);          
 DCL     AJOURFØRT       BIT       (01)      INIT      ('0'B);          
 DCL     SLUTINDV_BH655  BIT       (01)      INIT      ('0'B);          
 DCL     I               BIN FIXED (15)      INIT      (0);             
 DCL     J               BIN FIXED (15)      INIT      (0);             
 DCL     FEJL            PIC       '9'       INIT      (0);             
 DCL     TEST_PUT        BIT       (01)      INIT      ('0'B);          
 DCL     JA              BIT       (01)      INIT      ('1'B);          
 DCL     NEJ             BIT       (01)      INIT      ('0'B);          
 DCL     1 BH55300M,                                                    
           %INCLUDE BH55300M; /* OPSÆT AKTUELT INDKOMSTÅR M.V. */       
                                                                        
 DCL     HJ_SKPL         CHAR      (1);                                 
 DCL     HJ_SKPL_PIC     PIC       '9'       BASED(ADDR(HJ_SKPL));      
 DCL     HJ67            CHAR      (1);       /* KODE FOR 67-ÅRIGE    */
 DCL     HJHNV67         CHAR      (1);       /* KODE FOR ÆGTEFÆLLE 67*/
 DCL     HJ_CPR          PIC       '(10)9';                             
 DCL     1 HJ_CPR1       BASED(ADDR(HJ_CPR)),                           
           2 F1          CHAR      (04),                                
           2 ÅR          PIC       '99',                                
           2 LBNR        PIC       '9',                                 
           2 F2          CHAR      (2),                                 
           2 CHKCIF      PIC       '9';                                 
                                                                        
 DCL     ALDER           FIXED     (3);                                 
 DCL     PRDATO2         FIXED     (9);                                 
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*                                                                   */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH65300L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (54)      INIT      (                
           'AJOURFØRING MED PENSIONISTKODE M.V. PÅ MINI-MTFIL'),        
           2 F2          CHAR      (14)      INIT      (' '),           
           2 TEKST3      CHAR      (14)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (43)      INIT      (' '),           
           2 TEKST1      CHAR      (89)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (50),                                
           2 RCKOD       CHAR      (01)      INIT      (' '),           
           2 F1          CHAR      (02)      INIT      (' '),           
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (68)      INIT      (' ');           
 DCL     1 DETLIN2,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (50),                                
           2 F2          CHAR      (83)      INIT      (' ');           
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
         CALL ZS61900;                            /* UDSKRIV SSI-DATO */
         ON ERROR                                                       
            CALL PLIDUMP('SNHOB');                                      
         ON ENDFILE (BH683I)                                            
            BEGIN;                                                      
               EOF_BH683 = '1'B;                                        
               BH683.DCPRN = 9999999999;                                
            END;                                                        
         ON ENDFILE (BH649I)                                            
            BEGIN;                                                      
               EOF_BH649 = '1'B;                                        
               BH649.DCPRN = 9999999999;                                
            END;                                                        
         ON ENDFILE (BH650I)                                            
            BEGIN;                                                      
               EOF_BH650 = '1'B;                                        
               BH650.DCPRN = 9999999999;                                
            END;                                                        
         ON ENDFILE (BH652I)                                            
            BEGIN;                                                      
               EOF_BH652 = '1'B;                                        
               BH652.DCPRN = 9999999999;                                
            END;                                                        
         ON ENDFILE (BH655I)                                            
            BEGIN;                                                      
               EOF_BH655 = '1'B;                                        
               BH655.DCPRN = 9999999999;                                
            END;                                                        
         OPEN FILE (BH683I) TITLE ('BH68300I');                         
         OPEN FILE (BH649I) TITLE ('BH64900I');                         
         OPEN FILE (BH650I) TITLE ('BH65000I');                         
         OPEN FILE (BH652I) TITLE ('BH65201I');                         
         OPEN FILE (BH655I) TITLE ('BH65501I');                         
         OPEN FILE (BH653O) TITLE ('BH65300O');                         
         CALL ZZ20390 ('*OPEN','BH65301Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         TV = '';                                                       
         ANT_MT     = 0;                                                
         ANT_GLMT   = 0;                                                
         ANT_COR    = 0;                                                
         ANT_PENS   = 0;                                                
         ANT_SLUT   = 0;                                                
         ANT_SKR    = 0;                                                
         ANT_DUB    = 0;                                                
         ANT_BLOT_DØDE  = 0;                                            
         ANT_AJOUR  = 0;                                                
         ANT_NYGIFTE = 0;                                               
         ANT_TIDL_SAMB = 0;                                             
         DO I = 0 TO 9;                                                 
            ANT_SKPL(I) = 0;                                            
         END;                                                           
         MT_OPTALT  = 0;                                                
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
         CALL LÆS_MT;                                                   
         CALL LÆS_COR;                                                  
         CALL LÆS_SLUT;                                                 
         CALL LÆS_PENS;                                                 
         CALL LÆS_GLMT;                                                 
         DO WHILE (^EOF_BH683);                                         
            DO WHILE ( BH649.DCPRN < BH683.DCPRN );                     
               CALL LÆS_COR;                                            
            END;                                                        
            DO WHILE ( BH652.DCPRN < BH683.DCPRN );                     
               CALL LÆS_PENS;                                           
            END;                                                        
            DO WHILE ( BH650.DCPRN < BH683.DCPRN );                     
               CALL LÆS_SLUT;                                           
            END;                                                        
            DO WHILE ( BH655.DCPRN < BH683.DCPRN );                     
               IF  GL_BH653 < BH655.DCPRN                               
                &  BH655.CCIVS = 'D'                                    
                & (BH655.HIST_OPL.DCIÆN(GLSLUTÅR) > 0                   
                &  BH655.HIST_OPL.DCIÆN(GLSLUTÅR) < ÅR0101)             
               THEN                                                     
                   CALL SKRIV_DØD;                                      
               CALL LÆS_GLMT;                                           
            END;                                                        
            /* HEREFTER AJOURFØRES NY MT-FIL FRA FORSKUD */             
            /* MED OPLYSNINGER FRA COR, PENS OG/ELLER    */             
            /* GL. MT-FIL FRA EVS SLUT GL. ÅR            */             
            AJOURFØRT = '0'B;                                           
            BH653 = '';                                                 
            IF BH683.CFØREFT = 1                                        
            THEN              /* 'NY' EFTERLØNSMODTAGER IFLG. FORSKUD */
               BH653.CFØRFORS = 1;                                      
            IF BH649.DCPRN = BH683.DCPRN &                              
               BH649.BEFTLØN > 0                                        
            THEN              /* EFTERLØNSMODTAGER IFLG. COR */         
               BH653.CFØRCOR = 1;                                       
            IF BH650.DCPRN = BH683.DCPRN                /*SLUT06*/      
            THEN              /* FORSKUDT REGNSKABSÅR IFLG SLUT  */     
               BH653.CFRÅR = BH650.CFRÅR;                               
            IF BH652.DCPRN = BH683.DCPRN                                
            THEN              /* PENSIONIST M.V. IFLG. PENS  */         
               BH653.CFØRPENS = 1;                                      
            IF BH653.CFØRFORS = 1 !                                     
               BH653.CFØRCOR  = 1 !                                     
               BH653.CFØRPENS = 1                                       
            THEN                                                        
               CALL OPSÆT_KODE;                                         
            IF BH653.CFRÅR >= '01'                                      
            THEN                                                        
               DO;                                                      
                  IF BH653.CFRÅR > '01'                                 
                  THEN                                                  
                     DO;                                                
                        BH653.CFRÅROPR = 'S';                           
                        TV.SCFRÅR = TV.SCFRÅR + 1;                      
                     END;                                               
               END;                                                     
            ELSE                                                        
               IF BH683.CRGN > '01'               /*SLUT05*/            
               THEN                                                     
                  DO;                                                   
                     BH653.CFRÅR = BH683.CRGN;    /*SLUT05*/            
                     BH653.CFRÅROPR = 'F';                              
                     TV.FCFRÅR = TV.FCFRÅR + 1;                         
                  END;                                                  
               ELSE                                                     
                  BH653.CFRÅR = '  ';                                   
            CALL OPSÆT_INDKOMSTÅR                                       
                 (BH653.CFRÅR,BH653.DRGNÅRFRA,BH653.DRGNÅRTIL);         
            CALL TEST_ALDER;                                            
            CALL REGULER_MT_OPL;                                        
            IF BH655.DCPRN = BH683.DCPRN                                
            THEN                                                        
               CALL OVERF_HISTOPL;                                      
            CALL SKRIV_MT;                                              
            CALL LÆS_MT;                                                
         END;   /* DO WHILE */                                          
                                                                        
         DO WHILE (^EOF_BH649);                                         
            CALL LÆS_COR;                                               
         END;                                                           
         DO WHILE (^EOF_BH650);                                         
            CALL LÆS_SLUT;                                              
         END;                                                           
         DO WHILE (^EOF_BH652);                                         
            CALL LÆS_PENS;                                              
         END;                                                           
         DO WHILE ( ^EOF_BH655 );                                       
            IF GL_BH653 < BH655.DCPRN &                                 
               BH655.CCIVS = 'D' &                                      
              (BH655.HIST_OPL.DCIÆN(GLSLUTÅR) > 0 &                     
               BH655.HIST_OPL.DCIÆN(GLSLUTÅR) < ÅR0101)                 
            THEN                                                        
               CALL SKRIV_DØD;                                          
            CALL LÆS_GLMT;                                              
         END;                                                           
         CALL KONTROLLISTE;                                             
         CALL MASK_AFSTEMNING;                                          
         CALL AFSLUT;                                                   
 /*********************************************************************/
 /*      LÆSNING AF MT                                                */
 /*********************************************************************/
 LÆS_MT: PROC;                                                          
                                                                        
         TEST_PUT = NEJ;                                                
                                                                        
         READ FILE (BH683I) INTO (BH683);                               
         IF BH68399.DCPRN > 3112999999  THEN                            
           DO;                                                          
             DO WHILE (EOF_BH683 = '0'B);                               
                IF BH68399.DCPRN >= 9999999999  THEN  /* OPTÆL.INDV. */ 
                  MT_OPTALT = BH68399.ANT_INDV;                         
                ELSE                                                    
                  ANT_MT = ANT_MT + 1;                                  
                READ FILE (BH683I) INTO (BH683);                        
              END;                                                      
           END;                                                         
                                                                        
   /*    IF BH683.DCPRN = 101310027 !                                   
            BH683.DCPRN = 101310078 !                                   
            BH683.DCPRN = 102421620 !                                   
            BH683.DCPRN = 103370132 !                                   
            BH683.DCPRN = 103370361                                     
         THEN                                                           
            TEST_PUT = JA; */                                           
                                                                        
         IF ^EOF_BH683  THEN                                            
            ANT_MT = ANT_MT + 1;                                        
                                                                        
         NY_MT = BH683.DCPRN;                                           
                                                                        
         IF NY_MT <= GL_MT THEN                                         
            CALL ZS30101                                                
            (5,'BH65300 ** 250 SEKVENSFEJL PÅ B.H68300D '!!NY_MT,2);    
                                                                        
         GL_MT = NY_MT;                                                 
 END LÆS_MT;                                                            
                                                                        
 /********************************************************************/ 
 /*      LÆSNING AF COR-UDTRÆK                                       */ 
 /********************************************************************/ 
 LÆS_COR: PROC;                                                         
         READ FILE (BH649I) INTO (BH649);                               
         IF ^EOF_BH649                                                  
         THEN                                                           
            ANT_COR = ANT_COR + 1;                                      
         NY_COR = BH649.DCPRN;                                          
         IF NY_COR < GL_COR                                             
         THEN                                                           
            CALL ZS30101(5,'BH65300 ** 251 SEKVENSFEJL PÅ B.H64900D',2);
         GL_COR = NY_COR;                                               
 END LÆS_COR;                                                           
 /*********************************************************************/
 /*      LÆSNING AF SLUT-UDTRÆK                                       */
 /*********************************************************************/
 LÆS_SLUT: PROC;                                                        
         IF ^EOF_BH650                                                  
         THEN                                                           
            DO;                                                         
               READ FILE (BH650I) INTO (BH650);                         
               IF ^EOF_BH650                                            
               THEN                                                     
                  ANT_SLUT = ANT_SLUT + 1;                              
            END;                                                        
         NY_SLUT = BH650.DCPRN;                                         
         IF NY_SLUT < GL_SLUT                                           
         THEN                                                           
            CALL ZS30101(5,'BH65300 ** 252 SEKVENSFEJL PÅ B.H65000D',2);
         GL_SLUT = NY_SLUT;                                             
 END LÆS_SLUT;                                                          
 /*********************************************************************/
 /*      LÆSNING AF PENS-UDTRÆK                                       */
 /*********************************************************************/
 LÆS_PENS: PROC;                                                        
         IF ^EOF_BH652                                                  
         THEN                                                           
            DO;                                                         
               READ FILE (BH652I) INTO (BH652);                         
               IF ^EOF_BH652                                            
               THEN                                                     
                  ANT_PENS = ANT_PENS + 1;                              
            END;                                                        
         NY_PENS = BH652.DCPRN;                                         
         IF NY_PENS < GL_PENS                                           
         THEN                                                           
            CALL ZS30101(5,'BH65300 ** 252 SEKVENSFEJL PÅ B.H65201D',2);
         GL_PENS = NY_PENS;                                             
 END LÆS_PENS;                                                          
 /*********************************************************************/
 /*     MT-FILEN FRA EVS SLUT SIDSTE ÅR LÆSES                         */
 /*********************************************************************/
 LÆS_GLMT: PROC;                                                        
         READ FILE (BH655I) INTO (BH655_AR);                            
         IF BH65599.DCPRN >= 9999999999   /* OPTÆLLINGSINDIVID */       
         THEN                                                           
            DO;                                                         
               SLUTINDV_BH655 = '1'B;                                   
               READ FILE (BH655I) INTO (BH655_AR);                      
            END;                                                        
         IF ^EOF_BH655                                                  
         THEN                                                           
            ANT_GLMT = ANT_GLMT + 1;                                    
         NY_BH655 = BH655.DCPRN;                                        
         IF NY_BH655 < GL_BH655                                         
         THEN                                                           
            CALL ZS30101(5,'BH65300 ** 254 SEKVENSFEJL PÅ B.H65501D',2);
         GL_BH655 = NY_BH655;                                           
 END LÆS_GLMT;                                                          
 /*********************************************************************/
 /*      OPSÆTNING AF KODE FOR FØRTIDSPENS./EFTERLØN                  */
 /*********************************************************************/
 OPSÆT_KODE: PROC;                                                      
        IF BH653.CFØRCOR = 1              /* COR VEDRØRER GLSLUTÅR */   
        THEN                                                            
           BH653.CFØREFT(GLSLUTÅR) = 2;                                 
        SELECT;                                                         
          WHEN (BH653.CFØRPENS = 1)                                     
            DO;                                                         
               BH653.CFØREFT(SLUTÅR) = 1;                               
               BH653.CFØREFT(FORSKÅR) = 1;                              
            END;                                                        
          OTHERWISE                                                     
            DO;                                                         
               BH653.CFØREFT(SLUTÅR) = 2;                               
               BH653.CFØREFT(FORSKÅR) = 2;                              
            END;                                                        
        END; /* SELECT */                                               
        ANT_AJOUR = ANT_AJOUR + 1;                                      
        AJOURFØRT = '1'B;                                               
                                                                        
 END OPSÆT_KODE;                                                        
 /*********************************************************************/
 /*     OPSÆT START- OG SLUTDATO FOR EN PERSONS INDKOMSTÅR            */
 /*     HØRENDE TIL AKTUELT EVS SLUT ÅR UNDER HENSYNTAGEN TIL AT      */
 /*     PERSONEN MÅSKE HAR FORSKUDT REGNSKABSÅR.                      */
 /*********************************************************************/
 OPSÆT_INDKOMSTÅR: PROC(CFRÅR,DRGNÅRFRA,DRGNÅRTIL);                     
                                                                        
 DCL     CFRÅR          CHAR      (2);                                  
 DCL     DRGNÅRFRA      FIXED     (9);                                  
 DCL     DRGNÅRTIL      FIXED     (9);                                  
                                                                        
        IF BH55300M.TIL_EVS_AAR >= 2024                                 
        THEN                                                            
          DO;                                                           
            DRGNÅRFRA = BH55300M.ÅR0101;   /*#01 SLUTÅR+0101*/          
            DRGNÅRTIL = BH55300M.ÅR1231;   /*#01 SLUTÅR+1231*/          
            CFRÅR = '  ';                                               
          END;                                                          
        ELSE       /* Til slut 2023 */                                  
           SELECT;                                                      
             WHEN ( CFRÅR = '01' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.ÅR0101;   /*#01 SLUTÅR+0101*/    
                  DRGNÅRTIL = BH55300M.ÅR1231;   /*#01 SLUTÅR+1231*/    
                  CFRÅR = '  ';                                         
               END;                                                     
             WHEN ( CFRÅR = '02' ) /*Fremad forskudt regnskab*/         
               DO;                                                      
                  DRGNÅRFRA = BH55300M.ÅR0201;      /*#01 SLUTÅR+0201*/ 
                  DRGNÅRTIL = BH55300M.FORSKÅR0131; /*#01 FORSKÅR+0131*/
               END;                                                     
             WHEN ( CFRÅR = '03' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0131; /*#01 GLSLUTÅR+0301*/  
                  DRGNÅRTIL = BH55300M.ÅR0229;   /*#01 SLUTÅR+0229  */  
               END;                                                     
             WHEN ( CFRÅR = '04' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0401; /*#01 GLSLUTÅR+0401*/  
                  DRGNÅRTIL = BH55300M.ÅR0331;   /*#01 SLUTÅR+0331  */  
               END;                                                     
             WHEN ( CFRÅR = '05' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0501; /*#01 GLSLUTÅR+0501*/  
                  DRGNÅRTIL = BH55300M.ÅR0430;   /*#01 SLUTÅR+0430 X*/  
               END;                                                     
             WHEN ( CFRÅR = '06' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0601; /*#01 GLSLUTÅR+0601*/  
                  DRGNÅRTIL = BH55300M.ÅR0531;   /*#01 SLUTÅR+0531  */  
               END;                                                     
             WHEN ( CFRÅR = '07' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0701; /*#01 GLSLUTÅR+0701*/  
                  DRGNÅRTIL = BH55300M.ÅR0630;   /*#01 SLUTÅR+0630  */  
               END;                                                     
             WHEN ( CFRÅR = '08' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0801; /*#01 GLSLUTÅR+0801*/  
                  DRGNÅRTIL = BH55300M.ÅR0731;   /*#01 SLUTÅR+0731  */  
               END;                                                     
             WHEN ( CFRÅR = '09' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR0901; /*#01 GLSLUTÅR+0901*/  
                  DRGNÅRTIL = BH55300M.ÅR0831;   /*#01 SLUTÅR+0831  */  
               END;                                                     
             WHEN ( CFRÅR = '10' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR1001; /*#01 GLSLUTÅR+1001*/  
                  DRGNÅRTIL = BH55300M.ÅR0930;   /*#01 SLUTÅR+0930  */  
               END;                                                     
             WHEN ( CFRÅR = '11' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR1101; /*#01 GLSLUTÅR+1101*/  
                  DRGNÅRTIL = BH55300M.ÅR1031;   /*#01 SLUTÅR+1031  */  
               END;                                                     
             WHEN ( CFRÅR = '12' )                                      
               DO;                                                      
                  DRGNÅRFRA = BH55300M.GLÅR1201; /*#01 GLSLUTÅR+1201*/  
                  DRGNÅRTIL = BH55300M.ÅR1130;   /*#01 SLUTÅR+1130  */  
               END;                                                     
             OTHERWISE                                                  
               DO;                                                      
                  DRGNÅRFRA = BH55300M.ÅR0101;   /*#01 SLUTÅR+0101*/    
                  DRGNÅRTIL = BH55300M.ÅR1231;   /*#01 SLUTÅR+1231*/    
                  CFRÅR = '  ';                                         
               END;                                                     
           END;        /* SELECT */                                     
                                                                        
 END OPSÆT_INDKOMSTÅR;                                                  
 /*********************************************************************/
 /* KONTROLLER OM AKTUEL PERSON OG/ELLER ÆGTEFÆLLE ER FYLDT 65/67     */
 /* I ÅR, IDET SAMBESKATNINGSKODE 5 SKAL ÆNDRES TIL 1, HVIS DET       */
 /* IKKE ER TILFÆLDET (SPECIEL REGEL FOR EVS).                        */
 /*                                                                   */
 /* FOR AT VÆRE OMFATTET AF REGLER FOR REDUKTION AF FØRTIDS- ELLER    */
 /* INVALIDEPENSIONISTER, SKAL MAN VÆRE MELLEM 60 OG 64/66 ÅR, DERFOR */
 /* NULSTILLES CFØREFT KODEN HVIS MAN ER UNDER 60 ELLER OVER 64/66 ÅR.*/
 /*                                                                   */
 /* KODEN CPENS OPSÆTTES FOR PENSIONISTER OVER 64/66 ÅR ELLER         */
 /* UNDER 60 ÅR A.H.T. EVT. OVERSENDELSE AF TRANS TIL ESR.            */
 /*********************************************************************/
 TEST_ALDER: PROC;                                                      
                                                                        
         /* ER ALDER 65/67 ÅR ELLER DEROVER? */                         
         HJ_CPR = BH683.DCPRN;                /* FØRST AKTUEL PERSON */ 
         HJ67 = '0';                                                    
                                                                        
         IF BH683.CCIVS = 'D' &                                         
            BH683.DCIÆN <= BH653.DRGNÅRTIL                              
         THEN                                                           
           DO; /*  HVIS DØD I SLUTÅRET SKAL ALDER VÆRE PÅ DØDSDAGEN */  
             IF ER_ALDERS_PENSIONIST(BH683.DCIÆN,HJ_CPR)                
             THEN                                                       
               HJ67 = '1';                                              
           END;                                                         
         ELSE                                                           
           DO; /* ELLERS SKAL ALDER VÆRE */                             
                /* PR. 31.12. I AKTUELT REGNSKABSÅR */                  
             IF ER_ALDERS_PENSIONIST                                    
               (BH653.DRGNÅRTIL,HJ_CPR)                                 
             THEN                                                       
               DO;                                                      
                 HJ67 = '1';                                            
                 BH653.CPENS = '1'; /*A.H.T. EVT. TRANS TIL ESR*/       
               END;                                                     
           END;                                                         
                                                                        
         HJHNV67 = '0';                        /* DEREFTER ÆGTEFÆLLEN */
                                                                        
         IF BH683.DHENVN > 0 &                                          
            BH683.CSTATUS ^= '2'                                        
         THEN                                                           
            DO;                                                         
               HJ_CPR = BH683.DHENVN;                                   
               IF (BH683.CCIVS = 'E' !                                  
                   BH683.CCIVS = 'L') &                                 
                   BH683.DCIÆN <= BH653.DRGNÅRTIL                       
               THEN                                                     
                 DO; /*   HVIS DØD SKAL ALDER VÆRE PÅ DØDSDAGEN     */  
                     IF ER_ALDERS_PENSIONIST(BH683.DCIÆN,HJ_CPR)        
                     THEN                                               
                        HJHNV67 = '1';                                  
                 END;                                                   
               ELSE                                                     
                 DO;                                                    
                     /* HVIS IKKE ENKE SKAL ALDER VÆRE   */             
                     /* PR. 31.12. I AKTUELT REGNSKABSÅR */             
                     IF ER_ALDERS_PENSIONIST(BH653.DRGNÅRTIL,HJ_CPR)    
                     THEN                                               
                        HJHNV67 = '1';                                  
                 END;                                                   
            END;                                                        
                                                                        
         IF BH653.CFØREFT(SLUTÅR) = 0 & /* IKKE FØRTIDSPENSIONIST M.V */
            BH653.CFØREFT(FORSKÅR) = 0  /* I SLUT ELLER FORSKUD       */
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF HJ67 = '1'                                                  
         THEN                                                           
            DO;                                                         
               /* ER ALDERS PENSIONIST */                               
               BH653.CFØREFT(SLUTÅR) = 0;                               
               BH653.CFØREFT(FORSKÅR) = 0;                              
               IF AJOURFØRT                                             
               THEN                                                     
                  ANT_AJOUR = ANT_AJOUR - 1;                            
               RETURN;                                                  
            END;                                                        
                                                                        
         /* ER AKTUEL PERSONS ALDER < 60 ÅR ELLER 60-66 ÅR INCL.? */    
                                                                        
         HJ_CPR = BH683.DCPRN;                                          
                                                                        
         IF BH683.CCIVS = 'D'                                           
         THEN                                                           
            DO;     /*    HVIS DØD SKAL ALDER VÆRE PÅ DØDSDAGEN      */ 
               IF BH653.CFØREFT(SLUTÅR) > 0 &                           
                  BH683.DCIÆN <= BH653.DRGNÅRTIL &                      
                  HAR_EVS_FØRTIDS_PENSIONIST_ALDER(BH683.DCIÆN,HJ_CPR)  
               THEN;                                                    
               ELSE                                                     
                  DO;                                                   
                     BH653.CFØREFT(SLUTÅR)   = 0;                       
                     BH653.CFØREFT(GLSLUTÅR) = 0;                       
                  END;                                                  
                                                                        
               BH653.CFØREFT(FORSKÅR) = 0;                              
            END;                                                        
         ELSE                                                           
            DO;                                                         
              /* HVIS IKKE DØD SKAL ALDER VÆRE PR. 31.12  */            
              /* FØRST UNDERSØGES STATUS I AKTUELT REGNSKABSÅR */       
                                                                        
              IF BH653.CFØREFT(SLUTÅR) > 0 &                            
                 HAR_EVS_FØRTIDS_PENSIONIST_ALDER                       
                                    (BH653.DRGNÅRTIL,HJ_CPR)            
              THEN;                                                     
              ELSE                                                      
                 BH653.CFØREFT(SLUTÅR) = 0;                             
                                                                        
              /*Hvis førtids pensionist men alder mindre end */         
              /*EVS førtids skatte alder                     */         
                                                                        
              IF ^HAR_EVS_FØRTIDS_PENSIONIST_ALDER                      
                                   (BH653.DRGNÅRTIL,HJ_CPR)     &       
                 ^ER_ALDERS_PENSIONIST(BH653.DRGNÅRTIL,HJ_CPR)  &       
                 BH653.CFØRPENS = 1                                     
              THEN                                                      
                 BH653.CPENS = '1';                                     
                     /* A.H.T. EVT. TRANS TIL ESR */                    
                     /* OG SÅ UNDERSØGES STATUS I AKTUELT FORSKUDSÅR */ 
                                                                        
              PRDATO2 = BH653.DRGNÅRTIL + 10000;                        
                                                                        
              IF BH653.CFØREFT(FORSKÅR) > 0 &                           
                 HAR_EVS_FØRTIDS_PENSIONIST_ALDER(PRDATO2,HJ_CPR)       
              THEN;                             /* 60-66 ÅR INCL. */    
              ELSE                                                      
                 BH653.CFØREFT(FORSKÅR) = 0;   /* IKKE 60-66 ÅR INCL. */
            END;                                                        
                                                                        
         IF BH653.CFØREFT(SLUTÅR) = 0 & /* IKKE FØRTIDSPENSIONIST M.V */
            BH653.CFØREFT(FORSKÅR) = 0  /* I SLUT ELLER FORSKUD       */
         THEN                                                           
            IF AJOURFØRT                                                
            THEN                                                        
               ANT_AJOUR = ANT_AJOUR - 1;                               
                                                                        
                                                                        
 END TEST_ALDER;                                                        
                                                                        
 /*********************************************************************/
 /*      MINI MT-FIL TIL BEREGNING AJOURFØRES MED OPLYSNINGERNE FRA   */
 /*      FORSKUD IR REGULERET FOR CIVILSTANDSÆNDRINGER                */
 /*********************************************************************/
 REGULER_MT_OPL: PROC;                                                  
         IF BH683.CRGN > '01'                                           
         THEN                                                           
            BH653.CRGN   = BH683.CRGN;                                  
         IF BH683.DCIÆN >= GLÅR0901                                     
          & BH683.DCIÆN <= ÅR1231                                       
         THEN                                                           
            DO;                                                         
               /* RET EVT. SAMBESKATNINGSKODE HVIS */                   
               /* PERSONEN HAR ÆNDRET CIVILSTAND   */                   
               IF (BH683.CCIVS    = 'G'                                 
                !  BH683.CCIVS    = 'P')                                
                &  BH683.DHENVN   >  0                                  
                &  BH683.CSTATUS ^= '2'                                 
               THEN                                                     
                  DO;                                                   
                     IF  BH683.CSBSK < '2'                              
                      !  BH683.CSBSK = '4'                              
                     THEN                                               
                        DO;            /*       HVIS NYGIFT     */      
                           BH683.CSBSK = '3';                           
                           ANT_NYGIFTE = ANT_NYGIFTE + 1;               
                        END;                                            
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                     IF BH683.CSBSK = '2' !                             
                        BH683.CSBSK = '3' !                             
                        BH683.CSBSK = '5' !                             
                        BH683.CSBSK = '6'                               
                     THEN                                               
                        DO;            /*    HVIS IKKE LÆNGERE GIFT  */ 
                           BH683.CSBSK = '1';                           
                           ANT_TIDL_SAMB = ANT_TIDL_SAMB + 1;           
                        END;                                            
                  END;                                                  
            END;                                                        
         IF BH683.CSBSK = '5'                                           
         THEN                                                           
            DO;                                                         
               IF HJ67    = '1'                                         
                ! HJHNV67 = '1'                                         
               THEN;                                                    
               ELSE                                                     
                  BH683.CSBSK = '1';                                    
            END;                                                        
         IF (BH683.CCIVS = 'D' & BH683.DCIÆN < GLÅR0901) &              
            BH683.CSBSK ^= '4'                                          
         THEN                                                           
            /* DØDE FØR 01.09.XXXX ER BEHANDLET I EVS SLUT GL. ÅR */    
            BH683.CSBSK = '1';                                          
         BH653.DCPRN   = BH683.DCPRN;                                   
         BH653.CCIVS   = BH683.CCIVS;                                   
         BH653.CSTATUS = BH683.CSTATUS;                                 
         BH653.BPENSIND = BH683.BPENSIND;                               
         BH653.BPENSGR  = BH683.BPENSGR;                                
         BH653.COVF_DHE = BH683.COVF_DHE;                               
         BH653.FØROPL.DCIÆN_GL = BH683.FØROPL.DCIÆN_GL;                 
         BH653.FØROPL.CCIVS_GL = BH683.FØROPL.CCIVS_GL;                 
         BH653.DHENVN(SLUTÅR)  = BH683.DHENVN;                          
         BH653.CSBSK(SLUTÅR)   = BH683.CSBSK;                           
         BH653.CSKPLOM(SLUTÅR) = BH683.CSKPLOM;                         
                                                                        
         IF BH683.CSKPLOM >= '0'                                        
         THEN                                                           
            DO;                                                         
               HJ_SKPL = BH683.CSKPLOM;                                 
               I = HJ_SKPL_PIC;                                         
               ANT_SKPL(I) = ANT_SKPL(I) + 1;                           
            END;                                                        
                                                                        
         BH653.DCIÆN(SLUTÅR) = BH683.DCIÆN;                             
         IF BH683.CSTATUS ^= '2'                                        
         THEN DO;                                                       
            BH653.DHENVN(FORSKÅR) = BH683.DHENVN;                       
            if test_put                                                 
            then do;                                                    
               put skip list ('naeste_dhenvn overført fra' !!           
                             ' dette_dhenvn da cstatus ikke er 2');     
            end;                                                        
         END;                                                           
                                                                        
         IF BH683.CSBSK ^= '6'                                          
         THEN                                                           
            BH653.CSBSK(FORSKÅR) = BH683.CSBSK;                         
         ELSE                                                           
            BH653.CSBSK(FORSKÅR) = '5'; /* ÆGTEFÆLLES ALDER SKAL    */  
                                        /* KUNNE OPSÆTTES I BH94100 */  
                                                                        
         BH653.CSKPLOM(FORSKÅR) = BH683.CSKPLOM;                        
         BH653.DCIÆN(FORSKÅR) = 0;                                      
         IF BH653.CCIVS = 'D' !                                         
            BH653.CCIVS = 'E' !                                         
            BH653.CCIVS = 'L' !                                         
            BH653.CCIVS = 'O' !                                         
            BH653.CCIVS = 'F'                                           
         THEN                                                           
            /* NULSTILLING AF AKTUELT FORSKUDSÅR HVOR BEREGNING SÅ  */  
            /* VIL BLIVE STYRET AF EVT. KODE FOR ENKE EFTER (CENKE) */  
            BH653.CSBSK(FORSKÅR) = '1';                                 
         /* GENEREL OPSÆTNING AF DSORP OG CPART        */               
         /* JVF. CIVILSTANDSKODE OG SAMBESKATNINGSKODE */               
         BH653.DSORP   = BH683.DCPRN;                                   
         BH653.CPART   = '1';                                           
         IF (BH683.CSBSK    = '2'                                       
          !  BH683.CSBSK    = '3'                                       
          !  BH683.CSBSK    = '5'                                       
          !  BH683.CSBSK    = '6')                                      
          &  BH683.DHENVN   >  0                                        
          &  BH683.CSTATUS ^= '2'                                       
         THEN                                                           
            DO;                                                         
               IF BH683.CCIVS = 'G' &                                   
                  BH683.DCPRN < BH683.DHENVN      /* D642 */            
               THEN                                                     
                  DO;              /* SOM HVIS EN REGISTRERET PARTNER */
                     BH653.DSORP = BH683.DHENVN;                        
                     BH653.CPART = '2';                                 
                  END;                                                  
                                                                        
               IF BH683.CCIVS = 'P' &                                   
                  BH683.DCPRN < BH683.DHENVN                            
               THEN                                                     
                  DO;                  /* HVIS EN REGISTRERET PARTNER */
                     BH653.DSORP = BH683.DHENVN;                        
                     BH653.CPART = '2';                                 
                  END;                                                  
            END;                                                        
 END REGULER_MT_OPL;                                                    
 /*********************************************************************/
 /*      OPLYSNINGER FRA GL. MT-FIL OVERFØRES TIL NY MT-FIL           */
 /*      OVERFØRSLEN SKER AF HENSYN TIL OPSÆTNING AF KODE FOR ENKE    */
 /*      EFTER SAMT OPSÆTNING AF 1/7-98-STATUS HERUNDER OVERFØRSEL    */
 /*      AF DENNE STATUS MELLEM NUVÆRENDE OG TIDLIGERE ÆGTEFÆLLER.    */
 /*********************************************************************/
 OVERF_HISTOPL: PROC;                                                   
                                                                        
         IF BH653.CSTATUS ^= '2'                                        
         THEN                                                           
            BH653.DHENVN(GLSLUTÅR)  = BH655.DHENVN(GLSLUTÅR);           
         BH653.CSBSK(GLSLUTÅR)      = BH655.CSBSK(GLSLUTÅR);            
         IF BH653.CFØREFT(GLSLUTÅR) = 0                                 
         THEN                                                           
            BH653.CFØREFT(GLSLUTÅR) = BH655.CFØREFT(GLSLUTÅR);          
         BH653.CSKPLOM(GLSLUTÅR)    = BH655.CSKPLOM(GLSLUTÅR);          
         BH653.DCIÆN(GLSLUTÅR)      = BH655.DCIÆN(GLSLUTÅR);            
         IF BH653.DHENVN(SLUTÅR)    = 0                                 
          & BH653.CSTATUS          ^= '2'                               
          & ( BH653.CSBSK(SLUTÅR)  ^= '1' !          /* D1173  */       
              BH653.CCIVS           = 'E' !          /* D1173  */       
              BH653.CCIVS           = 'D'  )         /* D1173  */       
         THEN                                                           
            BH653.DHENVN(SLUTÅR)    = BH655.DHENVN(GLSLUTÅR);           
                                                                        
 END OVERF_HISTOPL;                                                     
                                                                        
 /*********************************************************************/
 /*      SKRIV AJOURFØRT MT-FIL                                       */
 /*********************************************************************/
 SKRIV_MT: PROC;                                                        
                                                                        
         IF BH653.CCIVS = 'D' &                                         
           (BH653.DCIÆN(SLUTÅR) >= GLÅR0901 &                           
            BH653.DCIÆN(SLUTÅR) <= ÅR1231) &                            
            BH653.DHENVN(SLUTÅR) > 0 &                                  
            BH653.CSTATUS ^= '2'                                        
         THEN                                                           
            DO;                                                         
  /* DUBLERING AF DØDE MED HENVISNINGSNUMMER, (D.V.S. HVIS DØD I     */ 
  /* AKTUELT SLUTÅR) UDFØRES AF HENSYN TIL OPSÆTNING AF CFØREFT      */ 
  /* PÅ EFTERLEVENDE (OPSÆTTES I BH65500)                            */ 
               BH653.DSORP   = BH683.DCPRN;                             
               BH653.CDUBLET = 1;                                       
               WRITE FILE (BH653O) FROM (BH653);    /* DSORP = DCPRN */ 
               ANT_SKR = ANT_SKR + 1;                                   
               ANT_DUB = ANT_DUB + 1;                                   
               BH653.DSORP   = BH653.DHENVN(SLUTÅR);                    
               BH653.CDUBLET = 2;                                       
            END;                                                        
         WRITE FILE (BH653O) FROM (BH653);                              
         ANT_SKR = ANT_SKR + 1;                                         
                                                                        
         GL_BH653 = BH653.DCPRN;                                        
                                                                        
 END SKRIV_MT;                                                          
 /*********************************************************************/
 /*      SKRIV DØD                                                    */
 /*********************************************************************/
 SKRIV_DØD: PROC;                                                       
                                                                        
      BH653.DCPRN    = BH655.DCPRN;                                     
      BH653.CCIVS    = BH655.CCIVS;                                     
      BH653.CPENS    = BH655.CPENS;                                     
      BH653.CRGN     = BH655.CRGN;                                      
      BH653.DSORP    = BH655.DSORP;                                     
      BH653.CPART    = BH655.CPART;                                     
      BH653.CFRÅR    = BH655.CFRÅR;                                     
      BH653.CFRÅROPR = BH655.CFRÅROPR;                                  
      BH653.BPENSIND = BH655.BPENSIND;                                  
      BH653.BPENSGR  = BH655.BPENSGR;                                   
      BH653.CSTATUS  = 'D';                                             
      BH653.CDØDÆGTF = BH655.CDØDÆGTF;                                  
      BH653.CDUBLET  = BH655.CDUBLET;                                   
      BH653.CÆGTF_OK = BH655.CÆGTF_OK;                                  
      BH653.CFØRFORS = BH655.CFØRFORS;                                  
      BH653.CFØRCOR  = BH655.CFØRCOR;                                   
      BH653.CFØRPENS = BH655.CFØRPENS;                                  
      BH653.COVF_DHE = BH655.COVF_DHE;                                  
      BH653.DCIÆN_GL = BH655.DCIÆN_GL;                                  
      BH653.CCIVS_GL = BH655.CCIVS_GL;                                  
                                                                        
      BH653.HIST_OPL(GLSLUTÅR) = BH655.HIST_OPL(GLSLUTÅR), BY NAME;     
      BH653.HIST_OPL(SLUTÅR)   = BH655.HIST_OPL(GLSLUTÅR), BY NAME;     
      BH653.HIST_OPL(FORSKÅR)  = BH655.HIST_OPL(GLSLUTÅR), BY NAME;     
                                                                        
      CALL OPSÆT_INDKOMSTÅR                                             
           (BH653.CFRÅR,BH653.DRGNÅRFRA,BH653.DRGNÅRTIL);               
                                                                        
      WRITE FILE (BH653O) FROM (BH653);                                 
      ANT_SKR = ANT_SKR + 1;                                            
      ANT_BLOT_DØDE = ANT_BLOT_DØDE + 1;                                
                                                                        
      GL_BH653 = BH653.DCPRN;                                           
                                                                        
 END SKRIV_DØD;                                                         
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE: PROC;                                                    
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H68300D';          
         DETLIN1.ANTAL = ANT_MT;                                        
         CALL ZZ20300(DETLIN1,'01');                                    
         DO I = 0 TO 9;                                                 
            IF ANT_SKPL(I) > 0                                          
            THEN                                                        
               DO;                                                      
                  DETLIN1.CCA = ZZWS2;                                  
                  HJ_SKPL_PIC = I;                                      
                  DETLIN1.TEKST =                                       
                  'HERAF MED SKATTEPL.OMFANG ' !! HJ_SKPL;              
                  DETLIN1.ANTAL = ANT_SKPL(I);                          
                  CALL ZZ20300 (DETLIN1,'01');                          
               END;                                                     
         END;                                                           
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H64900D';          
         DETLIN1.ANTAL = ANT_COR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H65000D';          
         DETLIN1.ANTAL = ANT_SLUT;                                      
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H65201D';          
         DETLIN1.ANTAL = ANT_PENS;                                      
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H65501D';          
         DETLIN1.ANTAL = ANT_GLMT;                                      
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL SKREVNE PÅ         B.H65300D';          
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SCFRÅR;                                     
         DETLIN1.TEKST = 'HERAF OPSAT FORSKUDT REGNSKABSÅR I SLUT    '; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.FCFRÅR;                                     
         DETLIN1.TEKST = 'HERAF OPSAT FORSKUDT REGNSKABSÅR I FORSKUD '; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'HERAF AJOURFØRTE PÅ      B.H65300D';          
         DETLIN1.ANTAL = ANT_AJOUR;                                     
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'HERAF ÆNDRET TIL SAMBESKATTET (NYGIFTE)';     
         DETLIN1.ANTAL = ANT_NYGIFTE;                                   
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'HERAF ÆNDRET TIL EJ SAMBESKATTET';            
         DETLIN1.ANTAL = ANT_TIDL_SAMB;                                 
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST = 'HERAF DUBLEREDE DØDE';                        
         DETLIN1.ANTAL = ANT_DUB;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST = 'HERAF BLOT DØDE';                             
         DETLIN1.ANTAL = ANT_BLOT_DØDE;                                 
         CALL ZZ20300(DETLIN1,'01');                                    
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*      MASKINEL AFSTEMNING                                          */
 /*********************************************************************/
 MASK_AFSTEMNING: PROC;                                                 
                                                                        
         BH65399.DCPRN    = 99999999999;                                
         BH65399.FILLER1  = ' ';                                        
         BH65399.DSORP    = 99999999999;                                
         BH65399.FILLER2  = ' ';                                        
         BH65399.ANT_INDV = ANT_SKR;                                    
         WRITE FILE (BH653O) FROM (BH65399);     /* OPTÆLLINGSINDIVID */
         IF FEJL > 0                                                    
         THEN                                                           
            DO;                                                         
               CALL FEJLLISTE;                                          
               CALL ZS30101(5,'BH65300 ** 253 ' !!                      
                  'MASKINEL KONSTATERET AFSTEMNINGSFEJL=' !! FEJL !!    
                  '   ',2);                                             
            END;                                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      UDSKRIV FEJLLISTE VEDR. KONSTATERET AFSTEMNINGFEJL PÅ        */
 /*********************************************************************/
 FEJLLISTE: PROC;                                                       
         CALL ZZ20390 ('*OPEN','BH65302Y');                             
         CALL ZZ20300(SIDELIN,'02');                                    
         CALL ZZ20300(OVERLIN1,'02');                                   
         OVERLIN2.TEKST1 = 'FEJLLISTE VEDR. MASKINEL AFSTEMNING';       
         CALL ZZ20300(OVERLIN2,'02');                                   
         IF FEJL = 1                                                    
         THEN                                                           
            DO;                                                         
               SIDELIN.CCA   = ZZWS3;                                   
               CALL ZZ20300(SIDELIN,'02');                              
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H68300D';    
               DETLIN1.ANTAL = ANT_MT;                                  
               DETLIN1.RCKOD = ' ';                                     
               CALL ZZ20300(DETLIN1,'02');                              
               DETLIN1.TEKST =                                          
               'ANTAL PERSONER IFØLGE AFSTEMNINGSINDIVID IALT  :';      
               DETLIN1.ANTAL = MT_OPTALT;                               
               DETLIN1.RCKOD = ' ';                                     
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
         IF FEJL = 2                                                    
         THEN                                                           
            DO;                                                         
               DETLIN2.CCA   = ZZWS3;                                   
               DETLIN2.TEKST = 'OPTÆLLINGSINDIVID MANGLER PÅ B.H65501D';
               CALL ZZ20300(DETLIN2,'02');                              
            END;                                                        
         IF FEJL = 3                                                    
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS2;                                   
               DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ  B.H65501D';    
               DETLIN1.ANTAL = ANT_GLMT;                                
               DETLIN1.RCKOD = ' ';                                     
               CALL ZZ20300(DETLIN1,'02');                              
               DETLIN1.TEKST =                                          
               'ANTAL PERSONER IFØLGE AFSTEMNINGSINDIVID IALT  :';      
               DETLIN1.ANTAL = BH65599.ANT_INDV;                        
               DETLIN1.RCKOD = ' ';                                     
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
 END FEJLLISTE;                                                         
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
         CALL ZZ20300(' ','99');                                        
         CLOSE FILE (BH683I);                                           
         CLOSE FILE (BH649I);                                           
         CLOSE FILE (BH652I);                                           
         CLOSE FILE (BH655I);                                           
         CLOSE FILE (BH653O);                                           
 END AFSLUT;                                                            
 /* %INCLUDE BH65500M;     /* Udgået */                                 
 /*%INCLUDE BH65501M;     /* BEREGN/UNDERSØG EN PERSON'S ALDER */       
 %INCLUDE BH65502M;                                                     
 END BH65300;                                                           
