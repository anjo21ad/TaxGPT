  PROCESS OPT(TIME);                                                    
 BH21100: /* EVS - BEHANDL EJERE AF UDENLANDSKE EJENDOMME */            
         PROC OPTIONS (MAIN) REORDER;                                   
 /*********************************************************************/
                                                                        
         DCL COPYRIGHT CHAR (30) STATIC                                 
         INIT ('COPYRIGHT KOMMUNEDATA I/S 2001');                       
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      ÅRSTILRETNING VIL 'NORMALT' KUN BESTÅ AF REKOMPILERING       */
 /*      PÅ GRUND AF ÆNDRING(ER) AF STRUKTURER.                       */
 /*                                                                   */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      PROGRAMMET BEHANDLER UDENLANDSKE EJENDOMME UDTRUKKET         */
 /*      FRA EVS SLUT OG/ELLER EVS FORSKUD.                           */
 /*                                                                   */
 /*      PROGRAMMETS FORMÅL ER AT UNDERSØGE OM EN PERSON EJER EN      */
 /*      ELLER FLERE EJENDOMME, OM DISSE KUN EJES JVF. EVS SLUT ELLER */
 /*      KUN JVF. EVS FORSKUD SAMT OM DER ER TALE OM SAMME EJENDOM(ME)*/
 /*      HVIS DER EJES EJENDOMME BÅDE I EVS SLUT OG I EVS FORSKUD.    */
 /*                                                                   */
 /*      INDDATA : B.H21000D SAMSORTEREDE UDTRÆK FRA SLUT OG FORSKUD  */
 /*                                                                   */
 /*      UDDATA  : B.H21100D BEHANDLET UDTRÆK                         */
 /*              : BH21101Y  KONTROLLISTE                             */
 /*                                                                   */
 /*      EXT. REF: ZS61900   (SSI-DATO)                               */
 /*                                                                   */
 /* PROGRAMMØR:    AAGE RASMUSSEN, LSU T&S  AUG.   2001               */
 /* ÅRSTILRETTET:                ,                      SE SEKV  ?->80*/
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      DECLARE AF ENTRIES OG BUILTINS                               */
 /*********************************************************************/
 DCL     ZS61900   ENTRY OPTIONS (INTER ASM);          /* UDSKRIV SSI */
 DCL     ZS38100   ENTRY OPTIONS (INTER ASM);          /* TIMESTAMP   */
 DCL     ADDR      BUILTIN;                                             
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
 DCL     BH210I          FILE RECORD INPUT;                             
 DCL     BH211O          FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      DECLARE AF INPUT/OUTPUT                                      */
 /*********************************************************************/
 DCL     BH210_AR   CHAR      (300) VAR;                                
                                                                        
 DCL     1 BH210              BASED(ADDR(BH210_AR)),                    
   4 LÆNGDE       BIN FIXED (15),                                       
   4 DCPRN        FIXED (11),          /* PERSONNUMMER                */
   4 EKOMNR       BIN FIXED (15) UNAL, /* KOMMUNENR                   */
   4 EEJDNR       BIN FIXED (31) UNAL, /* EJENDOMSNUMMER              */
   4 CFRATYPE     CHAR  (1);           /* KODE FOR UDTRÆK FRA?        */
                                       /*  1=FRA EVS SLUT 2000        */
                                       /*  2=FRA EVS FORSKUD 2001     */
                                                                        
 DCL     GEM_AR          CHAR      (300) VAR;                           
                                                                        
 DCL     1 GEM           BASED(ADDR(GEM_AR)),                           
   4 LÆNGDE       BIN FIXED (15),                                       
   4 DCPRN        FIXED (11),          /* PERSONNUMMER                */
   4 EKOMNR       BIN FIXED (15) UNAL, /* KOMMUNENR                   */
   4 EEJDNR       BIN FIXED (31) UNAL, /* EJENDOMSNUMMER              */
   4 CFRATYPE     CHAR  (1);           /* KODE FOR UDTRÆK FRA?        */
                                       /*  1=FRA EVS SLUT 2000        */
                                       /*  2=FRA EVS FORSKUD 2001     */
                                                                        
 DCL     1 TOT_INDV,                                                    
           %INCLUDE BH21100N;;                                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     1 TV,                                                          
           2  LÆSTE_IALT     FIXED DEC (7),                             
           2  LÆSTE_SLUT     FIXED DEC (7),                             
           2  LÆSTE_FORSKUD  FIXED DEC (7),                             
           2  OVSPR_SLUT     FIXED DEC (7), /* SKAL VÆRE LIG 0? */      
           2  OVSPR_FORSKUD  FIXED DEC (7), /* SKAL VÆRE LIG 0? */      
           2  SKRIV_IALT     FIXED DEC (7),                             
           2  SKRIV_EJERE    FIXED DEC (7), /* ANTAL EJERE */           
           2  SKRIV_SLUT     FIXED DEC (7), /* EJER KUN I SLUT */       
           2  SKRIV_FORSKUD  FIXED DEC (7), /* EJER KUN I FORSKUD */    
           2  SKRIV_BÅDE_OG  FIXED DEC (7), /* SAMME EJENDOMME I */     
                                            /* SLUT OG FORSKUD   */     
           2  SKRIV_I_FORSK  FIXED DEC (7), /* ALLE EJENDOMME I  */     
                                            /* FORSKUD + EVT. SLUT*/    
           2  SKRIV_EJ_SAMME FIXED DEC (7); /* EJ SAMME EJENDOMME */    
                                            /* I SLUT OG FORSKUD  */    
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     EOF_BH210       BIT       (1);                                 
 DCL     JA              BIT       (1)       INIT ('1'B);               
 DCL     NEJ             BIT       (1)       INIT ('0'B);               
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTE                      */
 /*********************************************************************/
 DCL     1 OVLIN1,                                                      
           2 CTL         CHAR      (01),                                
           2 TXT1        CHAR      (50) INIT ('PROGRAM: BH21100L'),     
           2 TXT2        CHAR      (58) INIT                            
             ('OPSÆT EJERSTATUS FOR UDENLANDSKE EJENDOMME'),            
           2 TXT3        CHAR      (14) INIT ('UDSKREVET DEN'),         
           2 DATO        CHAR      (10);                                
 DCL     1 OVLIN2,                                                      
           2 CTL         CHAR      (01),                                
           2 F1          CHAR      (51) INIT (' '),                     
           2 TXT1        CHAR      (91) INIT                            
           ('          KØRSELSKONTROLLISTE');                           
 DCL     LINIE           CHAR      (133);                               
 DCL     1  DETAIL                           DEF LINIE,                 
          2  STYR        CHAR      (1),                                 
          2  TEKST       CHAR      (70),                                
          2  ANTAL       PIC       'Z.ZZZ.ZZ9';                         
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      H O U S E - K E E P I N G                                    */
 /*********************************************************************/
                                                                        
         CALL ZS61900;                                                  
                                                                        
         OPEN FILE (BH210I) TITLE ('BH21000I');                         
         OPEN FILE (BH211O) TITLE ('BH21100O');                         
                                                                        
         CALL ZZ20390 ('*OPEN','BH21101Y');                             
                                                                        
         ON ENDFILE (BH210I)                                            
         BEGIN;                                                         
            EOF_BH210 = JA;                                             
         END;                                                           
                                                                        
         TV = '';                                                       
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
                                                                        
 /*********************************************************************/
 /*      P R O C E S S                                                */
 /*********************************************************************/
                                                                        
         CALL LÆS;                                                      
                                                                        
         GEM_AR = BH210_AR;                                             
         TOT_INDV = '';                                                 
                                                                        
         CALL LÆS;                                                      
                                                                        
         DO WHILE (EOF_BH210 = NEJ);                                    
                                                                        
            DO WHILE (EOF_BH210 = NEJ &                                 
                      GEM.DCPRN ^= BH210.DCPRN);                        
                                                                        
               CALL SKRIV;                                              
                                                                        
               TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_IALT + 1;           
                                                                        
               CALL SKRIV_TOTAL_INDV;                                   
                                                                        
               GEM_AR = BH210_AR;                                       
               CALL LÆS;                                                
                                                                        
            END;     /* DO WHILE NY PERSON */                           
                                                                        
            DO WHILE (EOF_BH210 = NEJ &                                 
                      GEM.DCPRN = BH210.DCPRN &                         
                      GEM.EEJDNR ^= BH210.EEJDNR);                      
                                                                        
               CALL SKRIV;                                              
                                                                        
               TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_IALT + 1;           
                                                                        
               GEM_AR = BH210_AR;                                       
               CALL LÆS;                                                
                                                                        
            END;     /* DO WHILE SAMME PERSON OG NY EJENDOM */          
                                                                        
            DO WHILE (EOF_BH210 = NEJ &                                 
                      GEM.DCPRN = BH210.DCPRN &                         
                      GEM.EEJDNR = BH210.EEJDNR);                       
                                                                        
               IF GEM.CFRATYPE = BH210.CFRATYPE                         
               THEN                                                     
                  DO;                                                   
                     IF BH210.CFRATYPE = '1'                            
                     THEN                                               
                        TV.OVSPR_SLUT = TV.OVSPR_SLUT + 1;              
                     ELSE                                               
                        TV.OVSPR_FORSKUD = TV.OVSPR_FORSKUD + 1;        
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                     TOT_INDV.FEJET_BÅDEOG =                            
                     TOT_INDV.FEJET_BÅDEOG + 1;                         
                                                                        
                     CALL SKRIV;                                        
                  END;                                                  
                                                                        
               GEM_AR = BH210_AR;                                       
               CALL LÆS;                                                
                                                                        
            END;     /* DO WHILE SAMME PERSON OG EJENDOM */             
                                                                        
         END; /* DO WHILE EOF_BH210 = NEJ */                            
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /*      LÆS SAMSORTEREDE UDTRÆK MED UDENLANDSKE EJENDOMME            */
 /*********************************************************************/
 LÆS:                                                                   
 PROC;                                                                  
                                                                        
         READ FILE (BH210I) INTO (BH210_AR);                            
                                                                        
         IF EOF_BH210 = NEJ                                             
         THEN                                                           
            DO;                                                         
               TV.LÆSTE_IALT = TV.LÆSTE_IALT + 1;                       
                                                                        
               IF BH210.CFRATYPE = '1'                                  
               THEN                                                     
                  TV.LÆSTE_SLUT = TV.LÆSTE_SLUT + 1;                    
               ELSE                                                     
                  TV.LÆSTE_FORSKUD = TV.LÆSTE_FORSKUD + 1;              
            END;                                                        
                                                                        
 END LÆS;                                                               
 /*********************************************************************/
 /*      SKRIV BEHANDLET UDTRÆK MED UDENLANDSKE EJENDOMME             */
 /*********************************************************************/
 SKRIV:                                                                 
 PROC;                                                                  
                                                                        
         WRITE FILE (BH211O) FROM (GEM_AR);                             
                                                                        
         TV.SKRIV_IALT = TV.SKRIV_IALT + 1;                             
                                                                        
         IF GEM.CFRATYPE = '1'                                          
         THEN                                                           
            TOT_INDV.FEJET_SLUT = TOT_INDV.FEJET_SLUT + 1;              
         ELSE                                                           
            TOT_INDV.FEJET_FORSKU = TOT_INDV.FEJET_FORSKU + 1;          
                                                                        
 END SKRIV;                                                             
 /*********************************************************************/
 /*      OPTÆL TIL KONTROLLISTE OG                                    */
 /*      SKRIV TOTAL-INDIVID FOR AKTUEL EJER                          */
 /*********************************************************************/
 SKRIV_TOTAL_INDV:                                                      
 PROC;                                                                  
                                                                        
         TOT_INDV.DCPRN = GEM.DCPRN;                                    
                                                                        
         TOT_INDV.FILLER01 = 999;                                       
         TOT_INDV.FILLER03 = '0';                                       
                                                                        
         WRITE FILE (BH211O) FROM (TOT_INDV);                           
                                                                        
         TV.SKRIV_EJERE = TV.SKRIV_EJERE + 1;                           
                                                                        
         SELECT;                                                        
                                                                        
           WHEN (TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_SLUT &            
                 TOT_INDV.FEJET_FORSKU = 0)                             
              DO;                                                       
                 /* ALLE EJES KUN I SLUT */                             
                 TV.SKRIV_SLUT = TV.SKRIV_SLUT + 1;                     
              END;                                                      
                                                                        
           WHEN (TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_FORSKU &          
                 TOT_INDV.FEJET_SLUT = 0)                               
              DO;                                                       
                 /* ALLE EJES KUN I FORSKUD */                          
                 TV.SKRIV_FORSKUD = TV.SKRIV_FORSKUD + 1;               
              END;                                                      
                                                                        
           WHEN (TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_BÅDEOG &          
                 TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_SLUT &            
                 TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_FORSKU)           
              DO;                                                       
                 /* ALLE EJES BÅDE I SLUT OG FORSKUD */                 
                 TV.SKRIV_BÅDE_OG = TV.SKRIV_BÅDE_OG + 1;               
              END;                                                      
                                                                        
           WHEN (TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_FORSKU)           
              DO;                                                       
                 /* ALLE EJES I FORSKUD SAMT NOGLE */                   
                 /* EJES BÅDE I SLUT OG FORSKUD    */                   
                 TV.SKRIV_I_FORSK = TV.SKRIV_I_FORSK + 1;               
              END;                                                      
                                                                        
           OTHERWISE                                                    
              DO;                                                       
                 /* IKKE SAMME EJENDOMME I SLUT OG FORSKUD */           
                 TV.SKRIV_EJ_SAMME = TV.SKRIV_EJ_SAMME + 1;             
              END;                                                      
                                                                        
         END; /* SELECT */                                              
                                                                        
         TOT_INDV = '';                                                 
                                                                        
 END SKRIV_TOTAL_INDV;                                                  
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         LINIE = ZZIK1;                                                 
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         OVLIN1.CTL= ZZWS1;                                             
         OVLIN1.DATO = UDDATO;                                          
         CALL ZZ20300 (OVLIN1,'01');                                    
                                                                        
         OVLIN2.CTL= ZZWS2;                                             
         CALL ZZ20300 (OVLIN2,'01');                                    
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL LÆSTE IALT                    B.H21000D';
         DETAIL.ANTAL = TV.LÆSTE_IALT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF FRA SLUT';                         
         DETAIL.ANTAL = TV.LÆSTE_SLUT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = '            HERAF OVERLÆSTE SLUT';             
         DETAIL.ANTAL = TV.OVSPR_SLUT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF FRA FORSKUD';                      
         DETAIL.ANTAL = TV.LÆSTE_FORSKUD;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = '            HERAF OVERLÆSTE FORSKUD';          
         DETAIL.ANTAL = TV.OVSPR_FORSKUD;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL SKREVET IALT                  B.H21100D';
         DETAIL.ANTAL = TV.SKRIV_IALT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = 'ANTAL EJERE IALT                             ';
         DETAIL.ANTAL = TV.SKRIV_EJERE;                                 
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF KUN MED EJENDOMME FRA SLUT';       
         DETAIL.ANTAL = TV.SKRIV_SLUT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF KUN MED EJENDOMME FRA FORSKUD';    
         DETAIL.ANTAL = TV.SKRIV_FORSKUD;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF MED SAMME EJENDOMME I SLUT OG FORSK
 UD';                                                                   
         DETAIL.ANTAL = TV.SKRIV_BÅDE_OG;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF ALTID I FORSKUD MEN EVT. OGSÅ I SLU
 T';                                                                    
         DETAIL.ANTAL = TV.SKRIV_I_FORSK;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = '      HERAF IKKE MED SAMME EJENDOMME I SLUT OG 
 FORSKUD';                                                              
         DETAIL.ANTAL = TV.SKRIV_EJ_SAMME;                              
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*      AFSLUTNING                                                   */
 /*********************************************************************/
 AFSLUT:                                                                
 PROC;                                                                  
                                                                        
         CALL SKRIV; /* SIDSTE EJERS SIDSTE INDIVID */                  
                                                                        
         TOT_INDV.FEJET_IALT = TOT_INDV.FEJET_IALT + 1;                 
                                                                        
         CALL SKRIV_TOTAL_INDV; /* FOR SIDSTE EJER */                   
                                                                        
         CALL KONTROLLISTE;                                             
                                                                        
         CALL ZZ20300(' ','99');                                        
                                                                        
         CLOSE FILE (BH210I),                                           
               FILE (BH211O);                                           
                                                                        
 END AFSLUT;                                                            
                                                                        
 END BH21100;                                                           
