 BH86800:/*** Genskab mandtalsfil fra SLUT 2021                     ***/
         PROCEDURE OPTIONS (MAIN);                                      
 /*********************************************************************/
 /*                                                                   */
 /*      ØNSKER OM ÆNDRET FUNKTIONALITET KRÆVER SELVFØLGELIG SPECIEL  */
 /*      TILRETTELSE.                                                 */
 /*                                                                   */
 /*      NAVN            Genskab mandtalsfil fra SLUT 202x            */
 /*                                                                   */
 /*      FUNKTION        At udtrække oplysninger fra BQ66000T for at  */
 /*                      kunne genskabe mandtalsfilen fra SLUT 2021   */
 /*                                                                   */
 /*      INDDATA         BQ66000T     Mandtals-tabel                  */
 /*                                                                   */
 /*      UDDATA          B.H86800D    Ny mandtalsfil                  */
 /*                      BH86801Y     KØRSELSKONTROLLISTE             */
 /*                      BH86802Y     FEJLLISTE VEDR. AFSTEMNING      */
 /*                                                                   */
 /*      PROGRAMMØR      Per Brunk PBR            18. marts 2024      */
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE(*) STATIC UNALIGNED CONN;                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF FILER                                             */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH868O          FILE RECORD OUTPUT;                            
 DCL     SYSPRINT        EXTERNAL FILE PRINT;                           
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF AJOURFØRT MT-FIL                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH868,                                                       
         %INCLUDE BH65321N;;  /*RUL FORSKUD 2021 */                     
 DCL     1 BH86899,                                                     
         %INCLUDE BH65399N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ENTRY OG BUILTIN                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;              /* PROGRAM-KOMMUNIKATION */
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS67000         ENTRY;                                         
                                                                        
 DCL     VERIFY          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     ANT_MT          FIXED     (7);       /* ANTAL LÆSTE MT       */
 DCL     ANT_SKR         FIXED     (7);       /* ANTAL SKREVNE MT     */
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*                                                                   */
 /*********************************************************************/
                                                                        
 DCL     WS_PROGRAMNAVN         CHAR(8)  INIT ('BH86800');              
                                                                        
 DCL     1 TV,                   /* TÆLLEVÆRKER TIL TOTALLISTEN */      
           2 FETCH_MT       FIXED DEC (10),                             
           2 ANT_COMM       FIXED DEC (10);                             
                                                                        
 DCL     MT_FUNDET               BIT       (1);                         
                                                                        
 DCL     AAR_SIDSTE   pic '9999';  /* benyttes i sidste_ */             
 DCL     AAR_DETTE    pic '9999';  /* benyttes i dette_ */              
 DCL     AAR_NAESTE   pic '9999';  /* benyttes i naeste_ */             
                                                                        
 DCL     WS_FORSKUD_SLUT_TXT    CHAR(16) INIT (' ');                    
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     WS_KØRSELS_ID                  CHAR(5);                        
                                                                        
 DCL     WS_DATA_ID                     BIN FIXED (15);                 
                                                                        
 DCL     WS_GLSLUTÅR                    BIN FIXED (15);                 
                                                                        
 DCL     WS_TIL_EVS_AAR_GL              BIN FIXED (15);                 
 DCL     WS_SLUT_AFVIKLINGS_ID          CHAR(16) INIT (' ');            
                                                                        
 DCL     I               BIN FIXED (15)      INIT      (0);             
 DCL     J               BIN FIXED (15)      INIT      (0);             
 DCL     FEJL            PIC       '9'       INIT      (0);             
                                                                        
 DCL     JA              BIT       (01)      INIT      ('1'B);          
 DCL     NEJ             BIT       (01)      INIT      ('0'B);          
                                                                        
 DCL     PRDATO2         FIXED     (9);                                 
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH86800'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*                                                                   */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH86800L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (54)      INIT      (                
           'Genskab mandtalsfil fra SLUT 2021'),                        
           2 F2          CHAR      (14)      INIT      (' '),           
           2 TEKST3      CHAR      (14)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (43)      INIT      (' '),           
           2 TEKST1      CHAR      (89)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (50),                                
           2 RCKOD       CHAR      (01)      INIT      (' '),           
           2 F1          CHAR      (02)      INIT      (' '),           
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (68)      INIT      (' ');           
 DCL     1 DETLIN2,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (50),                                
           2 F2          CHAR      (83)      INIT      (' ');           
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE SQLCA;           /* SQL KOMMUNIKATIONSAREAL */
                                                                        
         EXEC SQL INCLUDE BQ66000T;  /* MANDTAL              */         
                                                                        
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
         CALL ZS61900;                            /* UDSKRIV SSI-DATO */
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH86800L');             
            END;                                                        
                                                                        
            WS_KØRSELS_ID       = 'MT2';                                
            WS_DATA_ID          = 1;                                    
            WS_FORSKUD_SLUT_TXT = 'OPRET_EVS_SLUT';                     
            WS_TIL_EVS_AAR      = 2021;                                 
                                                                        
                                                                        
             AAR_SIDSTE = WS_TIL_EVS_AAR - 1;                           
             AAR_DETTE  = WS_TIL_EVS_AAR;                               
             AAR_NAESTE = WS_TIL_EVS_AAR + 1;                           
                                                                        
         OPEN FILE (BH868O) TITLE ('BH86800O');                         
                                                                        
         CALL ZZ20390 ('*OPEN','BH86801Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         TV = '';                                                       
         ANT_MT     = 0;                                                
         ANT_SKR    = 0;                                                
 /*********************************************************************/
 /* Declare af CURSORS                                                */
 /*********************************************************************/
                                                                        
         EXEC SQL DECLARE MTCURS CURSOR WITH HOLD FOR                   
            SELECT                                                      
               TIL_EVS_AAR,                                             
               AFVIKLINGS_ID,                                           
               DATA_ID,                                                 
               DCPRN,                                                   
               PROGRAMNAVN,                                             
               DB2FUNKTION,                                             
               OPRET_TS,                                                
               CCIVS,                                                   
               CPENS,                                                   
               CRGN,                                                    
               DSORP,                                                   
               CPART,                                                   
               CFRÅR,                                                   
               CFRÅROPR,                                                
               DRGNÅRFRA,                                               
               DRGNÅRTIL,                                               
               BPENSIND,                                                
               BPENSGR,                                                 
               CSTATUS,                                                 
               CDØDÆGTF,                                                
               CDUBLET,                                                 
               CÆGTF_OK,                                                
               CFØRFORS,                                                
               CFØRCOR,                                                 
               CFØRPENS,                                                
               COVF_DHE,                                                
               FØROPL_DCIÆN_GL,                                         
               FØROPL_CCIVS_GL,                                         
               HIST_OPD_SIDSTE_DHENVN,                                  
               HIST_OPD_SIDSTE_CSBSK,                                   
               HIST_OPD_SIDSTE_CFØREFT,                                 
               HIST_OPD_SIDSTE_CSKPLOM,                                 
               HIST_OPD_SIDSTE_DCIÆN,                                   
               HIST_OPD_DETTE_DHENVN,                                   
               HIST_OPD_DETTE_CSBSK,                                    
               HIST_OPD_DETTE_CFØREFT,                                  
               HIST_OPD_DETTE_CSKPLOM,                                  
               HIST_OPD_DETTE_DCIÆN,                                    
               HIST_OPD_NAESTE_DHENVN,                                  
               HIST_OPD_NAESTE_CSBSK,                                   
               HIST_OPD_NAESTE_CFØREFT,                                 
               HIST_OPD_NAESTE_CSKPLOM,                                 
               HIST_OPD_NAESTE_DCIÆN,                                   
               PER_DERHVPER_FRA1,                                       
               PER_DERHVPER_TIL1,                                       
               PER_DERHVPER_FRA2,                                       
               PER_DERHVPER_TIL2,                                       
               PER_DERHVPER_FRA3,                                       
               PER_DERHVPER_TIL3,                                       
               PER_DERHVPER_FRA4,                                       
               PER_DERHVPER_TIL4,                                       
               PER_DERHVPER_FRA5,                                       
               PER_DERHVPER_TIL5                                        
            FROM BQ66000T                                               
            WHERE TIL_EVS_AAR   = :WS_TIL_EVS_AAR                       
              AND AFVIKLINGS_ID = :WS_FORSKUD_SLUT_TXT                  
              AND DATA_ID       = :WS_DATA_ID                           
              AND KØRSELS_ID    = :WS_KØRSELS_ID                        
            ORDER BY DCPRN, CPART, DSORP   ;                            
                                                                        
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
                                                                        
         CALL OPEN_MTCURS;                                              
         CALL FETCH_MT;                                                 
                                                                        
         DO WHILE (MT_FUNDET);                                          
            CALL FLYT_FELTER_BH868;                                     
            CALL SKRIV_MT;                                              
            CALL FETCH_MT;                                              
         END;   /* DO WHILE */                                          
                                                                        
         CALL CLOSE_MTCURS;                                             
                                                                        
         CALL KONTROLLISTE;                                             
         CALL MASK_AFSTEMNING;                                          
         CALL AFSLUT;                                                   
                                                                        
         IF ANT_COMM > 0                                                
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
            Put skip list ('Commit efter ' !! TV.FETCH_MT !!            
                           ' læsninger på BQ66000T. ');                 
         END;                                                           
                                                                        
 /*********************************************************************/
 /*      LÆSNING AF MT                                                */
 /*********************************************************************/
 FETCH_MT: PROC;                                                        
                                                                        
         MT_FUNDET = '0'B;                                              
                                                                        
         DCLBQ66000T = '';                                              
                                                                        
         EXEC SQL FETCH MTCURS                                          
            INTO                                                        
               :DCLBQ66000T.TIL_EVS_AAR,                                
               :DCLBQ66000T.AFVIKLINGS_ID,                              
               :DCLBQ66000T.DATA_ID,                                    
               :DCLBQ66000T.DCPRN,                                      
               :DCLBQ66000T.PROGRAMNAVN,                                
               :DCLBQ66000T.DB2FUNKTION,                                
               :DCLBQ66000T.OPRET_TS,                                   
               :DCLBQ66000T.CCIVS,                                      
               :DCLBQ66000T.CPENS,                                      
               :DCLBQ66000T.CRGN,                                       
               :DCLBQ66000T.DSORP,                                      
               :DCLBQ66000T.CPART,                                      
               :DCLBQ66000T.CFRÅR,                                      
               :DCLBQ66000T.CFRÅROPR,                                   
               :DCLBQ66000T.DRGNÅRFRA,                                  
               :DCLBQ66000T.DRGNÅRTIL,                                  
               :DCLBQ66000T.BPENSIND,                                   
               :DCLBQ66000T.BPENSGR,                                    
               :DCLBQ66000T.CSTATUS,                                    
               :DCLBQ66000T.CDØDÆGTF,                                   
               :DCLBQ66000T.CDUBLET,                                    
               :DCLBQ66000T.CÆGTF_OK,                                   
               :DCLBQ66000T.CFØRFORS,                                   
               :DCLBQ66000T.CFØRCOR,                                    
               :DCLBQ66000T.CFØRPENS,                                   
               :DCLBQ66000T.COVF_DHE,                                   
               :DCLBQ66000T.FØROPL_DCIÆN_GL,                            
               :DCLBQ66000T.FØROPL_CCIVS_GL,                            
               :DCLBQ66000T.HIST_OPD_SIDSTE_DHENVN,                     
               :DCLBQ66000T.HIST_OPD_SIDSTE_CSBSK,                      
               :DCLBQ66000T.HIST_OPD_SIDSTE_CFØREFT,                    
               :DCLBQ66000T.HIST_OPD_SIDSTE_CSKPLOM,                    
               :DCLBQ66000T.HIST_OPD_SIDSTE_DCIÆN,                      
               :DCLBQ66000T.HIST_OPD_DETTE_DHENVN,                      
               :DCLBQ66000T.HIST_OPD_DETTE_CSBSK,                       
               :DCLBQ66000T.HIST_OPD_DETTE_CFØREFT,                     
               :DCLBQ66000T.HIST_OPD_DETTE_CSKPLOM,                     
               :DCLBQ66000T.HIST_OPD_DETTE_DCIÆN,                       
               :DCLBQ66000T.HIST_OPD_NAESTE_DHENVN,                     
               :DCLBQ66000T.HIST_OPD_NAESTE_CSBSK,                      
               :DCLBQ66000T.HIST_OPD_NAESTE_CFØREFT,                    
               :DCLBQ66000T.HIST_OPD_NAESTE_CSKPLOM,                    
               :DCLBQ66000T.HIST_OPD_NAESTE_DCIÆN,                      
               :DCLBQ66000T.PER_DERHVPER_FRA1,                          
               :DCLBQ66000T.PER_DERHVPER_TIL1,                          
               :DCLBQ66000T.PER_DERHVPER_FRA2,                          
               :DCLBQ66000T.PER_DERHVPER_TIL2,                          
               :DCLBQ66000T.PER_DERHVPER_FRA3,                          
               :DCLBQ66000T.PER_DERHVPER_TIL3,                          
               :DCLBQ66000T.PER_DERHVPER_FRA4,                          
               :DCLBQ66000T.PER_DERHVPER_TIL4,                          
               :DCLBQ66000T.PER_DERHVPER_FRA5,                          
               :DCLBQ66000T.PER_DERHVPER_TIL5;                          
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               MT_FUNDET = '1'B;                                        
               TV.FETCH_MT = TV.FETCH_MT + 1;                           
               ANT_COMM = ANT_COMM + 1;                                 
             END;                                                       
           WHEN (100)                                                   
             DO;                                                        
             END;                                                       
           OTHERWISE                                                    
             DO;                                                        
               ABENDTXT = '101 FETCH MTCURS';                           
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* SELECT*/                                               
                                                                        
                                                                        
         IF ANT_COMM > 9999                                             
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
            Put skip list ('Commit efter ' !! TV.FETCH_MT !!            
                           ' læsninger på BQ66000T ');                  
            ANT_COMM = 0;                                               
         END;                                                           
                                                                        
 END FETCH_MT;                                                          
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF ESR-OPLYSNINGER                     RUTINE */
 /*********************************************************************/
 OPEN_MTCURS: PROC;                                                     
                                                                        
         EXEC SQL OPEN MTCURS;                                          
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              ABENDTXT = '102 OPEN MTCURS';                             
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_MTCURS;                                                       
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF ESR                                RUTINE */
 /*********************************************************************/
 CLOSE_MTCURS: PROC;                                                    
                                                                        
         EXEC SQL CLOSE MTCURS;                                         
                                                                        
 END CLOSE_MTCURS;                                                      
                                                                        
 /*********************************************************************/
 /* FLYT FELTER FRA MT2-struktur TIL DB2-STRUKTURER  FRA FORSKUD      */
 /*********************************************************************/
 FLYT_FELTER_BH868: PROC;                                               
                                                                        
    BH868 = '';                                                         
                                                                        
    BH868.DCPRN = DCLBQ66000T.DCPRN;                                    
    BH868.CCIVS = DCLBQ66000T.CCIVS;                                    
    BH868.CPENS = DCLBQ66000T.CPENS;                                    
    BH868.CRGN = DCLBQ66000T.CRGN;                                      
    BH868.DSORP = DCLBQ66000T.DSORP;                                    
    BH868.CPART = DCLBQ66000T.CPART;                                    
    BH868.CFRÅR = DCLBQ66000T.CFRÅR;                                    
    BH868.CFRÅROPR = DCLBQ66000T.CFRÅROPR;                              
    BH868.DRGNÅRFRA = DCLBQ66000T.DRGNÅRFRA;                            
    BH868.DRGNÅRTIL = DCLBQ66000T.DRGNÅRTIL;                            
    BH868.BPENSIND = DCLBQ66000T.BPENSIND;                              
    BH868.BPENSGR = DCLBQ66000T.BPENSGR;                                
    BH868.CSTATUS = DCLBQ66000T.CSTATUS;                                
    BH868.CDØDÆGTF = DCLBQ66000T.CDØDÆGTF;                              
    BH868.CDUBLET = DCLBQ66000T.CDUBLET;                                
    BH868.CÆGTF_OK = DCLBQ66000T.CÆGTF_OK;                              
    BH868.CFØRFORS = DCLBQ66000T.CFØRFORS;                              
    BH868.CFØRCOR = DCLBQ66000T.CFØRCOR;                                
    BH868.CFØRPENS = DCLBQ66000T.CFØRPENS;                              
    BH868.COVF_DHE = DCLBQ66000T.COVF_DHE;                              
    BH868.FØROPL.DCIÆN_GL = DCLBQ66000T.FØROPL_DCIÆN_GL;                
    BH868.FØROPL.CCIVS_GL = DCLBQ66000T.FØROPL_CCIVS_GL;                
    BH868.HIST_OPL(AAR_SIDSTE).DHENVN =                                 
        DCLBQ66000T.HIST_OPD_SIDSTE_DHENVN;                             
    BH868.HIST_OPL(AAR_SIDSTE).CSBSK =                                  
        DCLBQ66000T.HIST_OPD_SIDSTE_CSBSK;                              
    BH868.HIST_OPL(AAR_SIDSTE).CFØREFT =                                
        DCLBQ66000T.HIST_OPD_SIDSTE_CFØREFT;                            
    BH868.HIST_OPL(AAR_SIDSTE).CSKPLOM =                                
        DCLBQ66000T.HIST_OPD_SIDSTE_CSKPLOM;                            
    BH868.HIST_OPL(AAR_SIDSTE).DCIÆN =                                  
        DCLBQ66000T.HIST_OPD_SIDSTE_DCIÆN;                              
    BH868.HIST_OPL(AAR_DETTE).DHENVN =                                  
        DCLBQ66000T.HIST_OPD_DETTE_DHENVN;                              
    BH868.HIST_OPL(AAR_DETTE).CSBSK =                                   
        DCLBQ66000T.HIST_OPD_DETTE_CSBSK;                               
    BH868.HIST_OPL(AAR_DETTE).CFØREFT =                                 
        DCLBQ66000T.HIST_OPD_DETTE_CFØREFT;                             
    BH868.HIST_OPL(AAR_DETTE).CSKPLOM =                                 
        DCLBQ66000T.HIST_OPD_DETTE_CSKPLOM;                             
    BH868.HIST_OPL(AAR_DETTE).DCIÆN =                                   
        DCLBQ66000T.HIST_OPD_DETTE_DCIÆN;                               
    BH868.HIST_OPL(AAR_NAESTE).DHENVN =                                 
        DCLBQ66000T.HIST_OPD_NAESTE_DHENVN;                             
    BH868.HIST_OPL(AAR_NAESTE).CSBSK =                                  
        DCLBQ66000T.HIST_OPD_NAESTE_CSBSK;                              
    BH868.HIST_OPL(AAR_NAESTE).CFØREFT =                                
        DCLBQ66000T.HIST_OPD_NAESTE_CFØREFT;                            
    BH868.HIST_OPL(AAR_NAESTE).CSKPLOM =                                
        DCLBQ66000T.HIST_OPD_NAESTE_CSKPLOM;                            
    BH868.HIST_OPL(AAR_NAESTE).DCIÆN =                                  
        DCLBQ66000T.HIST_OPD_NAESTE_DCIÆN;                              
    BH868.PER.DERHVPER_FRA1 = DCLBQ66000T.PER_DERHVPER_FRA1;            
    BH868.PER.DERHVPER_TIL1 = DCLBQ66000T.PER_DERHVPER_TIL1;            
    BH868.PER.DERHVPER_FRA2 = DCLBQ66000T.PER_DERHVPER_FRA2;            
    BH868.PER.DERHVPER_TIL2 = DCLBQ66000T.PER_DERHVPER_TIL2;            
    BH868.PER.DERHVPER_FRA3 = DCLBQ66000T.PER_DERHVPER_FRA3;            
    BH868.PER.DERHVPER_TIL3 = DCLBQ66000T.PER_DERHVPER_TIL3;            
    BH868.PER.DERHVPER_FRA4 = DCLBQ66000T.PER_DERHVPER_FRA4;            
    BH868.PER.DERHVPER_TIL4 = DCLBQ66000T.PER_DERHVPER_TIL4;            
    BH868.PER.DERHVPER_FRA5 = DCLBQ66000T.PER_DERHVPER_FRA5;            
    BH868.PER.DERHVPER_TIL5 = DCLBQ66000T.PER_DERHVPER_TIL5;            
                                                                        
 END FLYT_FELTER_BH868;                                                 
                                                                        
 /*********************************************************************/
 /*      SKRIV MT-FIL                                                 */
 /*********************************************************************/
 SKRIV_MT: PROC;                                                        
                                                                        
         WRITE FILE (BH868O) FROM (BH868);                              
         ANT_SKR = ANT_SKR + 1;                                         
                                                                        
 END SKRIV_MT;                                                          
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE: PROC;                                                    
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE PERSONER PÅ BQ66000T';            
         DETLIN1.ANTAL = ANT_MT;                                        
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS2;                                         
         DETLIN1.TEKST = 'ANTAL SKREVNE PERSONER PÅ B.H86800D';         
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*      MASKINEL AFSTEMNING                                          */
 /*********************************************************************/
 MASK_AFSTEMNING: PROC;                                                 
                                                                        
         BH86899.DCPRN    = 99999999999;                                
         BH86899.FILLER1  = ' ';                                        
         BH86899.DSORP    = 99999999999;                                
         BH86899.FILLER2  = ' ';                                        
         BH86899.ANT_INDV = ANT_SKR;                                    
         WRITE FILE (BH868O) FROM (BH86899);     /* OPTÆLLINGSINDIVID */
                                                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
         CALL ZZ20300(' ','99');                                        
         CLOSE FILE (BH868O);                                           
 END AFSLUT;                                                            
                                                                        
 END BH86800;                                                           
                                                                        
