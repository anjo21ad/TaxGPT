 BH97800: /* BEREGNINGSBÅND OVERFØR EJD.SKAT FRA ESR      */            
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /**********************************************************************
 *                                                                     *
 * FORMÅL :  AT OVERFØRE EJD.SKAT FRA ESR-DATA TIL BEREGNINGSBÅND      *
 *                                                                     *
 * INDDATA:  B.H94500D - BEREGNINGSBÅND ENDELIGT.                      *
 *           B.H97501D - EJENDOMS ESR UDTRÆK                           *
 *                                                                     *
 * OUTPUT:   B.H97800D  BEREGNINGSBÅND ALLE EJENDOMME                  *
 *           BH97801Y   KØRSELSKONTROLLISTE                            *
 *                                                                     *
 * PROGRAMMØR: BO SCHMIDT          MARTS    2005                       *
 *                                                                     *
 **********************************************************************/
         DFT RANGE (*) STATIC;                                          
                                                                        
         DCL COPYRIGHT   CHAR      (30) STATIC                          
         INIT ('COPYRIGHT KOMMUNEDATA A/S 2004');                       
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH9450I FILE RECORD INPUT;     /* BEREGNINGSBÅND            */ 
 DCL     BH9750I FILE RECORD INPUT;     /* ESR-DATA                  */ 
 DCL     BH9780O FILE RECORD OUTPUT;    /* BEREGNINGSBÅND            */ 
 /********************************************************************* 
 *       DECLARE AF BEREGNINGSBÅND                                    * 
 *********************************************************************/ 
 DCL     BH945           CHAR      (6000) VAR;                          
 DCL     1 BH945I        BASED     (ADDR(BH945)),                       
           2 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90000N;;                                          
                                                                        
 DCL     1 BH999I        BASED     (ADDR(BH945)),                       
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;;                                          
                                                                        
 DCL     BH975           CHAR      (2000) VAR;                          
 DCL     1 BH975I        BASED     (ADDR(BH975)),                       
           4 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH31800N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    LIN_DEB          CHAR      (133);                               
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH97800L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (                
           'BEREGNINGSBÅND OVERFØR EJD.SKAT FRA ESR     '),             
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     INDEX           BIN FIXED (31);                                
 DCL     EOF_BH945       BIT (1) INIT ('0'B);                           
 DCL     EOF_BH975       BIT (1) INIT ('0'B);                           
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     ANTAL_BH945     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_BH975     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_SKRIV     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_OVERFØRTE BIN FIXED (31) INIT(0);                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
                                                                        
         ON ENDFILE (BH9450I)                                           
            EOF_BH945 = '1'B;                                           
                                                                        
         ON ENDFILE (BH9750I)                                           
            EOF_BH975 = '1'B;                                           
                                                                        
         OPEN FILE (BH9450I) TITLE ('BH94500I');                        
         OPEN FILE (BH9750I) TITLE ('BH97500I');                        
         OPEN FILE (BH9780O) TITLE ('BH97800O');                        
                                                                        
         CALL ZZ20390 ('*OPEN','BH97801Y');                             
         DATO1 = '';                                                    
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         DETLIN1 = '';                                                  
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         READ FILE(BH9450I) INTO (BH945);                               
         READ FILE(BH9750I) INTO (BH975);                               
         DO WHILE (^EOF_BH945);                                         
            DO WHILE (BH945I.SORT_KOMNR > BH975I.EKOMNR & ^EOF_BH975);  
                  READ FILE(BH9750I) INTO (BH975);                      
                  ANTAL_BH975 = ANTAL_BH975 +1;                         
            END;/* DO WHILE*/                                           
                                                                        
            DO WHILE (BH945I.SORT_KOMNR = BH975I.EKOMNR                 
                      & BH945I.SORT_EJDNR > BH975I.EEJDNR               
                      & ^EOF_BH975);                                    
               READ FILE(BH9750I) INTO (BH975);                         
               ANTAL_BH975 = ANTAL_BH975 +1;                            
            END;/* DO WHILE*/                                           
                                                                        
            IF BH945I.SORT_KOMNR = BH975I.EKOMNR                        
             & BH945I.SORT_EJDNR = BH975I.EEJDNR                        
            THEN                                                        
               CALL OVERFØR_DATA;                                       
                                                                        
            WRITE FILE (BH9780O) FROM (BH945);                          
            ANTAL_SKRIV = ANTAL_SKRIV +1;                               
            READ FILE(BH9450I) INTO (BH945);                            
            ANTAL_BH945 = ANTAL_BH945 +1;                               
         END;  /*DO WHILE*/                                             
         CALL LUK_PROG;                                                 
 /********************************************************************* 
 *                       OVERFØR DATA                                 * 
 *********************************************************************/ 
 OVERFØR_DATA: PROC;                                                    
                                                                        
         IF BH945I.AKT_EJER.DCPRCIR > 0                                 
          & BH945I.AKT_EJER.DOVTG > 0                                   
          & BH945I.AKT_EJER.DOVTG_GL > 0                                
          & BH945I.AKT_EJER.DOVTG > BH945I.AKT_EJER.DOVTG_GL            
         THEN                                                           
            BH945I.TIL_EVS.DOVTG = BH945I.AKT_EJER.DOVTG;               
                                                                        
         BH945I.ESR.STAMOPL.GL_CBENYT  = BH975I.GL_CBENYT;              
         BH945I.ESR.STAMOPL.GL_FLEJL   = BH975I.GL_FLEJL;               
         BH945I.ESR.STAMOPL.GL_BEJDV   = BH975I.GL_BEJDV;               
         BH945I.ESR.STAMOPL.GL_BEJEB   = BH975I.GL_BEJEB;               
         BH945I.ESR.STAMOPL.GL_BSTUE   = BH975I.GL_BSTUE;               
         BH945I.ESR.STAMOPL.GL_BSTUGR  = BH975I.GL_BSTUGR;              
         BH945I.ESR.STAMOPL.GBOANDPCT  = BH975I.GBOANDPCT;              
         BH945I.ESR.STAMOPL.BFSKAT     = BH975I.BFSKAT;                 
         BH945I.ESR.STAMOPL.BYDAF      = BH975I.BYDAF;                  
         BH945I.ESR.STAMOPL.BDAF       = BH975I.BDAF;                   
         BH945I.ESR.STAMOPL.BAFPGRVKOM = BH975I.BAFPGRVKOM;             
         BH945I.ESR.STAMOPL.BFKOMSKAT  = BH975I.BFKOMSKAT;              
         BH945I.ESR.STAMOPL.BFKOMSKST  = BH975I.BFKOMSKST;              
         BH945I.ESR.STAMOPL.BFAMTSKST  = BH975I.BFAMTSKST;              
         BH945I.ESR.STAMOPL.BAFPGRVAMT = BH975I.BAFPGRVAMT;             
         BH945I.ESR.STAMOPL.BFAMTSKLA  = BH975I.BFAMTSKLA;              
                                                                        
         ANTAL_OVERFØRTE   = ANTAL_OVERFØRTE + 1;                       
                                                                        
 END OVERFØR_DATA;                                                      
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_BH945;                                   
         DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H94500D........:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_BH975;                                   
         DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H97500D........:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_SKRIV;                                   
         DETLIN1.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H97800D......:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_OVERFØRTE;                               
         DETLIN1.TEKST = 'ANTAL OVERFØRTE INDIVIDER FRA B.H97500D...:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
 END TOTALLISTE;                                                        
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 LUK_PROG: PROC;                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH9450I),                                          
               FILE (BH9750I),                                          
               FILE (BH9780O);                                          
 END LUK_PROG;                                                          
 END  BH97800;                                                          
