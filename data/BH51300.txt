 PROCESS OPT(TIME);                                                     
 BH51300: /* DAN UDTRÆK TIL SLS-U & SLS-P FRA EVS SLUT */               
         PROC OPTIONS (MAIN) REORDER;                                   
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2006');       
 /*********************************************************************/
 /*      NAVN         UDTRÆK TIL SLS-U & SLS-P                        */
 /*                                                                   */
 /*      INDDATA      BQ75000T                                        */
 /*                   BQ76000T                                        */
 /*                   BQ77000T                                        */
 /*                   BQ78000T                                        */
 /*                                                                   */
 /*      UDDATA       D0314314.A206 EJD.OPLYSNINGER TIL SLS-U & -P    */
 /*                   BH51301Y KØRSELSKONTROLLISTE                    */
 /*                                                                   */
 /*      PROGRAMMØR   AKSEL HANSEN                    AC   DEC 1977   */
 /*                                                                   */
 /*      RETTET       AAGE RASMUSSEN              PFB      JAN 1999   */
 /*      RETTET       AAGE RASMUSSEN              PUS      JAN 2000   */
 /*      (OVERTAGELSESDATO BLEV  K U N  SENDT TIL SLS FOR SÆLGERE)    */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      JAN 2001   */
 /*      LINIER (INDIVID 2214) SKAL IKKE DANNES LÆNGERE, DA DISSE FRA */
 /*      OG MED EVS SLUT 2000 DANNES AF BQ0X400+BQ0X410.              */
 /*                                                                   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      NOV 2001   */
 /*      OBS.: HVAD MED FORSKUDT REGNSKABSÅR HIST OG HER ??????????   */
 /*            I.F.M. ÅR0101 OG ÅR1231 SAMT FDAGE        ??????????   */
 /*                                                                   */
 /*      TILRETTET TIL EVS 2001 ESBEN BRODERSEN   LSU      JAN 2002   */
 /*                                                                   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      DEC 2002   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      DEC 2003   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      NOV 2004   */
 /*                                                                   */
 /*  SLUT05   SUSANNE KROGH VILLUMSEN                    11/11-2005   */
 /*           TILRETTET SLUT 2005                                     */
 /*                                                                   */
 /*  SLUT06   SUSANNE KROGH VILLUMSEN                    04/10-2006   */
 /*           TILRETTET SLUT 2006                                     */
 /*                                                                   */
 /*  SLUT07   INGRID FISCHER IFI                         14/11-2007   */
 /*           TILRETTET SLUT 2007 (KUN ÅRSTAL ÆNDRET)                 */
 /*                                                                   */
 /*  SLUT08   BO SCHMIDT                                 15/11-2008   */
 /*           TILRETTET SLUT 2008                                     */
 /*                                                                   */
 /*  SLUT09   GERD HOLM-PEDERSEN                         09/10-2009   */
 /*           TILRETTET SLUT 2009                                     */
 /*                                                                   */
 /*  SLUT10   GERD HOLM-PEDERSEN                         05/10-2010   */
 /*           TILRETTET SLUT 2010                                     */
 /*  SLUT10   GERD HOLM-PEDERSEN                         25/03-2011   */
 /*           TILFØJET TÆLLER FOR AT SE HVOR MANGE FOREKOMSTER, DER   */
 /*           ER MED BERHVAN > BFSKAT, SOM IKKE ER MULIGT, DA         */
 /*           BFSKAT  = TOTALE EJENDOMSSKAT OG                        */
 /*           BERHVAN = DEN ERHVERSMÆSSIGE ANDEL AF TOTAL             */
 /*                                                                   */
 /*  SLUT11   GERD HOLM-PEDERSEN                         18/10-2011   */
 /*           TILRETTET SLUT 2011. DVS. 2010 -> 2011                  */
 /*                                                                   */
 /*  SLUT12   LARS KAAE HARRITSHØJ                       SEP 2012     */
 /*           TILRETTET SLUT 2012. DVS. 2011 -> 2012                  */
 /*                                                                   */
 /*  SLUT13   JØRGEN SAUGMANN                            OKT 2012     */
 /*           TILRETTET SLUT 2013. DVS. 2012 -> 2013                  */
 /*                                                                   */
 /*  SLUT14   JØRGEN SAUGMANN                            OKT 2014     */
 /*           TILRETTET SLUT 2014. DVS. 2013 -> 2014                  */
 /*                                                                   */
 /*  SLUT15   Z6JNK                                      OKT 2015     */
 /*           Årsrullet: 2014 -> 2015.  (Søg efter   *RUL*)           */
 /*                                                                   */
 /*  SLUT16   Lene Maj Jensen QLE                        JUN 2016     */
 /*           Årsrullet: 2015 -> 2016.                                */
 /*           Årstal i lister er udskiftet til årstalsmacro,          */
 /*           så der ikke behøves årsrulning                          */
 /*                                                                   */
 /*           Esben Brodersen QEB                       Marts 2017    */
 /*           Oprettet som kopi af BH61300 til DB2 omlægning af       */
 /*           beregningsbånd                                          */
 /*                                                                   */
 /*  SLUT18   Esben Brodersen                           November      */
 /*           Ingen ænderinger indil videre skal EENHEDLBNR ikke      */
 /*           leveres                                                 */
 /*  SLUT19   Esben tilføjet fejlnumer 5163, 5165, 5166               */
 /*  SLUT20   PBR retede includes                       Oktober 2020  */
 /*  SLUT21   SEP                                       Marts   2021  */
 /*           BH65300M rettet til BH55300M                            */
 /*           Fil BH51300P er fjernet                                 */
 /*           Brug af AFVIKLING_PARM erstattet af BH55300M            */
 /*           Parameter 2 til OPDATER_KØRSELSTABEL fjernet            */
 /*           INCLUDE BH54999M er fjernet                             */
 /* SLUT 2022      Z6PBR compileret                    Oktober 2022   */
 /*********************************************************************/
          DEFAULT RANGE (*) STATIC UNALIGNED CONN;                      
 /*********************************************************************/
 /*      ENTRY-DECLARE                                                */
 /*********************************************************************/
 DCL     ZS30101         ENTRY;                       /*ABEND         */
 DCL     ZS67000         ENTRY;                       /*SQL FEJL      */
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM);   /* SSI-DATO     */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS34900         ENTRY OPTIONS (INTER ASM);   /* FAKTURERING  */
 /*********************************************************************/
 /*      FILE-DECLARE                                                 */
 /*********************************************************************/
 DCL     BH5130O         FILE RECORD OUTPUT;                            
                                                                        
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                   /* TÆLLEVÆRKER TIL TOTALLISTEN */      
           2 FETCH_RESULT      FIXED DEC (7) init (0),                  
           2 SELECT_MT         FIXED DEC (7) init (0),                  
           2 SELECT_SLUT       FIXED DEC (7) init (0),                  
           2 SELECT_ESR        FIXED DEC (7) init (0),                  
           2 ANT_COMM          FIXED DEC (7) init (0),                  
           2 ANT_UPD           FIXED DEC (7) init (0),                  
           2 OVERLÆS           FIXED DEC (7) init (0),                  
           2 BK16              FIXED DEC (7) init (0),                  
           2 ANV_INDFLYT       FIXED DEC (7) init (0),                  
           2 ANV_FRAFLYT       FIXED DEC (7) init (0),                  
           2 SKREVET           FIXED DEC (7) init (0),                  
           2 PENS_TR           FIXED DEC (7) init (0),                  
           2 F158_ER_7         FIXED DEC (7) init (0),                  
           2 F158_TIL_0        FIXED DEC (7) init (0),                  
           2 F158_1TIL3        FIXED DEC (7) init (0),                  
           2 F158_ANT_FL       FIXED DEC (7) init (0),                  
           2 FELT759_ANT       FIXED DEC (7) init (0),     /*#GEH*/     
           2 FELT106_ANT4      FIXED DEC (7) init (0),                  
           2 FELT106_ANT4_FEJL FIXED DEC (7) init (0);                  
                                                                        
 DCL     1 SKR_FEJLNR(4000:6000),  /* TÆLLEVÆRKER TIL TOTALLISTEN */    
           2 ANTAL       FIXED DEC (7);                                 
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*********************************************************************/
 DCL     WS_FORSKUD_SLUT_TXT            CHAR(16) INIT (' ');            
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
 DCL     WS_GLSLUTÅR                    BIN FIXED (15);                 
 DCL     WS_PROGRAMNAVN                 CHAR(8)  INIT ('BH51300');      
 /*********************************************************************/
 /* TIL BH85111X                                                      */
 /*********************************************************************/
 DCL     RESULT_FUNDET         BIT       (1);                           
 DCL     MT_FUNDET             BIT       (1);                           
 DCL     SLUT_FUNDET           BIT       (1);                           
 DCL     ESR_FUNDET            BIT       (1);                           
                                                                        
 /********************************************************************* 
 *       DECLARE AF DIV. FÆLLESMACROER (SLUT/BOY/FORSKUD)             * 
 *********************************************************************/ 
 DCL     1 BH55300M      ALIGNED,                                       
           %INCLUDE BH55300M;                                           
                                                                        
 DCL     1 BH60101M      ALIGNED,                                       
           %INCLUDE BH60101M;                                           
                                                                        
 DCL     1 BH51310N ALIGNED,                                            
           %INCLUDE BH61310N;                                           
                                                                        
 /*********************************************************************/
 /*     DECLARE AF DB2-variabler                                      */
 /*********************************************************************/
 EXEC SQL INCLUDE SQLCA;                                                
 EXEC SQL INCLUDE BQ75000T;                                             
 EXEC SQL INCLUDE BQ76000T;                                             
 EXEC SQL INCLUDE BQ77000T;                                             
 EXEC SQL INCLUDE BQ78000T;                                             
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH86000'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL     1 LIN1,                                                        
          2 CTL          CHAR      (1),                                 
          2 TX1          CHAR      (47)      INIT                       
                         ('PROGRAM: BH51300L'),                         
          2 TX2          CHAR      (59)      INIT                       
                         ('UDTRÆK FRA EVS SLUT TIL SLS PÅ CSC'),        
          2 TX3          CHAR      (14)      INIT                       
                         ('UDSKREVET DEN '),                            
          2 DATO         CHAR      (10),                                
          2 REST         CHAR      (02)      INIT      (' ');           
 DCL     1 LIN2,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (54)      INIT      ('  '),          
          2 TX1          CHAR      (20)      INIT                       
                         ('KØRSELSKONTROLLISTE'),                       
          2 REST         CHAR      (58)      INIT      (' ');           
 DCL     1 LIN3,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (2)       INIT      (' '),           
          2 TEKST        CHAR      (52),                                
          2 ANT          PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZZ',               
          2 REST         CHAR      (59)      INIT      (' ');           
 DCL     1 LIN_BLANK,                                                   
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (132)     INIT      (' ');           
 DCL     LIN_DEB         CHAR      (133);                               
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     TV_LÆS              FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_CPR          FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_SENR         FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_ØVR          FIXED     (9)       INIT      (0);         
 DCL     TV_KOM              FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_CPR         FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_SENR        FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_ØVR         FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_CPR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_SENR     FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_ØVR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_CPR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_SENR     FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_ØVR      FIXED     (9)       INIT      (0);         
 DCL     TV_SKRIV            FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_CPR        FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_SENR       FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_ØVR        FIXED     (15)      INIT      (0);         
 DCL     TV_UDLAND           FIXED     (9)       INIT      (0);         
 DCL     TV_EJ_FRA_ESR       FIXED     (9)       INIT      (0);         
 DCL     TV_KØB              FIXED     (9)       INIT      (0);         
 DCL     TV_SALG             FIXED     (9)       INIT      (0);         
 DCL     TV_BERHVAN_FEJL     FIXED     (15)      INIT      (0);         
 /*********************************************************************/
 /*      DECLARE AF BUILTIN                                           */
 /*********************************************************************/
 DCL     LOW             BUILTIN;                                       
 DCL     HIGH            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2                              BASED(ADDR(DATO1)),       
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     PDATO           PIC       '(8)9';                              
 DCL     1 PDATO1                             BASED(ADDR(PDATO)),       
           2 ÅÅ          CHAR      (04),                                
           2 MM          CHAR      (02),                                
           2 DD          CHAR      (02);                                
                                                                        
 DCL     VDATO           PIC       '(8)9';                              
 DCL     1 VDATO1                             BASED(ADDR(VDATO)),       
           2 DD          CHAR      (02),                                
           2 MM          CHAR      (02),                                
           2 ÅÅ          CHAR      (04);                                
                                                                        
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
 /*********************************************************************/
 /*      HOUSE-KEEPING                                                */
 /*********************************************************************/
                                                                        
         CALL ZS61900;                 /*   UDSKRIVNING AF SSI-DATO   */
                                                                        
         LIN_BLANK.CTL = ZZIK1;                                         
         LIN1.CTL = ZZWS2;                                              
         LIN2.CTL = ZZWS3;                                              
         LIN3.CTL = ZZWS2;                                              
         OPEN FILE (BH5130O) TITLE ('BH51300O');                        
         CALL ZZ20390 ('*OPEN','BH51301Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         LIN1.DATO = UDDATO;                                            
                                                                        
         BH51310N  = '';                                                
                                                                        
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH51300L');             
            END;                                                        
                                                                        
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
         SELECT (BH55300M.AFVIKLINGS_ID);                               
            WHEN ('OPRET_EVS_FORSK')                                    
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BH55300M.FORSKÅR;                    
               END;                                                     
            WHEN ('OPRET_EVS_SLUT ')                                    
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BH55300M.SLUTÅR;                     
               END;                                                     
            WHEN ('SUPPL_EVS_SLUT')                                     
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BH55300M.SLUTÅR;                     
               END;                                                     
            OTHERWISE                                                   
               DO;                                                      
                  ABENDTXT = '101 PARAMETERFEJL';                       
                  CALL PROGRAM_FEJL;                                    
               END;                                                     
         END; /* select */                                              
                                                                        
         /********************************************************/     
         /*  NU STARTER NORMALT FLOW                             */     
         /********************************************************/     
         CALL OPDATER_KØRSELSTABEL('START');                            
                                                                        
 /*********************************************************************/
 /*      PROCES OMLÆGGES TIL SQL                                      */
 /*********************************************************************/
                                                                        
         CALL OPEN_RESULTCURS;                                          
         CALL FETCH_RESULT;       /* BQ78 */                            
                                                                        
         DO WHILE (RESULT_FUNDET);                                      
            TV_LÆS = TV_LÆS + 1;                                        
            CALL SELECT_ESR;      /* BQ75 */                            
            CALL SELECT_MT;       /* BQ76 */                            
            CALL SELECT_SLUT;     /* BQ77 */                            
            CALL DAN_SLS_UDTRÆK;                                        
            CALL FETCH_RESULT;    /* BQ78 */                            
         END;                                                           
                                                                        
         CALL SLUT;                                                     
         CALL ZZ20300(LIN_BLANK,'99');                                  
         CLOSE FILE (BH5130O);                                          
                                                                        
 /*********************************************************************/
 /*    UNDERSØG OM DER SKAL DANNES UDTRÆK                             */
 /*********************************************************************/
 DAN_SLS_UDTRÆK: PROC;                                                  
                                                                        
         IF DCLBQ78000T.DCPRN > 99999999                                
         THEN                                                           
            TV_LÆS_CPR = TV_LÆS_CPR + 1;                                
         ELSE                                                           
         IF DCLBQ78000T.DCPRN > 999999                                  
         THEN                                                           
            TV_LÆS_SENR = TV_LÆS_SENR + 1;                              
         ELSE                                                           
            TV_LÆS_ØVR = TV_LÆS_ØVR + 1;                                
                                                                        
                                                                        
         IF DCLBQ78000T.DCPRN > 99999999 &                              
            DCLBQ78000T.TILEVS_DOVTG > DCLBQ76000T.DRGNÅRTIL            
         THEN                                                           
            DO; /* KØB EFTER 31.12.XXXX */                              
               TV_FRA_KØB_CPR = TV_FRA_KØB_CPR + 1;                     
               RETURN;                                                  
            END;                                                        
                                                                        
         IF DCLBQ78000T.DCPRN < 100000000                               
          & DCLBQ78000T.TILEVS_A_EJ_DOVTG > DCLBQ76000T.DRGNÅRTIL       
         THEN                                                           
            DO; /* KØB EFTER 31.12.XXXX */                              
               IF DCLBQ78000T.DCPRN > 999999                            
               THEN                                                     
                  TV_FRA_KØB_SENR = TV_FRA_KØB_SENR + 1;                
               ELSE                                                     
                  TV_FRA_KØB_ØVR = TV_FRA_KØB_ØVR + 1;                  
               RETURN;                                                  
            END;                                                        
                                                                        
         /* HVIS  EJEREN ER OFFENTLIG D.V.S. KOMM./AMT/STAT */          
         IF (DCLBQ78000T.TILEVS_A_EJ_CEJFORH = '50'  !                  
             DCLBQ78000T.TILEVS_A_EJ_CEJFORH = '60'  !                  
             DCLBQ78000T.TILEVS_A_EJ_CEJFORH = '70'  !                  
             DCLBQ78000T.TILEVS_A_EJ_CEJFORH = '80') &                  
             DCLBQ78000T.TILEVS_CINVPNR > '0'  /* IKKE VALIDT CPRNR */  
         THEN                                                           
            DO;                                                         
               IF DCLBQ78000T.DCPRN > 99999999                          
               THEN                                                     
                  TV_FRA_OFF_CPR = TV_FRA_OFF_CPR + 1;                  
               ELSE                                                     
               IF DCLBQ78000T.DCPRN > 999999                            
               THEN                                                     
                  TV_FRA_OFF_SENR = TV_FRA_OFF_SENR + 1;                
               ELSE                                                     
                  TV_FRA_OFF_ØVR = TV_FRA_OFF_ØVR + 1;                  
               RETURN;                                                  
            END;                                                        
                                                                        
         IF DCLBQ78000T.TILEVS_CINVPNR = '0' !  /* HVIS VALIDT CPRNR */ 
            DCLBQ78000T.TILEVS_CINVPNR = '2'    /* HVIS SE-NR        */ 
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               IF DCLBQ78000T.DCPRN > 99999999                          
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF DCLBQ78000T.DCPRN > 999999                            
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF DCLBQ78000T.FEJLNR = 5060 !                                 
            DCLBQ78000T.FEJLNR = 5163 ! /* slut 2019 */                 
            DCLBQ78000T.FEJLNR = 5165 ! /* slut 2019 */                 
            DCLBQ78000T.FEJLNR = 5166   /* slut 2019 */                 
         THEN                                                           
            DO;                                                         
               IF DCLBQ78000T.DCPRN > 99999999                          
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF DCLBQ78000T.DCPRN > 999999                            
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         BH51310N = '';                                                 
                                                                        
         IF DCLBQ78000T.FEJLNR = 5001 !                                 
            DCLBQ78000T.TILEVS_CINVPNR = '2'            /* HVIS SE-NR */
         THEN                                                           
            DO;                                                         
               /* DETTE ER EJENDOMME SOM IKKE ER OMFATTET */            
               /* AF EVS-REGLERNE, MEN DE SKAL OVERSENDES */            
               /* TIL SLS PÅ CSC                          */            
               /*QEB RET 5-1-2018 */                                    
               /*DCLBQ78000T.TILEVS_CSKATBER = '0';*/                   
               DCLBQ78000T.TILSLS_CSKATBER = '0';                       
            END;                                                        
                                                                        
                                                                        
         /* CHECK ESR KRAV QEB 2018-01-05 */                            
         IF DCLBQ78000T.EKOMNR = 999 !                                  
           (DCLBQ78000T.TILEVS_A_EJ_DCPRCIR = 0 &                       
            DCLBQ77000T.EVSFEJ_DCPRN > 0 &                              
            DCLBQ77000T.EVSFEJ_CEJBEVS ^= '9')                          
         THEN                                                           
           DO;                                                          
              CALL BEHANDL_EJ_FRA_ESR;                                  
              RETURN;                                                   
           END;                                                         
                                                                        
         /* BEHANDL EJENDOMME FRA ESR-UDTRÆK */                         
                                                                        
         /* KØB FØR 0101XXXX */                                         
         /* E L L E R        */                                         
         /* KØB I INDK. ÅR   */                                         
         /* OG MÅSKE SOLGT   */                                         
         CALL OPSÆT_KØB_SALG;                                           
         CALL OVF_EJSÆLGER_OPL;                                         
         CALL OVF_GENEREL_OPL;                                          
         CALL SKRIV_SLS_UDTRÆK;                                         
                                                                        
 END DAN_SLS_UDTRÆK;                                                    
 /*********************************************************************/
 /*    DAN SLS-UDTRÆK P.G.A. UDENLANDSKE EJENDOMME SAMT EJENDOMME     */
 /*    OPRETTET VIA EVS-UDTRÆK FRA FORSKUD SOM IKKE FANDTES PÅ UDTRÆK */
 /*    FRA ESR.                                                       */
 /*********************************************************************/
 BEHANDL_EJ_FRA_ESR: PROC;                                              
                                                                        
         BH51310N.DOVTG = DCLBQ78000T.TILEVS_DOVTG;                     
                                                                        
         IF DCLBQ78000T.TILEVS_DOVTG >= ÅR0101 &                        
            DCLBQ78000T.TILEVS_DOVTG <= ÅR1231                          
         THEN                                                           
            BH51310N.CKØB = 'K';  /* KØB I INDK. ÅR */                  
                                                                        
         IF DCLBQ78000T.TILEVS_DAFSTAA > 0 &                            
            DCLBQ78000T.TILEVS_DAFSTAA <= ÅR1231                        
         THEN    /* SÆLGER    */                                        
            DO;                                                         
               BH51310N.DAFSTÅ =                                        
                 DCLBQ78000T.TILEVS_DAFSTAA;                            
               BH51310N.CSALG  = 'S';                                   
            END;                                                        
                                                                        
         IF BH51310N.CKØB = 'K'                                         
         THEN                                                           
            TV_KØB = TV_KØB + 1;                                        
                                                                        
         IF BH51310N.CSALG = 'S'                                        
         THEN                                                           
            TV_SALG = TV_SALG + 1;                                      
                                                                        
         IF DCLBQ77000T.EVSFEJ_CEJBEVS = '0' !                          
            DCLBQ77000T.EVSFEJ_CEJBEVS = '4'                            
         THEN                                                           
            BH51310N.CEJ_100 = '1';                                     
         ELSE                                                           
            BH51310N.CEJ_100 = '0';                                     
                                                                        
         IF DCLBQ77000T.EVSFEJ_GPCTEJER < 10000                         
         THEN                                                           
            BH51310N.GANE = DCLBQ77000T.EVSFEJ_GPCTEJER;                
                                                                        
         BH51310N.CFORDEL = '0';                                        
         TV_EJ_FRA_ESR = TV_EJ_FRA_ESR + 1;                             
         CALL SKRIV_SLS_UDTRÆK;                                         
                                                                        
 END BEHANDL_EJ_FRA_ESR;                                                
 /*********************************************************************/
 /*    SKRIV SLS-UDTRÆK                                               */
 /*********************************************************************/
 SKRIV_SLS_UDTRÆK: PROC;                                                
                                                                        
         IF DCLBQ78000T.DCPRN > 99999999                                
         THEN                                                           
            TV_SKRIV_CPR = TV_SKRIV_CPR + 1;                            
         ELSE                                                           
         IF DCLBQ78000T.DCPRN > 999999                                  
         THEN                                                           
            TV_SKRIV_SENR = TV_SKRIV_SENR + 1;                          
         ELSE                                                           
            TV_SKRIV_ØVR = TV_SKRIV_ØVR + 1;                            
                                                                        
         /* OPSÆT FÆLLES FOR ALLE */                                    
         BH51310N.INDVNR    = 2212;                                     
         BH51310N.FELTLG    = 130;                                      
         BH51310N.INDVLG    = 130;                                      
         BH51310N.DCPRCIR   = DCLBQ78000T.DCPRN;                        
         BH51310N.EKOMNR    = DCLBQ78000T.EKOMNR;                       
         BH51310N.EEJDNR    = DCLBQ78000T.EEJDNR;                       
         BH51310N.UDSKRLBNR = DCLBQ78000T.UDSKRLBNR;                    
       /*BH51310N.CSKATBER  = DCLBQ78000T.TILEVS_CSKATBER;*/            
         /*QEB RET 2018-01-04*/                                         
         BH51310N.CSKATBER  = DCLBQ78000T.TILSLS_CSKATBER;              
         BH51310N.CRETTELSE = '0';                                      
                                                                        
         IF DCLBQ78000T.TILEVS_CFRED = '1'                              
         THEN                                                           
            BH51310N.CFRED = DCLBQ78000T.TILEVS_CFRED;                  
                                                                        
         WRITE FILE (BH5130O) FROM (BH51310N);                          
         TV_SKRIV = TV_SKRIV + 1;                                       
         IF DCLBQ78000T.EKOMNR = 999                                    
         THEN                                                           
            TV_UDLAND = TV_UDLAND + 1;                                  
                                                                        
         IF BH51310N.BERHVAN > BH51310N.BFSKAT                          
         THEN                                                           
            DO;                                                         
              PUT SKIP LIST ('TÆL CPRNR  : ',DCLBQ78000T.DCPRN);        
              PUT SKIP LIST ('    KOMNR  : ',DCLBQ78000T.EKOMNR);       
              PUT SKIP LIST ('    EJDNR  : ',DCLBQ78000T.EEJDNR);       
              PUT SKIP LIST ('    BFSKAT : ',BH51310N.BFSKAT);          
              PUT SKIP LIST ('    BERHVAN: ',BH51310N.BERHVAN);         
              PUT SKIP LIST ('           ');                            
              TV_BERHVAN_FEJL = TV_BERHVAN_FEJL + 1;                    
            END;                                                        
                                                                        
 END SKRIV_SLS_UDTRÆK;                                                  
 /*********************************************************************/
 /*    PROCEDUREN OVERFØRER KØBS- OG SALGSDATO OG OPSÆTTER KODER FOR  */
 /*    KØB OG SALG I HENHOLD TIL SKEMA OVER SKÆRINGSTILFÆLDE          */
 /*    FOR SÆLGERE OVERFØRES ENDVIDERE SLUTSEDDELDATO                 */
 /*    OBS.: HER BEHANDLES KUN EJERE FRA ESR                          */
 /*********************************************************************/
 OPSÆT_KØB_SALG: PROC;                                                  
                                                                        
         /* BEMÆRK AT DIVERSE      */                                   
         /* DATOER VENDES IKKE MERE*/                                   
         BH51310N.DOVTG = DCLBQ78000T.TILEVS_A_EJ_DOVTG;                
         IF DCLBQ78000T.TILEVS_A_EJ_DOVTG >= ÅR0101 & /*KØB I INDK. ÅR*/
            DCLBQ78000T.TILEVS_A_EJ_DOVTG <= ÅR1231                     
         THEN                                                           
            BH51310N.CKØB = 'K';                                        
                                                                        
         IF DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ > 0 &     /*SÆLGER*/         
            DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ <= ÅR1231                    
         THEN                                                           
            DO;                                                         
               BH51310N.DAFSTÅ = DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ;        
               BH51310N.DSLUTS = DCLBQ78000T.TILEVS_A_EJ_DSLUTS;        
               BH51310N.CSALG  = 'S';                                   
            END;                                                        
                                                                        
         IF BH51310N.CKØB = 'K'                                         
         THEN                                                           
            TV_KØB = TV_KØB + 1;                                        
                                                                        
         IF BH51310N.CSALG = 'S'                                        
         THEN                                                           
            TV_SALG = TV_SALG + 1;                                      
                                                                        
 END OPSÆT_KØB_SALG;                                                    
 /********************************************************************/ 
 /*      PROCEDUREN OVERFØRER DE OPLYSNINGER TIL SLS, SOM SKAL       */ 
 /*      OVERFØRES FOR BÅDE SÆLGERE OG ANDRE                         */ 
 /*      OBS.: HER BEHANDLES KUN EJERE FRA ESR                       */ 
 /********************************************************************/ 
 OVF_GENEREL_OPL: PROC;                                                 
                                                                        
         BH51310N.CEJFORH = DCLBQ78000T.TILEVS_A_EJ_CEJFORH;            
         BH51310N.CBENYT  = DCLBQ78000T.TILEVS_A_V_CBENYT;              
         BH51310N.HEJLTX  = DCLBQ75000T.OPL_HEJLTX;                     
         BH51310N.GANE    = DCLBQ78000T.TILEVS_A_EJ_GANE;               
         BH51310N.BFSKAT  = DCLBQ78000T.TILSLS_BFSKAT_SLS;              
                                                                        
         BH51310N.CEJDVSKATKOD = '0';                                   
                                                                        
         IF DCLBQ78000T.TILEVS_CEJBEVS = '7'                            
         THEN                                                           
           BH51310N.CEJDVSKATKOD = '7';                                 
                                                                        
         IF DCLBQ78000T.TILEVS_CEJBEVS = '8'                            
         THEN                                                           
           BH51310N.CEJDVSKATKOD = '6';                                 
                                                                        
         IF DCLBQ78000T.TILSLS_BFSKAT_SLS > 0 &  /*EJENDOMSSKAT IKKE  */
            DCLBQ78000T.TILSLS_BPRIVAN_SLS = 0 & /*PERIODEBEREGNET */   
           (DCLBQ78000T.TILEVS_FDAGE > 0 &                              
            DCLBQ78000T.TILEVS_FDAGE < 360)                             
         THEN                                                           
            CALL PERIODEFORDEL_SKAT;                                    
                                                                        
         BH51310N.BERHVAN = DCLBQ78000T.TILSLS_BERHVAN_SLS;             
         BH51310N.FLEJL = DCLBQ78000T.TILEVS_A_V_FLEJL;                 
         BH51310N.FEJER = DCLBQ78000T.TILEVS_A_EJ_FEJER;                
                                                                        
         SELECT (DCLBQ78000T.TILEVS_A_V_CBENYT);                        
            WHEN ('02','03','04','05','06','07','13','16','17','33',    
                  '34','42','43','44','45','70','71','78','98')         
               BH51310N.BYDAF = DCLBQ75000T.OPL_BYDAF;                  
            WHEN ('01','41')                                            
               DO;                                                      
                  IF DCLBQ78000T.TILEVS_A_V_FLEJL > 1                   
                  THEN                                                  
                     BH51310N.BYDAF = DCLBQ75000T.OPL_BYDAF;            
               END;                                                     
         OTHERWISE                                                      
               BH51310N.BYDAF = 0;                                      
         END;                                                           
                                                                        
         BH51310N.CEJ_100 = DCLBQ78000T.TILEVS_A_EJ_CEJ_100;            
                                                                        
         IF DCLBQ78000T.TILEVS_A_EJ_CEJ_100 = '2'                       
         THEN                                                           
            DO;                                                         
               IF DCLBQ78000T.TILEVS_A_EJ_FEJER > 2                     
               THEN                                                     
                  BH51310N.CEJ_100 = '0';                               
               ELSE                                                     
                  BH51310N.CEJ_100 = '1';                               
            END;                                                        
                                                                        
         IF DCLBQ78000T.TILEVS_A_EJ_FEJER = 1 &                         
           (DCLBQ78000T.TILEVS_A_EJ_GANE = 0 !                          
            DCLBQ78000T.TILEVS_A_EJ_GANE = 10000)                       
         THEN                                                           
            BH51310N.CEJ_100 = '1';                                     
                                                                        
         IF BH51310N.CEJ_100 ^= '1'    /* BLANK */                      
         THEN                                                           
            BH51310N.CEJ_100 = '0';                                     
                                                                        
         /*Forskud 2018/slut 2017 CEJSO skal ikke bruges mere */        
         IF  DCLBQ78000T.TILEVS_A_V_CEJERL = '1' &                      
             DCLBQ78000T.TILEVS_A_EJ_DOVTG <= ÅR0101  &                 
             DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ > ÅR0101  &                 
             DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ <= ÅR1231 &                 
            (DCLBQ78000T.TILEVS_A_V_CBENYT = '21' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '22' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '25' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '26' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '27' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '28' !                     
             DCLBQ78000T.TILEVS_A_V_CBENYT = '29')                      
         THEN                                                           
            BH51310N.CUDLEJ = '1';                                      
         ELSE                                                           
            BH51310N.CUDLEJ = '0';                                      
                                                                        
         IF (DCLBQ78000T.TILEVS_A_V_CEJERL = '1'      &                 
             DCLBQ78000T.TILEVS_A_EJ_DOVTG <= ÅR0101   &                
            (DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ = 0        !                
             DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ > ÅR1231)) &                
            (DCLBQ78000T.TILEVS_A_V_CBENYT = '21'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '22'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '25'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '26'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '27'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '28'     !                 
             DCLBQ78000T.TILEVS_A_V_CBENYT = '29')                      
         THEN                                                           
            BH51310N.CUDLEJ = '1';                                      
                                                                        
         IF DCLBQ78000T.TILEVS_A_V_CUOP = '5'                           
         THEN                                                           
            BH51310N.CUNOPFØR = '1';                                    
         ELSE                                                           
            BH51310N.CUNOPFØR = '0';                                    
                                                                        
         IF DCLBQ78000T.TILEVS_A_V_CBENYT = '70' !                      
            DCLBQ78000T.TILEVS_A_V_CBENYT = '71' !                      
            DCLBQ78000T.TILEVS_A_V_CBENYT < '01'                        
         THEN                                                           
            BH51310N.CVURD = '0';                                       
         ELSE                                                           
            BH51310N.CVURD = '1';                                       
                                                                        
         BH51310N.CFORDEL = '0';                                        
                                                                        
         /* EJBOLTYP = '4' STUEHUSVÆRDI */                              
         IF DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_1 = '4' !            
            DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_2 = '4' !            
            DCLBQ78000T.TILEVS_A_V_BSKOV ^= 0                           
         THEN                                                           
            BH51310N.CFORDEL = '1';                                     
                                                                        
         /* EJBOLTYP > '4' EJERBOLIGVÆRDI */                            
         IF DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_1 > '4' !            
            DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_2 > '4'              
         THEN                                                           
            BH51310N.CFORDEL = '1';                                     
                                                                        
         IF ((DCLBQ78000T.TILEVS_A_V_CBENYT = '01' !                    
              DCLBQ78000T.TILEVS_A_V_CBENYT = '41') &                   
              DCLBQ78000T.TILEVS_A_V_FLEJL = 2) &                       
             (DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_1 >= '4' !         
              DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_2 >= '4')          
         THEN                                                           
            BH51310N.CFORDEL = '0';                                     
                                                                        
         /* EJBOLTYP = '8' MINDRE LANDEJD*/                             
         /* EJBOLTYP = '9' VINDMØL.PARCEL*/                             
         IF DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_1 = '8' !            
            DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_1 = '9' !            
            DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_2 = '8' !            
            DCLBQ78000T.TILEVS_A_V_EJEBOL_EJBOLTYP_2 = '9'              
         THEN                                                           
            BH51310N.CFORDEL = '0';                                     
                                                                        
         BH51310N.BEJDVÆ_P4B = DCLBQ75000T.OPL_BEJDVÆP4B;               
                                                                        
         IF DCLBQ78000T.TILEVS_A_V_ANT_FOR_1 > 0 !                      
            DCLBQ78000T.TILEVS_A_V_ANT_FOR_2 > 0                        
         THEN                                                           
            BH51310N.EJBOLTYP = '1';                                    
                                                                        
 END OVF_GENEREL_OPL;                                                   
 /*********************************************************************/
 /*   PROCEDUREN OVERFØRER DE OPLYSNINGER TIL SLS , SOM IKKE SKAL     */
 /*   OVERFØRES FOR SÆLGERE ( EJENDOMSVÆRDIER FOR SLUTÅRET )          */
 /*   OBS.: HER BEHANDLES KUN EJERE FRA ESR                           */
 /*********************************************************************/
 OVF_EJSÆLGER_OPL: PROC;                                                
                                                                        
         IF (BH51310N.CSALG ^= 'S' !                                    
            (DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ > 0 &                       
             DCLBQ78000T.TILEVS_A_EJ_DAFSTÅ > ÅR1231))                  
         THEN                                                           
            DO;                                                         
               BH51310N.BEJDVÆR    = DCLBQ78000T.TILEVS_A_V_BEJDVÆR;    
               BH51310N.BEJDVÆ_P4A = DCLBQ78000T.TILEVS_A_V_BEJDVÆP4A;  
                                                                        
               IF DCLBQ78000T.TILEVS_A_V_BEJDVÆP4A  = 0                 
               THEN                                                     
                  DO;                                                   
                    IF DCLBQ78000T.TILEVS_A_V_BEJDVÆR > 999999999       
                    THEN                                                
                       BH51310N.BKONT_AND = 0;                          
                    ELSE                                                
                       BH51310N.BKONT_AND =                             
                                       DCLBQ78000T.TILEVS_A_V_BEJDVÆR;  
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                    IF DCLBQ78000T.TILEVS_A_V_BEJDVÆP4A > 999999999     
                    THEN                                                
                       BH51310N.BKONT_AND = 0;                          
                    ELSE                                                
                       BH51310N.BKONT_AND =                             
                                       DCLBQ78000T.TILEVS_A_V_BEJDVÆP4A;
                  END;                                                  
                                                                        
               IF DCLBQ78000T.TILEVS_A_EJ_GANE > 0 &                    
                  DCLBQ78000T.TILEVS_A_EJ_GANE < 10000                  
               THEN                                                     
                  BH51310N.BKONT_AND = BH51310N.BKONT_AND *             
                           DCLBQ78000T.TILEVS_A_EJ_GANE / 10000;        
            END;                                                        
                                                                        
 END OVF_EJSÆLGER_OPL;                                                  
 /*********************************************************************/
 /*   PROCEDUREN BEREGNER EJENDOMSSKAT FOR EJERPERIODEN, DA           */
 /*   EJD.SKATTEN ER IKKE PERIODEFORDELT I BH96000 I ALLE TILFÆLDE    */
 /*   OBS.: HER BEHANDLES KUN EJERE FRA ESR                           */
 /*********************************************************************/
 PERIODEFORDEL_SKAT: PROC;                                              
                                                                        
    BH51310N.BFSKAT =                                                   
     (DCLBQ78000T.TILSLS_BFSKAT_SLS * DCLBQ78000T.TILEVS_FDAGE) / 360;  
                                                                        
  /* OBS.: DER SKAL P.T. IKKE TAGES HØJDE FOR FORSKUDT REGNSKABSÅR */   
 /*       VED DENNE PERIODEFORDELING                               */   
                                                                        
 END PERIODEFORDEL_SKAT;                                                
 /*********************************************************************/
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*********************************************************************/
 SLUT: PROC;                                                            
                                                                        
         CALL ZZ20300(LIN_BLANK,'01');                                  
         CALL ZZ20300(LIN1,'01');                                       
         CALL ZZ20300(LIN2,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST ='ANTAL LÆSTE INDIVIDER PÅ BEREGNINGS TABELLER';    
         LIN3.ANT = TV_LÆS;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = '     HERAF FRASORTEREDE OPTÆLL.INDV.  ';         
         LIN3.ANT = TV_KOM;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE PERSONER ';                         
         LIN3.ANT = TV_LÆS_CPR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE SELSKABER ';                        
         LIN3.ANT = TV_LÆS_SENR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE ØVRIGE ';                           
         LIN3.ANT = TV_LÆS_ØVR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE PERSONER';                   
         LIN3.ANT = TV_FRAS_CPR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE SELSKABER';                  
         LIN3.ANT = TV_FRAS_SENR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE ØVRIGE';                     
         LIN3.ANT = TV_FRAS_ØVR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH55300M.INDKÅR1 !! ' CPR';                       
         LIN3.ANT = TV_FRA_KØB_CPR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH55300M.INDKÅR1 !! ' SENR';                      
         LIN3.ANT = TV_FRA_KØB_SENR;                                    
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH55300M.INDKÅR1 !! ' ØVR';                       
         LIN3.ANT = TV_FRA_KØB_ØVR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = CPR';             
         LIN3.ANT = TV_FRA_OFF_CPR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = SENR';            
         LIN3.ANT = TV_FRA_OFF_SENR;                                    
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = ØVR';             
         LIN3.ANT = TV_FRA_OFF_ØVR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ D0314314.A206';       
         LIN3.ANT = TV_SKRIV;                                           
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = '      HERAF UDENLANDSKE EJENDOMME';              
         LIN3.ANT = TV_UDLAND;                                          
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = '      HERAF ØVRIGE EJ FRA ESR     ';             
         LIN3.ANT = TV_EJ_FRA_ESR;                                      
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE PERSONER PÅ D0314314.A206';       
         LIN3.ANT = TV_SKRIV_CPR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE SELSKABER PÅ D0314314.A206';      
         LIN3.ANT = TV_SKRIV_SENR;                                      
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE ØVRIGE PÅ D0314314.A206';         
         LIN3.ANT = TV_SKRIV_ØVR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL OVERFØRTE KØBERE';                         
         LIN3.ANT = TV_KØB;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL OVERFØRTE SÆLGERE';                        
         LIN3.ANT = TV_SALG;                                            
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL MED FEJL I DEN ERHVERVSMÆSSIGE SKAT ';     
         LIN3.ANT = TV_BERHVAN_FEJL;                                    
         CALL ZZ20300(LIN3,'01');                                       
                                                                        
         CALL OPDATER_KØRSELSTABEL('SLUT');                             
                                                                        
 END SLUT;                                                              
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 %INCLUDE BH54998M;     /* OPDATER KØRSELSTABEL GL VERS */              
 %INCLUDE BH51300M;     /* DB2 CURSOR TIL LÆS */                        
 END BH51300;                                                           
