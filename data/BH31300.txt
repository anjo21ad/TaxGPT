 BH31300: PROC OPTIONS(MAIN);                                           
 /*********************************************************************/
 /* 1. DAN DATASET MED DE UDENLANDSKE EJENDOMME SOM HAR BLANK         */
 /*    BELIGGENHED ELLER BLANK LANDEKODE PÅ EJENDOMSTABEL BN00500T    */
 /*    SOM KAN OPDATERES MED KORREKT DATA FRA SLUT 2013/2014          */
 /* 2. DAN DATASET MED DE UDENLANDSKE EJENDOMME SOM HAR BLANK         */
 /*    BELIGGENHED ELLER BLANK LANDEKODE PÅ EJENDOMSTABEL BN00500T    */
 /*    SOM  I K K E  KAN OPDATERES MED DATA FRA SLUT 2013/2014        */
 /*    N.B.  UPDATE ER UDKOMMENTERET                                  */
 /*                                                                   */
 /*********************************************************************/
 /* RETTELSESLOG:                                                     */
 /*                                                                   */
 /* 14.11.2016  Lene Maj Jensen  QLE                                  */
 /*             NYT PROGRAM                                           */
 /*                                                                   */
 /*********************************************************************/
 /*     ENTRIES OG BUILTIN FUNKTIONS                                  */
 /*********************************************************************/
 DCL     ZS61900 ENTRY;                                                 
 DCL     ZS38100 ENTRY OPTIONS (INTER ASM); /* TIMESTAMP */             
 DCL     ZS67000 ENTRY; /* LOG DB2 FEJL  */                             
 DCL    (TIME,DATE,PLIDUMP,ADDR,LOW,LENGTH) BUILTIN;                    
 /*********************************************************************/
 /*     DECLARES AF FILER                                            */ 
 /*********************************************************************/
 DCL     BH3131O  FILE RECORD OUTPUT; /*LISTE OVER MULIGE RETTELSER */  
 DCL     BH3132O  FILE RECORD OUTPUT; /*LISTE OVER MANGLER DER IKKE */  
                                      /*KAN RETTES VIA 2013/2014-data */
                                                                        
 /*********************************************************************/
 /*      DECLARE AF SEQUENTIELLE RECORDS                              */
 /*********************************************************************/
                                                                        
 DCL   1 UPDREC,                                                        
       3 DINDKAAR        BIN FIXED (15),                                
       3 EKOMNR          BIN FIXED (15),                                
       3 EEJDNR          BIN FIXED (31),                                
       3 LANDEKODE       CHAR(2),                                       
       3 EJDBELIG        CHAR (31),                                     
       3 TEKST           CHAR (100);                                    
                                                                        
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ABEND-FELTER                                      */
 /*********************************************************************/
 DCL     ABEND_STR      CHAR(56);                                       
 DCL     1 ABENDMEDD    BASED(ADDR(ABEND_STR)),                         
           2 ABENDMODUL CHAR      (7) INIT ('BH31300'),                 
           2 ABENDFILL1 CHAR      (1) INIT (' '),                       
           2 ABENDMEDNR CHAR      (2) INIT ('**'),                      
           2 ABENDFILL2 CHAR      (1) INIT (' '),                       
           2 ABENDTXT   CHAR      (45);                                 
                                                                        
 DCL     ABENDKODE      FIXED     (2)    INIT (5);                      
 DCL     ABENDRTKODE    FIXED     (1)    INIT (2);                      
 DCL     SQLKODE         PIC '9999';                                    
                                                                        
                                                                        
 DCL     HJ_PIC4        PIC '(04)9';                                    
 DCL     HJ_INDKÅR      BIN FIXED(15);                                  
 DCL     HJ_DATO8       CHAR(08);                                       
 DCL     HJ_EJDBELIG    CHAR(31);                                       
                                                                        
 /*********************************************************************/
 /*      DIVERSE DECLARES                                             */
 /*********************************************************************/
                                                                        
 DCL  1 WS,                                                             
      3 WS_DINDKAAR            BIN FIXED (15),                          
      3 WS_EKOMNR              BIN FIXED (15),                          
      3 WS_EEJDNR              BIN FIXED (31),                          
      3 WS_LANDEKODE           CHAR(2),                                 
      3 WS_EJDBELIG            CHAR (31),                               
      3 WS_2014_LANDEKODE      CHAR(2),                                 
      3 WS_2014_EJDBELIG       CHAR (31),                               
      3 WS_ANTAL_OPDAT16       DEC FIXED(9) INIT(0),                    
      3 WS_ANTAL_OPDAT17       DEC FIXED(9) INIT(0),                    
      3 WS_ANTAL_EJOPDAT16     DEC FIXED(9) INIT(0),                    
      3 WS_ANTAL_EJOPDAT17     DEC FIXED(9) INIT(0),                    
      3 ANTAL_UPDATE_2016      DEC FIXED (9) INIT (0),                  
      3 ANTAL_UPDATE_2017      DEC FIXED (9) INIT (0),                  
      3 ANTAL_BØR_RETTES          DEC FIXED (9) INIT (0),               
      3 DATO                   CHAR  (8),                               
      3 UDTRÆK_START_TIMESTMP  CHAR(26),                                
      3 UDTRÆK_SLUT_TIMESTMP   CHAR(26),                                
      3 WS_TEKST               CHAR(100),                               
      3 OPDATERES              BIT(1) INIT ('0'B),                      
      3 EJDBELIG_HENTET_FRA_2013   BIT(1) INIT ('0'B),                  
      3 LANDEKODE_HENTET_FRA_2013  BIT(1) INIT ('0'B),                  
      3 WS_BELIG16_2013_ANTAL  DEC FIXED(9) INIT (0),                   
      3 WS_LAND16_2013_ANTAL   DEC FIXED(9) INIT (0),                   
      3 WS_BELIG17_2013_ANTAL  DEC FIXED(9) INIT (0),                   
      3 WS_LAND17_2013_ANTAL   DEC FIXED(9) INIT (0),                   
      3 WS_BELIG16_2014_ANTAL  DEC FIXED(9) INIT (0),                   
      3 WS_LAND16_2014_ANTAL   DEC FIXED(9) INIT (0),                   
      3 WS_BELIG17_2014_ANTAL  DEC FIXED(9) INIT (0),                   
      3 WS_LAND17_2014_ANTAL   DEC FIXED(9) INIT (0);                   
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DB2 - TABEL - SAMT CURSORS                        */
 /*********************************************************************/
         EXEC SQL INCLUDE BN00500T;                                     
         EXEC SQL INCLUDE BQ21000T;                                     
         EXEC SQL INCLUDE SQLCA;                                        
                                                                        
         /* læs 2016 og 2017 forskud - med mangler */                   
         EXEC SQL DECLARE HELT_NY_CUR CURSOR FOR                        
                                                                        
            SELECT  T1.DINDKAAR,                                        
                    T1.EKOMNR,                                          
                    T1.EEJDNR,                                          
                    T1.LANDEKODE,                                       
                    T1.EJDBELIG                                         
                  FROM  BN00500T T1                                     
                  WHERE  T1.DINDKAAR in (2016, 2017)                    
                  AND    T1.EKOMNR   = 999                              
                  AND    (T1.EJDBELIG = ' ' or T1.LANDEKODE = ' ')      
            ORDER BY 1, 2, 3;                                           
                                                                        
 /*********************************************************************/
 /*      DATOER/KONSTANTER                                            */
 /*********************************************************************/
 DCL     PTRD            PTR; /*POINTER TIL DATO MACRO */               
 DCL     1 D,                                                           
         %INCLUDE BR77700N;                                             
 /*********************************************************************/
        CALL ZS61900;                       /*  UDSKRIVER SSI-DATO */   
 /*********************************************************************/
 /*      ON - CONDITIONS                                              */
 /*********************************************************************/
        OPEN FILE (BH3131O)  TITLE ('BH31310O');                        
        OPEN FILE (BH3132O)  TITLE ('BH31320O');                        
                                                                        
                                                                        
 /********************************************************************/ 
 /*         H O V E D S T Y R I N G                                  */ 
 /********************************************************************/ 
                                                                        
        PUT SKIP LIST('OPDATERING AF LANDEKODE/BELIGGENHED FORSKUD ');  
                                                                        
        ws = '';                                                        
                                                                        
        /* Find ejendomme med mangler i 2016 og 2017 FORSKUD */         
        /* samt data fra 2013/2014 SLUT uanset indhold         */       
        CALL OPEN_HELT_NY_CUR;                                          
        CALL FETCH_HELT_NY_CUR;                                         
        DO WHILE (SQLCA.SQLCODE = 0);                                   
           ANTAL_BØR_RETTES = ANTAL_BØR_RETTES + 1;                     
           CALL LÆS_1314_LAND_BELIG;                                    
           CALL CHECK_OPDATER;                                          
           CALL FETCH_HELT_NY_CUR;                                      
        END;                                                            
        CALL CLOSE_HELT_NY_CUR;                                         
                                                                        
        CALL AFSLUT;                                                    
                                                                        
 /********************************************************************/ 
 /* Læs landekode og beliggenhed fra 2013/2014                       */ 
 /********************************************************************/ 
 LÆS_1314_LAND_BELIG: PROC;                                             
                                                                        
        WS_2014_LANDEKODE = ' ';                                        
        WS_2014_EJDBELIG  = ' ';                                        
                                                                        
        EJDBELIG_HENTET_FRA_2013 = '0'B;                                
        LANDEKODE_HENTET_FRA_2013 = '0'B;                               
                                                                        
        EXEC SQL                                                        
                                                                        
        SELECT T14.INDHOLD_C                                            
        INTO :WS_2014_LANDEKODE                                         
        FROM BQ21000T T14                                               
        WHERE T14.DINDKAAR = 2014                                       
        AND   T14.EEJDNR = :WS_EEJDNR                                   
        AND   T14.EKOMNR = :WS_EKOMNR                                   
        AND   T14.FELTKODE = 276;                                       
                                                                        
        SELECT (SQLCA.SQLCODE);                                         
           WHEN (0)                                                     
              DO;                                                       
              END;                                                      
           WHEN (100)                                                   
              DO;                                                       
                 EXEC SQL                                               
                 SELECT T14.INDHOLD_C                                   
                 INTO :WS_2014_LANDEKODE                                
                 FROM BQ21000T T14                                      
                 WHERE T14.DINDKAAR = 2013                              
                 AND   T14.EEJDNR = :WS_EEJDNR                          
                 AND   T14.EKOMNR = :WS_EKOMNR                          
                 AND   T14.FELTKODE = 276;                              
                 SELECT (SQLCA.SQLCODE);                                
                 WHEN (0)                                               
                   DO;                                                  
                     LANDEKODE_HENTET_FRA_2013 = '1'B;                  
                   END;                                                 
                 WHEN (100)                                             
                   DO;                                                  
                   END;                                                 
                 OTHERWISE                                              
                   DO;                                                  
                     ABENDTXT  =  'BH31300 ** 262  LÆS_2013_LAND';      
                     CALL SQL_FEJL;                                     
                   END;                                                 
                END; /* SELECT*/                                        
             END;                                                       
           OTHERWISE                                                    
             DO;                                                        
               ABENDTXT  =  'BH31300 ** 262  LÆS_2014_LAND';            
               CALL SQL_FEJL;                                           
             END;                                                       
        END; /* SELECT*/                                                
                                                                        
        EXEC SQL                                                        
                                                                        
        SELECT T14.INDHOLD_C                                            
        INTO :WS_2014_EJDBELIG                                          
        FROM BQ21000T T14                                               
        WHERE T14.DINDKAAR = 2014                                       
        AND   T14.EEJDNR = :WS_EEJDNR                                   
        AND   T14.EKOMNR = :WS_EKOMNR                                   
        AND   T14.FELTKODE = 105;                                       
                                                                        
        SELECT (SQLCA.SQLCODE);                                         
           WHEN (0)                                                     
             DO;                                                        
             END;                                                       
           WHEN (100)                                                   
             DO;                                                        
             EXEC SQL                                                   
             SELECT T14.INDHOLD_C                                       
             INTO :WS_2014_EJDBELIG                                     
             FROM BQ21000T T14                                          
             WHERE T14.DINDKAAR = 2013                                  
             AND   T14.EEJDNR = :WS_EEJDNR                              
             AND   T14.EKOMNR = :WS_EKOMNR                              
             AND   T14.FELTKODE = 105;                                  
             SELECT (SQLCA.SQLCODE);                                    
              WHEN (0)                                                  
                DO;                                                     
                  EJDBELIG_HENTET_FRA_2013 = '1'B;                      
                END;                                                    
              WHEN (100)                                                
                DO;                                                     
                END;                                                    
              OTHERWISE                                                 
                DO;                                                     
                  ABENDTXT  =  'BH31300 ** 262  LÆS_2013_BELIG';        
                  CALL SQL_FEJL;                                        
                END;                                                    
             END; /* SELECT*/                                           
             END;                                                       
           OTHERWISE                                                    
             DO;                                                        
               ABENDTXT  =  'BH31300 ** 262  LÆS_2014_BELIG';           
               CALL SQL_FEJL;                                           
             END;                                                       
        END; /* SELECT*/                                                
                                                                        
 END LÆS_1314_LAND_BELIG;                                               
                                                                        
 /********************************************************************/ 
 /* Check om række kan opdateres                                     */ 
 /********************************************************************/ 
 CHECK_OPDATER: PROC;                                                   
                                                                        
        OPDATERES = '0'B;                                               
                                                                        
        UPDREC = '';                                                    
        UPDREC.DINDKAAR  = WS_DINDKAAR;                                 
        UPDREC.EKOMNR    = WS_EKOMNR;                                   
        UPDREC.EEJDNR    = WS_EEJDNR;                                   
        UPDREC.LANDEKODE = WS_LANDEKODE;                                
        UPDREC.EJDBELIG  = WS_EJDBELIG;                                 
                                                                        
        IF WS_LANDEKODE = ' ' &                                         
           WS_2014_LANDEKODE > ' '                                      
        THEN DO;                                                        
              UPDREC.LANDEKODE = WS_2014_LANDEKODE;                     
              OPDATERES = '1'B;                                         
              if LANDEKODE_HENTET_FRA_2013                              
              then                                                      
                 IF WS_DINDKAAR = 2016                                  
                 THEN                                                   
                    WS_LAND16_2013_ANTAL = WS_LAND16_2013_ANTAL + 1;    
                 ELSE                                                   
                    WS_LAND17_2013_ANTAL = WS_LAND17_2013_ANTAL + 1;    
              else                                                      
                 IF WS_DINDKAAR = 2016                                  
                 THEN                                                   
                    WS_LAND16_2014_ANTAL = WS_LAND16_2014_ANTAL + 1;    
                 ELSE                                                   
                    WS_LAND17_2014_ANTAL = WS_LAND17_2014_ANTAL + 1;    
        END;                                                            
                                                                        
        IF WS_EJDBELIG = ' ' &                                          
           WS_2014_EJDBELIG > ' '                                       
        THEN DO;                                                        
              UPDREC.EJDBELIG = WS_2014_EJDBELIG;                       
              OPDATERES = '1'B;                                         
              if EJDBELIG_HENTET_FRA_2013                               
              then                                                      
                 IF WS_DINDKAAR = 2016                                  
                 THEN                                                   
                    WS_BELIG16_2013_ANTAL = WS_BELIG16_2013_ANTAL + 1;  
                 ELSE                                                   
                    WS_BELIG17_2013_ANTAL = WS_BELIG17_2013_ANTAL + 1;  
              else                                                      
                 IF WS_DINDKAAR = 2016                                  
                 THEN                                                   
                    WS_BELIG16_2014_ANTAL = WS_BELIG16_2014_ANTAL + 1;  
                 ELSE                                                   
                    WS_BELIG17_2014_ANTAL = WS_BELIG17_2014_ANTAL + 1;  
        END;                                                            
                                                                        
        CALL OPSÆT_UPD_TEKST;                                           
        UPDREC.TEKST = WS_TEKST;                                        
                                                                        
        IF OPDATERES                                                    
        THEN DO;                                                        
           WRITE FILE (BH3131O) FROM (UPDREC);                          
           /* CALL UPDATE_BN00500T; */                                  
           if WS_DINDKAAR = 2016                                        
           THEN                                                         
              WS_ANTAL_OPDAT16 = WS_ANTAL_OPDAT16 + 1;                  
           ELSE                                                         
              WS_ANTAL_OPDAT17 = WS_ANTAL_OPDAT17 + 1;                  
        END;                                                            
        ELSE DO;                                                        
           WRITE FILE (BH3132O) FROM (UPDREC);                          
           if WS_DINDKAAR = 2016                                        
           THEN                                                         
              WS_ANTAL_EJOPDAT16 = WS_ANTAL_EJOPDAT16 + 1;              
           ELSE                                                         
              WS_ANTAL_EJOPDAT17 = WS_ANTAL_EJOPDAT17 + 1;              
        END;                                                            
                                                                        
 END CHECK_OPDATER;                                                     
                                                                        
 /*********************************************************************/
 /* Update DB2 tabel BN00500T med data fra 2013/2014                  */
 /*********************************************************************/
 UPDATE_BN00500T: PROC;                                                 
                                                                        
                                                                        
        EXEC SQL                                                        
                                                                        
           UPDATE BN00500T                                              
           SET LANDEKODE = :UPDREC.LANDEKODE,                           
               EJDBELIG  = :UPDREC.EJDBELIG                             
           WHERE DINDKAAR = :UPDREC.DINDKAAR                            
           AND   EKOMNR   = :UPDREC.EKOMNR                              
           AND   EEJDNR   = :UPDREC.EEJDNR;                             
                                                                        
        SELECT (SQLCA.SQLCODE);                                         
              WHEN (0)                                                  
                DO;                                                     
                  IF UPDREC.DINDKAAR = 2016                             
                  THEN                                                  
                    ANTAL_UPDATE_2016 = ANTAL_UPDATE_2016 + 1;          
                  ELSE                                                  
                    ANTAL_UPDATE_2017 = ANTAL_UPDATE_2017 + 1;          
                END;                                                    
              WHEN (100);                                               
              OTHERWISE                                                 
                DO;                                                     
                  ABENDTXT  =  'BH31300 ** 262  UPDATE_BN00500T';       
                  CALL SQL_FEJL;                                        
                END;                                                    
            END; /* SELECT*/                                            
                                                                        
 END UPDATE_BN00500T;                                                   
                                                                        
 /*********************************************************************/
 /*  Opsæt beskrivende tekst til hver record i dataset                */
 /*********************************************************************/
 OPSÆT_UPD_TEKST: PROC;                                                 
                                                                        
       IF WS_LANDEKODE = ' ' & WS_EJDBELIG = ' '                        
       THEN DO;                                                         
          /* begge felter bør opdateres */                              
          IF WS_2014_LANDEKODE > ' ' & WS_2014_EJDBELIG > ' '           
          THEN DO;                                                      
             WS_TEKST = 'Begge felter kan opdateres fra 2013/2014';     
          END;                                                          
          ELSE DO;                                                      
             IF WS_2014_LANDEKODE > ' '                                 
             THEN DO;                                                   
                WS_TEKST = 'Kun landekode kan opdateres fra 2013/2014'; 
             END;                                                       
             ELSE DO;                                                   
                IF WS_2014_EJDBELIG > ' '                               
                THEN DO;                                                
                   WS_TEKST = 'Kun beliggenhed kan opdateres' !!        
                              ' fra 2013/2014';                         
                END;                                                    
                ELSE DO;                                                
                   WS_TEKST = 'Hverken landekode eller beliggenhed ' !! 
                              'kan opdateres fra 2013/2014';            
                END;                                                    
             END;                                                       
          END;                                                          
       END;                                                             
       ELSE DO;                                                         
          IF WS_LANDEKODE = ' '                                         
          THEN DO;                                                      
             /* kun landekode bør opdateres */                          
             IF WS_2014_LANDEKODE > ' '                                 
             THEN DO;                                                   
                WS_TEKST = 'Landekode kan opdateres fra 2013/2014';     
             END;                                                       
             ELSE DO;                                                   
                WS_TEKST = 'Landekode kan ikke opdateres fra 2013/2014';
             END;                                                       
          END;                                                          
          ELSE DO;                                                      
             IF WS_EJDBELIG = ' '                                       
             THEN DO;                                                   
                /* kun beliggenhed bør opdateres */                     
                IF WS_2014_EJDBELIG > ' '                               
                THEN DO;                                                
                   WS_TEKST = 'Beliggenhed kan opdateres fra 2013/2014';
                END;                                                    
                ELSE DO;                                                
                   WS_TEKST = 'Beliggenhed kan ikke opdateres' !!       
                              ' fra 2013/2014';                         
                END;                                                    
             END;                                                       
          END;                                                          
       END;                                                             
                                                                        
 END OPSÆT_UPD_TEKST;                                                   
                                                                        
 /********************************************************************/ 
 /* AFSLUT                                                           */ 
 /********************************************************************/ 
 AFSLUT:PROC;                                                           
                                                                        
        CALL SKRIV_TOTAL;                                               
        CLOSE FILE (BH3131O);                                           
        CLOSE FILE (BH3132O);                                           
                                                                        
 END AFSLUT;                                                            
 /********************************************************************/ 
 SKRIV_TOTAL:PROC;                                                      
                                                                        
                                                                        
     PUT SKIP LIST('ANTAL LÆST PÅ EJENDOM TABEL DER BØR OPDATERES.: '   
                   !! ANTAL_BØR_RETTES);                                
                                                                        
     PUT SKIP LIST('   HERAF 2016 der kan opdateres via 2013/2014.: '   
                   !! WS_ANTAL_OPDAT16);                                
     PUT SKIP LIST ('     ' !! WS_BELIG16_2013_ANTAL !!                 
                    ' beliggenheder fra 2013 og');                      
     PUT SKIP LIST ('     ' !! WS_LAND16_2013_ANTAL !!                  
                    ' landekoder fra 2013');                            
     PUT SKIP LIST ('     ' !! WS_BELIG16_2014_ANTAL !!                 
                    ' beliggenheder fra 2014 og');                      
     PUT SKIP LIST ('     ' !! WS_LAND16_2014_ANTAL !!                  
                    ' landekoder fra 2014');                            
                                                                        
     PUT SKIP LIST('   HERAF 2017 der kan opdateres via 2013/2014.: '   
                   !! WS_ANTAL_OPDAT17);                                
     PUT SKIP LIST ('     ' !! WS_BELIG17_2013_ANTAL !!                 
                    ' beliggenheder fra 2013 og');                      
     PUT SKIP LIST ('     ' !! WS_LAND17_2013_ANTAL !!                  
                    ' landekoder fra 2013');                            
     PUT SKIP LIST ('     ' !! WS_BELIG17_2014_ANTAL !!                 
                    ' beliggenheder fra 2014 og');                      
     PUT SKIP LIST ('     ' !! WS_LAND17_2014_ANTAL !!                  
                    ' landekoder fra 2014');                            
                                                                        
     PUT SKIP LIST('   HERAF 2016 der ikke kan opdateres..........: '   
                   !! WS_ANTAL_EJOPDAT16);                              
     PUT SKIP LIST('   HERAF 2017 der ikke kan opdateres..........: '   
                   !! WS_ANTAL_EJOPDAT17);                              
                                                                        
     PUT SKIP LIST('ANTAL DB2 OPDATERINGER FORETAGET i 2016.......: '   
                   !! ANTAL_UPDATE_2016);                               
     PUT SKIP LIST('ANTAL DB2 OPDATERINGER FORETAGET i 2017.......: '   
                   !! ANTAL_UPDATE_2017);                               
                                                                        
 END SKRIV_TOTAL;                                                       
 /*********************************************************************/
 OPEN_HELT_NY_CUR: PROC;                                                
                                                                        
         EXEC SQL OPEN HELT_NY_CUR;                                     
         IF SQLCODE ^= 0                                                
         THEN                                                           
            DO;                                                         
               SQLKODE    = SQLCODE;                                    
               ABENDTXT     = '254 OPEN HELT_NY_CUR - SQLCODE= ' !!     
                              SQLKODE;                                  
               CALL SQL_FEJL;                                           
            END;                                                        
                                                                        
 END OPEN_HELT_NY_CUR;                                                  
                                                                        
 /*********************************************************************/
 CLOSE_HELT_NY_CUR: PROC;                                               
                                                                        
         EXEC SQL CLOSE HELT_NY_CUR;                                    
                                                                        
         IF SQLCODE ^= 0                                                
         THEN                                                           
            DO;                                                         
               SQLKODE    = SQLCODE;                                    
               ABENDTXT     = '254 CLOSE HELT_NY_CUR - SQLCODE= ' !!    
                              SQLKODE;                                  
               CALL SQL_FEJL;                                           
            END;                                                        
                                                                        
 END CLOSE_HELT_NY_CUR;                                                 
                                                                        
 SQL_FEJL: PROC;                                                        
                                                                        
        SQLKODE = SQLCODE;                                              
                                                                        
        PUT SKIP LIST ('ABENDTXT: ' !! ABENDTXT);                       
                                                                        
        SELECT(SQLCODE);                                                
           WHEN (-805)                                                  
              DO;                                                       
                 PUT SKIP LIST ('254 PROGRAM SKAL LINKES  ' !!          
                 SQLKODE);                                              
              END;                                                      
           WHEN (-818)                                                  
              DO;                                                       
                 PUT SKIP LIST ('254 DER SKAL BINDES, KODE: ' !!        
                 SQLKODE);                                              
              END;                                                      
           OTHERWISE                                                    
              DO;                                                       
                 PUT SKIP LIST ('253 FEJL I SQL KALD, KODE: ' !!        
                 SQLKODE);                                              
              END;                                                      
       END;                                                             
                                                                        
 END SQL_FEJL;                                                          
                                                                        
                                                                        
 /*********************************************************************/
 FETCH_HELT_NY_CUR: PROC;                                               
                                                                        
       EXEC SQL FETCH HELT_NY_CUR                                       
                                                                        
        INTO :WS_DINDKAAR,                                              
            :WS_EKOMNR,                                                 
            :WS_EEJDNR,                                                 
            :WS_LANDEKODE,                                              
            :WS_EJDBELIG;                                               
                                                                        
            SELECT (SQLCA.SQLCODE);                                     
              WHEN (0);                                                 
              WHEN (100);                                               
              OTHERWISE                                                 
                DO;                                                     
                  ABENDTXT  =  'BH31300 ** 262  FETCH HELT_NY_CUR';     
                  CALL SQL_FEJL;                                        
                END;                                                    
            END; /* SELECT*/                                            
                                                                        
                                                                        
 END FETCH_HELT_NY_CUR;                                                 
 /*********************************************************************/
 END BH31300;                                                           
