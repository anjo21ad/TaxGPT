 BH88700: /*Individe 2212  EVS SLUT Oprettelse*/                        
       PROC OPTIONS (MAIN) REORDER;                                     
                                                                        
                                                                        
       %include zm2EnvB;                                                
                                                                        
 DCL   COPYRIGHT    CHAR    (30) STATIC INIT ('Copyright KMD A/S 2020');
                                                                        
                                                                        
                                                                        
 /*---------------------------------------------------------------------
       NAVN         Individe 2212  EVS SLUT                             
                                                                        
       INDDATA                                                          
                                                                        
       UDDATA                                                           
                                                                        
                                                                        
       PROGRAMMØR   Ole Nielsen                      Oktober 2020       
                                                                        
 ---------------------------------------------------------------------*/
 /* Slut 2020: Elena Flygenring EFL                       marts 2021  */
 /*              - struktur BH65300M erstattes med BH55300M           */
 /*              - Brug af FILE BH887I og BH865_PARM fjernes          */
 /*              - BH865_PARM.FS_MRK fjernes                          */
 /*              - BH865_PARM.FORSKUD_TXT, SLUT_TXT, SUPPL_TXT fjernes*/
 /*                erstattes med brug af BH55300M.AFVIKLINGS_ID       */
 /*              - BH865_PARM.AAR fjernes og erstattes                */
 /*                med brug af BH55300M.TIL_EVS_AAR                   */
 /*              - Parameter 3 tilføjes i kald til FindSekvensNummer  */
 /*                og SletData                                        */
 /*              - BQ74000T: nyt felt DATA_ID tilføjet                */
 /* SLUT 2022      Z6PBR compileret                    Oktober 2022   */
 /*********************************************************************/
       DEFAULT RANGE (*) STATIC UNALIGNED CONN;                         
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
                                                                        
 /*---------------------------------------------------------------------
       DECLARE AF BUILTIN                                               
 ---------------------------------------------------------------------*/
 DCL   (PLIDUMP,SUBSTR,ADDR)   BUILTIN;                                 
                                                                        
                                                                        
 /*-------------------------------------------------------------------  
       Includes ZM2BATA                                                 
 -------------------------------------------------------------------*/  
       %include ZM2SRXA;           // Standard-rutiner og DUALPLI opsæt 
                                                                        
       zm2Appl    = 'BFSK';                                             
       zm2Program = 'BH88700';                                          
                                                                        
 /*---------------------------------------------------------------------
       FILE-DECLARE                                                     
 ---------------------------------------------------------------------*/
                                                                        
 DCL   BHX71I FILE RECORD INPUT;   /* EVS DATA 2212 HOS DXC */          
                                                                        
 /*---------------------------------------------------------------------
       DECLARE AF UDTRÆK (SLUT PÅ CSC)                                  
 ---------------------------------------------------------------------*/
                                                                        
 DCL   1 BHX71,                                                         
         %INCLUDE BH61310N;;                                            
                                                                        
       BHX71 = '';                                                      
                                                                        
       EXEC SQL INCLUDE BQ74000T;   /* Individ 810            */        
                                                                        
       DCLBQ74000T = '';                                                
 /*---------------------------------------------------------------------
       DECLARE AF DIVERSE FELTER                                        
 ---------------------------------------------------------------------*/
 DCL   JA                      BIT       (1)      INIT ('1'B);          
 DCL   NEJ                     BIT       (1)      INIT ('0'B);          
 DCL   BHX71I_ANTAL            FIXED DEC (11)     INIT (0);             
 DCL   SletAntal               FIXED DEC (11)     INIT (0);             
 DCL   WS_FORSKUD_SLUT_TXT     CHAR      (16)     INIT (' ');           
 DCL   WS_TIL_EVS_AAR          BIN FIXED (15)     INIT (0);             
 DCL   WS_GLSLUTÅR             BIN FIXED (15)     INIT (0);             
 DCL   WS_TIL_EVS_AAR_GL       BIN FIXED (15)     INIT (0);             
 DCL   WS_SLUT_AFVIKLINGS_ID   CHAR      (16)     INIT (' ');           
 DCL   WS_PROGRAMNAVN          CHAR      (8)      INIT ('BH88700');     
 DCL   UDDATO                  CHAR      (10);                          
 DCL   DATO1                   CHAR      (26);                          
 DCL   1 DATO2  DEF  DATO1,                                             
         2 ÅR                  CHAR      (04),                          
         2 F1                  CHAR      (01),                          
         2 MD                  CHAR      (02),                          
         2 F2                  CHAR      (01),                          
         2 DG                  CHAR      (02);                          
                                                                        
 /*---------------------------------------------------------------------
       PARM FIL                                                         
 ---------------------------------------------------------------------*/
                                                                        
 DCL   EOF_BHX71I              BIT       (1)      INIT ('0'B);          
 DCL   BH51300P                FILE RECORD INPUT;                       
                                                                        
 DCL   1 BH55300M      ALIGNED,                                         
         %INCLUDE BH55300M;                                             
                                                                        
 /*---------------------------------------------------------------------
       DB2 definitioner                                                 
 ---------------------------------------------------------------------*/
                                                                        
       %include ZM2DB2B;              // Standard til Zm2-programmer    
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH88700'),                                         
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'ESR INPUT TIL BEREGNING '),                                 
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
 /*---------------------------------------------------------------------
       HOUSE-KEEPING                                                    
 ---------------------------------------------------------------------*/
                                                                        
       ON ERROR                                                         
          BEGIN;                                                        
            ON ERROR SYSTEM;                                            
            PUT SKIP LIST ('PROGRAMFEJL');                              
            CALL PLIDUMP  ('SHOB');                                     
          END;                                                          
                                                                        
       ON ENDFILE (BHX71I)                                              
       BEGIN;                                                           
          EOF_BHX71I = JA;                                              
       END;                                                             
                                                                        
       OPEN FILE (BHX71I) TITLE ('BHX7120I'); // Individ 2212           
       CALL ZZ20390('*OPEN','BH88701Y');      // ÅBEN KONTROLLISTE      
                                                                        
            SELECT (BH55300M.AFVIKLINGS_ID);                            
               WHEN ('OPRET_EVS_FORSK')                                 
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR = BH55300M.TIL_EVS_AAR;             
                     OVERLIN1.TEKST2 = 'EVS Individ 2212 FORSKUD';      
                  END;                                                  
               WHEN ('OPRET_EVS_SLUT')                                  
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR = BH55300M.TIL_EVS_AAR;;            
                     OVERLIN1.TEKST2 = 'EVS Individ 2212 SLUT';         
                  END;                                                  
               WHEN ('SUPPL_EVS_SLUT')                                  
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH55300M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR = BH55300M.TIL_EVS_AAR;;            
                     OVERLIN1.TEKST2 = 'EVS Individ 2212 SUPPLEMENT';   
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                    CALL zm2Abend('100','PARAMETERFEJL');               
                  END;                                                  
            END; /* select */                                           
                                                                        
       CALL ZS38100(DATO1);                                             
       UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;         
       //OVERLIN1.ÅR   = GLSLUTÅR - 1;                                  
       OVERLIN1.ÅR = WS_TIL_EVS_AAR;                                    
       OVERLIN1.DATO = UDDATO;                                          
       SIDELIN.CCA   = ZZIK1;                                           
       OVERLIN1.CCA  = ZZWS2;                                           
       OVERLIN2.CCA  = ZZWS3;                                           
                                                                        
 /*---------------------------------------------------------------------
       Hovedprogram                                                     
 ---------------------------------------------------------------------*/
                                                                        
       //Antal rækker som der vil blive slettet ved omkørsel            
       SletAntal =                                                      
           FindSekvensNummer(WS_TIL_EVS_AAR,WS_FORSKUD_SLUT_TXT,        
                             BH55300M.DATA_ID);                         
                                                                        
       // Ved omkørsel slettes data                                     
       Call SletData(WS_TIL_EVS_AAR,WS_FORSKUD_SLUT_TXT,                
                     BH55300M.DATA_ID);                                 
                                                                        
       READ FILE (BHX71I) INTO (BHX71);                                 
                                                                        
       DCLBQ74000T.TIL_EVS_AAR = WS_TIL_EVS_AAR;                        
       DCLBQ74000T.AFVIKLINGS_ID = WS_FORSKUD_SLUT_TXT;                 
       DCLBQ74000T.DATA_ID       = BH55300M.DATA_ID;                    
                                                                        
       Do While (^EOF_BHX71I);                                          
          BHX71I_ANTAL = BHX71I_ANTAL + 1;                              
          DCLBQ74000T.INDIVID_SEKVENS_NR  = BHX71I_ANTAL;               
          DCLBQ74000T.DCPRN = BHX71.DCPRCIR;                            
          DCLBQ74000T.ELBNR = BHX71.UDSKRLBNR;                          
          DCLBQ74000T = BHX71, by name;                                 
          Call Insert_BQ740;                                            
          READ FILE (BHX71I) INTO (BHX71);                              
       End; //do while                                                  
                                                                        
       Call KONTROLLISTE;                                               
                                                                        
       Call LukFiler;                                                   
                                                                        
                                                                        
 // Insert række på BQ740 individ 2212                                  
 Insert_BQ740: proc;                                                    
                                                                        
       EXEC SQL                                                         
            INSERT INTO BQ74000T                                        
              VALUES ( :DCLBQ74000T.TIL_EVS_AAR,                        
                       :DCLBQ74000T.AFVIKLINGS_ID,                      
                       :DCLBQ74000T.DATA_ID,                            
                       :DCLBQ74000T.INDIVID_SEKVENS_NR,                 
                       :DCLBQ74000T.DCPRN,                              
                       :DCLBQ74000T.ELBNR,                              
                       :DCLBQ74000T.INDVNR,                             
                       :DCLBQ74000T.FELTLG,                             
                       :DCLBQ74000T.INDVLG,                             
                       :DCLBQ74000T.DCPRCIR,                            
                       :DCLBQ74000T.EKOMNR,                             
                       :DCLBQ74000T.EEJDNR,                             
                       :DCLBQ74000T.UDSKRLBNR,                          
                       :DCLBQ74000T.CBENYT,                             
                       :DCLBQ74000T.CEJDVSKATKOD,                       
                       :DCLBQ74000T.BFSKAT,                             
                       :DCLBQ74000T.BYDAF,                              
                       :DCLBQ74000T.FLEJL,                              
                       :DCLBQ74000T.GANE,                               
                       :DCLBQ74000T.CKØB,                               
                       :DCLBQ74000T.DOVTG,                              
                       :DCLBQ74000T.DSLUTS,                             
                       :DCLBQ74000T.CSALG,                              
                       :DCLBQ74000T.DAFSTÅ,                             
                       :DCLBQ74000T.FEJER,                              
                       :DCLBQ74000T.BEJDVÆR,                            
                       :DCLBQ74000T.BEJDVÆ_P4A,                         
                       :DCLBQ74000T.CEJFORH,                            
                       :DCLBQ74000T.BERHVAN,                            
                       :DCLBQ74000T.HEJLTX,                             
                       :DCLBQ74000T.BKONT_AND,                          
                       :DCLBQ74000T.CEJ_100,                            
                       :DCLBQ74000T.CUDLEJ,                             
                       :DCLBQ74000T.CUNOPFØR,                           
                       :DCLBQ74000T.CVURD,                              
                       :DCLBQ74000T.CSKATBER,                           
                       :DCLBQ74000T.CFORDEL,                            
                       :DCLBQ74000T.CRETTELSE,                          
                       :DCLBQ74000T.BEJDVÆ_P4B,                         
                       :DCLBQ74000T.EJBOLTYP,                           
                       :DCLBQ74000T.CFRED    );                         
                                                                        
       Select(sqlca.sqlcode);                                           
          When(0);  //Det gik godt                                      
          Otherwise                                                     
             Call SQL_FEJL('001','Fejl i insert');                      
       End; //select                                                    
                                                                        
 End Insert_BQ740;                                                      
 //Find højeste sekvensnummer givet år og afviklings id.                
 FindSekvensNummer: Proc(Aar,AfviklingsId,DATAID)                       
                    Returns(FIXED DEC (15));                            
 dcl Aar          BIN FIXED (15);                                       
 dcl AfviklingsId Char(16);                                             
 DCL DATAID       BIN FIXED(15);                                        
 dcl MaxNummer    FIXED DEC (15)     INIT (0);                          
                                                                        
       EXEC SQL                                                         
          Select COALESCE(MAX(INDIVID_SEKVENS_NR),0)                    
          Into :MaxNummer                                               
          From BQ74000T                                                 
          Where TIL_EVS_AAR = :Aar                                      
          And AFVIKLINGS_ID = :AfviklingsId                             
          AND DATA_ID       = :DATAID                                   
       ;                                                                
                                                                        
       Select(sqlca.sqlcode);                                           
          When(0);    //Det gik godt                                    
          When(100);  //Det er ok at den ikke findes                    
          Otherwise                                                     
             Call SQL_FEJL('002','Fejl i FindSekvensNummer');           
       End; //select                                                    
                                                                        
       Return(MaxNummer);                                               
                                                                        
                                                                        
 End FindSekvensNummer;                                                 
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL SLETTEDE RÆKKER PÅ BQ74000T';                           
         DETLIN1.ANTAL = SletAntal;                                     
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL SKREVNE RÆKKER PÅ BQ74000T';                            
         DETLIN1.ANTAL = BHX71I_ANTAL;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* Ryd db2-tabeller før start                                 RUTINE */
 /*********************************************************************/
 SletData: Proc(Aar,AfviklingsId,DATAID);                               
 dcl Aar          BIN FIXED (15);                                       
 dcl AfviklingsId Char(16);                                             
 DCL DATAID       BIN FIXED(15);                                        
                                                                        
         EXEC SQL                                                       
            Delete from BQ74000T                                        
            Where TIL_EVS_AAR = :Aar                                    
            And   AFVIKLINGS_ID = :AfviklingsId                         
            AND   DATA_ID       = :DATAID                               
           ;                                                            
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
            END;                                                        
            WHEN(100) DO;                                               
            END;                                                        
            OTHERWISE DO;                                               
               Call SQL_FEJL('003','DELETE BQ73000T');                  
            END;                                                        
         END;                                                           
                                                                        
 END SletData;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: proc (cc, txt);                                              
 dcl cc  char (03);                                                     
 dcl txt char (41);                                                     
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! TXT);            
         PUT SKIP LIST ('*****************************************');   
                                                                        
         Call LukFiler;                                                 
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL zm2Abend(cc,txt);                                         
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC(cc, txt);                                           
 dcl cc  char (03);                                                     
 dcl txt char (41);                                                     
                                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! TXT);            
         PUT SKIP LIST ('*****************************************');   
                                                                        
         Call LukFiler;                                                 
                                                                        
         CALL zm2Abend(cc,txt);                                         
                                                                        
 END PROGRAM_FEJL;                                                      
 LukFiler: PROC;                                                        
                                                                        
         CLOSE FILE (BHX71I);                                           
         CALL ZZ20300(' ','99');                                        
                                                                        
 End LukFiler;                                                          
                                                                        
 END BH88700;                                                           
