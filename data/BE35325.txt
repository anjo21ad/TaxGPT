 PROCESS OPT(TIME);                                                     
 BE35325: PROCEDURE OPTIONS (MAIN) REORDER;                             
 /*********************************************************************/
         DCL COPYRIGHT CHAR (30) STATIC INIT ('COPYRIGHT KMD A/S 2019');
 /********************************************************************* 
 *                                                                    * 
 * FORMÅL: HVIS DER ER FORSKEL I EJENDOMSTYPEN MELLEM ESR             * 
 *         OG SLUT OPLYSNINGER FOR 2 FAMILEHUSE SÅ SKAL               * 
 *         SLUT OPLYSNINGER IKKE ANVENDES                             * 
 *                                                                    * 
 * INPUT:  B.H30000D  EVS SLUT UDTRÆK                                 * 
 *         BE339001D  BEHANDLET ESR DATA                              * 
 *                                                                    * 
 * OUTPUT: B.H35300D EVS SLUT- UDTRÆK                                 * 
 *         B.H35300D FRAVALGTE EVS SLUT- UDTRÆK                       * 
 *                                                                    * 
 * PROGRAMMØR: ESBEN BRODERSEN EBD                         AUG 2019   * 
 * PROGRAMMØR: Per Brunk PBR Ny struktur BE33700N   Okt 2020          * 
 * PROGRAMMØR: Per Brunk PBR Ny struktur BE30000N   Jan 2021 MP       * 
 * PROGRAMMØR: Tina Carstens, TIC                    BF17 15.05.2023  * 
 *             Årsrulning                                             * 
 *--------------------------------------------------------------------* 
 *********************************************************************/ 
 /* Slut 2020: Elena Flygenring EFL                      marts 2021  */ 
 /*             - ny kopi af BE35300                                 */ 
 /*             - struktur BE65300M erstattes med BE55300M           */ 
 /*             - Brug af FILE BE353I og BE353_PARM fjernes          */ 
 /*             - BE353_PARM.FS_MARK fjernes                         */ 
 /*             - BE353_PARM.FORSKUD_TXT, SLUT_TXT, SUPPL_TXT fjernes*/ 
 /*               erstattes med brug af BE55300M.AFVIKLINGS_ID       */ 
 /*             - WS_TIL_EVS_AAR_GL og WS_SLUT_AFVIKLINGS_ID bruges  */ 
 /*               ikke mere og kan derfor slettes                    */ 
 /*             - BE80000T: nyt felt DATA_ID tilføjes                */ 
 /* Forskud2025: Jan Jensen, JJJ BE55600M erstatter BE55300M Juni 24 */ 
 /********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************* 
 *       DECLARE AF REGISTRE                                          * 
 *********************************************************************/ 
 DCL     BE300I FILE RECORD INPUT;      /* EVS SLUT UDTRÆK           */ 
 DCL     BE339I FILE RECORD INPUT;      /* ESR DATA MED ENHEDSLBNR   */ 
 DCL     BE353O FILE RECORD OUTPUT;     /* EVS SLUT UDTRÆK           */ 
 DCL     BE3531O FILE RECORD OUTPUT;    /* FRAVALGTE SLUT DATA       */ 
                                                                        
 DCL     SYSPRINT FILE EXTERNAL PRINT;                                  
 /*********************************************************************/
                                                                        
 DCL     WS_FORSKUD_SLUT_TXT    CHAR(16) INIT (' ');                    
                                                                        
 DCL     WS_TIL_EVS_AAR         BIN FIXED (15);                         
                                                                        
 DCL     WS_PROGRAMNAVN         CHAR(8)  INIT ('BE35325');              
                                                                        
 DCL     BE339_AR        CHAR      (3000) VAR;                          
                                                                        
 DCL     1 BE339    BASED      (ADDR(BE339_AR)),                        
           4 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BE33700N;;                                          
                                                                        
 DCL     1 BE55600M,                                                    
           %INCLUDE BE55600M;                                           
                                                                        
 /***************************************************************       
 /*      DECLARE AF ABEND-RUTINE                                        
 /**************************************************************/       
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BE35325'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS67000         ENTRY;                       /* SQL-ABEND    */
                                                                        
 /********************************************************************* 
 *       DECLARE AF EVS UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     1 BE300,                                                       
           %INCLUDE BE30000N;;                                          
                                                                        
                                                                        
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     STG             BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 LINIE,                                                        
          2 CCA         CHAR      (01),                                 
          2 TEKST       CHAR      (132)     INIT      (' ');            
                                                                        
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     TV_LÆS_EVS        BIN FIXED (31)    INIT (0);                  
 DCL     TV_SKRIV_EVS      BIN FIXED (31)    INIT (0);                  
 DCL     TV_LÆS_ESR          BIN FIXED (31)    INIT (0);                
 DCL     TV_LOG_EVS        BIN FIXED (31)    INIT (0);                  
                                                                        
 DCL     TV_ESR_OVF        BIN FIXED (31)    INIT (0);                  
 DCL     TV_SLUT_OVF       BIN FIXED (31)    INIT (0);                  
 DCL     PUT_SKIP_TV       BIN FIXED (31)    INIT (0);                  
 DCL     EOF_BE300         BIT       (1)     INIT ('0'B);               
 DCL     EOF_BE339         BIT       (1)     INIT ('0'B);               
                                                                        
                                                                        
 DCL     JA                BIT       (1)     INIT ('1'B);               
 DCL     NEJ               BIT       (1)     INIT ('0'B);               
 DCL     EJER_FRA_ESR      BIT       (1)     INIT ('0'B);               
 DCL     SKRIV_SLUT_OPL    BIT       (1)     INIT ('0'B);               
 DCL     ESR_2FAMILIE      BIT       (1)     INIT ('0'B);               
 DCL     FØRSTE_ESR        BIT       (1)     INIT ('0'B);               
                                                                        
                                                                        
 DCL     UDDATO            CHAR      (10);                              
 DCL     DATO1             CHAR      (26);                              
 DCL     1 DATO2           DEF DATO1,                                   
           2 ÅR            CHAR      (04),                              
           2 F1            CHAR      (01),                              
           2 MD            CHAR      (02),                              
           2 F2            CHAR      (01),                              
           2 DG            CHAR      (02);                              
                                                                        
                                                                        
 EXEC SQL INCLUDE SQLCA;        /* SQL KOMMUNIKATIONSAREAL    */        
 /* EXEC SQL INCLUDE BE65000T;   * MODTAGET ESR OPLYSNINGER   */        
 EXEC SQL INCLUDE BE80000T;     /* 2 FAMILIE EJENDOMME        */        
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         ON ERROR                                                       
           PUT SKIP LIST('ERROR');                                      
                                                                        
         OPEN FILE (BE300I)  TITLE ('BE30000I');                        
         OPEN FILE (BE339I)  TITLE ('BE33900I'); /*ESR data*/           
         OPEN FILE (BE353O)  TITLE ('BE35300O'); /*evs data*/           
         OPEN FILE (BE3531O) TITLE ('BE35310O'); /*fravalgte evs data*/ 
                                                                        
                                                                        
         ON ENDFILE (BE300I)                                            
            EOF_BE300 = JA;                                             
                                                                        
         ON ENDFILE (BE339I)                                            
            EOF_BE339 = JA;                                             
                                                                        
         CALL ZZ20390 ('*OPEN','BE35301Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
                                                                        
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
                                                                        
         LINIE.CCA    = ZZIK1;                                          
         LINIE.TEKST = 'BE35325 LISTE OVER EJERFORHOLD MED FJERNED ' !! 
                       'SLUT OPL ' !! UDDATO;                           
         CALL ZZ20300(LINIE,'01');                                      
                                                                        
         SELECT (BE55600M.AFVIKLINGS_ID);                               
            WHEN ('OPRET_EVS_SLUT')                                     
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SLUT kørsel');         
              END;                                                      
            WHEN ('OPRET_EVS_FORSK')                                    
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS FORSKUD kørsel');      
              END;                                                      
            WHEN ('SUPPL_EVS_SLUT')                                     
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SUPPLEMENT kørsel');   
              END;                                                      
            OTHERWISE                                                   
              DO;                                                       
                 ABENDTXT = '101 Afviklings_id skal være '       !!     
                            'OPRET_EVS_FORSK, OPRET_EVS_SLUT ' !!       
                            'eller SUPPL_EVS_SLUT';                     
                 CALL PROGRAM_FEJL;                                     
              END;                                                      
         END;                                                           
                                                                        
         SELECT (BE55600M.AFVIKLINGS_ID);                               
           WHEN ('OPRET_EVS_FORSK')                                     
              DO;                                                       
                 WS_FORSKUD_SLUT_TXT   = BE55600M.AFVIKLINGS_ID;        
                 WS_TIL_EVS_AAR        = BE55600M.FORSKÅR;              
              END;                                                      
           WHEN ('OPRET_EVS_SLUT')                                      
              DO;                                                       
                 WS_FORSKUD_SLUT_TXT   = BE55600M.AFVIKLINGS_ID;        
                 WS_TIL_EVS_AAR        = BE55600M.SLUTÅR;               
              END;                                                      
           WHEN ('SUPPL_EVS_SLUT')                                      
              DO;                                                       
                 WS_FORSKUD_SLUT_TXT   = BE55600M.AFVIKLINGS_ID;        
                 WS_TIL_EVS_AAR        = BE55600M.SLUTÅR;               
              END;                                                      
           OTHERWISE                                                    
              DO;                                                       
                 ABENDTXT = '101 PARAMETERFEJL';                        
                 CALL PROGRAM_FEJL;                                     
              END;                                                      
        END; /* select */                                               
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_EVS_UDTRÆK;                                           
         CALL LÆS_BE339;                                                
                                                                        
         SKRIV_SLUT_OPL = JA;                                           
                                                                        
                                                                        
         DO WHILE (EOF_BE300 = NEJ);                                    
                                                                        
           /* ESR MINDRE END EVS */                                     
           DO WHILE (EOF_BE339 = NEJ  &                                 
                    ( BE339.DCPRCIR < BE300.DCPRN   !                   
                     (BE339.DCPRCIR = BE300.DCPRN   &                   
                      BE339.EKOMNR  < BE300.EKOMNR) !                   
                     (BE339.DCPRCIR = BE300.DCPRN   &                   
                      BE339.EKOMNR  = BE300.EKOMNR  &                   
                      BE339.EEJDNR  < BE300.EEJDNR)));                  
             CALL LÆS_BE339; /*LÆS ESR*/                                
           END;                                                         
                                                                        
           SELECT;                                                      
                                                                        
              /* ESR STØRRE END EVS */                                  
              WHEN ( BE339.DCPRCIR > BE300.DCPRN   !                    
                    (BE339.DCPRCIR = BE300.DCPRN  &                     
                     BE339.EKOMNR  > BE300.EKOMNR) !                    
                    (BE339.DCPRCIR = BE300.DCPRN  &                     
                     BE339.EKOMNR  = BE300.EKOMNR  &                    
                     BE339.EEJDNR  > BE300.EEJDNR))                     
                DO;                                                     
                  EJER_FRA_ESR = NEJ;                                   
                END;                                                    
                                                                        
              WHEN (BE339.DCPRCIR = BE300.DCPRN  &                      
                    BE339.EKOMNR  = BE300.EKOMNR  &                     
                    BE339.EEJDNR  = BE300.EEJDNR)                       
                DO;                                                     
                  EJER_FRA_ESR = JA;                                    
                END;                                                    
                                                                        
              OTHER                                                     
                DO; /* EVS STØRRE END ESR og EOF_BE339 */               
                  EJER_FRA_ESR = NEJ;                                   
                  PUT SKIP LIST('BE339.DCPRCIR =',BE339.DCPRCIR);       
                  PUT SKIP LIST('BE339.EKOMNR  =',BE339.EKOMNR);        
                  PUT SKIP LIST('BE339.EEJDNR  =',BE339.EEJDNR);        
                  PUT SKIP LIST('BE300.DCPRN   =',BE300.DCPRN );        
                  PUT SKIP LIST('BE300.EKOMNR  =',BE300.EKOMNR);        
                  PUT SKIP LIST('BE300.EEJDNR  =',BE300.EEJDNR);        
                END;                                                    
                                                                        
           END;                                                         
                                                                        
           /* Hvis ejer er fra ESR kan ejendomstype */                  
           /* hentes i BE80000T for 2familie        */                  
           IF EJER_FRA_ESR = JA                                         
           THEN                                                         
             DO;                                                        
               CALL RET_SLUT_OPLYSNINGER;                               
             END;                                                       
                                                                        
           CALL SKRIV_EVS_UDTRÆK;                                       
                                                                        
           CALL LÆS_EVS_UDTRÆK;                                         
           SKRIV_SLUT_OPL = JA;                                         
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /* Hvis der findes ESR oplysninger på et ejerforhold og der er       */
 /* forskel på cejdtypen i mellem ESR og slut for 2 familie huse      */
 /* så fravælges slut oplysninger                                     */
 /*********************************************************************/
 RET_SLUT_OPLYSNINGER: PROC;                                            
                                                                        
       CALL HENT_ESR_CEJDTYPE;                                          
                                                                        
       IF (BE300.SIDSTE.CEJDTYPE = '4' !                                
           BE300.SIDSTE.CEJDTYPE = '5' !                                
           DCLBE80000T.CEJDTYPE  = '4' !                                
           DCLBE80000T.CEJDTYPE  = '5' ) &                              
           EJER_FRA_ESR = JA                                            
       THEN                                                             
         DO;                                                            
           IF DCLBE80000T.CEJDTYPE = BE300.SIDSTE.CEJDTYPE              
           THEN                                                         
             DO;                                                        
             END;                                                       
           ELSE                                                         
             DO;                                                        
               LINIE = '';                                              
               LINIE.CCA   = ZZWS1;                                     
               LINIE.TEKST = 'FORSKEL 2FAM '  !!                        
                             BE300.DCPRN      !!                        
                             BE300.EKOMNR     !! '-' !!                 
                             BE300.EEJDNR     !! '-' !!                 
                             BE300.EENHEDLBNR !!                        
                             ' SIDSTE.CEJDTYPE ' !!                     
                                     BE300.SIDSTE.CEJDTYPE  !!          
                              ' BE80000T.CEJDTYPE ' !!                  
                                     DCLBE80000T.CEJDTYPE;              
               CALL ZZ20300(LINIE,'01');                                
               SKRIV_SLUT_OPL  = NEJ;                                   
             END;                                                       
         END;                                                           
                                                                        
                                                                        
 HENT_ESR_CEJDTYPE:PROC;                                                
                                                                        
       DCLBE80000T.CEJDTYPE = '';                                       
       ESR_2FAMILIE         = NEJ;                                      
                                                                        
                                                                        
       DCLBE80000T.TIL_EVS_AAR   = WS_TIL_EVS_AAR;                      
       DCLBE80000T.AFVIKLINGS_ID = WS_FORSKUD_SLUT_TXT;                 
       DCLBE80000T.DATA_ID       = BE55600M.DATA_ID;                    
       DCLBE80000T.EKOMNR        = BE300.EKOMNR;                        
       DCLBE80000T.EEJDNR        = BE300.EEJDNR;                        
                                                                        
                                                                        
       EXEC SQL SELECT                                                  
          BE80000T.CEJDTYPE                                             
       INTO  :DCLBE80000T.CEJDTYPE                                      
       FROM BE80000T                                                    
       WHERE TIL_EVS_AAR    = :DCLBE80000T.TIL_EVS_AAR                  
         AND AFVIKLINGS_ID  = :DCLBE80000T.AFVIKLINGS_ID                
         AND DATA_ID        = :DCLBE80000T.DATA_ID                      
         AND EKOMNR         = :DCLBE80000T.EKOMNR                       
         AND EEJDNR         = :DCLBE80000T.EEJDNR                       
       FETCH FIRST 1 ROW ONLY;                                          
                                                                        
       SELECT (SQLCA.SQLCODE);                                          
          WHEN (0)                                                      
            DO;                                                         
              ESR_2FAMILIE         = JA;                                
            END;                                                        
          WHEN (100)                                                    
            DO;                                                         
            END;                                                        
          OTHERWISE                                                     
            DO;                                                         
              ABENDTXT = '101 SELECT CEJDTYPE';                         
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
       END; /* SELECT*/                                                 
                                                                        
 END HENT_ESR_CEJDTYPE;                                                 
 END RET_SLUT_OPLYSNINGER;                                              
 /********************************************************************* 
 *       LÆS EVS UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_EVS_UDTRÆK:PROC;                                                   
                                                                        
        READ FILE (BE300I) INTO (BE300);                                
                                                                        
        IF EOF_BE300 = NEJ                                              
        THEN                                                            
          TV_LÆS_EVS =  TV_LÆS_EVS + 1;                                 
                                                                        
                                                                        
 END LÆS_EVS_UDTRÆK;                                                    
 /*********************************************************************/
 LÆS_BE339: PROC;                                                       
                                                                        
        IF FØRSTE_ESR = JA                                              
        THEN                                                            
          DO;                                                           
            READ FILE (BE339I) INTO (BE339_AR);                         
            TV_LÆS_ESR = TV_LÆS_ESR + 1;                                
    /*      GEM_ESR_DCPRCIR     = BE339.DCPRCIR;                        
            GEM_ESR_EKOMNR      = BE339.EKOMNR;                         
            GEM_ESR_EEJDNR      = BE339.EEJDNR;                         
            GEM_ESR_EENHEDLBNR  = BE339.EENHEDLBNR;*/                   
            FØRSTE_ESR = NEJ;                                           
          END;                                                          
        ELSE                                                            
          DO;                                                           
            READ FILE (BE339I) INTO (BE339_AR);                         
            TV_LÆS_ESR = TV_LÆS_ESR + 1;                                
          END;                                                          
                                                                        
    /*  GEM_ESR_DCPRCIR     = BE339.DCPRCIR;                            
        GEM_ESR_EKOMNR      = BE339.EKOMNR;                             
        GEM_ESR_EEJDNR      = BE339.EEJDNR;                             
        GEM_ESR_EENHEDLBNR  = BE339.EENHEDLBNR; */                      
                                                                        
 END LÆS_BE339;                                                         
 /********************************************************************* 
 *       SKRIV UDTRÆK FRA EVS                                         * 
 *********************************************************************/ 
 SKRIV_EVS_UDTRÆK: PROC;                                                
                                                                        
        IF SKRIV_SLUT_OPL = JA                                          
        THEN                                                            
          DO;                                                           
            WRITE FILE (BE353O) FROM (BE300);                           
            TV_SKRIV_EVS = TV_SKRIV_EVS + 1;                            
          END;                                                          
        ELSE                                                            
          DO;                                                           
            WRITE FILE (BE3531O) FROM (BE300);                          
            TV_LOG_EVS = TV_LOG_EVS + 1;                                
          END;                                                          
                                                                        
 END SKRIV_EVS_UDTRÆK;                                                  
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE: PROC;                                                      
                                                                        
         LINIE.CCA    = ZZWS1;                                          
         LINIE.TEKST = 'ANTAL LÆST SLUT OPLYSNINGER   =  ' !!           
                       TV_LÆS_EVS;                                      
         CALL ZZ20300(LINIE,'01');                                      
                                                                        
         LINIE.CCA    = ZZWS1;                                          
         LINIE.TEKST = 'ANTAL SKREVET SLUT OPLYSNINGER = ' !!           
                        TV_SKRIV_EVS ;                                  
         CALL ZZ20300(LINIE,'01');                                      
                                                                        
         LINIE.CCA    = ZZWS1;                                          
         LINIE.TEKST = 'ANTAL FRAVALGTE SLUT OPLYSNINGER='  !!          
                       TV_LOG_EVS;                                      
         CALL ZZ20300(LINIE,'01');                                      
                                                                        
         LINIE.CCA    = ZZWS1;                                          
         LINIE.TEKST = 'ANTAL LÆST PÅ ESR              = ' !!           
                        TV_LÆS_ESR;                                     
         CALL ZZ20300(LINIE,'01');                                      
                                                                        
 END TOTALLISTE;                                                        
 /**********************************************************************
 * HER UDSKRIVES SQL FEJL VED HJÆLP AF STANDARD RUTINE          RUTINE *
 **********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*       AFSLUT PROGRAM                                              */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BE300I);                                           
         CLOSE FILE (BE353O);                                           
                                                                        
 END AFSLUT;                                                            
 /*********************************************************************/
 END  BE35325;                                                          
