 /**********************************************************************
 * PROJEKT:     EVS SLUT 2021.                                         *
 * MAKRO  :     BE24013M.                                              *
  **********************************************************************
 DCL     HJPCT                 DEC FIXED (5,4);                         
 /*********************************************************************/
 /* LISTER TAKSATION OPLYSNINGER                                      */
 /*********************************************************************/
 HENT_TAKSATIONER: PROC;                                                
                                                                        
         TS_TAKSATION            = '';                                  
         KOM_AREAL.RETURKODE     = 0;                                   
         KOM_AREAL.KOMM.TS_ANTAL = 0;                                   
                                                                        
         CALL DECLARE_TAKSATION_CUR;                                    
                                                                        
         EXEC SQL OPEN TAKSATION_CUR;                                   
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.KOMM.SQLKODE    = SQLCA.SQLCODE;                 
             KOM_AREAL.KOMM.RETURKODE  = -1;                            
             KOM_AREAL.KOMM.RETURTEKST =                                
                            'SQLFEJL: OPEN TAKSATION_CUR';              
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
         CALL INIT_TAKSATION;                                           
         CALL FETCH_TAKSATION_CUR;                                      
                                                                        
         DO WHILE(SQLCODE = 0);                                         
            CALL MOVE_TO_PERSONTAB;                                     
                                                                        
            CALL SELECT_BE20000T;                                       
            CALL MOVE_TO_EJENDOMTAB;                                    
                                                                        
            CALL SKRIV_TS_TAKSATION;                                    
                                                                        
            CALL INIT_TAKSATION;                                        
                                                                        
            CALL FETCH_TAKSATION_CUR;                                   
         END;                                                           
                                                                        
         EXEC SQL CLOSE TAKSATION_CUR;                                  
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.KOMM.SQLKODE    = SQLCA.SQLCODE;                 
             KOM_AREAL.KOMM.RETURKODE  = -1;                            
             KOM_AREAL.KOMM.RETURTEKST =                                
                            'SQLFEJL: CLOSE TAKSATION_CUR';             
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END HENT_TAKSATIONER;                                                  
                                                                        
 /*********************************************************************/
 /* MOVE_TO_EJENDOMTAB                                                */
 /*********************************************************************/
 MOVE_TO_EJENDOMTAB: PROC;                                              
                                                                        
         EJENDOMTAB.EJDBELIG   =  EVS_EJD.EJDBELIG;                     
         EJENDOMTAB.ENHEDBELIG =  EVS_EJD.ENHEDBELIG;                   
                                                                        
 END MOVE_TO_EJENDOMTAB;                                                
                                                                        
 /*********************************************************************/
 /* MOVE_TO_PERSONTAB                                                 */
 /*********************************************************************/
 MOVE_TO_PERSONTAB: PROC;                                               
                                                                        
         PERSONTAB.DCPRN            = EVS_ESK.DCPRN;                    
         PERSONTAB.DINDKAAR         = EVS_ESK.DINDKAAR;                 
         PERSONTAB.EKOMNR           = EVS_ESK.EKOMNR;                   
         PERSONTAB.EEJDNR           = EVS_ESK.EEJDNR;                   
         PERSONTAB.EENHEDLBNR       = EVS_ESK.EENHEDLBNR;               
         PERSONTAB.ELBNR            = EVS_ESK.ELBNR;                    
         PERSONTAB.EGENNR           = EVS_ESK.EGENNR;                   
         PERSONTAB.FEJLNR           = EVS_ESK.FEJLNR;                   
         IF INDK_EJERSK.BPENSNED_I = 0 THEN                             
            PERSONTAB.BPENSNED         = EVS_ESK.BPENSNED;              
         ELSE                                                           
            PERSONTAB.BPENSNED         = 0;                             
         PERSONTAB.CFRÅR            = EVS_ESK.CFRÅR;                    
         PERSONTAB.CNEDSL1          = EVS_ESK.CNEDSL1;                  
         PERSONTAB.DAFSTAA          = EVS_ESK.DAFSTAA;                  
         PERSONTAB.DOVTG            = EVS_ESK.DOVTG;                    
         IF INDK_EJERSK.GPCTEJER_I = 0 THEN                             
            PERSONTAB.GPCTEJER         = EVS_ESK.GPCTEJER;              
         ELSE                                                           
            PERSONTAB.GPCTEJER         = 0;                             
         PERSONTAB.DRGNÅRFRA        = EVS_ESK.DRGNÅRFRA;                
         PERSONTAB.DRGNÅRTIL        = EVS_ESK.DRGNÅRTIL;                
         IF INDK_EJERSK.BBERGRL_AKT_I = 0 THEN                          
            PERSONTAB.BBERGRL_AKT      = EVS_ESK.BBERGRL_AKT;           
         ELSE                                                           
            PERSONTAB.BBERGRL_AKT      = 0;                             
         PERSONTAB.BTAKSEFTERRABAT = EVS_ESK.BTAKSEFTERRABAT;           
         PERSONTAB.EJET20240101    = EVS_ESK.EJET20240101;              
                                                                        
 END MOVE_TO_PERSONTAB;                                                 
                                                                        
 /*********************************************************************/
 /* INIT_TAKSATION                                                    */
 /*********************************************************************/
 INIT_TAKSATION: PROC;                                                  
                                                                        
         EJENDOMTAB.EJDBELIG = ' ';                                     
                                                                        
         PERSONTAB.FEJLNR    = '0000';                                  
         PERSONTAB.BPENSNED  = 0;                                       
         PERSONTAB.GPCTEJER  = 0;                                       
         PERSONTAB.CFRÅR     = ' ';                                     
         PERSONTAB.CNEDSL1   = ' ';                                     
         PERSONTAB.DAFSTAA   = ' ';                                     
         PERSONTAB.DOVTG     = ' ';                                     
         PERSONTAB.DRGNÅRFRA = ' ';                                     
         PERSONTAB.DRGNÅRTIL = ' ';                                     
                                                                        
 END INIT_TAKSATION;                                                    
                                                                        
 /*********************************************************************/
 /* SELECT_BE20000T                                                   */
 /*********************************************************************/
 SELECT_BE20000T: PROC;                                                 
                                                                        
         EXEC SQL                                                       
               SELECT T1.EJDBELIG                                       
                     ,T1.ENHEDBELIG                                     
               INTO  :EVS_EJD.EJDBELIG                                  
                    ,:EVS_EJD.ENHEDBELIG                                
               FROM BE20000T T1                                         
               WHERE T1.DINDKAAR    = :EVS_ESK.DINDKAAR                 
               AND   T1.EEJDNR      = :EVS_ESK.EEJDNR                   
               AND   T1.EKOMNR      = :EVS_ESK.EKOMNR                   
               AND   T1.EENHEDLBNR  = :EVS_ESK.EENHEDLBNR;              
                                                                        
                                                                        
           IF SQLCA.SQLCODE ^= 0 THEN                                   
             DO;                                                        
              KOM_AREAL.KOMM.SQLKODE    = SQLCA.SQLCODE;                
              KOM_AREAL.KOMM.RETURKODE  = -1;                           
              KOM_AREAL.KOMM.RETURTEKST =                               
                   'SQLFEJL: SELECT ON BE20000T';                       
              CALL SQL_FEJL;                                            
           END;                                                         
                                                                        
 END SELECT_BE20000T;                                                   
                                                                        
 /*********************************************************************/
 /* LISTER TAKSATION OPLYSNINGER                                      */
 /* FUNKTION 17                                                       */
 /*********************************************************************/
 DECLARE_TAKSATION_CUR: PROC;                                           
                                                                        
         EXEC SQL                                                       
                                                                        
              DECLARE TAKSATION_CUR CURSOR FOR                          
                                                                        
              SELECT T1.DCPRN                                           
                    ,T1.DINDKAAR                                        
                    ,T1.EKOMNR                                          
                    ,T1.EEJDNR                                          
                    ,T1.EENHEDLBNR                                      
                    ,T1.ELBNR                                           
                    ,T1.EGENNR                                          
                    ,T1.FEJLNR                                          
                    ,T1.BNETTO                                          
                    ,T1.BPENSNED                                        
                    ,T1.CFRÅR                                           
                    ,T1.CNEDSL1                                         
                    ,T1.DAFSTAA                                         
                    ,T1.DOVTG                                           
                    ,T1.GPCTEJER                                        
                    ,T1.DRGNÅRFRA                                       
                    ,T1.DRGNÅRTIL                                       
                    ,T1.BBERGRL_AKT                                     
                    ,T1.BTAKSEFTERRABAT                                 
                    ,T1.EJET20240101                                    
                                                                        
              FROM   BE30000T T1                                        
                                                                        
              WHERE T1.DINDKAAR = :WK_DINDKAAR                          
              AND   T1.DCPRN    = :SØG.DCPRN                            
              AND   T1.DTILGLD  = '9999-12-31-23.59.59.999999'          
                                                                        
              ORDER BY T1.EKOMNR, T1.EEJDNR, T1.EENHEDLBNR, T1.ELBNR    
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             KOM_AREAL.KOMM.SQLKODE    = SQLCA.SQLCODE;                 
             KOM_AREAL.KOMM.RETURKODE  = -1;                            
             KOM_AREAL.KOMM.RETURTEKST =                                
                            'SQLFEJL: DECLARE TAKSATION_CUR';           
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END DECLARE_TAKSATION_CUR;                                             
 /*********************************************************************/
 /* LISTER TAKSATION OPLYSNINGER                                      */
 /* FUNKTION 17                                                       */
 /*********************************************************************/
 FETCH_TAKSATION_CUR: PROC;                                             
                                                                        
         EVS_ESK = '';                                                  
                                                                        
         EXEC SQL                                                       
                                                                        
              FETCH  TAKSATION_CUR                                      
                                                                        
              INTO  :EVS_ESK.DCPRN                                      
                   ,:EVS_ESK.DINDKAAR                                   
                   ,:EVS_ESK.EKOMNR                                     
                   ,:EVS_ESK.EEJDNR                                     
                   ,:EVS_ESK.EENHEDLBNR                                 
                   ,:EVS_ESK.ELBNR                                      
                   ,:EVS_ESK.EGENNR                                     
                   ,:EVS_ESK.FEJLNR                                     
                   ,:EVS_ESK.BNETTO                                     
                       :INDK_EJERSK.BNETTO_I                            
                   ,:EVS_ESK.BPENSNED                                   
                       :INDK_EJERSK.BPENSNED_I                          
                   ,:EVS_ESK.CFRÅR                                      
                   ,:EVS_ESK.CNEDSL1                                    
                   ,:EVS_ESK.DAFSTAA                                    
                   ,:EVS_ESK.DOVTG                                      
                   ,:EVS_ESK.GPCTEJER                                   
                       :INDK_EJERSK.GPCTEJER_I                          
                   ,:EVS_ESK.DRGNÅRFRA                                  
                   ,:EVS_ESK.DRGNÅRTIL                                  
                   ,:EVS_ESK.BBERGRL_AKT                                
                       :INDK_EJERSK.BBERGRL_AKT_I                       
                   ,:EVS_ESK.BTAKSEFTERRABAT                            
                   ,:EVS_ESK.EJET20240101                               
         ;                                                              
                                                                        
         KOMM.SQLKODE = SQLCA.SQLCODE;                                  
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0);                                                    
                                                                        
           WHEN (100);                                                  
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               KOM_AREAL.KOMM.SQLKODE    = SQLCA.SQLCODE;               
               KOM_AREAL.KOMM.RETURKODE  = -1;                          
               KOM_AREAL.KOMM.RETURTEKST =                              
                              'SQLFEJL: FETCH TAKSATION_CUR';           
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* END-SELECT */                                          
                                                                        
 END FETCH_TAKSATION_CUR;                                               
 /*********************************************************************/
 /* LISTER TAKSATION OPLYSNINGER                                      */
 /* FUNKTION 17                                                RUTINE */
 /*********************************************************************/
 SKRIV_TS_TAKSATION: PROC;                                              
                                                                        
 DCL     HJ_FELTKODE   BIN FIXED(15);                                   
                                                                        
         /* EJD/ENHEDBELIG */                                           
         IF EVS_ESK.EENHEDLBNR > 0                                      
         THEN                                                           
           HJ_FELTKODE = 302;                                           
         ELSE                                                           
           HJ_FELTKODE = 105; /* EJD/ENHEDBELIG */                      
                                                                        
                                                                        
                                                                        
                                                                        
     /*       AND   T1.FELTKODE = 37;              /* BNETTO     */     
         /*   AND   T1.FELTKODE = 41;              /* BPENSNED   */     
         /*   AND   T1.FELTKODE = 78;              /* CFRÅR      */     
        /*    AND   T1.FELTKODE = 82;              /* CNEDSL1    */     
     /*       AND   T1.FELTKODE = 96;              /* DAFSTAA    */     
         /*   AND   T1.FELTKODE = 102;             /* DOVTG      */     
         /*   AND   T1.FELTKODE = 117;             /* GPCTEJER    */    
         /*   AND   T1.FELTKODE = 126;             /* DRGNÅRFRA   */    
         /*   AND   T1.FELTKODE = 127;             /* DRGNÅRTIL   */    
         /*   AND   T1.FELTKODE = 171;             /* BBERGRL_AKT */    
                                                                        
                                                                        
                                                                        
         IF PERSONTAB.DRGNÅRFRA = ' '                                   
         THEN                                                           
            CALL OPSÆT_INDKOMSTÅR (EVS_ESK.DINDKAAR,                    
                                   PERSONTAB.CFRÅR,                     
                                   PERSONTAB.DRGNÅRFRA,                 
                                   PERSONTAB.DRGNÅRTIL);                
                                                                        
         TS_TAKSATION.EKOMNR   = EVS_ESK.EKOMNR;                        
         TS_TAKSATION.EEJDNR   = EVS_ESK.EEJDNR;                        
         TS_TAKSATION.EENHEDLBNR = EVS_ESK.EENHEDLBNR;                  
                                                                        
         TS_TAKSATION.EJDBELIG = EJENDOMTAB.EJDBELIG;                   
         TS_TAKSATION.FEJLNR   = EVS_ESK.FEJLNR;                        
                                                                        
         TS_TAKSATION.BBERGRL_AKT = PERSONTAB.BBERGRL_AKT;              
         TS_TAKSATION.BTAKSEFTERRABAT = PERSONTAB.BTAKSEFTERRABAT;      
         TS_TAKSATION.EJET20240101 = PERSONTAB.EJET20240101;            
                                                                        
         IF PERSONTAB.CNEDSL1 >= '10'                                   
          & PERSONTAB.CNEDSL1 <= '29'                                   
         THEN                                                           
            TS_TAKSATION.FELT712 = '1';                                 
         ELSE                                                           
            TS_TAKSATION.FELT712 = '2';                                 
                                                                        
         IF EVS_ESK.FEJLNR > '0000'                                     
            THEN;                                                       
         ELSE                                                           
            IF (PERSONTAB.DOVTG >= PERSONTAB.DRGNÅRFRA &                
                PERSONTAB.DOVTG <= PERSONTAB.DRGNÅRTIL)                 
             ! (PERSONTAB.DAFSTAA >= PERSONTAB.DRGNÅRFRA &              
                PERSONTAB.DAFSTAA <= PERSONTAB.DRGNÅRTIL)               
            THEN                                                        
              DO;                                                       
               TS_TAKSATION.FEJLNR = '1001';                            
              END;                                                      
            ELSE                                                        
              IF PERSONTAB.BPENSNED > 0                                 
               THEN                                                     
                 DO;                                                    
                    TS_TAKSATION.FEJLNR = '1002';                       
                 END;                                                   
                                                                        
         CALL ZM_SKRIV_TS (KOM_AREAL.KOMM.TS_NAVN,                      
                           TS_TAKSATION_PTR,                            
                           STG (TS_TAKSATION),                          
                           TS_SKRIV_EVS_OK);                            
                                                                        
         KOM_AREAL.KOMM.TS_ANTAL =                                      
                   KOM_AREAL.KOMM.TS_ANTAL + 1;                         
                                                                        
 END SKRIV_TS_TAKSATION;                                                
