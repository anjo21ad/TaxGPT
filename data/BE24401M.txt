 /*********************************************************************/
 /*  MACRO BE24401M : DAN STRENG                                      */
 /*  BRUGES AF PROGRAMMERNE                                           */
 /*  -  BE22400                                                       */
 /*  -  BE22600                                                       */
 /*********************************************************************/
 /*  KAN HÅNDTERE CHAR FELTER UDEN INHOLD DVS '''' HVIS CSC SENDER    */
 /*  DEM.                                          ESB 29-03-2006     */
 /*      RETTET AF   : ONE                               nov-2021     */
 /*      VEDRØRENDE  : ÅRSTILRETNING 2021                             */
 /*      RETTET AF   : JJJ                               Sep-2022     */
 /*      VEDRØRENDE  : ÅRSTILRETNING 2024                             */
 /*      RETTET AF   : LTJ                               nov-2023     */
 /*      VEDRØRENDE  : Ifm. EVS ny lov, er der lavet en ny version af */
 /*                    macro BQ24401M til BE24401M                    */
 /*********************************************************************/
 DAN_STRENG: PROC(POS,FELTNR) RETURNS(CHAR(127));                       
                                                                        
    DCL POS        BINARY FIXED(15);                                    
    DCL FELTNR     BINARY FIXED(15);                                    
                                                                        
    DCL RETUR_STR  CHAR(127);                                           
    DCL RETUR_TAB  (127) CHAR(1) DEF RETUR_STR;                         
    DCL TST        CHAR(1);                                             
    DCL POS_RET    BINARY FIXED(15);                                    
    DCL SLUT       BIT(1);                                              
                                                                        
    IF TFLAG THEN DO;                                                   
       PUT SKIP LIST('***** DAN_STRENG ******');                        
       PUT SKIP LIST('INPUT: POS = '!!POS!!'   FELTNR = '!!FELTNR);     
       PUT SKIP LIST('SK_2 = '!!SK_2!!'   TAB_ZIZE = '!!TAB_SIZE);      
    END;                                                                
    FELT_POS_START = 1;                                                 
    FELT_POS_SLUT  = 0;                                                 
    ANT_ANF_TGN    = 0;                                                 
    SLUT = '0'B;                                                        
    POS_RET = 0;                                                        
    TST = 'A';                                                          
    RETUR_STR = ' ';                                                    
    DO WHILE (POS < (TAB_SIZE + 1) & SLUT = NEJ);                       
                                                                        
       POS = POS + 1;                                                   
       IND_POS = IND_POS + 1;                                           
       POS_RET = POS_RET + 1;                                           
       SELECT(INPUTTAB(POS));                                           
          WHEN(SK_2)                                                    
            DO;                                                         
               SLUT = JA;                                               
               IF POS > 1 & INPUTTAB((POS-1)) = SK_2                    
               THEN                                                     
                 DO;                                                    
                /*  PUT SKIP LIST('NULL-FELTNR',FELTNR); */             
                    RETUR_TAB(POS_RET) = '';                            
                    FELTBIT = '0'B;                                     
                 END;                                                   
               ELSE                                                     
                 DO; /* NY SLUT2005 ESB */                              
                   IF INPUTTAB((POS-1)) = '"' &                         
                      INPUTTAB((POS-2)) = '"' &                         
                      FELT_POS_SLUT = 2                                 
                   THEN                                                 
                     DO;                                                
                    /* PUT SKIP LIST('CHARNULL-FELTNR',FELTNR); */      
                       RETUR_TAB(POS_RET) = '';                         
                       FELTBIT = '0'B;                                  
                     END;                                               
                   ELSE                                                 
                     FELTBIT = '1'B;                                    
                 END;                                                   
            END;                                                        
          WHEN('"') DO;                                                 
            ANT_ANF_TGN   = ANT_ANF_TGN   + 1;                          
            FELT_POS_SLUT = FELT_POS_SLUT + 1;                          
          END;                                                          
          OTHERWISE DO;                                                 
             RETUR_TAB(POS_RET) = INPUTTAB(POS);                        
             FELT_POS_SLUT = FELT_POS_SLUT + 1;                         
          END;                                                          
       END;                                                             
    END;                                                                
                                                                        
    IF TFLAG THEN PUT SKIP LIST('RETUR_STR = '!!RETUR_STR);             
                                                                        
    RETURN(RETUR_STR);                                                  
 END DAN_STRENG;                                                        
