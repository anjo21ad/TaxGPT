 BH32400: /* SAMMENLÆG ESR-UDTRÆK FOR ÆGTEFÆLLER/PARTNERE */            
         PROC OPTIONS (MAIN) REORDER;                                   
 /*********************************************************************/
                                                                        
    DCL COPYRIGHT  CHAR (30) STATIC   INIT ('COPYRIGHT KMD A/S 2004');  
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      ÅRSTILRETNING VIL 'NORMALT' KUN BESTÅ AF REKOMPILERING       */
 /*      PÅ GRUND AF ÆNDRING(ER) AF STRUKTURER.  X                    */
 /*                                                                   */
 /*********************************************************************/
                                                                        
 /********************************************************************/ 
 /*                                                                   */
 /*      PROGRAMMET DANNER ET 'SAMLET ESR-UDTRÆK' PR. EJER I ESR      */
 /*      INDEHOLDENDE BÅDE EGNE OG EVT. ÆGTEFÆLLES/PARTNERS ESR-OP-   */
 /*      LYSNINGER. DER DANNES 1 INDIVID PR. HANDELSSITUATION PR.     */
 /*      EJENDOM PR. PERSON. DOG HVIS AKTUEL PERSON (EJER) ER DØD     */
 /*      DANNES DER KUN 1. INDIVID PR. EJENDOM, MEN SÅLEDES AT DETTE  */
 /*      INDIVID OPSÆTTES MED DOVTG_GL LIG DOVTG FRA EJERENS FØRSTE   */
 /*      EJERPERIODE A.H.T. SENERE AT KUNNE 'JUSTERE' DEN PERIODISE-  */
 /*      RING SOM SKAL GÆLDE FOR DEN AFDØDES FORSKUDSSKAT.            */
 /*                                                                   */
 /*      DER DANNES DESUDEN 1 INDIVID PR. KOMMUNE/EJENDOMSNR. INDE-   */
 /*      HOLDENDE KUN STAMOPLYSNINGER TIL BRUG VED EVT. OVERFØRSEL    */
 /*      TIL EJERE SOM IKKE ER/VAR REGISTRERET I ESR, MEN SOM OPRET-  */
 /*      TES PÅ GRUNDLAG AF OPLYSNINGER FRA EVS SLUT GL. ÅR OG/ELLER  */
 /*      EVS FORSKUD FOR ÅR LIG AKTUELT SLUTÅR.                       */
 /*                                                                   */
 /*      INDDATA : B.H32201D ESR-UDTRÆK INCL. DUBLETTER - SORTERET    */
 /*                                                                   */
 /*      UDDATA  : B.H32400D BEHANDLET UDTRÆK                         */
 /*              : B.H32410D STAMOPLYSNINGER                          */
 /*              : BH32401Y  KONTROLLISTE                             */
 /*                                                                   */
 /*      EXT. REF: ZS61900   (SSI-DATO)                               */
 /*                                                                   */
 /* PROGRAMMØR:    AAGE RASMUSSEN, LSU T&S  NOV.   2001               */
 /* ÅRSTILRETTET:  OLUF MØRK,      LSU T&S  JUNI   2002               */
 /* RETTET:        AAGE RASMUSSEN  LSU T&S  DEC.   2002               */
 /* RETTET:        AAGE RASMUSSEN  LSU T&S  DEC.   2003               */
 /* RETTET:        BO SCHMIDT      LSU T&S  JUN.   2004               */
 /* FORSKUD06   SUSANNE KROGH VILLUMSEN                 26/05-2005    */
 /*             ÅRSTILRETNING TIL FORSKUD 2006                        */
 /*             VURDOPL3 ÆNDRET TIL VURDOPL01                         */
 /*                                                                   */
 /* SLUT 2012   LKA, OKTOBER 2012                                     */
 /*             OMCOMPILERET P.G.A.                                   */
 /*             NYT FELT CEANV I STRUKTUR BH32200N (BH32202N)         */
 /*             KRAV 18-01 - BENYTTELSESKODE 25                       */
 /*             FLYTTER INFORMATIONEN I CEANV IMELLEM BH322 OG BH324  */
 /*                                                                   */
 /* FORSKUD2014 Lars Harritshøj                            Maj 2013   */
 /*             Omcompileret Ingen ændring                            */
 /*                                                                   */
 /* SLUT 2103   Jørgen Saugmann                            okt 2013   */
 /*             Omcompileret Ingen ændring                            */
 /*                                                                   */
 /* FORSKUD2015 Jørgen Saugmann                            jun 2014   */
 /*             Compileret Ingen ændringer                            */
 /* SLUT 2104   Jørgen Saugmann                            okt 2014   */
 /*             Omcompileret Ingen ændring                            */
 /* FORSKUD2016 Z6JNK                                      08.06.2015 */
 /*             Compileret. Ingen ændringer                           */
 /* FORSKUD2017 Z8QEB                                      12.06.2016 */
 /*             Compileret. Ingen ændringer                           */
 /* SLUT2017    Z8QEB                                      31.10.2017 */
 /*             Rettet ægtefælle dublet som ikke har en ejer          */
 /*             gav ekstra række på beregningsbånd #01                */
 /* SLUT2018    Esben Brodersen z6ebd                      12.09.2018 */
 /*             BH32200N struktur udvidet til 2Familiebolig.          */
 /*             Enhedsløbenummer EENHEDLBNR indgår i                  */
 /*             undersøgelse om det er et nyt ejerforhold             */
 /* Rettelse #02: Edward Laryea, EDL                     22-03-2019   */
 /*               Nyt felt STRAKS_EVS i BH32202N                      */
 /* Forskud2021 Per Brunk                              Marts 2020     */
 /*               Nye felter i BH32202N VURMARK og VUR2001            */
 /* Slut 2021 : Per Brunk PBR Defect 889                     Jun 2021 */
 /*             Flyt BFENR og VUREJENDOMID fra BH322 til BH324        */
 /* Forskud2023 Per Brunk   Årsrul                       08.06.2022   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      DECLARE AF ENTRIES OG BUILTINS                               */
 /*********************************************************************/
 DCL     ZS61900   ENTRY OPTIONS (INTER ASM);          /* UDSKRIV SSI */
 DCL     ZS38100   ENTRY OPTIONS (INTER ASM);          /* TIMESTAMP   */
 DCL     ADDR      BUILTIN;                                             
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
 DCL     BH322I          FILE RECORD INPUT;                             
                                                                        
 DCL     BH3240O         FILE RECORD OUTPUT;                            
 DCL     BH3241O         FILE RECORD OUTPUT;                            
 DCL     BH3242O         FILE RECORD OUTPUT;                            
                                                                        
 /*********************************************************************/
 /*      DECLARE AF INPUT                                             */
 /*********************************************************************/
 DCL     BH322_AR        CHAR      (2000) VAR;                          
 DCL     1 BH322         BASED     (ADDR(BH322_AR)),                    
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH32200N;;                                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF OUTPUT                                            */
 /*********************************************************************/
 DCL     1 BH324,                                                       
           %INCLUDE BH32400N;;                                          
                                                                        
 DCL     1 BH32410,                                                     
           %INCLUDE BH32410N;;                                          
                                                                        
                                                                        
 DCL  LOG_BEHANDLING      CHAR(4000) VAR;                               
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     1 TV,                                                          
           2  LÆSTE_IALT     FIXED DEC (7),                             
           2  RETEJB1        FIXED DEC (7),                             
           2  RETEJB2        FIXED DEC (7),                             
           2  FLYTEJB        FIXED DEC (7),                             
           2  SKR_EJER_IALT  FIXED DEC (7),                             
           2  SKR_STAM_IALT  FIXED DEC (7);                             
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     EOF_BH322       BIT       (1);                                 
 DCL     JA              BIT       (1)       INIT ('1'B);               
 DCL     NEJ             BIT       (1)       INIT ('0'B);               
 DCL     GEM_DOVTG_GL    FIXED     (9)       INIT (0);                  
                                                                        
 DCL     ESR_EJER_FINDES BIT       (1)       INIT ('0'B);               
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTE                      */
 /*********************************************************************/
 DCL     1 OVLIN1,                                                      
           2 CTL         CHAR      (01),                                
           2 TXT1        CHAR      (40) INIT ('PROGRAM: BH32400L'),     
           2 TXT2        CHAR      (68) INIT                            
             ('DAN ENDELIGT ESR UDTRÆK TIL BEREGNING AF EVS SLUT'),     
           2 TXT3        CHAR      (14) INIT ('UDSKREVET DEN'),         
           2 DATO        CHAR      (10);                                
 DCL     1 OVLIN2,                                                      
           2 CTL         CHAR      (01),                                
           2 F1          CHAR      (51) INIT (' '),                     
           2 TXT1        CHAR      (91) INIT                            
           ('          KØRSELSKONTROLLISTE');                           
 DCL     LINIE           CHAR      (133);                               
 DCL     1  DETAIL                           DEF LINIE,                 
          2  STYR        CHAR      (1),                                 
          2  TEKST       CHAR      (70),                                
          2  ANTAL       PIC       'Z.ZZZ.ZZ9';                         
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      H O U S E - K E E P I N G                                    */
 /*********************************************************************/
                                                                        
         CALL ZS61900;                                                  
                                                                        
         OPEN FILE (BH322I)  TITLE ('BH32201I');                        
         OPEN FILE (BH3240O) TITLE ('BH32400O');                        
         OPEN FILE (BH3241O) TITLE ('BH32410O');                        
         OPEN FILE (BH3242O) TITLE ('BH32420O');                        
                                                                        
                                                                        
         CALL ZZ20390 ('*OPEN','BH32401Y');                             
                                                                        
         ON ENDFILE (BH322I)                                            
         BEGIN;                                                         
            EOF_BH322 = JA;                                             
         END;                                                           
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
                                                                        
         TV = '';                                                       
         BH324 = '';                                                    
         BH32410 = '';                                                  
                                                                        
 /*********************************************************************/
 /*      P R O C E S S                                                */
 /*********************************************************************/
                                                                        
         CALL LÆS;                                                      
                                                                        
         DO WHILE (EOF_BH322 = NEJ);                                    
            CALL BEHANDL;                                               
            CALL LÆS;                                                   
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /*      LÆS SAMSORTEREDE UDTRÆK                                      */
 /*********************************************************************/
 LÆS: PROC;                                                             
                                                                        
         READ FILE (BH322I) INTO (BH322_AR);                            
                                                                        
         IF EOF_BH322 = NEJ                                             
         THEN                                                           
            TV.LÆSTE_IALT = TV.LÆSTE_IALT + 1;                          
                                                                        
 END LÆS;                                                               
 /*********************************************************************/
 /*      BEHANDLING AF INPUT                                          */
 /*********************************************************************/
 BEHANDL: PROC;                                                         
                                                                        
         IF BH322.VURDOPL01.EJEBOL(1).BEJEBOL = 0 &    /*FORSKUD06*/    
            BH322.VURDOPL01.EJEBOL(1).EJBOLTYP > '0'   /*FORSKUD06*/    
         THEN                                                           
            DO;                                                         
               /* DER BURDE IKKE VÆRE OPSAT EJERBOLIG-OPLYSNINGER   */  
               /* I ESR-UDTRÆKKET VEDR. 2001-ÅRSVURDERINGEN HVIS    */  
               /* EJERBOLIGVÆRDI 1 ER LIG 0, DERFOR DETTE REDNINGS- */  
               /* FORSØG IFLG. AFTALE MED KG T&S                    */  
                                                                        
               /* DENNE REGL ER GJORT INAKTIV TIL FORSKUD 2020      */  
               /* SE DEFECT 46 DER LOGGES INDTIL VIDERE EBD 03-06-2019*/
                                                                        
 /*           SELECT (BH322.VURDOPL01.EJEBOL(1).EJBOLTYP);/*FORSKUD06*/ 
 /*                WHEN ('1')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_1 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_1 - 1;        /*FORSKUD06*/
 /*                WHEN ('2')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_2 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_2 - 1;        /*FORSKUD06*/
 /*                WHEN ('4')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_4 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_4 - 1;        /*FORSKUD06*/
 /*                WHEN ('5')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_5 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_5 - 1;        /*FORSKUD06*/
 /*                WHEN ('6')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_6 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_6 - 1;        /*FORSKUD06*/
 /*                WHEN ('8')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_8 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_8 - 1;        /*FORSKUD06*/
 /*                WHEN ('9')                                           
 /*                  BH322.VURDOPL01.ANT_FOR_9 =           /*FORSKUD06*/
 /*                  BH322.VURDOPL01.ANT_FOR_9 - 1;        /*FORSKUD06*/
 /*                OTHERWISE;                                           
 /*              END; /* SELECT */                                      
                                                                        
 /*              BH322.VURDOPL01.EJEBOL(1) = '';       /*FORSKUD06*/    
                                                                        
               TV.RETEJB1 = TV.RETEJB1 + 1;                             
                                                                        
               LOG_BEHANDLING = '';                                     
               LOG_BEHANDLING = 'NULSTIL VURDOPL01.EJEBOL(1) ';         
                                                                        
               LOG_BEHANDLING = LOG_BEHANDLING !!  ' , ' !!             
                                BH322.SORTCPR  !!  ' , ' !!             
                                BH322.EKOMNR   !!  ' , ' !!             
                                BH322.EEJDNR   !!  ' , ' !!             
                                BH322.EENHEDLBNR;                       
                                                                        
               WRITE FILE(BH3242O) FROM(LOG_BEHANDLING);                
                                                                        
            END;                                                        
                                                                        
         IF BH322.VURDOPL01.EJEBOL(2).BEJEBOL = 0 &  /*FORSKUD06*/      
            BH322.VURDOPL01.EJEBOL(2).EJBOLTYP > '0' /*FORSKUD06*/      
         THEN                                                           
            DO;                                                         
               /* DER BURDE IKKE VÆRE OPSAT EJERBOLIG-OPLYSNINGER   */  
               /* I ESR-UDTRÆKKET VEDR. 2001-ÅRSVURDERINGEN HVIS    */  
               /* EJERBOLIGVÆRDI 2 ER LIG 0, DERFOR DETTE REDNINGS- */  
               /* FORSØG IFLG. AFTALE MED KG T&S                    */  
                                                                        
               /* DENNE REGL ER GJORT INAKTIV TIL FORSKUD 2020      */  
               /* SE DEFECT 46 DER LOGGES INDTIL VIDERE EBD 03-06-2019*/
                                                                        
 /*            SELECT (BH322.VURDOPL01.EJEBOL(2).EJBOLTYP);/*FORSKUD06*/
 /*             WHEN ('1')                                              
 /*                BH322.VURDOPL01.ANT_FOR_1 =                          
 /*                BH322.VURDOPL01.ANT_FOR_1 - 1;                       
 /*             WHEN ('2')                                              
 /*                BH322.VURDOPL01.ANT_FOR_2 =                          
 /*                BH322.VURDOPL01.ANT_FOR_2 - 1;                       
 /*             WHEN ('4')                                              
 /*                BH322.VURDOPL01.ANT_FOR_4 =                          
 /*                BH322.VURDOPL01.ANT_FOR_4 - 1;                       
 /*             WHEN ('5')                                              
 /*                BH322.VURDOPL01.ANT_FOR_5 =                          
 /*                BH322.VURDOPL01.ANT_FOR_5 - 1;                       
 /*             WHEN ('6')                                              
 /*                BH322.VURDOPL01.ANT_FOR_6 =                          
 /*                BH322.VURDOPL01.ANT_FOR_6 - 1;                       
 /*             WHEN ('8')                                              
 /*                BH322.VURDOPL01.ANT_FOR_8 =                          
 /*                BH322.VURDOPL01.ANT_FOR_8 - 1;                       
 /*             WHEN ('9')                                              
 /*                BH322.VURDOPL01.ANT_FOR_9 =                          
 /*                BH322.VURDOPL01.ANT_FOR_9 - 1;                       
 /*             OTHERWISE;                                              
 /*         END; /* SELECT */                                           
                                                                        
 /*            BH322.VURDOPL01.EJEBOL(2) = ''; */                       
               TV.RETEJB2 = TV.RETEJB2 + 1;                             
                                                                        
               LOG_BEHANDLING = '';                                     
               LOG_BEHANDLING = 'NULSTIL VURDOPL01.EJEBOL(2) ';         
                                                                        
               LOG_BEHANDLING = LOG_BEHANDLING !! ' , '  !!             
                                BH322.SORTCPR  !! ' , '  !!             
                                BH322.EKOMNR   !! ' , '  !!             
                                BH322.EEJDNR   !! ' , '  !!             
                                BH322.EENHEDLBNR;                       
                                                                        
               WRITE FILE(BH3242O) FROM(LOG_BEHANDLING);                
                                                                        
            END;                                                        
                                                                        
         IF (BH322.VURDOPL01.EJEBOL(1).BEJEBOL = 0 &                    
             BH322.VURDOPL01.EJEBOL(1).EJBOLTYP < '1') &                
            (BH322.VURDOPL01.EJEBOL(2).BEJEBOL ^= 0 !                   
             BH322.VURDOPL01.EJEBOL(2).EJBOLTYP > '0')                  
         THEN                                                           
            DO;                                                         
               BH322.VURDOPL01.EJEBOL(1) =                              
               BH322.VURDOPL01.EJEBOL(2), BY NAME;                      
               BH322.VURDOPL01.EJEBOL(2) = '';                          
               TV.FLYTEJB = TV.FLYTEJB + 1;                             
            END;                                                        
                                                                        
         CALL SKRIV_STAMOPL;                                            
                                                                        
         SELECT;                                                        
                                                                        
            WHEN (BH324.SORTCPR ^= BH322.SORTCPR)                       
               DO;                                                      
                  IF BH324.SORTCPR ^= 0   /* IKKE FØRSTE GANG */        
                  THEN                                                  
                     CALL SKRIV;                                        
                                                                        
                  GEM_DOVTG_GL = 0;                                     
                                                                        
                  IF BH322.SORTTYPE = '1' /* AKTUEL EJER */             
                  THEN                                                  
                     GEM_DOVTG_GL = BH322.EJER.DOVTG;                   
                                                                        
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
            WHEN (BH324.SORTCPR = BH322.SORTCPR &                       
                 (BH324.EKOMNR ^= BH322.EKOMNR !                        
                  BH324.EEJDNR ^= BH322.EEJDNR !                        
                  BH324.EENHEDLBNR ^= BH322.EENHEDLBNR))                
               DO;                                                      
                  /* SAMME PERSON MEN NY EJENDOM/ENHED 2FAMILIE BOLIG */
                  CALL SKRIV;                                           
                                                                        
                  GEM_DOVTG_GL = 0;                                     
                                                                        
                  IF BH322.SORTTYPE = '1' /* AKTUEL EJER */             
                  THEN                                                  
                     GEM_DOVTG_GL = BH322.EJER.DOVTG;                   
                                                                        
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
            WHEN (BH324.SORTCPR = BH322.SORTCPR &                       
                  BH324.EKOMNR  = BH322.EKOMNR  &                       
                  BH324.EEJDNR  = BH322.EEJDNR  &                       
                  BH324.EENHEDLBNR = BH322.EENHEDLBNR &                 
                  BH324.SORTDATO ^= BH322.SORTDATO)                     
              DO;                                                       
                 /* SAMME PERSON OG SAMME EJENDOM */                    
                 /* MEN NY EJERPERIODE            */                    
                IF BH322.CCIVS  ^= 'D' &                                
                   BH324.CCIVS  ^= 'D' &                                
                   BH324.EJER.DCPRCIR > 0                               
                THEN                                                    
                  DO;                                                   
                    IF BH324.EJER.DCPRCIR  =  BH322.EJER.DCPRCIR        
                    THEN                                                
                      DO;                                               
                        BH322.EJER.CTILKØB = 'J';                       
                                                                        
                        IF BH324.EJER.DOVTG  =  GEM_DOVTG_GL            
                        THEN                                            
                           BH324.EJER.CTILKØB = 'J';                    
                        IF GEM_DOVTG_GL  >=  BH324.EJER.DRGNÅRFRA       
                        THEN                                            
                           BH324.EJER.CTILKØB = ' ';                    
                      END;                                              
                                                                        
                    CALL SKRIV;                                         
                 END;                                                   
                                                                        
                CALL FLYT_DATA;                                         
              END;                                                      
                                                                        
            OTHERWISE                                                   
               DO;                                                      
                  /* SAMME PERSON OG SAMME EJENDOM */                   
                  /* OG SAMME EJERPERIODE          */                   
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
         END; /* SELECT */                                              
                                                                        
 END BEHANDL;                                                           
 /*********************************************************************/
 /*      FLYT OPLYSNINGER FRA INPUT TIL RELEVANT PLADS I OUTPUT       */
 /*********************************************************************/
 FLYT_DATA: PROC;                                                       
                                                                        
         BH324.SORTCPR      = BH322.SORTCPR;                            
         BH324.EKOMNR       = BH322.EKOMNR;                             
         BH324.EEJDNR       = BH322.EEJDNR;                             
         BH324.EENHEDLBNR   = BH322.EENHEDLBNR;                         
         BH324.SORTDATO     = BH322.SORTDATO;                           
         BH324.BFENR        = BH322.BFENR;         /*D889*/             
         BH324.VUREJENDOMID = BH322.VUREJENDOMID;  /*D889*/             
                                                                        
         IF BH324.SORTTYPE = ' ' !  BH322.SORTTYPE = '1'                
         THEN                                                           
            BH324.SORTTYPE = BH322.SORTTYPE;                            
                                                                        
         BH324.CSBSK = BH322.CSBSK;                                     
         BH324.STAMOPL = BH322.STAMOPL, BY NAME;                        
                                                                        
     /*   PUT SKIP LIST('BH324.EJER.DCPRCIR: ' !! BH324.EJER.DCPRCIR);  
          PUT SKIP LIST('BH324.EKOMNR: ' !! BH324.EKOMNR);              
          PUT SKIP LIST('BH324.EEJDNR: ' !! BH324.EEJDNR);              
          PUT SKIP LIST('BH324.CEANV: ' !! BH324.CEANV); */             
                                                                        
         SELECT;                                                        
                                                                        
           WHEN (BH322.SORTTYPE = '1')         /* AKTUEL EJER */        
             DO;                                                        
                BH324.CCIVS = BH322.CCIVS;                              
                BH324.EJER = BH322.EJER, BY NAME;                       
                ESR_EJER_FINDES   = JA;                                 
             END;                                                       
                                                                        
           WHEN (BH322.SORTTYPE = '2')         /* DUBLET */             
             DO;                                                        
               BH324.ÆGTF = BH322.EJER, BY NAME;                        
             END;                                                       
                                                                        
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
 END FLYT_DATA;                                                         
 /*********************************************************************/
 /*      SKRIV BEHANDLET UDTRÆK                                       */
 /*********************************************************************/
 SKRIV: PROC;                                                           
                                                                        
         IF BH324.EJER.DCPRCIR > 0                                      
         THEN                                                           
            DO;                                                         
               BH324.SORTCITYP = BH324.EJER.CITYP;                      
               BH324.EJER.DOVTG_GL = GEM_DOVTG_GL;                      
                                                                        
               IF BH324.EJER.DOVTG ^= GEM_DOVTG_GL                      
               THEN                                                     
                  DO;                                                   
                     IF BH324.EJER.DAFSTÅ = 0 !                         
                        BH324.EJER.DAFSTÅ > BH324.EJER.DRGNÅRTIL        
                     THEN                                               
                        BH324.EJER.CTILKØB = 'J';                       
                                                                        
                     IF (BH324.EJER.DAFSTÅ > 0 &                        
                         BH324.EJER.DAFSTÅ <= BH324.EJER.DRGNÅRTIL) &   
                         BH322.EJER.CTILKØB = ' '                       
                     THEN                                               
                        BH324.EJER.CTILKØB = ' ';                       
                  END;                                                  
            END;                                                        
         ELSE                                                           
            BH324.SORTCITYP = BH324.ÆGTF.CITYP;                         
                                                                        
                                                                        
       /*PUT SKIP LIST('SKRIV');                                        
         PUT SKIP LIST('BH322.SORTCPR  = ',BH322.SORTCPR);              
         PUT SKIP LIST('BH322.EKOMNR   = ',BH322.EKOMNR);               
         PUT SKIP LIST('BH322.EEJDNR   = ',BH322.EEJDNR);               
         PUT SKIP LIST('BH322.SORTDATO = ',BH322.SORTDATO);             
         PUT SKIP LIST('BH324.SORTCPR  = ',BH324.SORTCPR);              
         PUT SKIP LIST('BH324.EKOMNR   = ',BH324.EKOMNR);               
         PUT SKIP LIST('BH324.EEJDNR   = ',BH324.EEJDNR);               
         PUT SKIP LIST('BH324.SORTDATO = ',BH324.SORTDATO);*/           
                                                                        
         /* FOR AT FOR FLYTTET ÆGTEFÆLLE OPLYSNINGER     */             
         /* PÅ PLADS NÅR DE KOMMER I OMVENDT RÆKKE FØLGE */             
         IF BH324.SORTCPR = BH322.SORTCPR &                             
            BH324.EKOMNR  = BH322.EKOMNR  &                             
            BH324.EEJDNR  = BH322.EEJDNR  &                             
            BH324.EENHEDLBNR = BH322.EENHEDLBNR &                       
            BH324.SORTDATO < BH322.SORTDATO &                           
            BH322.SORTTYPE = '2' &                                      
            BH324.SORTTYPE = '1'                                        
         THEN                                                           
           DO;                                                          
             /*                                                         
             PUT SKIP LIST('EKSTRA FLYT DATA FOR');                     
             PUT SKIP LIST('BH324.SORTCPR = ',BH324.SORTCPR);           
             PUT SKIP LIST('BH324.EKOMNR   = ',BH324.EKOMNR);           
             PUT SKIP LIST('BH324.EEJDNR   = ',BH324.EEJDNR);           
             PUT SKIP LIST('BH324.SORTDATO =',BH324.SORTDATO);          
             */                                                         
                                                                        
             BH324.ÆGTF = BH322.EJER, BY NAME;                          
             CALL LÆS; /*NÆSTE RÆKKE LÆSES IND I BH322*/                
           END;                                                         
                                                                        
           WRITE FILE (BH3240O) FROM (BH324);                           
           TV.SKR_EJER_IALT = TV.SKR_EJER_IALT + 1;                     
                                                                        
         BH324 = '';                                                    
                                                                        
 END SKRIV;                                                             
 /*********************************************************************/
 /*      SKRIV STAMOPLYSNINGER                                        */
 /*********************************************************************/
 SKRIV_STAMOPL:                                                         
 PROC;                                                                  
                                                                        
         IF BH32410.EKOMNR   = BH322.EKOMNR   &                         
            BH32410.EEJDNR   = BH322.EEJDNR   &                         
            BH32410.EENHEDLBNR = BH322.EENHEDLBNR                       
         THEN                                                           
            RETURN;                                                     
                                                                        
         BH32410.EKOMNR  = BH322.EKOMNR;                                
         BH32410.EEJDNR  = BH322.EEJDNR;                                
         BH32410.EENHEDLBNR = BH322.EENHEDLBNR;                         
         BH32410.STAMOPL = BH322.STAMOPL, BY NAME;                      
                                                                        
         WRITE FILE (BH3241O) FROM (BH32410);                           
         TV.SKR_STAM_IALT = TV.SKR_STAM_IALT + 1;                       
                                                                        
 END SKRIV_STAMOPL;                                                     
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         LINIE = ZZIK1;                                                 
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         OVLIN1.CTL= ZZWS1;                                             
         OVLIN1.DATO = UDDATO;                                          
         CALL ZZ20300 (OVLIN1,'01');                                    
                                                                        
         OVLIN2.CTL= ZZWS2;                                             
         CALL ZZ20300 (OVLIN2,'01');                                    
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL LÆSTE IALT                    B.H32201D';
         DETAIL.ANTAL = TV.LÆSTE_IALT;                                  
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = 'ANTAL EJERBOLIG1=0 (2001+5%) IKKE SLETTET    ';
         DETAIL.ANTAL = TV.RETEJB1;                                     
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL EJERBOLIG2=0 (2001+5%) IKKE SLETTET    ';
         DETAIL.ANTAL = TV.RETEJB2;                                     
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL EJERBOLIG (2001+5%) FLYTTET           '; 
         DETAIL.ANTAL = TV.FLYTEJB;                                     
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL SKREVET IALT                  B.H32400D';
         DETAIL.ANTAL = TV.SKR_EJER_IALT;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'ANTAL SKREVNE  K U N  STAMOPLYSNING B.H32410D';
         DETAIL.ANTAL = TV.SKR_STAM_IALT;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /*      AFSLUTNING                                                   */
 /*********************************************************************/
 AFSLUT:                                                                
 PROC;                                                                  
                                                                        
         IF BH324.SORTCPR ^= 0    /* SIDSTE EJERS SIDSTE INDIVID */     
         THEN                                                           
            CALL SKRIV;                                                 
                                                                        
         CALL KONTROLLISTE;                                             
         CALL ZZ20300(' ','99');                                        
                                                                        
         CLOSE FILE (BH322I),                                           
               FILE (BH3240O),                                          
               FILE (BH3241O),                                          
               FILE (BH3242O);                                          
                                                                        
 END AFSLUT;                                                            
                                                                        
 END BH32400;                                                           
