 BH33700: PROCEDURE OPTIONS (MAIN);                                     
 /********************************************************************/ 
 /* Projekt:     EVS Forskud / EVS Slut                              */ 
 /* Programnavn: BH33700                                             */ 
 /* Programtype: Batch                                               */ 
 /* Funktion:    Forbrænder program, som opsætter lejlighedsnummer   */ 
 /*              for 2-familiehuse (ejendomstype 4 eller 5)          */ 
 /*              pr. indkomstår og EVS-kørsel (Slut eller Forskud).  */ 
 /*              Dvs. rækker der mangler i tabel:                    */ 
 /*                 EVS-enhedsanvendelsestabel (BQ80300T)            */ 
 /*                 EVS-enhedssporbarhed (BQ80400T)                  */ 
 /*              insertes og diverse felter udfyldes i output filen  */ 
 /*              BH337O.                                             */ 
 /*                                                                  */ 
 /*              Feltet TEKFEJLNR udfyldes med følgende fejlnumre:   */ 
 /*                 3700 - CN570I record findes ikke i BQ80200T      */ 
 /*                 3701 - Personen med bopæl på en af de            */ 
 /*                        ejerboligfordelingerne findes ikke i      */ 
 /*                        CSRP-tabellerne BQ70000T og BQ71000T      */ 
 /*                                                                  */ 
 /* Input:       CN570I   - Grund- eller supplementsleverancen       */ 
 /*                         fra ESR/VUR (CN57000)                    */ 
 /*                                                                  */ 
 /*              BH337P   - Parameter fil - Find EVS Kørsel type     */ 
 /*                                                                  */ 
 /*              DEBUGI   - Skal programmet debugge (put skip):      */ 
 /*                            Hvis  (filen DEBUGI ikke findes     ) */ 
 /*                            eller (DEBUG_FROM = 0 og DEBU_TO = 0) */ 
 /*                               Ingen debug                        */ 
 /*                            ellers                                */ 
 /*                               Debug alt for de valgte            */ 
 /*                               record numre i CN570I              */ 
 /*                                                                  */ 
 /*              BQ80200T - EVS-enhedsløbenummertabel (DB2)          */ 
 /*                                                                  */ 
 /*              BQ70000T - Aktuel CSRP-tabel (DB2)                  */ 
 /*                                                                  */ 
 /*              BQ71000T - Historik CSRP-tabel (DB2)                */ 
 /*                                                                  */ 
 /* Output:      BQ80300T - EVS-enhedsanvendelsestabel (DB2)         */ 
 /*                                                                  */ 
 /*              BQ80400T - EVS-enhedssporbarhed (DB2)               */ 
 /*                                                                  */ 
 /*              BH337O   - Indhold lig CN570I                       */ 
 /*                         + felt EENHEDLBNR                        */ 
 /*                         + felt TEKFEJLNR                         */ 
 /*                         + felt ENHEDBELIG                        */ 
 /*                         + felt VEJR_ENHED                        */ 
 /*                         + felt HUSNR_ENHED                       */ 
 /*                         + felt ETAGE_ENHED                       */ 
 /*                         + felt SDØR_ENHED                        */ 
 /*                                                                  */ 
 /*------------------------------------------------------------------*/ 
 /*                                                                  */ 
 /* Oprettet:    Gitte Jensen, GJN                        11-09-2018 */ 
 /*                                                                  */ 
 /* Rettelse #01: Gitte Jensen, GJN                       25-01-2019 */ 
 /*               Nye felter NY_VURDERING & NY_VURDERING i BH33701M  */ 
 /* Rettelse #02: Edward Laryea, EDL                     22-03-2019  */ 
 /*               Nyt felt STRAKS_EVS i BH31800N                     */ 
 /* Rettelse #03: Esben Brodersen, EBD                   26-06-2019  */ 
 /*               TRIM på DB2 sammenligning af CHAR felter           */ 
 /* Rettelse #03: Esben Brodersen, EBD                   19-11-2019  */ 
 /*               TRIM på DB2 sammenligning af CHAR felter ændret    */ 
 /* Forskud2021   Per Brunk                              mar 2020    */ 
 /*               Nye felter i BH31800N/BH33700N VURMARK og VUR2001  */ 
 /* Slut 2020 : Per Brunk PBR Ny struktur BH33700N/BH31800N Okt 2020 */ 
 /********************************************************************/ 
                                                                        
 DCL   COPYRIGHT       CHAR(30)       INIT ('(C) KMD A/S 2018');        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT KALD AF EKSTERNE MODULER */ 
                                                                        
 /********************************************************************/ 
 /* Builtin functions                                                */ 
 /********************************************************************/ 
 DCL   ADDR            BUILTIN;                                         
 DCL   NULL            BUILTIN;                                         
 DCL   PLIDUMP         BUILTIN;                                         
                                                                        
 /********************************************************************/ 
 /* CN570I - Grund- eller supplementsleverancen fra ESR/VUR (CN57000)*/ 
 /********************************************************************/ 
 DCL   CN570I          FILE RECORD INPUT;                               
                                                                        
 DCL   CN570_AREA      CHAR(2000) VAR;                                  
                                                                        
 DCL 1 CN570           BASED(ADDR(CN570_AREA)),                         
       4 CN570_LENGTH  FIXED BIN(15),                                   
       %INCLUDE BH31800N;                                               
                                                                        
 /********************************************************************/ 
 /* BH337P - Find EVS Kørsel type                                    */ 
 /********************************************************************/ 
 DCL   BH337P          FILE RECORD INPUT;                               
                                                                        
 DCL 1 BH337_PARM,                                                      
       %INCLUDE BH54900N;                                               
                                                                        
 /********************************************************************/ 
 /* DEBUGI - Skal programmet debugge (put skip)                      */ 
 /********************************************************************/ 
 DCL   DEBUGI          FILE RECORD INPUT;                               
                                                                        
 DCL 1 DEBUG_REC,                                                       
       3 DEBUG_FROM    PIC'(7)9',                                       
       3 DEBUG_FILL1   CHAR(1),                                         
       3 DEBUG_TO      PIC'(7)9',                                       
       3 DEBUG_FILL2   CHAR(65);                                        
                                                                        
 /********************************************************************/ 
 /* BH337O - Indhold lig CN570I                                      */ 
 /*          + felt EENHEDLBNR                                       */ 
 /*          + felt TEKFEJLNR                                        */ 
 /*          + felt ENHEDBELIG                                       */ 
 /*          + felt VEJR_ENHED                                       */ 
 /*          + felt HUSNR_ENHED                                      */ 
 /*          + felt ETAGE_ENHED                                      */ 
 /*          + felt SDØR_ENHED                                       */ 
 /********************************************************************/ 
 DCL   BH337O          FILE RECORD OUTPUT;                              
                                                                        
 DCL   BH337_AREA      CHAR(2000) VAR;                                  
                                                                        
 DCL 1 BH337           BASED(ADDR(BH337_AREA)),                         
       4 BH337_LENGTH  FIXED BIN(15),                                   
       %INCLUDE BH33700N;                                               
                                                                        
 /********************************************************************/ 
 /* Interne arbejdsfelter                                            */ 
 /********************************************************************/ 
 DCL JA                BIT(01)        INIT('1'B);                       
 DCL NEJ               BIT(01)        INIT('0'B);                       
 DCL EOF_BH337P        BIT(01)        INIT('0'B);                       
 DCL EVS_FORSKUD       BIT(01)        INIT('0'B);                       
 DCL EVS_SLUT          BIT(01)        INIT('0'B);                       
 DCL EVS_SUPPL         BIT(01)        INIT('0'B);                       
 DCL FLERE_CN570I      BIT(01)        INIT('1'B);                       
 DCL EN_EJERBOLIG      BIT(01)        INIT('0'B);                       
 DCL TO_EJERBOLIG      BIT(01)        INIT('0'B);                       
 DCL GL_VURDERING      BIT(01)        INIT('0'B); /*#01*/               
 DCL NY_VURDERING      BIT(01)        INIT('0'B); /*#01*/               
 DCL BQ802T_FOUND      BIT(01)        INIT('0'B);                       
 DCL BQ803T_FOUND      BIT(01)        INIT('0'B);                       
 DCL CSRP_FOUND        BIT(01)        INIT('0'B);                       
 DCL CSRP_AKTUEL       BIT(01)        INIT('0'B);                       
 DCL CSRP_HISTORIK     BIT(01)        INIT('0'B);                       
                                                                        
 DCL PUTSKIP_THIS      BIT(01)        INIT('0'B);                       
                                                                        
 DCL EVS_KOERSEL       CHAR(07)       INIT(' ');                        
 DCL EVS_KOERSEL_F     CHAR(07)       INIT('FORSKUD');                  
 DCL EVS_KOERSEL_S     CHAR(07)       INIT('SLUT   ');                  
 DCL EVS_KOERSEL_U     CHAR(07)       INIT('SUPPL  ');                  
                                                                        
 DCL INDKAAR           CHAR(04)       INIT('');                         
 DCL AFVIKL_ID         CHAR(16)       INIT('');                         
                                                                        
 DCL OUT_FEJLNR        PIC'(4)9'      INIT(0);                          
 DCL OUT_EKOMNR        FIXED BIN(15)  INIT(0);                          
 DCL OUT_EEJDNR        FIXED BIN(31)  INIT(0);                          
 DCL OUT_EENHEDLBNR    FIXED BIN(15)  INIT(0);                          
 DCL OUT_BELIG         CHAR(50)       INIT('');                         
 DCL OUT_ADRSNAVN      CHAR(20)       INIT('');                         
 DCL OUT_VEJK          FIXED BIN(15)  INIT(0);                          
 DCL OUT_HUSNR         CHAR(04)       INIT('');                         
 DCL OUT_ETAGE         CHAR(02)       INIT('');                         
 DCL OUT_SDØR          CHAR(04)       INIT('');                         
                                                                        
 DCL 1 SØG_KRITERIER,                                                   
       3 SØG_PNR       FIXED DEC(11)  INIT(0),                          
       3 SØG_BOPAEK    FIXED BIN(15)  INIT(0),                          
       3 SØG_VEJKOD_X  CHAR(04)       INIT(''),                         
       3 SØG_HUSNR1    CHAR(03)       INIT(''),                         
       3 SØG_HUSNR2    CHAR(03)       INIT(''),                         
       3 SØG_BOGSTAV1  CHAR(01)       INIT(''),                         
       3 SØG_BOGSTAV2  CHAR(01)       INIT(''),                         
       3 SØG_ETAGE1    CHAR(02)       INIT(''),                         
       3 SØG_ETAGE2    CHAR(02)       INIT(''),                         
       3 SØG_ETAGE3    CHAR(02)       INIT(''),                         
       3 SØG_SDØR1     CHAR(04)       INIT(''),                         
       3 SØG_SDØR2     CHAR(04)       INIT(''),                         
       3 SØG_SDØR3     CHAR(04)       INIT('');                         
                                                                        
 DCL SØG_VEJKOD_9  BASED(ADDR(SØG_VEJKOD_X))                            
                       PIC '(4)9';                                      
                                                                        
 DCL OUT_CSRP_HISTNR   FIXED BIN(15)  INIT(0);                          
 DCL OUT_CSRP_FRADTO   CHAR(08)       INIT('');                         
 DCL OUT_CSRP_TILDTO   CHAR(08)       INIT('');                         
 DCL OUT_CSRP_BOPAEK   FIXED BIN(15)  INIT(0);                          
 DCL OUT_CSRP_VEJKOD   CHAR(04)       INIT('');                         
 DCL OUT_CSRP_HUSNR    CHAR(03)       INIT('');                         
 DCL OUT_CSRP_BOGSTAV  CHAR(01)       INIT('');                         
 DCL OUT_CSRP_ETAGE    CHAR(02)       INIT('');                         
 DCL OUT_CSRP_SDØR     CHAR(04)       INIT('');                         
                                                                        
 DCL CONDCODE          FIXED BIN(31)  INIT(0);                          
 DCL DISP_1            PIC'9'         INIT(0);                          
 DCL DISP_4            PIC'-(3)9'     INIT(0);                          
                                                                        
 DCL DEBUG_TXT         CHAR(12)       INIT('*** DEBUG - ');             
 DCL MAKRO_TXT         CHAR(12)       INIT('BH337_MAKRO ');             
 DCL SØG_TXT           CHAR(12)       INIT('UDFYLD_SØG  ');             
 DCL FOUND_TXT         CHAR(12)       INIT(' - FOUND  - ');             
 DCL NOTFND_TXT        CHAR(12)       INIT(' - NOTFND - ');             
 DCL INSERT_TXT        CHAR(12)       INIT(' - INSERT - ');             
 DCL WRITE_TXT         CHAR(12)       INIT(' - WRITE  - ');             
 DCL INPUT_TXT         CHAR(12)       INIT(' - INPUT  - ');             
 DCL OUTPUT_TXT        CHAR(12)       INIT(' - OUTPUT - ');             
 DCL FEJL_TXT          CHAR(12)       INIT('### FEJL  - ');             
 DCL FEJL_TXT_2        CHAR(12)       INIT('');                         
                                                                        
 DCL FEJL_TXT_3        CHAR(12)       INIT('');                         
                                                                        
 DCL 1 FEJL_BASED_3 BASED(ADDR(FEJL_TXT_3)),                            
       3 FEJL_FILLER1  CHAR(03),                                        
       3 FEJL_CN570I   CHAR(09);                                        
                                                                        
 DCL DISP_X            CHAR(44)       INIT('');                         
                                                                        
 DCL 1 DISP_DATA   BASED(ADDR(DISP_X)),                                 
       3 DISP_EJERBO   CHAR(02),                                        
       3 DISP_FILLER1  CHAR(01),                                        
       3 DISP_EKOMNR   PIC'(4)9',                                       
       3 DISP_FILLER2  CHAR(01),                                        
       3 DISP_EEJDNR   PIC'(8)9',                                       
       3 DISP_FILLER3  CHAR(01),                                        
       3 DISP_VEJRK    PIC'(4)9',                                       
       3 DISP_FILLER4  CHAR(01),                                        
       3 DISP_HUSNR    CHAR(04),                                        
       3 DISP_FILLER5  CHAR(01),                                        
       3 DISP_ETAGE    CHAR(02),                                        
       3 DISP_FILLER6  CHAR(01),                                        
       3 DISP_DØRNR    CHAR(04),                                        
       3 DISP_FILLER7  CHAR(01),                                        
       3 DISP_ENHED    PIC'(4)9',                                       
       3 DISP_FILLER8  CHAR(01),                                        
       3 DISP_STATUS   CHAR(04);                                        
                                                                        
 DCL SØG_X             CHAR(48)       INIT('');                         
                                                                        
 DCL 1 SØG_DATA    BASED(ADDR(SØG_X)),                                  
       3 SØG_FILLER1   CHAR(01),                                        
       3 SØG_1_1       PIC'(10)9',                                      
       3 SØG_FILLER2   CHAR(01),                                        
       3 SØG_2_1       CHAR(03),                                        
       3 SØG_FILLER3   CHAR(01),                                        
       3 SØG_2_2       CHAR(03),                                        
       3 SØG_FILLER4   CHAR(01),                                        
       3 SØG_3_1       CHAR(01),                                        
       3 SØG_FILLER5   CHAR(01),                                        
       3 SØG_3_2       CHAR(01),                                        
       3 SØG_FILLER6   CHAR(01),                                        
       3 SØG_4_1       CHAR(02),                                        
       3 SØG_FILLER7   CHAR(01),                                        
       3 SØG_4_2       CHAR(02),                                        
       3 SØG_FILLER8   CHAR(01),                                        
       3 SØG_4_3       CHAR(02),                                        
       3 SØG_FILLER9   CHAR(01),                                        
       3 SØG_5_1       CHAR(04),                                        
       3 SØG_FILLER10  CHAR(01),                                        
       3 SØG_5_2       CHAR(04),                                        
       3 SØG_FILLER11  CHAR(01),                                        
       3 SØG_5_3       CHAR(04);                                        
                                                                        
 DCL COMMIT_VALUE      PIC'(7)9'      INIT(10000);                      
 DCL COMMIT_COUNT      PIC'(7)9'      INIT(0);                          
 DCL COMMIT_NR         PIC'(7)9'      INIT(0);                          
                                                                        
 DCL CN570I_READ       PIC'(7)9'      INIT(0);                          
 DCL CN570I_READ_EN    PIC'(7)9'      INIT(0);                          
 DCL CN570I_READ_TO    PIC'(7)9'      INIT(0);                          
 DCL CN570I_READ_REST  PIC'(7)9'      INIT(0);                          
 DCL BQ802T_SEL        PIC'(7)9'      INIT(0);                          
 DCL BQ802T_SEL_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ802T_SEL_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ802T_SEL_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ802T_NOT_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ802T_NOT_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ802T_NOT_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ700T_SEL        PIC'(7)9'      INIT(0);                          
 DCL BQ700T_SEL_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ700T_SEL_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ700T_SEL_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ700T_NOT_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ700T_NOT_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ700T_NOT_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ710T_SEL        PIC'(7)9'      INIT(0);                          
 DCL BQ710T_SEL_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ710T_SEL_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ710T_SEL_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ710T_NOT_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ710T_NOT_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ710T_NOT_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_SEL        PIC'(7)9'      INIT(0);                          
 DCL BQ803T_SEL_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ803T_SEL_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_SEL_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_NOT_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ803T_NOT_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_NOT_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ804T_SEL        PIC'(7)9'      INIT(0);                          
 DCL BQ804T_SEL_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ804T_SEL_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ804T_SEL_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_INS        PIC'(7)9'      INIT(0);                          
 DCL BQ803T_INS_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ803T_INS_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ803T_INS_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BQ804T_INS        PIC'(7)9'      INIT(0);                          
 DCL BQ804T_INS_EN     PIC'(7)9'      INIT(0);                          
 DCL BQ804T_INS_TO_1   PIC'(7)9'      INIT(0);                          
 DCL BQ804T_INS_TO_2   PIC'(7)9'      INIT(0);                          
 DCL BH337O_WRITE      PIC'(7)9'      INIT(0);                          
 DCL BH337O_REST       PIC'(7)9'      INIT(0);                          
 DCL BH337O_OK_EN      PIC'(7)9'      INIT(0);                          
 DCL BH337O_OK_TO      PIC'(7)9'      INIT(0);                          
 DCL BH337O_3700_EN    PIC'(7)9'      INIT(0);                          
 DCL BH337O_3700_TO    PIC'(7)9'      INIT(0);                          
 DCL BH337O_3701_EN    PIC'(7)9'      INIT(0);                          
 DCL BH337O_3701_TO    PIC'(7)9'      INIT(0);                          
                                                                        
 /********************************************************************/ 
 /* Standard rutiner                                                 */ 
 /********************************************************************/ 
 DCL   ZS61900         ENTRY;          /* Udskrivning af SSI         */ 
 DCL   ZS30101         ENTRY;          /* Abendrutine                */ 
 DCL   ZS67000         ENTRY;          /* Hent sql tekster           */ 
                                                                        
 DCL   1 BH65300M,                     /* Ajourfør mini MT-fil til   */ 
         %INCLUDE BH65300M;            /* EVS forskud eller EVS slut */ 
                                                                        
 DCL 1 ABEND_MEDDEL,                                                    
       2 ABEND_MODUL   CHAR (07) INIT ('BH33700'),                      
       2 ABEND_FILL1   CHAR(01) INIT(' '),                              
       2 ABEND_MEDNR   CHAR(02) INIT('**'),                             
       2 ABEND_FILL2   CHAR(01) INIT(' '),                              
       2 ABEND_FEJLNR  CHAR(03) INIT(' '),                              
       2 ABEND_FILL3   CHAR(01) INIT(' '),                              
       2 ABEND_TXT     CHAR(41) INIT('');                               
                                                                        
 DCL  SYSPRINT FILE;                                                    
                                                                        
 /********************************************************************/ 
 /* Db2 tabeller                                                     */ 
 /********************************************************************/ 
 EXEC SQL INCLUDE BQ70000T;           /* Aktuel CSRP-tabel           */ 
 EXEC SQL INCLUDE BQ71000T;           /* Historik CSRP-tabel         */ 
 EXEC SQL INCLUDE BQ80200T;           /* EVS-enhedsløbenummertabel   */ 
 EXEC SQL INCLUDE BQ80300T;           /* EVS-enhedsanvendelsestabel  */ 
 EXEC SQL INCLUDE BQ80400T;           /* EVS-enhedssporbarhed        */ 
 EXEC SQL INCLUDE SQLCA;              /* SQL kommunikationsareal     */ 
                                                                        
 /********************************************************************/ 
 /* On Conditions                                                    */ 
 /********************************************************************/ 
 CALL ZS61900;                                                          
                                                                        
 ON ERROR BEGIN;                                                        
   ON ERROR SYSTEM;                                                     
   PUT SKIP LIST('PROGRAMFEJL');                                        
   CALL PLIDUMP('SNHOB','PROGRAM BH33700L');                            
 END;                                                                   
                                                                        
 ON ENDFILE(BH337P) BEGIN;                                              
   EOF_BH337P = JA;                                                     
 END;                                                                   
                                                                        
 ON ENDFILE(CN570I) BEGIN;                                              
   FLERE_CN570I = NEJ;                                                  
 END;                                                                   
                                                                        
                                                                        
 /********************************************************************/ 
 /* Main proces                                                      */ 
 /********************************************************************/ 
 CALL HOUSEKEEPING;                                                     
 CALL READ_CN570I;                                                      
                                                                        
 DO WHILE (FLERE_CN570I);                                               
   CALL FIND_EJERBOLIG_ANTAL;                                           
   CALL INIT_BH337O;                                                    
                                                                        
   IF EN_EJERBOLIG                                                      
    ! TO_EJERBOLIG THEN DO;                                             
     CALL SELECT_BQ802(1);                                              
                                                                        
     IF BQ802T_FOUND THEN DO;                                           
       CALL UDFYLD_SØG;                                                 
       CALL SELECT_BQ700;                                               
                                                                        
       IF ^CSRP_FOUND THEN                                              
         CALL SELECT_BQ710(1);                                          
     END;                                                               
                                                                        
     IF  TO_EJERBOLIG                                                   
      &  BQ802T_FOUND                                                   
      & ^CSRP_FOUND   THEN DO;                                          
       CALL SELECT_BQ802(2);                                            
                                                                        
       IF BQ802T_FOUND THEN DO;                                         
         CALL UDFYLD_SØG;                                               
         CALL SELECT_BQ700;                                             
                                                                        
         IF ^CSRP_FOUND THEN                                            
           CALL SELECT_BQ710(2);                                        
       END;                                                             
     END;                                                               
   END;                                                                 
                                                                        
   IF EN_EJERBOLIG                                                      
    ! TO_EJERBOLIG THEN DO;                                             
     IF BQ802T_FOUND THEN DO;                                           
       CALL SELECT_BQ803;                                               
       IF ^BQ803T_FOUND THEN DO;                                        
         CALL INSERT_BQ803;                                             
       END;                                                             
                                                                        
       CALL INSERT_BQ804;                                               
     END;                                                               
   END;                                                                 
                                                                        
   CALL WRITE_BH337O;                                                   
   CALL DB2_COMMIT;                                                     
   CALL READ_CN570I;                                                    
 END;                                                                   
                                                                        
 IF COMMIT_COUNT > 0 THEN DO;                                           
   COMMIT_COUNT = COMMIT_COUNT + COMMIT_VALUE;                          
   CALL DB2_COMMIT;                                                     
 END;                                                                   
                                                                        
 CALL AFSLUT;                                                           
                                                                        
 /********************************************************************/ 
 /* Housekeeping                                                     */ 
 /********************************************************************/ 
 HOUSEKEEPING: PROC;                                                    
                                                                        
 DCL PUTSKIP_FROM    CHAR(17) INIT('   PUTSKIP_FROM: ');                
 DCL PUTSKIP_TO      CHAR(17) INIT('   PUTSKIP_TO  : ');                
 DCL EVS_KOERSEL_TXT CHAR(19) INIT('   EVS_KOERSEL :   ');              
 DCL INDKAAR_TXT     CHAR(22) INIT('   INDKOMSTÅR  :      ');           
 DCL COMMIT_TXT      CHAR(17) INIT('   COMMIT_VALUE: ');                
                                                                        
 DCL DISP_FROM       PIC'Z.ZZZ.ZZ9';                                    
 DCL DISP_TO         PIC'Z.ZZZ.ZZ9';                                    
 DCL DISP_COMMIT     PIC'Z.ZZZ.ZZ9';                                    
                                                                        
 PUT SKIP LIST('Program BH33700 startet');                              
 PUT SKIP LIST('');                                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Skal programmet fortage debug (put skip)                         */ 
 /*------------------------------------------------------------------*/ 
 ON UNDEFINEDFILE(DEBUGI) BEGIN;                                        
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 OPEN FILE(DEBUGI) TITLE('DEBUG00I');                                   
                                                                        
 ON ENDFILE(DEBUGI) BEGIN;                                              
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 READ FILE(DEBUGI) INTO(DEBUG_REC);                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* HOUSEKEEPING_PUTSKIP_FOUND                                       */ 
 /*------------------------------------------------------------------*/ 
 HOUSEKEEPING_PUTSKIP_FOUND:                                            
                                                                        
 PUT SKIP LIST('BH33700 parametre:');                                   
                                                                        
 DISP_FROM = DEBUG_REC.DEBUG_FROM;                                      
 DISP_TO   = DEBUG_REC.DEBUG_TO;                                        
                                                                        
 PUT SKIP LIST(PUTSKIP_FROM !! DISP_FROM);                              
 PUT SKIP LIST(PUTSKIP_TO !! DISP_TO);                                  
                                                                        
 CLOSE FILE(DEBUGI);                                                    
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Find EVS Kørsel type                                             */ 
 /*------------------------------------------------------------------*/ 
 OPEN FILE(BH337P) TITLE('BH33700P');                                   
                                                                        
 READ FILE(BH337P) INTO(BH337_PARM);                                    
                                                                        
 SELECT (BH337_PARM.FS_MRK);                                            
   WHEN('F') DO;                                                        
     EVS_FORSKUD = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_F;                                       
     AFVIKL_ID   = BH337_PARM.FORSKUD_TXT;                              
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   WHEN('S') DO;                                                        
     EVS_SLUT    = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_S;                                       
     AFVIKL_ID   = BH337_PARM.SLUT_TXT;                                 
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   WHEN('U') DO;                                                        
     EVS_SUPPL   = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_U;                                       
     AFVIKL_ID   = BH337_PARM.SUPPL_TXT;                                
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     ABEND_FEJLNR = '101';                                              
     ABEND_TXT    = 'Parameter skal være F, S eller U';                 
     CALL PROGRAM_FEJL;                                                 
   END;                                                                 
 END;                                                                   
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Find Indkomstår afhængig af EVS kørsel type                      */ 
 /*------------------------------------------------------------------*/ 
 IF EVS_KOERSEL = EVS_KOERSEL_S                                         
  ! EVS_KOERSEL = EVS_KOERSEL_U THEN DO;                                
    INDKAAR = BH65300M.INDKÅR1;                                         
 END;                                                                   
 ELSE DO;                                                               
    INDKAAR = BH65300M.INDKÅR2;                                         
 END;                                                                   
                                                                        
 DISP_COMMIT = COMMIT_VALUE;                                            
                                                                        
 PUT SKIP LIST(INDKAAR_TXT !! INDKAAR);                                 
 PUT SKIP LIST(COMMIT_TXT !! DISP_COMMIT);                              
 PUT SKIP LIST('');                                                     
                                                                        
 CLOSE FILE(BH337P);                                                    
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Open filer                                                       */ 
 /*------------------------------------------------------------------*/ 
 OPEN FILE(CN570I) TITLE('CN57000I');                                   
 OPEN FILE(BH337O) TITLE('BH33700O');                                   
                                                                        
 END HOUSEKEEPING;                                                      
                                                                        
 /********************************************************************/ 
 /* Læs næste CN570I record                                          */ 
 /********************************************************************/ 
 READ_CN570I: PROC;                                                     
                                                                        
 DCL REC_TXT          CHAR(13) INIT(' - record nr ');                   
 DCL DCPRCIR_TXT      CHAR(11) INIT(' - DCPRCIR ');                     
 DCL DISP_CN570I      CHAR(9);                                          
 DCL DISP_CPRCIR      CHAR(11);                                         
                                                                        
 DCL DISP_9_X           CHAR(9);                                        
 DCL DISP_9       BASED(ADDR(DISP_9_X))                                 
                      PIC 'Z.ZZZ.ZZ9';                                  
                                                                        
 DCL DISP_11_X        CHAR(11);                                         
 DCL DISP_11      BASED(ADDR(DISP_11_X))                                
                      PIC 'Z(10)9';                                     
                                                                        
 PUTSKIP_THIS = NEJ;                                                    
                                                                        
 READ FILE(CN570I) INTO(CN570_AREA);                                    
                                                                        
 IF FLERE_CN570I THEN DO;                                               
   CN570I_READ = CN570I_READ + 1;                                       
                                                                        
   IF (DEBUG_REC.DEBUG_TO    > 0                   )                    
    & (  CN570I_READ         > DEBUG_REC.DEBUG_FROM                     
       ! CN570I_READ         = DEBUG_REC.DEBUG_FROM)                    
    & (  CN570I_READ         < DEBUG_REC.DEBUG_TO                       
       ! CN570I_READ         = DEBUG_REC.DEBUG_TO  ) THEN DO;           
     PUTSKIP_THIS = JA;                                                 
   END;                                                                 
 END;                                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   DISP_9      = CN570I_READ;                                           
   DISP_CN570I = TRIM(DISP_9_X,' ');                                    
   DISP_11     = CN570.DCPRCIR;                                         
   DISP_CPRCIR = TRIM(DISP_11_X,' ');                                   
                                                                        
   PUT SKIP LIST(DEBUG_TXT !! PROCNAME                                  
                !! REC_TXT !! DISP_CN570I                               
                !! DCPRCIR_TXT !! DISP_CPRCIR);                         
 END;                                                                   
                                                                        
 END READ_CN570I;                                                       
                                                                        
 /********************************************************************/ 
 /* Initier felterne EN_EJERBOLIG & TO_EJERBOLIG                     */ 
 /********************************************************************/ 
 FIND_EJERBOLIG_ANTAL: PROC;                                            
                                                                        
 %INCLUDE BH33701M;      /* Kode skal ligge her før koden i proc     */ 
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   CN570I_READ_EN = CN570I_READ_EN + 1;                                 
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   CN570I_READ_TO = CN570I_READ_TO + 1;                                 
 END;                                                                   
                                                                        
 IF ^EN_EJERBOLIG                                                       
  & ^TO_EJERBOLIG THEN DO;                                              
   CN570I_READ_REST = CN570I_READ_REST + 1;                             
 END;                                                                   
                                                                        
 END FIND_EJERBOLIG_ANTAL;                                              
                                                                        
 /********************************************************************/ 
 /* Initier BH337O record med CN570I record                          */ 
 /********************************************************************/ 
 INIT_BH337O: PROC;                                                     
                                                                        
 BH337             = CN570, BY NAME;                                    
 BH337.EENHEDLBNR  = 0;                 /* Felt længde =   2 bytes   */ 
 BH337.TEKFEJLNR   = 0;                 /* Felt længde =   4 bytes   */ 
 BH337.ENHEDBELIG  = '';                /* Felt længde =  50 bytes   */ 
 BH337.VEJK_ENHED  = '';                /* Felt længde =   2 bytes   */ 
 BH337.HUSNR_ENHED = '';                /* Felt længde =   4 bytes   */ 
 BH337.ETAGE_ENHED = '';                /* Felt længde =   2 bytes   */ 
 BH337.SDØR_ENHED  = '';                /* Felt længde =   4 bytes   */ 
                                        /* -----------------------   */ 
 BH337_LENGTH      = CN570_LENGTH + 68; /* Sum         =  68 bytes   */ 
                                                                        
 OUT_FEJLNR        = 0;                                                 
 OUT_EKOMNR        = 0;                                                 
 OUT_EEJDNR        = 0;                                                 
 OUT_EENHEDLBNR    = 0;                                                 
 OUT_BELIG         = '';                                                
 OUT_ADRSNAVN      = '';                                                
 OUT_VEJK          = 0;                                                 
 OUT_HUSNR         = '';                                                
 OUT_ETAGE         = '';                                                
 OUT_SDØR          = '';                                                
                                                                        
 CSRP_FOUND        = NEJ;                                               
 CSRP_AKTUEL       = NEJ;                                               
 CSRP_HISTORIK     = NEJ;                                               
 OUT_CSRP_HISTNR   = 0;                                                 
 OUT_CSRP_FRADTO   = '';                                                
 OUT_CSRP_TILDTO   = '';                                                
 OUT_CSRP_BOPAEK   = 0;                                                 
 OUT_CSRP_VEJKOD   = '';                                                
 OUT_CSRP_HUSNR    = '';                                                
 OUT_CSRP_BOGSTAV  = '';                                                
 OUT_CSRP_ETAGE    = '';                                                
 OUT_CSRP_SDØR     = '';                                                
                                                                        
 END INIT_BH337O;                                                       
                                                                        
 /********************************************************************/ 
 /* Find CN570I i EVS-enhedsløbenummertabel                          */ 
 /********************************************************************/ 
 SELECT_BQ802: PROC(IX);                                                
                                                                        
 DCL IX FIXED BIN(15);                                                  
                                                                        
 DCL PROC_TXT CHAR(12) INIT('SELECT_BQ802');                            
                                                                        
 BQ802T_FOUND = NEJ;                                                    
                                                                        
 DCLBQ80200T               = '';                                        
 DCLBQ80200T.EKOMNR        = BH337.EKOMNR;                              
 DCLBQ80200T.EEJDNR        = BH337.EEJDNR;                              
                                                                        
 IF EVS_KOERSEL = EVS_KOERSEL_S                                         
  ! EVS_KOERSEL = EVS_KOERSEL_U THEN DO;                                
    IF NY_VURDERING THEN DO;                                   /*#01*/  
       DCLBQ80200T.VEJK_ENH  = BH337.VURDOPLNY.VEJK_EJEB(IX);           
       DCLBQ80200T.HUSNR_ENH = BH337.VURDOPLNY.HUSNR_EJEB(IX);          
       DCLBQ80200T.ETAGE_ENH = BH337.VURDOPLNY.ETAGE_EJEB(IX);          
       DCLBQ80200T.DØRNR_ENH = BH337.VURDOPLNY.SDØR_EJEB(IX);           
    END;                                                       /*#01*/  
    ELSE DO;                                                   /*#01*/  
       DCLBQ80200T.VEJK_ENH  = BH337.VURDOPLGL.VEJK_EJEB(IX);  /*#01*/  
       DCLBQ80200T.HUSNR_ENH = BH337.VURDOPLGL.HUSNR_EJEB(IX); /*#01*/  
       DCLBQ80200T.ETAGE_ENH = BH337.VURDOPLGL.ETAGE_EJEB(IX); /*#01*/  
       DCLBQ80200T.DØRNR_ENH = BH337.VURDOPLGL.SDØR_EJEB(IX);  /*#01*/  
    END;                                                       /*#01*/  
 END;                                                                   
 ELSE DO;                                                               
   DCLBQ80200T.VEJK_ENH  = BH337.VURDOPLGL.VEJK_EJEB(IX);               
   DCLBQ80200T.HUSNR_ENH = BH337.VURDOPLGL.HUSNR_EJEB(IX);              
   DCLBQ80200T.ETAGE_ENH = BH337.VURDOPLGL.ETAGE_EJEB(IX);              
   DCLBQ80200T.DØRNR_ENH = BH337.VURDOPLGL.SDØR_EJEB(IX);               
 END;                                                                   
                                                                        
 /* #03 DB2 match på char felter */                                     
 EXEC SQL SELECT   EENHEDLBNR                                           
                  ,ADRSNAVN_ENH                                         
          INTO     :DCLBQ80200T.EENHEDLBNR                              
                  ,:DCLBQ80200T.ADRSNAVN_ENH                            
          FROM    BQ80200T                                              
          WHERE   EKOMNR     = :DCLBQ80200T.EKOMNR                      
          AND     EEJDNR     = :DCLBQ80200T.EEJDNR                      
          AND     VEJK_ENH   = :DCLBQ80200T.VEJK_ENH                    
          AND     LPAD(TRIM(' ' FROM HUSNR_ENH), 4, '0') =              
                  LPAD(TRIM(' ' FROM :DCLBQ80200T.HUSNR_ENH), 4, '0')   
          AND     LPAD(TRIM(' ' FROM ETAGE_ENH), 2, '0') =              
                  LPAD(TRIM(' ' FROM :DCLBQ80200T.ETAGE_ENH), 2, '0')   
          AND     LPAD(TRIM(' ' FROM DØRNR_ENH), 4, '0') =              
                  LPAD(TRIM(' ' FROM :DCLBQ80200T.DØRNR_ENH), 4, '0')   
          FETCH FIRST 1 ROW ONLY;                                       
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     BQ802T_FOUND = JA;                                                 
                                                                        
     BQ802T_SEL = BQ802T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ802T_SEL_EN   = BQ802T_SEL_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ802T_SEL_TO_1 = BQ802T_SEL_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ802T_SEL_TO_2 = BQ802T_SEL_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF IX = 1  THEN DO;                                                
       CALL GEM_BQ802_DATA;                                             
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ802_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     OUT_FEJLNR = 3700;                                                 
                                                                        
     BQ802T_SEL = BQ802T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ802T_NOT_EN   = BQ802T_NOT_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ802T_NOT_TO_1 = BQ802T_NOT_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ802T_NOT_TO_2 = BQ802T_NOT_TO_2 + 1;                           
     END;                                                               
                                                                        
     CALL PUTSKIP_BQ802_DATA;                                           
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBQ80200T);                                        
     DISP_1       = IX;                                                 
     DISP_4       = SQLCA.SQLCODE;                                      
                                                                        
     ABEND_FEJLNR = '102';                                              
     ABEND_TXT    = 'Fejl i SELECT_BQ802(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END SELECT_BQ802;                                                      
                                                                        
 /********************************************************************/ 
 /* Putskip opslagsdata på BQ80200T                                  */ 
 /********************************************************************/ 
 PUTSKIP_BQ802_DATA: PROC;                                              
                                                                        
 DCL PROC_TXT     CHAR(12) INIT('SELECT_BQ802');                        
 DCL PARM_TXT     CHAR(12) INIT('');                                    
                                                                        
 DCL DISP_9_X     CHAR(9);                                              
 DCL DISP_9   BASED(ADDR(DISP_9_X))                                     
                  PIC 'Z.ZZZ.ZZ9';                                      
                                                                        
 CALL INIT_DISP_X;                                                      
                                                                        
 DISP_EKOMNR = DCLBQ80200T.EKOMNR;                                      
 DISP_EEJDNR = DCLBQ80200T.EEJDNR;                                      
 DISP_VEJRK  = DCLBQ80200T.VEJK_ENH;                                    
 DISP_HUSNR  = DCLBQ80200T.HUSNR_ENH;                                   
 DISP_ETAGE  = DCLBQ80200T.ETAGE_ENH;                                   
 DISP_DØRNR  = DCLBQ80200T.DØRNR_ENH;                                   
 DISP_ENHED  = DCLBQ80200T.EENHEDLBNR;                                  
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0)   PARM_TXT = FOUND_TXT;                                      
   WHEN(100) PARM_TXT = NOTFND_TXT;                                     
   OTHERWISE ;                                                          
 END;                                                                   
                                                                        
 IF OUT_FEJLNR = 3700 THEN DO;                                          
   DISP_9      = CN570I_READ;                                           
   FEJL_CN570I = TRIM(DISP_9_X,' ');                                    
   PUT SKIP LIST(FEJL_TXT !! PROC_TXT !! PARM_TXT !! DISP_X);           
   PUT SKIP LIST(FEJL_TXT_2 !! FEJL_TXT_3);                             
 END;                                                                   
 ELSE DO;                                                               
   PUT SKIP LIST(DEBUG_TXT !! PROC_TXT !! PARM_TXT !! DISP_X);          
 END;                                                                   
                                                                        
 END PUTSKIP_BQ802_DATA;                                                
                                                                        
 /********************************************************************/ 
 /* Match på CSRP adresse taget fra BH84300 og tilrettet således at  */ 
 /* der søges i CSRP med den enhed, som undersøges                   */ 
 /********************************************************************/ 
 UDFYLD_SØG: PROC;                                                      
                                                                        
 DCL CHAR_1 CHAR(1);                                                    
 DCL CHAR_2 CHAR(2);                                                    
 DCL NUM_4  PIC'(4)9';                                                  
                                                                        
 DCL STORE  CHAR(29) INIT ('ABCDEFGHIJKLMNOPQRSTUVWXYZÅØÆ');            
 DCL SMÅ    CHAR(29) INIT ('abcdefghijklmnopqrstuvwxyzåøæ');            
                                                                        
 SØG_KRITERIER = '';                                                    
                                                                        
 SØG_PNR      = BH337.DCPRCIR;                                          
 SØG_BOPAEK   = BH337.EKOMNR;                                           
 SØG_VEJKOD_9 = DCLBQ80200T.VEJK_ENH;                                   
                                                                        
 SØG_HUSNR1 = SUBSTR(DCLBQ80200T.HUSNR_ENH,1,3);                        
 SØG_HUSNR2 = SUBSTR(DCLBQ80200T.HUSNR_ENH,1,3);                        
                                                                        
 IF SØG_HUSNR1 = '   ' THEN DO;                                         
   SØG_HUSNR2 = '000';                                                  
 END;                                                                   
                                                                        
 IF SØG_HUSNR1 = '000' THEN DO;                                         
   SØG_HUSNR2 = '   ';                                                  
 END;                                                                   
                                                                        
 CHAR_1       = SUBSTR(DCLBQ80200T.HUSNR_ENH,4,1);                      
 SØG_BOGSTAV1 = TRANSLATE (CHAR_1, STORE, SMÅ);                         
 SØG_BOGSTAV2 = TRANSLATE (CHAR_1, SMÅ, STORE);                         
                                                                        
 CHAR_2 = DCLBQ80200T.ETAGE_ENH;                                        
 SELECT;                                                                
   WHEN(   CHAR_2 = '  '                                                
         ! CHAR_2 = '00') DO;                                           
     SØG_ETAGE1 = CHAR_2;                                               
     SØG_ETAGE2 = '  ';                                                 
     SØG_ETAGE3 = '00';                                                 
   END;                                                                 
                                                                        
   WHEN(  VERIFY(SUBSTR(CHAR_2,1,1),' 0'        ) = 0                   
        & VERIFY(SUBSTR(CHAR_2,2,1),'0123456789') = 0) DO;              
     SØG_ETAGE1 = '0' !! SUBSTR(CHAR_2,2,1);   /* Numerisk       */     
     SØG_ETAGE2 = ' ' !! SUBSTR(CHAR_2,2,1);   /* Højrestillet   */     
     SØG_ETAGE3 = SUBSTR(CHAR_2,2,1) !! ' ';   /* Venstrestillet */     
   END;                                                                 
                                                                        
   WHEN(  VERIFY(SUBSTR(CHAR_2,1,1),'123456789' ) = 0                   
        & VERIFY(SUBSTR(CHAR_2,2,1),'0123456789') = 0) DO;              
     SØG_ETAGE1 = CHAR_2;                                               
     SØG_ETAGE2 = CHAR_2;                                               
     SØG_ETAGE3 = CHAR_2;                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     SØG_ETAGE1 = CHAR_2;                                               
     SØG_ETAGE2 = TRANSLATE(CHAR_2, STORE, SMÅ);                        
     SØG_ETAGE3 = TRANSLATE(CHAR_2, SMÅ, STORE);                        
   END;                                                                 
 END;                                                                   
                                                                        
 IF VERIFY(DCLBQ80200T.DØRNR_ENH,'0123456789') = 0 THEN DO;             
   SØG_SDØR1 = TRIM(DCLBQ80200T.DØRNR_ENH,' 0');                        
   SØG_SDØR2 = DCLBQ80200T.DØRNR_ENH;                                   
   NUM_4     = DCLBQ80200T.DØRNR_ENH;                                   
   SØG_SDØR3 = NUM_4;                                                   
 END;                                                                   
 ELSE DO;   /* Feltet er venstrestillet i tabellen */                   
   SØG_SDØR1 = TRIM(DCLBQ80200T.DØRNR_ENH,' 0');                        
   SØG_SDØR2 = TRANSLATE(SØG_SDØR1, STORE, SMÅ);                        
   SØG_SDØR3 = TRANSLATE(SØG_SDØR1, SMÅ, STORE);                        
 END;                                                                   
                                                                        
 SØG_1_1 = SØG_PNR;                                                     
 SØG_2_1 = SØG_HUSNR1;                                                  
 SØG_2_2 = SØG_HUSNR2;                                                  
 SØG_3_1 = SØG_BOGSTAV1;                                                
 SØG_3_2 = SØG_BOGSTAV2;                                                
 SØG_4_1 = SØG_ETAGE1;                                                  
 SØG_4_2 = SØG_ETAGE2;                                                  
 SØG_4_3 = SØG_ETAGE3;                                                  
 SØG_5_1 = SØG_SDØR1;                                                   
 SØG_5_2 = SØG_SDØR2;                                                   
 SØG_5_3 = SØG_SDØR3;                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   PUT SKIP LIST(DEBUG_TXT !! SØG_TXT !! SØG_X);                        
 END;                                                                   
                                                                        
 END UDFYLD_SØG;                                                        
                                                                        
 /********************************************************************/ 
 /* Find BQ802T i Aktuel CSRP-tabel                                  */ 
 /********************************************************************/ 
 SELECT_BQ700: PROC;                                                    
                                                                        
 DCLBQ70000T  = '';                                                     
                                                                        
 EXEC SQL SELECT   PNR                                                  
                  ,ADRFRADTO                                            
                  ,BOPAEKMNR                                            
                  ,VEJKOD                                               
                  ,HUSNR                                                
                  ,HUSBOGSTAADR                                         
                  ,ETAGENR                                              
                  ,SIDEDOERNR                                           
          INTO     :DCLBQ70000T.PNR                                     
                  ,:DCLBQ70000T.ADRFRADTO                               
                  ,:DCLBQ70000T.BOPAEKMNR                               
                  ,:DCLBQ70000T.VEJKOD                                  
                  ,:DCLBQ70000T.HUSNR                                   
                  ,:DCLBQ70000T.HUSBOGSTAADR                            
                  ,:DCLBQ70000T.ETAGENR                                 
                  ,:DCLBQ70000T.SIDEDOERNR                              
          FROM    BQ70000T                                              
          WHERE   PNR             = :SØG_PNR                            
          AND     BOPAEKMNR       = :SØG_BOPAEK                         
          AND     VEJKOD          = :SØG_VEJKOD_X                       
          AND    (   HUSNR        = :SØG_HUSNR1                         
                  OR HUSNR        = :SØG_HUSNR2      )                  
          AND    (   HUSBOGSTAADR = :SØG_BOGSTAV1                       
                  OR HUSBOGSTAADR = :SØG_BOGSTAV2    )                  
          AND    (   ETAGENR      = :SØG_ETAGE1                         
                  OR ETAGENR      = :SØG_ETAGE2                         
                  OR ETAGENR      = :SØG_ETAGE3      )                  
          AND    (   SIDEDOERNR   = :SØG_SDØR1                          
                  OR SIDEDOERNR   = :SØG_SDØR2                          
                  OR SIDEDOERNR   = :SØG_SDØR3)                         
          FETCH FIRST 1 ROW ONLY;                                       
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     CSRP_FOUND  = JA;                                                  
     CSRP_AKTUEL = JA;                                                  
                                                                        
     BQ700T_SEL = BQ700T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ700T_SEL_EN   = BQ700T_SEL_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ700T_SEL_TO_1 = BQ700T_SEL_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ700T_SEL_TO_2 = BQ700T_SEL_TO_2 + 1;                           
     END;                                                               
                                                                        
     CALL GEM_BQ802_DATA;                                               
                                                                        
     OUT_CSRP_HISTNR  = 0;                                              
     OUT_CSRP_FRADTO  = DCLBQ70000T.ADRFRADTO;                          
     OUT_CSRP_TILDTO  = '';                                             
     OUT_CSRP_BOPAEK  = DCLBQ70000T.BOPAEKMNR;                          
     OUT_CSRP_VEJKOD  = DCLBQ70000T.VEJKOD;                             
     OUT_CSRP_HUSNR   = DCLBQ70000T.HUSNR;                              
     OUT_CSRP_BOGSTAV = DCLBQ70000T.HUSBOGSTAADR;                       
     OUT_CSRP_ETAGE   = DCLBQ70000T.ETAGENR;                            
     OUT_CSRP_SDØR    = DCLBQ70000T.SIDEDOERNR;                         
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ700_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     BQ700T_SEL = BQ700T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ700T_NOT_EN   = BQ700T_NOT_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ700T_NOT_TO_1 = BQ700T_NOT_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ700T_NOT_TO_2 = BQ700T_NOT_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ700_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(SØG_KRITERIER);                                      
     DISP_1       = DCLBQ80200T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '103';                                              
     ABEND_TXT    = 'Fejl i SELECT_BQ700(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END SELECT_BQ700;                                                      
                                                                        
 /********************************************************************/ 
 /* Putskip opslagsdata på BQ70000T                                  */ 
 /********************************************************************/ 
 PUTSKIP_BQ700_DATA: PROC;                                              
                                                                        
 DCL PROC_TXT CHAR(12) INIT('SELECT_BQ700');                            
 DCL PARM_TXT CHAR(12) INIT('');                                        
                                                                        
 CALL INIT_DISP_X;                                                      
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     PARM_TXT    = FOUND_TXT;                                           
     DISP_EKOMNR = DCLBQ70000T.BOPAEKMNR;                               
     DISP_VEJRK  = DCLBQ80200T.VEJK_ENH;                                
     DISP_HUSNR  = DCLBQ70000T.HUSNR !! DCLBQ70000T.HUSBOGSTAADR;       
     DISP_ETAGE  = DCLBQ70000T.ETAGENR;                                 
     DISP_DØRNR  = DCLBQ70000T.SIDEDOERNR;                              
     DISP_ENHED  = DCLBQ80200T.EENHEDLBNR;                              
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     PARM_TXT    = NOTFND_TXT;                                          
     DISP_EKOMNR = DCLBQ80200T.EKOMNR;                                  
     DISP_EEJDNR = DCLBQ80200T.EEJDNR;                                  
     DISP_VEJRK  = DCLBQ80200T.VEJK_ENH;                                
     DISP_HUSNR  = DCLBQ80200T.HUSNR_ENH;                               
     DISP_ETAGE  = DCLBQ80200T.ETAGE_ENH;                               
     DISP_DØRNR  = DCLBQ80200T.DØRNR_ENH;                               
     DISP_ENHED  = DCLBQ80200T.EENHEDLBNR;                              
   END;                                                                 
                                                                        
   OTHERWISE ;                                                          
 END;                                                                   
                                                                        
 PUT SKIP LIST(DEBUG_TXT !! PROC_TXT !! PARM_TXT !! DISP_X);            
                                                                        
 END PUTSKIP_BQ700_DATA;                                                
                                                                        
 /********************************************************************/ 
 /* Find BQ802T i Historik CSRP-tabel                                */ 
 /********************************************************************/ 
 SELECT_BQ710: PROC(IX);                                                
                                                                        
 DCL IX FIXED BIN(15);                                                  
                                                                        
 DCLBQ71000T   = '';                                                    
                                                                        
 EXEC SQL SELECT   PNR                                                  
                  ,HISTNR                                               
                  ,ADRFRADTO                                            
                  ,VEJKOD                                               
                  ,HUSNR                                                
                  ,HUSBOGSTAADR                                         
                  ,ETAGENR                                              
                  ,SIDEDOERNR                                           
          INTO     :DCLBQ71000T.PNR                                     
                  ,:DCLBQ71000T.HISTNR                                  
                  ,:DCLBQ71000T.ADRFRADTO                               
                  ,:DCLBQ71000T.VEJKOD                                  
                  ,:DCLBQ71000T.HUSNR                                   
                  ,:DCLBQ71000T.HUSBOGSTAADR                            
                  ,:DCLBQ71000T.ETAGENR                                 
                  ,:DCLBQ71000T.SIDEDOERNR                              
          FROM    BQ71000T                                              
          WHERE   PNR             = :SØG_PNR                            
          AND     BOPAEKMNR       = :SØG_BOPAEK                         
          AND     VEJKOD          = :SØG_VEJKOD_X                       
          AND    (   HUSNR        = :SØG_HUSNR1                         
                  OR HUSNR        = :SØG_HUSNR2      )                  
          AND    (   HUSBOGSTAADR = :SØG_BOGSTAV1                       
                  OR HUSBOGSTAADR = :SØG_BOGSTAV2    )                  
          AND    (   ETAGENR      = :SØG_ETAGE1                         
                  OR ETAGENR      = :SØG_ETAGE2                         
                  OR ETAGENR      = :SØG_ETAGE3      )                  
          AND    (   SIDEDOERNR   = :SØG_SDØR1                          
                  OR SIDEDOERNR   = :SØG_SDØR2                          
                  OR SIDEDOERNR   = :SØG_SDØR3)                         
          ORDER BY ADRFRADTO DESC                                       
          FETCH FIRST 1 ROW ONLY;                                       
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     CSRP_FOUND    = JA;                                                
     CSRP_HISTORIK = JA;                                                
                                                                        
     BQ710T_SEL = BQ710T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ710T_SEL_EN   = BQ710T_SEL_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ710T_SEL_TO_1 = BQ710T_SEL_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ710T_SEL_TO_2 = BQ710T_SEL_TO_2 + 1;                           
     END;                                                               
                                                                        
     CALL GEM_BQ802_DATA;                                               
                                                                        
     OUT_CSRP_HISTNR  = DCLBQ71000T.HISTNR;                             
     OUT_CSRP_FRADTO  = DCLBQ71000T.ADRFRADTO;                          
     OUT_CSRP_TILDTO  = DCLBQ71000T.ADRTILDTO;                          
     OUT_CSRP_BOPAEK  = DCLBQ71000T.BOPAEKMNR;                          
     OUT_CSRP_VEJKOD  = DCLBQ71000T.VEJKOD;                             
     OUT_CSRP_HUSNR   = DCLBQ71000T.HUSNR;                              
     OUT_CSRP_BOGSTAV = DCLBQ71000T.HUSBOGSTAADR;                       
     OUT_CSRP_ETAGE   = DCLBQ71000T.ETAGENR;                            
     OUT_CSRP_SDØR    = DCLBQ71000T.SIDEDOERNR;                         
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ710_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     BQ710T_SEL = BQ710T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ710T_NOT_EN   = BQ710T_NOT_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 1) THEN DO;            
       BQ710T_NOT_TO_1 = BQ710T_NOT_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80200T.EENHEDLBNR = 2) THEN DO;            
       BQ710T_NOT_TO_2 = BQ710T_NOT_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF (EN_EJERBOLIG & IX = 1)                                         
      ! (TO_EJERBOLIG & IX = 2) THEN DO;                                
       OUT_FEJLNR = 3701;                                               
     END;                                                               
                                                                        
     IF PUTSKIP_THIS                                                    
      ! OUT_FEJLNR = 3701 THEN DO;                                      
       CALL PUTSKIP_BQ710_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(SØG_KRITERIER);                                      
     DISP_1       = DCLBQ80200T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '104';                                              
     ABEND_TXT    = 'Fejl i SELECT_BQ710(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END SELECT_BQ710;                                                      
                                                                        
 /********************************************************************/ 
 /* Putskip opslagsdata på BQ71000T                                  */ 
 /********************************************************************/ 
 PUTSKIP_BQ710_DATA: PROc;                                              
                                                                        
 DCL PROC_TXT     CHAR(12) INIT('SELECT_BQ710');                        
                                                                        
 DCL DISP_9_X     CHAR(9);                                              
 DCL DISP_9   BASED(ADDR(DISP_9_X))                                     
                  PIC 'Z.ZZZ.ZZ9';                                      
                                                                        
 CALL INIT_DISP_X;                                                      
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     DISP_EKOMNR = DCLBQ71000T.BOPAEKMNR;                               
     DISP_VEJRK  = DCLBQ80200T.VEJK_ENH;                                
     DISP_HUSNR  = DCLBQ71000T.HUSNR !! DCLBQ71000T.HUSBOGSTAADR;       
     DISP_ETAGE  = DCLBQ71000T.ETAGENR;                                 
     DISP_DØRNR  = DCLBQ71000T.SIDEDOERNR;                              
     DISP_ENHED  = DCLBQ80200T.EENHEDLBNR;                              
                                                                        
     PUT SKIP LIST(DEBUG_TXT !! PROC_TXT !! FOUND_TXT !! DISP_X);       
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     DISP_EKOMNR = DCLBQ80200T.EKOMNR;                                  
     DISP_EEJDNR = DCLBQ80200T.EEJDNR;                                  
     DISP_VEJRK  = DCLBQ80200T.VEJK_ENH;                                
     DISP_HUSNR  = DCLBQ80200T.HUSNR_ENH;                               
     DISP_ETAGE  = DCLBQ80200T.ETAGE_ENH;                               
     DISP_DØRNR  = DCLBQ80200T.DØRNR_ENH;                               
     DISP_ENHED  = DCLBQ80200T.EENHEDLBNR;                              
                                                                        
     IF OUT_FEJLNR = 3701 THEN DO;                                      
       DISP_9      = CN570I_READ;                                       
       FEJL_CN570I = TRIM(DISP_9_X,' ');                                
       PUT SKIP LIST(FEJL_TXT   !! PROC_TXT !! NOTFND_TXT !! DISP_X);   
       PUT SKIP LIST(FEJL_TXT_2 !! FEJL_TXT_3 !! SØG_X);                
     END;                                                               
     ELSE DO;                                                           
       PUT SKIP LIST(DEBUG_TXT  !! PROC_TXT !! NOTFND_TXT !! DISP_X);   
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE ;                                                          
 END;                                                                   
                                                                        
 END PUTSKIP_BQ710_DATA;                                                
                                                                        
 /********************************************************************/ 
 /* Find BQ802T i EVS-enhedsløbenummertabel                          */ 
 /********************************************************************/ 
 SELECT_BQ803: PROC;                                                    
                                                                        
 DCL PROC_TXT CHAR(12) INIT('SELECT_BQ803');                            
                                                                        
 BQ803T_FOUND = NEJ;                                                    
                                                                        
 DCLBQ80300T               = '';                                        
 DCLBQ80300T.TIL_EVS_AAR   = INDKAAR;                                   
 DCLBQ80300T.AFVIKLINGS_ID = AFVIKL_ID;                                 
 DCLBQ80300T.EKOMNR        = OUT_EKOMNR;                                
 DCLBQ80300T.EEJDNR        = OUT_EEJDNR;                                
 DCLBQ80300T.EENHEDLBNR    = OUT_EENHEDLBNR;                            
                                                                        
 EXEC SQL SELECT  INSERT_TS                                             
          INTO    :DCLBQ80300T.INSERT_TS                                
          FROM    BQ80300T                                              
          WHERE   TIL_EVS_AAR   = :DCLBQ80300T.TIL_EVS_AAR              
          AND     AFVIKLINGS_ID = :DCLBQ80300T.AFVIKLINGS_ID            
          AND     EKOMNR        = :DCLBQ80300T.EKOMNR                   
          AND     EEJDNR        = :DCLBQ80300T.EEJDNR                   
          AND     EENHEDLBNR    = :DCLBQ80300T.EENHEDLBNR;              
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     BQ803T_FOUND = JA;                                                 
                                                                        
     BQ803T_SEL = BQ803T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_SEL_EN   = BQ803T_SEL_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_SEL_TO_1 = BQ803T_SEL_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 2) THEN DO;            
       BQ803T_SEL_TO_2 = BQ803T_SEL_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ803_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   WHEN(100) DO;                                                        
     BQ803T_SEL = BQ803T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_NOT_EN   = BQ803T_NOT_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_NOT_TO_1 = BQ803T_NOT_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 2) THEN DO;            
       BQ803T_NOT_TO_2 = BQ803T_NOT_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL PUTSKIP_BQ803_DATA;                                         
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBQ80300T);                                        
     DISP_1       = DCLBQ80300T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '105';                                              
     ABEND_TXT    = 'Fejl i SELECT_BQ803(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END SELECT_BQ803;                                                      
                                                                        
 /********************************************************************/ 
 /* Putskip opslagsdata på BQ80200T                                  */ 
 /********************************************************************/ 
 PUTSKIP_BQ803_DATA: PROC;                                              
                                                                        
 DCL PROC_TXT CHAR(12) INIT('SELECT_BQ803');                            
 DCL PARM_TXT CHAR(12) INIT('');                                        
                                                                        
 CALL INIT_DISP_X;                                                      
                                                                        
 DISP_EKOMNR = DCLBQ80300T.EKOMNR;                                      
 DISP_EEJDNR = DCLBQ80300T.EEJDNR;                                      
 DISP_ENHED  = DCLBQ80300T.EENHEDLBNR;                                  
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0)   PARM_TXT = FOUND_TXT;                                      
   WHEN(100) PARM_TXT = NOTFND_TXT;                                     
   OTHERWISE ;                                                          
 END;                                                                   
                                                                        
 PUT SKIP LIST(DEBUG_TXT !! PROC_TXT !! PARM_TXT !! DISP_X);            
                                                                        
 END PUTSKIP_BQ803_DATA;                                                
                                                                        
 /********************************************************************/ 
 /* Insert række i EVS-enhedsanvendelsestabellen (BQ803)             */ 
 /********************************************************************/ 
 INSERT_BQ803: PROC;                                                    
                                                                        
 DCLBQ80300T               = '';                                        
 DCLBQ80300T.TIL_EVS_AAR   = INDKAAR;                                   
 DCLBQ80300T.AFVIKLINGS_ID = AFVIKL_ID;                                 
 DCLBQ80300T.EKOMNR        = OUT_EKOMNR;                                
 DCLBQ80300T.EEJDNR        = OUT_EEJDNR;                                
 DCLBQ80300T.EENHEDLBNR    = OUT_EENHEDLBNR;                            
 DCLBQ80300T.INSERT_AF     = 'BH33700';                                 
                                                                        
 EXEC SQL INSERT                                                        
          INTO  BQ80300T                                                
                (TIL_EVS_AAR                                            
                ,AFVIKLINGS_ID                                          
                ,EKOMNR                                                 
                ,EEJDNR                                                 
                ,EENHEDLBNR                                             
                ,INSERT_TS                                              
                ,INSERT_AF)                                             
          VALUES(:DCLBQ80300T.TIL_EVS_AAR                               
                ,:DCLBQ80300T.AFVIKLINGS_ID                             
                ,:DCLBQ80300T.EKOMNR                                    
                ,:DCLBQ80300T.EEJDNR                                    
                ,:DCLBQ80300T.EENHEDLBNR                                
                ,CURRENT TIMESTAMP                                      
                ,:DCLBQ80300T.INSERT_AF);                               
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     BQ803T_INS = BQ803T_INS + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_INS_EN   = BQ803T_INS_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 1) THEN DO;            
       BQ803T_INS_TO_1 = BQ803T_INS_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80300T.EENHEDLBNR = 2) THEN DO;            
       BQ803T_INS_TO_2 = BQ803T_INS_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBQ80300T.EKOMNR;                                
       DISP_EEJDNR = DCLBQ80300T.EEJDNR;                                
       DISP_ENHED  = DCLBQ80300T.EENHEDLBNR;                            
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! PROCNAME !! INSERT_TXT !! DISP_X);    
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBQ80300T);                                        
     DISP_1       = DCLBQ80300T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '106';                                              
     ABEND_TXT    = 'Fejl i INSERT_BQ803(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END INSERT_BQ803;                                                      
                                                                        
 /********************************************************************/ 
 /* Insert række i EVS-enhedssporbarhed (BQ804)                      */ 
 /********************************************************************/ 
 INSERT_BQ804: PROC;                                                    
                                                                        
 DCLBQ80400T                   = '';                                    
 DCLBQ80400T.TIL_EVS_AAR       = INDKAAR;                               
 DCLBQ80400T.AFVIKLINGS_ID     = AFVIKL_ID;                             
 DCLBQ80400T.EKOMNR            = OUT_EKOMNR;                            
 DCLBQ80400T.EEJDNR            = OUT_EEJDNR;                            
 DCLBQ80400T.EENHEDLBNR        = OUT_EENHEDLBNR;                        
 DCLBQ80400T.DCPRN             = BH337.DCPRCIR;                         
 DCLBQ80400T.EGENNR            = SELECT_BQ804;                          
                                                                        
 SELECT;                                                                
   WHEN(OUT_FEJLNR > 0) DCLBQ80400T.TEKFEJLNR = OUT_FEJLNR;             
   OTHERWISE            ;                                               
 END;                                                                   
                                                                        
 DCLBQ80400T.HISTNR_CSRP       = OUT_CSRP_HISTNR;                       
 DCLBQ80400T.ADRFRADTO_CSRP    = OUT_CSRP_FRADTO;                       
 DCLBQ80400T.ADRTILDTO_CSRP    = OUT_CSRP_TILDTO;                       
 DCLBQ80400T.BOPAEKMNR_CSRP    = OUT_CSRP_BOPAEK;                       
 DCLBQ80400T.VEJKOD_CSRP       = OUT_CSRP_VEJKOD;                       
 DCLBQ80400T.HUSNR_CSRP        = OUT_CSRP_HUSNR;                        
 DCLBQ80400T.HUSBOGSTAADR_CSRP = OUT_CSRP_BOGSTAV;                      
 DCLBQ80400T.ETAGENR_CSRP      = OUT_CSRP_ETAGE;                        
 DCLBQ80400T.SIDEDOERNR_CSRP   = OUT_CSRP_SDØR;                         
 DCLBQ80400T.ABEL_ENH          = OUT_BELIG;                             
 DCLBQ80400T.INSERT_AF         = 'BH33700';                             
                                                                        
 EXEC SQL INSERT                                                        
          INTO  BQ80400T                                                
                (TIL_EVS_AAR                                            
                ,AFVIKLINGS_ID                                          
                ,EKOMNR                                                 
                ,EEJDNR                                                 
                ,EENHEDLBNR                                             
                ,DCPRN                                                  
                ,EGENNR                                                 
                ,TEKFEJLNR                                              
                ,HISTNR_CSRP                                            
                ,ADRFRADTO_CSRP                                         
                ,ADRTILDTO_CSRP                                         
                ,BOPAEKMNR_CSRP                                         
                ,VEJKOD_CSRP                                            
                ,HUSNR_CSRP                                             
                ,HUSBOGSTAADR_CSRP                                      
                ,ETAGENR_CSRP                                           
                ,SIDEDOERNR_CSRP                                        
                ,ABEL_ENH                                               
                ,INSERT_TS                                              
                ,INSERT_AF)                                             
          VALUES(:DCLBQ80400T.TIL_EVS_AAR                               
                ,:DCLBQ80400T.AFVIKLINGS_ID                             
                ,:DCLBQ80400T.EKOMNR                                    
                ,:DCLBQ80400T.EEJDNR                                    
                ,:DCLBQ80400T.EENHEDLBNR                                
                ,:DCLBQ80400T.DCPRN                                     
                ,:DCLBQ80400T.EGENNR                                    
                ,:DCLBQ80400T.TEKFEJLNR                                 
                ,:DCLBQ80400T.HISTNR_CSRP                               
                ,:DCLBQ80400T.ADRFRADTO_CSRP                            
                ,:DCLBQ80400T.ADRTILDTO_CSRP                            
                ,:DCLBQ80400T.BOPAEKMNR_CSRP                            
                ,:DCLBQ80400T.VEJKOD_CSRP                               
                ,:DCLBQ80400T.HUSNR_CSRP                                
                ,:DCLBQ80400T.HUSBOGSTAADR_CSRP                         
                ,:DCLBQ80400T.ETAGENR_CSRP                              
                ,:DCLBQ80400T.SIDEDOERNR_CSRP                           
                ,:DCLBQ80400T.ABEL_ENH                                  
                ,CURRENT TIMESTAMP                                      
                ,:DCLBQ80400T.INSERT_AF);                               
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     BQ804T_INS = BQ804T_INS + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 1) THEN DO;            
       BQ804T_INS_EN   = BQ804T_INS_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 1) THEN DO;            
       BQ804T_INS_TO_1 = BQ804T_INS_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 2) THEN DO;            
       BQ804T_INS_TO_2 = BQ804T_INS_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBQ80400T.EKOMNR;                                
       DISP_EEJDNR = DCLBQ80400T.EEJDNR;                                
       DISP_ENHED  = DCLBQ80400T.EENHEDLBNR;                            
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! PROCNAME !! INSERT_TXT !! DISP_X);    
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBQ80400T);                                        
     DISP_1       = DCLBQ80400T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '107';                                              
     ABEND_TXT    = 'Fejl i INSERT_BQ804(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END INSERT_BQ804;                                                      
                                                                        
 /********************************************************************/ 
 /* Tæl hvor mange rækker der er per TIL_EVS_AAR, AFVIKLINGS_ID,     */ 
 /* EKOMNR, EEJDNR og EENHEDLBNR i                                   */ 
 /* EVS-enhedssporbarhed tabellen (BQ804)                            */ 
 /********************************************************************/ 
 SELECT_BQ804: PROC RETURNS(FIXED BIN(15));                             
                                                                        
 DCL CNT_ANTAL FIXED BIN(15) INIT(0);                                   
                                                                        
 EXEC SQL SELECT COUNT(*)                                               
          INTO   :CNT_ANTAL                                             
          FROM    BQ80400T                                              
          WHERE   TIL_EVS_AAR   = :DCLBQ80400T.TIL_EVS_AAR              
          AND     AFVIKLINGS_ID = :DCLBQ80400T.AFVIKLINGS_ID            
          AND     EKOMNR        = :DCLBQ80400T.EKOMNR                   
          AND     EEJDNR        = :DCLBQ80400T.EEJDNR                   
          AND     EENHEDLBNR    = :DCLBQ80400T.EENHEDLBNR;              
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     CNT_ANTAL = CNT_ANTAL + 1;                                         
                                                                        
     BQ804T_SEL = BQ804T_SEL + 1;                                       
                                                                        
     IF (EN_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 1) THEN DO;            
       BQ804T_SEL_EN   = BQ804T_SEL_EN   + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 1) THEN DO;            
       BQ804T_SEL_TO_1 = BQ804T_SEL_TO_1 + 1;                           
     END;                                                               
                                                                        
     IF (TO_EJERBOLIG & DCLBQ80400T.EENHEDLBNR = 2) THEN DO;            
       BQ804T_SEL_TO_2 = BQ804T_SEL_TO_2 + 1;                           
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBQ80400T.EKOMNR;                                
       DISP_EEJDNR = DCLBQ80400T.EEJDNR;                                
       DISP_ENHED  = DCLBQ80400T.EENHEDLBNR;                            
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! PROCNAME !! FOUND_TXT !! DISP_X);     
     END;                                                               
                                                                        
     RETURN(CNT_ANTAL);                                                 
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBQ80400T);                                        
     DISP_1       = DCLBQ80400T.EENHEDLBNR;                             
     DISP_4       = SQLCA.SQLCODE;                                      
     ABEND_FEJLNR = '108';                                              
     ABEND_TXT    = 'Fejl i SELECT_BQ804(' !! DISP_1                    
                    !! ')- Sqlcode ' !! DISP_4;                         
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END SELECT_BQ804;                                                      
                                                                        
 /********************************************************************/ 
 /* Skriv BH337O record                                              */ 
 /********************************************************************/ 
 WRITE_BH337O: PROC;                                                    
                                                                        
 IF EN_EJERBOLIG                                                        
  ! TO_EJERBOLIG THEN DO;                                               
   SELECT;                                                              
     WHEN(OUT_FEJLNR > 0) BH337.TEKFEJLNR = OUT_FEJLNR;                 
     OTHERWISE            ;                                             
   END;                                                                 
                                                                        
   BH337.EENHEDLBNR  = OUT_EENHEDLBNR;                                  
   BH337.ENHEDBELIG  = OUT_BELIG;                                       
   BH337.VEJK_ENHED  = OUT_VEJK;                                        
   BH337.HUSNR_ENHED = OUT_HUSNR;                                       
   BH337.ETAGE_ENHED = OUT_ETAGE;                                       
   BH337.SDØR_ENHED  = OUT_SDØR;                                        
 END;                                                                   
                                                                        
 WRITE FILE(BH337O) FROM(BH337_AREA);                                   
                                                                        
 BH337O_WRITE = BH337O_WRITE + 1;                                       
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   SELECT(BH337.TEKFEJLNR);                                             
     WHEN(0)    BH337O_OK_EN   = BH337O_OK_EN   + 1;                    
     WHEN(3700) BH337O_3700_EN = BH337O_3700_EN + 1;                    
     WHEN(3701) BH337O_3701_EN = BH337O_3701_EN + 1;                    
     OTHERWISE  ;                                                       
   END;                                                                 
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   SELECT(BH337.TEKFEJLNR);                                             
     WHEN(0)    BH337O_OK_TO   = BH337O_OK_TO   + 1;                    
     WHEN(3700) BH337O_3700_TO = BH337O_3700_TO + 1;                    
     WHEN(3701) BH337O_3701_TO = BH337O_3701_TO + 1;                    
     OTHERWISE  ;                                                       
   END;                                                                 
 END;                                                                   
                                                                        
 IF ^EN_EJERBOLIG                                                       
  & ^TO_EJERBOLIG THEN DO;                                              
   BH337O_REST = BH337O_REST + 1;                                       
 END;                                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   CALL INIT_DISP_X;                                                    
                                                                        
   DISP_EKOMNR = BH337.EKOMNR;                                          
   DISP_EEJDNR = BH337.EEJDNR;                                          
   DISP_VEJRK  = BH337.VEJK_ENHED;                                      
   DISP_HUSNR  = BH337.HUSNR_ENHED;                                     
   DISP_ETAGE  = BH337.ETAGE_ENHED;                                     
   DISP_DØRNR  = BH337.SDØR_ENHED;                                      
   DISP_ENHED  = BH337.EENHEDLBNR;                                      
                                                                        
   PUT SKIP LIST(DEBUG_TXT !! PROCNAME !! WRITE_TXT !! DISP_X);         
 END;                                                                   
                                                                        
 END WRITE_BH337O;                                                      
                                                                        
 /********************************************************************/ 
 /* Luk filer og put skip tællere                                    */ 
 /********************************************************************/ 
 AFSLUT: PROC;                                                          
                                                                        
 DCL DISP_CNT_1  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_2  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_3  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_4  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_5  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_6  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_7  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_8  PIC'Z.ZZZ.ZZ9';                                        
                                                                        
 CLOSE FILE(CN570I),                                                    
       FILE(BH337O);                                                    
                                                                        
 PUT SKIP LIST('');                                                     
 PUT SKIP LIST('Resultat for BH33700:');                                
                                                                        
 DISP_CNT_1  = CN570I_READ;                                             
 DISP_CNT_2  = CN570I_READ_EN;                                          
 DISP_CNT_3  = CN570I_READ_TO;                                          
 DISP_CNT_4  = CN570I_READ_REST;                                        
                                                                        
 PUT SKIP LIST('Read CN570   - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('                      En ejerbolig:' !! DISP_CNT_2 );   
 PUT SKIP LIST('                      To ejerbolig:' !! DISP_CNT_3 );   
 PUT SKIP LIST('                      Alle andre  :' !! DISP_CNT_4 );   
                                                                        
 DISP_CNT_1  = BQ802T_SEL;                                              
 DISP_CNT_2  = BQ802T_SEL_EN;                                           
 DISP_CNT_3  = BQ802T_SEL_TO_1;                                         
 DISP_CNT_4  = BQ802T_SEL_TO_2;                                         
 DISP_CNT_5  = BQ802T_NOT_EN;                                           
 DISP_CNT_6  = BQ802T_NOT_TO_1;                                         
 DISP_CNT_7  = BQ802T_NOT_TO_2;                                         
                                                                        
 PUT SKIP LIST('Select BQ802 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('          En ejerbolig    - Found :' !! DISP_CNT_2 );   
 PUT SKIP LIST('          To ejerbolig(1) - Found :' !! DISP_CNT_3 );   
 PUT SKIP LIST('          To ejerbolig(2) - Found :' !! DISP_CNT_4 );   
 PUT SKIP LIST('          En ejerbolig    - Notfnd:' !! DISP_CNT_5 );   
 PUT SKIP LIST('          To ejerbolig(1) - Notfnd:' !! DISP_CNT_6 );   
 PUT SKIP LIST('          To ejerbolig(2) - Notfnd:' !! DISP_CNT_7 );   
                                                                        
 DISP_CNT_1  = BQ700T_SEL;                                              
 DISP_CNT_2  = BQ700T_SEL_EN;                                           
 DISP_CNT_3  = BQ700T_SEL_TO_1;                                         
 DISP_CNT_4  = BQ700T_SEL_TO_2;                                         
 DISP_CNT_5  = BQ700T_NOT_EN;                                           
 DISP_CNT_6  = BQ700T_NOT_TO_1;                                         
 DISP_CNT_7  = BQ700T_NOT_TO_2;                                         
                                                                        
 PUT SKIP LIST('Select BQ700 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('          En ejerbolig    - Found :' !! DISP_CNT_2 );   
 PUT SKIP LIST('          To ejerbolig(1) - Found :' !! DISP_CNT_3 );   
 PUT SKIP LIST('          To ejerbolig(2) - Found :' !! DISP_CNT_4 );   
 PUT SKIP LIST('          En ejerbolig    - Notfnd:' !! DISP_CNT_5 );   
 PUT SKIP LIST('          To ejerbolig(1) - Notfnd:' !! DISP_CNT_6 );   
 PUT SKIP LIST('          To ejerbolig(2) - Notfnd:' !! DISP_CNT_7 );   
                                                                        
 DISP_CNT_1  = BQ710T_SEL;                                              
 DISP_CNT_2  = BQ710T_SEL_EN;                                           
 DISP_CNT_3  = BQ710T_SEL_TO_1;                                         
 DISP_CNT_4  = BQ710T_SEL_TO_2;                                         
 DISP_CNT_5  = BQ710T_NOT_EN;                                           
 DISP_CNT_6  = BQ710T_NOT_TO_1;                                         
 DISP_CNT_7  = BQ710T_NOT_TO_2;                                         
                                                                        
 PUT SKIP LIST('Select BQ710 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('          En ejerbolig    - Found :' !! DISP_CNT_2 );   
 PUT SKIP LIST('          To ejerbolig(1) - Found :' !! DISP_CNT_3 );   
 PUT SKIP LIST('          To ejerbolig(2) - Found :' !! DISP_CNT_4 );   
 PUT SKIP LIST('          En ejerbolig    - Notfnd:' !! DISP_CNT_5 );   
 PUT SKIP LIST('          To ejerbolig(1) - Notfnd:' !! DISP_CNT_6 );   
 PUT SKIP LIST('          To ejerbolig(2) - Notfnd:' !! DISP_CNT_7 );   
                                                                        
 DISP_CNT_1  = BQ803T_SEL;                                              
 DISP_CNT_2  = BQ803T_SEL_EN;                                           
 DISP_CNT_3  = BQ803T_SEL_TO_1;                                         
 DISP_CNT_4  = BQ803T_SEL_TO_2;                                         
 DISP_CNT_5  = BQ803T_NOT_EN;                                           
 DISP_CNT_6  = BQ803T_NOT_TO_1;                                         
 DISP_CNT_7  = BQ803T_NOT_TO_2;                                         
                                                                        
 PUT SKIP LIST('Select BQ803 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('          En ejerbolig    - Found :' !! DISP_CNT_2 );   
 PUT SKIP LIST('          To ejerbolig(1) - Found :' !! DISP_CNT_3 );   
 PUT SKIP LIST('          To ejerbolig(2) - Found :' !! DISP_CNT_4 );   
 PUT SKIP LIST('          En ejerbolig    - Notfnd:' !! DISP_CNT_5 );   
 PUT SKIP LIST('          To ejerbolig(1) - Notfnd:' !! DISP_CNT_6 );   
 PUT SKIP LIST('          To ejerbolig(2) - Notfnd:' !! DISP_CNT_7 );   
                                                                        
 DISP_CNT_1  = BQ803T_INS;                                              
 DISP_CNT_2  = BQ803T_INS_EN;                                           
 DISP_CNT_3  = BQ803T_INS_TO_1;                                         
 DISP_CNT_4  = BQ803T_INS_TO_2;                                         
                                                                        
 PUT SKIP LIST('Insert BQ803 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('                   En ejerbolig   :' !! DISP_CNT_2 );   
 PUT SKIP LIST('                   To ejerbolig(1):' !! DISP_CNT_3 );   
 PUT SKIP LIST('                   To ejerbolig(2):' !! DISP_CNT_4 );   
                                                                        
 DISP_CNT_1  = BQ804T_SEL;                                              
 DISP_CNT_2  = BQ804T_SEL_EN;                                           
 DISP_CNT_3  = BQ804T_SEL_TO_1;                                         
 DISP_CNT_4  = BQ804T_SEL_TO_2;                                         
                                                                        
 PUT SKIP LIST('Select Count BQ804 - Ialt         :' !! DISP_CNT_1 );   
 PUT SKIP LIST('                   En ejerbolig   :' !! DISP_CNT_2 );   
 PUT SKIP LIST('                   To ejerbolig(1):' !! DISP_CNT_3 );   
 PUT SKIP LIST('                   To ejerbolig(2):' !! DISP_CNT_4 );   
                                                                        
 DISP_CNT_1  = BQ804T_INS;                                              
 DISP_CNT_2  = BQ804T_INS_EN;                                           
 DISP_CNT_3  = BQ804T_INS_TO_1;                                         
 DISP_CNT_4  = BQ804T_INS_TO_2;                                         
                                                                        
 PUT SKIP LIST('Insert BQ804 - Ialt               :' !! DISP_CNT_1 );   
 PUT SKIP LIST('                   En ejerbolig   :' !! DISP_CNT_2 );   
 PUT SKIP LIST('                   To ejerbolig(1):' !! DISP_CNT_3 );   
 PUT SKIP LIST('                   To ejerbolig(2):' !! DISP_CNT_4 );   
                                                                        
 DISP_CNT_1  = BH337O_WRITE;                                            
 DISP_CNT_2  = BH337O_REST;                                             
 DISP_CNT_3  = BH337O_OK_EN;                                            
 DISP_CNT_4  = BH337O_OK_TO;                                            
 DISP_CNT_5  = BH337O_3700_EN;                                          
 DISP_CNT_6  = BH337O_3700_TO;                                          
 DISP_CNT_7  = BH337O_3701_EN;                                          
 DISP_CNT_8  = BH337O_3701_TO;                                          
                                                                        
 PUT SKIP LIST('Write BH337O - Ialt               :' !! DISP_CNT_1);    
 PUT SKIP LIST('          Alle andre              :' !! DISP_CNT_2);    
 PUT SKIP LIST('          En ejerbolig - OK       :' !! DISP_CNT_3);    
 PUT SKIP LIST('          To ejerbolig - OK       :' !! DISP_CNT_4);    
 PUT SKIP LIST('          En ejerbolig - FEJL 3700:' !! DISP_CNT_5);    
 PUT SKIP LIST('          To ejerbolig - FEJL 3700:' !! DISP_CNT_6);    
 PUT SKIP LIST('          En ejerbolig - FEJL 3701:' !! DISP_CNT_7);    
 PUT SKIP LIST('          To ejerbolig - FEJL 3701:' !! DISP_CNT_8);    
                                                                        
 PUT SKIP LIST('');                                                     
                                                                        
 PUT SKIP LIST('Program BH33700 sluttet');                              
                                                                        
 END AFSLUT;                                                            
                                                                        
 /********************************************************************/ 
 /* Gem BQ802 data i out_* felter                                    */ 
 /********************************************************************/ 
 GEM_BQ802_DATA: PROC;                                                  
                                                                        
 OUT_EKOMNR     = DCLBQ80200T.EKOMNR;                                   
 OUT_EEJDNR     = DCLBQ80200T.EEJDNR;                                   
 OUT_EENHEDLBNR = DCLBQ80200T.EENHEDLBNR;                               
 OUT_ADRSNAVN   = DCLBQ80200T.ADRSNAVN_ENH;                             
 OUT_VEJK       = DCLBQ80200T.VEJK_ENH;                                 
 OUT_HUSNR      = DCLBQ80200T.HUSNR_ENH;                                
 OUT_ETAGE      = DCLBQ80200T.ETAGE_ENH;                                
 OUT_SDØR       = DCLBQ80200T.DØRNR_ENH;                                
 OUT_BELIG      = USE_MAKRO_BH33700M;                                   
                                                                        
 END GEM_BQ802_DATA;                                                    
                                                                        
 /********************************************************************/ 
 /* Anvend makro BH33700M - Dan et venstrestillet adresse felt       */ 
 /*                         på 50 karakterer                         */ 
 /********************************************************************/ 
 USE_MAKRO_BH33700M: PROC RETURNS(CHAR(50));                            
                                                                        
 VEJ_ADRESSENAVN = OUT_ADRSNAVN;                                        
 HUS_NUMMER      = OUT_HUSNR;                                           
 BYGNING_NUMMER  = '';                                                  
 ETAGE           = OUT_ETAGE;                                           
 SIDE_DØRNR      = OUT_SDØR;                                            
                                                                        
 %INCLUDE BH33700M;      /* Kode skal ligge her efter init af felter */ 
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   PUT SKIP LIST(DEBUG_TXT !! MAKRO_TXT !! INPUT_TXT                    
                !! VEJ_ADRESSENAVN !! '  '                              
                !! HUS_NUMMER      !! ' '                               
                !! BYGNING_NUMMER  !! ' '                               
                !! ETAGE           !! ' '                               
                !! SIDE_DØRNR);                                         
                                                                        
   PUT SKIP LIST(DEBUG_TXT !! MAKRO_TXT !! OUTPUT_TXT !! WAREAL);       
 END;                                                                   
                                                                        
 RETURN(WAREAL);                                                        
                                                                        
 END USE_MAKRO_BH33700M;                                                
                                                                        
 /********************************************************************/ 
 /* Init DISP_X strengen                                             */ 
 /********************************************************************/ 
 INIT_DISP_X: PROC;                                                     
                                                                        
 DCL EN_TXT CHAR(02) INIT('EN');                                        
 DCL TO_TXT CHAR(02) INIT('TO');                                        
 DCL OK_TXT CHAR(04) INIT('OK  ');                                      
                                                                        
 DISP_X = '';                                                           
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   DISP_EJERBO = EN_TXT;                                                
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   DISP_EJERBO = TO_TXT;                                                
 END;                                                                   
                                                                        
 SELECT;                                                                
   WHEN(  OUT_FEJLNR > 0) DISP_STATUS = OUT_FEJLNR;                     
   OTHERWISE              DISP_STATUS = OK_TXT;                         
 END;                                                                   
                                                                        
 END INIT_DISP_X;                                                       
                                                                        
 /********************************************************************/ 
 /* Commit og put skip                                               */ 
 /********************************************************************/ 
 DB2_COMMIT: PROC;                                                      
                                                                        
 COMMIT_COUNT = COMMIT_COUNT + 1;                                       
                                                                        
 IF COMMIT_COUNT > COMMIT_VALUE                                         
  ! COMMIT_COUNT = COMMIT_VALUE THEN DO;                                
   EXEC SQL COMMIT;                                                     
                                                                        
   COMMIT_COUNT = 0;                                                    
   COMMIT_NR     = COMMIT_NR + 1;                                       
                                                                        
   PUT SKIP LIST('Commit nr ' !! COMMIT_NR);                            
 END;                                                                   
                                                                        
 END DB2_COMMIT;                                                        
                                                                        
 /********************************************************************/ 
 /* Udskriv program fejl                                             */ 
 /********************************************************************/ 
 PROGRAM_FEJL: PROC;                                                    
                                                                        
 PUT SKIP LIST ('*****************************************');           
 PUT SKIP LIST ('Der abendes med programfejl');                         
 PUT SKIP LIST ('ABEND_TXT:                 ' !! ABEND_TXT);            
 PUT SKIP LIST ('*****************************************');           
 CALL ZS30101(5,ABEND_MEDDEL,2);                                        
                                                                        
 END PROGRAM_FEJL;                                                      
                                                                        
 /********************************************************************/ 
 /* Udskriv sql fejl                                                 */ 
 /********************************************************************/ 
 SQL_FEJL: PROC;                                                        
                                                                        
 PUT SKIP LIST ('*****************************************');           
 PUT SKIP LIST ('Der abendes med sqlcode: ' !! SQLCA.SQLCODE);          
 PUT SKIP LIST ('ABEND_TXT:               ' !! ABEND_TXT);              
 PUT SKIP LIST ('*****************************************');           
                                                                        
 CALL ZS67000(SQLCA);                                                   
 CALL ZS30101(5,ABEND_MEDDEL,2);                                        
                                                                        
 CONDCODE = 99;                                                         
                                                                        
 END SQL_FEJL;                                                          
                                                                        
 END BH33700;                                                           
