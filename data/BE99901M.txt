 /*********************************************************************/
 /* Include: BE99901M                                                 */
 /*                                                                   */
 /* Kald abend rutinen ZS30101. Må kun anvendes i batch programmer.   */
 /*                                                                   */
 /* I user abend meddelelsen udskrives kun de første 41 tegn af       */
 /* teksten.                                                          */
 /*                                                                   */
 /* Denne procedure kalder proceduren AFSLUT_PROGRAM_FØR_ABEND, hvor  */
 /* der kan foretages "pæn nedlukning" af programmet før abend.       */
 /* Proceduren AFSLUT_PROGRAM_FØR_ABEND skal defineres med samme      */
 /* parametre som denne procedure og kan implementeres i en tom       */
 /* udgave via include af BE99902M. Include BE99902M kan ved behov    */
 /* kopieres ind i hovedprogrammet og kode implementeres i proceduren.*/
 /*                                                                   */
 /*********************************************************************/
 ABEND: PROC (P_PROCNAME, P_USER_ABEND_CODE, P_USER_ABEND_TEXT);        
                                                                        
 DCL P_PROCNAME                     CHAR(*) VAR;                        
 DCL P_USER_ABEND_CODE              PIC '999';                          
 DCL P_USER_ABEND_TEXT              CHAR(*) VAR;                        
                                                                        
 DCL 1 ABENDMEDD,                                                       
       2 ABENDMODUL                 CHAR (7) INIT (PACKAGENAME()),      
       2 ABENDFILL1                 CHAR (1) INIT (' '),                
       2 ABENDMEDNR                 CHAR (2) INIT ('**'),               
       2 ABENDFILL2                 CHAR (1) INIT (' '),                
       2 ABENDTXT                   CHAR (45);                          
                                                                        
 DCL ZS30101                        ENTRY; /* User abend */             
                                                                        
    /* Sæt nummer og tekst til user abend */                            
    ABENDMEDD.ABENDTXT = P_USER_ABEND_CODE !! ' ' !!                    
                         P_USER_ABEND_TEXT;                             
                                                                        
    /* Udskriv abend information */                                     
    PUT SKIP LIST (' ');                                                
    PUT SKIP LIST ('Abend udføres fra procedure ' !! TRIM(P_PROCNAME) !!
                   ' i program ' !! PACKAGENAME() !! ': ' !!            
                    ABENDMEDD.ABENDTXT);                                
    PUT SKIP LIST (' ');                                                
                                                                        
    /* Foretag kald til procedure hvor programmet kan foretage     */   
    /* "pæn nedlukning" umiddelbart før abend.                     */   
    CALL AFSLUT_PROGRAM_FØR_ABEND (P_PROCNAME,                          
                                   P_USER_ABEND_CODE,                   
                                   P_USER_ABEND_TEXT);                  
                                                                        
    /* Udfør abend via ZS30101 uden SYSUDUMP */                         
    CALL ZS30101(5,ABENDMEDD,2);                                        
                                                                        
 END ABEND;                                                             
                                                                        
