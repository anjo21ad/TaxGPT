 BE32625: /* SKAF HENVISNINGSNR. FRA NYT SLUTÅR TIL ESR-UDTRÆK */       
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /********************************************************************/ 
  DCL COPYRIGHT  CHAR  (30) STATIC     INIT ('COPYRIGHT KMD A/S 2004'); 
 /********************************************************************* 
 *                                                                   *  
 * FORMÅL : AT UNDERSØGE OM DET ER SKET EN OMPLACERING                * 
 *                                                                    * 
 * INPUT  : B.H32100D  UDTRÆK FRA ESR EFTER 2-EJER-BEHANDLING         * 
 *          B.H65501D  MINI MT-FIL FRA EVS NYT SLUTÅR (HOVEDLEV.).    * 
 *                                                                    * 
 * OUTPUT : B.H32600D  ESR UDTRÆK MED OMPLACERINGS DATA BL.A SOM      * 
 *                     DUBLETTER.                                     * 
 *          BE32601Y   KØRSELSKONTROLLISTE                            * 
 *                                                                    * 
 * PROGRAMMØR: GGP              LSU T&S EVS            NOV 2003       * 
 * RETTET:     BO SCHMIDT       LSU T&S EVS            JUN 2004       * 
 * SLUT 2012:  KRAV 18-01 - BENYTTELSESKODE 25, LKA, OKTOBER 2012     * 
 *             OMCOMPILERET P.G.A.                                    * 
 *             NYT FELT CEANV I STRUKTUR BE31800N                     * 
 *             OG NYT FELT CEANV I STRUKTUR BE32200N (BE32202N)       * 
 *             DE TO FELTER SÆTTES LIG HINANDEN                       * 
 *                                                                    * 
 * FORSKUD2014 Lars Harritshøj                           Maj 2013     * 
 *             Compileret Ingen ændringer                             * 
 *                                                                    * 
 * SLUT 2013   Jørgen Saugmann                           okt 2013     * 
 *             Compileret Ingen ændringer                             * 
 *                                                                    * 
 * FORSKUD2015 Jørgen Saugmann                           jun 2014     * 
 *             Compileret Ingen ændringer                             * 
 * SLUT 2014   Jørgen Saugmann                           okt 2014     * 
 *             Compileret Ingen ændringer                             * 
 * FORSKUD2016 Z6JNK                                     08.06.2015   * 
 *             Compileret. Ingen ændringer                            * 
 * FORSKUD2017 Z8QEB                                     16.08.2016   * 
 *             Indlagt EXECPARM                                       * 
 *             Så omplacerings check kan spriges over i test. Da det  * 
 *             kører meget længe. Hvis der angives TEST springes      * 
 *             undersøgelsen af omplacering over                      * 
 *                                                                    * 
 * SLUT 2018   Esben Brodersen                           12.09.2018   * 
 *             ændret struktur BE31800N -> BE33700N                   * 
 *             ændret struktur BE32200N                               * 
 *             ændret struktur BE30000N samt udtræk pga. af           * 
 *             udvidelse til 2familiebolig                            * 
 * Rettelse #02: Edward Laryea, EDL                     22-03-2019    * 
 *               Nyt felt STRAKS_EVS i BE33700N                       * 
 * Forskud2021 Per Brunk                               Marts 2020     * 
 *               Nye felter i BE32202N/BE33700N VURMARK og VUR2001    * 
 * Slut 2020 : Per Brunk PBR Ny struktur BE33700N/BE31800N   Okt 2020 * 
 * SLUT 2021   Z6SEP                                   Marts 2021     * 
 *             BE65300M rettet til BE55300M                           * 
 *             BE65300N rettet til BE65321N                           * 
 * Slut 2021 : Per Brunk PBR Defect 889                      Jun 2021 * 
 *             Flyt BFENR og VUREJENDOMID fra BE321 til BE326         * 
 * Forskud2025 : Jan Jensen JJJ BE55600M erstatter BE55300M  Jun 2024 * 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************  
 *                       DECLARE AF REGISTRE                          * 
 **********************************************************************/
 DCL     BE655I FILE RECORD INPUT;     /* MINI MT-FIL */                
 DCL     BE321I FILE RECORD INPUT;     /* ESR UDTRÆK */                 
 DCL     BE326O FILE RECORD OUTPUT;    /* BEHANDLET ESR UDTRÆK */       
 /********************************************************************* 
 *       DECLARE AF MINI MT-FIL FRA NYT SLUTÅR                        * 
 *********************************************************************/ 
 DCL     BE655_AR CHAR (200) VAR;                                       
 DCL     1 BE655 BASED(ADDR(BE655_AR)),                                 
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BE65325N;;  /* RUL */                               
                                                                        
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER TESTKØRSEL              */
 /*      FOR AT KUNNE AFVIKLE KØRSLER HURTIGER I TEST.                */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
                                                                        
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     BE321_AR        CHAR      (2000) VAR;                          
 DCL     1 BE321         BASED     (ADDR(BE321_AR)),                    
           4 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BE33700N;                                           
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK OUTPUT                                 * 
 *********************************************************************/ 
 DCL     1 BE326,                                                       
           %INCLUDE BE32200N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                        /* TÆLLEVÆRKER TIL TOTALLISTEN */ 
           2 LÆS_BE655   FIXED DEC (7),                                 
           2 LÆS_BE321   FIXED DEC (7),                                 
           2 FUNDET      FIXED DEC (7),                                 
           2 NOMPLA      FIXED DEC (7),                                 
           2 SKROMPLA    FIXED DEC (7),                                 
           2 FOMPLA      FIXED DEC (7),                                 
           2 SOMPLA      FIXED DEC (7),                                 
           2 SKREVET     FIXED DEC (7);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     1 BE55600M,                                                    
           %INCLUDE BE55600M; /* OPSÆT AKTUELT INDKOMSTÅR M.V. */       
                                                                        
 DCL     HJSLUTÅR        BIN FIXED (15);                                
 DCL     EOF_BE655       BIT       (1);                                 
 DCL     EOF_BE321       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     FEJLTEKST         CHAR (80);                                   
 DCL     ICOMPLA           BIT;                                         
 DCL     IICOMPLA          BIT;                                         
 DCL     ZS67000        ENTRY;                                          
 DCL     ZS30101        ENTRY;                                          
 /***************************************************************       
 /*      DECLARE AF ABEND-RUTINE                                        
 /**************************************************************/       
 DCL     ABENDKODE    FIXED       (2);                                  
 DCL     1 ABENDMEDD,                                                   
           2 ABENDMODUL CHAR      (7),                                  
           2 ABENDFILL1 CHAR      (1),                                  
           2 ABENDMEDNR CHAR      (2),                                  
           2 ABENDFILL2 CHAR      (1),                                  
           2 ABENDTXT   CHAR      (45);                                 
 DCL     ABENDRTKODE    FIXED     (2);                                  
                                                                        
 DCL    SQLKODE         FIXED     (5);                                  
 DCL    SQLCODE_CH      CHAR      (5);                                  
 DCL    SQLCODE_PIC     PIC       'SSSS9' DEF SQLCODE_CH;               
 /*******************************************************************/  
                                                                        
  EXEC SQL INCLUDE BE30000T;                                            
  EXEC SQL INCLUDE SQLCA;                                               
                                                                        
 DCL     1 EVS_ESK,                     /* EVS EJERSKABSTABELEN       */
         %INCLUDE BE30002N;;            /* Fra BQ30000N               */
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BE32625L'),                                        
           2 F1          CHAR      (21)      INIT      (' '),           
           2 TEKST2      CHAR      (52)      INIT      (                
           'BEHANDL EVT. OMPLACEREDE FRA ESR UDTRÆK'),                  
           2 F2          CHAR      (21)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (63)      INIT      ((63)' ');       
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BE655 = NEJ;                                               
         EOF_BE321 = NEJ;                                               
                                                                        
         ON ERROR CALL PLIDUMP ('SNHOB');                               
         OPEN FILE (BE655I) TITLE ('BE65501I');                         
         OPEN FILE (BE321I) TITLE ('BE32100I');                         
         OPEN FILE (BE326O) TITLE ('BE32600O');                         
         ON ENDFILE (BE655I)                                            
         BEGIN;                                                         
            EOF_BE655 = JA;                                             
         END;                                                           
         ON ENDFILE (BE321I)                                            
         BEGIN;                                                         
            EOF_BE655 = JA;                                             
            EOF_BE321 = JA;                                             
         END;                                                           
         CALL ZZ20390 ('*OPEN','BE32601Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         TV = '';                                                       
         FEJLTEKST = ' ';                                               
         ABENDMEDD = ' ';                                               
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
         CALL LÆS_BE655;                                                
         CALL LÆS_BE321;                                                
                                                                        
         DO WHILE (EOF_BE655 = NEJ ! EOF_BE321 = NEJ);                  
            SELECT;                                                     
               WHEN (BE321.DCPRCIR > BE655.DCPRN & EOF_BE655 = NEJ)     
                  CALL LÆS_BE655;                                       
               WHEN (BE321.DCPRCIR < BE655.DCPRN & EOF_BE321 = NEJ)     
                  DO;                                                   
                     ICOMPLA = '0'B;                                    
                     IICOMPLA = '0'B;                                   
                     CALL BEHANDL_EJ_PÅ_MT;                             
                     CALL LÆS_BE321;                                    
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     TV.FUNDET = TV.FUNDET + 1;                         
                     CALL BEHANDL_MT_OK;                                
                     CALL LÆS_BE321;                                    
                  END;                                                  
            END;                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS ESR UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_BE321:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BE321I) INTO (BE321_AR);                            
                                                                        
         IF EOF_BE321 = NEJ                                             
         THEN                                                           
            DO;                                                         
               BE326 = '';                                              
               BE326.CEANV = ' ';          /* SLUT12 LKA, NOV 2012 */   
               BE326.CEANV = BE321.CEANV;  /* SLUT12 LKA, NOV 2012 */   
                                                                        
               BE326.EKOMNR       = BE321.EKOMNR;                       
               BE326.EEJDNR       = BE321.EEJDNR;                       
               BE326.EENHEDLBNR   = BE321.EENHEDLBNR;                   
               BE326.BFENR        = BE321.BFENR;         /*D889*/       
               BE326.VUREJENDOMID = BE321.VUREJENDOMID;  /*D889*/       
                                                                        
                                                                        
               BE326.EJER = BE321, BY NAME;                             
                                                                        
               IF BE326.EJER.DAFSTÅ > 0                                 
               THEN                                                     
                  BE326.SORTDATO = BE326.EJER.DAFSTÅ;                   
               ELSE                                                     
                  BE326.SORTDATO = 99991231;                            
                                                                        
               BE326.STAMOPL = BE321, BY NAME;                          
                                                                        
               TV.LÆS_BE321 = TV.LÆS_BE321 + 1;                         
            END;                                                        
                                                                        
 END LÆS_BE321;                                                         
 /********************************************************************* 
 *       LÆS MINI MT-FIL                                              * 
 *********************************************************************/ 
 LÆS_BE655:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BE655I) INTO (BE655_AR);                            
                                                                        
         IF EOF_BE655 = NEJ & BE655.DCPRN < 9999999999                  
         THEN                                                           
            DO;                                                         
               TV.LÆS_BE655 = TV.LÆS_BE655 + 1;                         
               IF BE655.CSTATUS = '2'                                   
               THEN                                                     
                  TV.NOMPLA = TV.NOMPLA + 1;                            
            END;                                                        
                                                                        
 END LÆS_BE655;                                                         
                                                                        
 /********************************************************************* 
 *       BEHANDL EJER SOM IKKE FINDES PÅ MINI-MT                      * 
 *********************************************************************/ 
 BEHANDL_EJ_PÅ_MT:                                                      
 PROC;                                                                  
 DCL STOPOP BIT;                                                        
 DCL CPR_PIC PIC '9999999999';                                          
                                                                        
         STOPOP = '0'B;                                                 
         CPR_PIC = 0;                                                   
         BE326.SORTCPR  = BE321.DCPRCIR;                                
         BE326.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BE326.CCIVS = ' ';                                             
         BE326.CSBSK = ' ';                                             
                                                                        
         IF EX_PARM ^= 'TEST'                                           
         THEN                                                           
           DO;                                                          
                                                                        
             CALL DECLARE_CURSOR;                                       
             CALL OPEN_BE300_Å1_CURSOR;                                 
             DO UNTIL (STOPOP = '1'B);                                  
               CALL FETCH_BE300_Å1_CURSOR;                              
               IF SQLCA.SQLCODE ^= 0                                    
                  ! ICOMPLA = '1'B                                      
               THEN                                                     
                 STOPOP = '1'B;                                         
             END;                                                       
             IF SQLCA.SQLCODE ^= 0                                      
              & SQLCA.SQLCODE ^= 100                                    
             THEN                                                       
               DO;                                                      
                 SQLCODE_PIC = SQLCODE;                                 
                 ABENDTXT   =  '281 FEJL FETCH_BE300_Å1,SQLCODE= '      
                               !! SQLCODE_CH;                           
                 CALL SQL_FEJL;                                         
               END;                                                     
             CALL CLOSE_BE300_Å1_CURSOR;                                
                                                                        
             IF ICOMPLA = '0'B                                          
             THEN                                                       
               DO;                                                      
                  STOPOP = '0'B;                                        
                  EVS_ESK.DCPRN = BE321.DCPRCIR;                        
                  CALL OPEN_BE300_Å2_CURSOR;                            
                  DO UNTIL (SQLCA.SQLCODE ^= 0);                        
                    CALL FETCH_BE300_Å2_CURSOR;                         
                    IF SQLCA.SQLCODE ^= 0                               
                     ! IICOMPLA = '1'B                                  
                    THEN                                                
                      STOPOP = '1'B;                                    
                  END;                                                  
                  IF SQLCA.SQLCODE ^= 0                                 
                   & SQLCA.SQLCODE ^= 100                               
                  THEN                                                  
                    DO;                                                 
                      SQLCODE_PIC = SQLCODE;                            
                      ABENDTXT   =  '282 FEJL FETCH_BE300_Å2,' !!       
                                    'SQLCODE= ' !! SQLCODE_CH;          
                      CALL SQL_FEJL;                                    
                    END;                                                
                  CALL CLOSE_BE300_Å2_CURSOR;                           
               END;                                                     
           END; /* EX_PARM ^= 'TEST' */                                 
                                                                        
                                                                        
         SELECT;                                                        
           WHEN (ICOMPLA = '1'B)                                        
            DO;                                                         
              /*BEVAR RECORD*/                                          
              BE326.COMPLA = '1';                                       
              BE326.DOMPLAÅR = GLSLUTÅR;                                
              BE326.DOMPLACPR = EVS_ESK.DOMPLACPR;                      
              WRITE FILE (BE326O) FROM (BE326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              /*DUBLET RECORD*/                                         
              BE326.COMPLA = '2';                                       
              BE326.DOMPLAÅR = GLSLUTÅR;                                
              CPR_PIC = BE321.DCPRCIR;                                  
              BE326.DOMPLACPR = CPR_PIC;                                
              CPR_PIC = EVS_ESK.DOMPLACPR;                              
              BE326.DCPRCIR = CPR_PIC;                                  
              BE326.SORTCPR = CPR_PIC;                                  
              WRITE FILE (BE326O) FROM (BE326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              TV.SKROMPLA = TV.SKROMPLA + 1;                            
              TV.SOMPLA = TV.SOMPLA + 1;                                
            END;                                                        
           WHEN (IICOMPLA = '1'B)                                       
            DO;                                                         
              /*BEVAR RECORD*/                                          
              BE326.COMPLA = '1';                                       
              BE326.DOMPLAÅR = GLSLUTÅR - 1;                            
              BE326.DOMPLACPR = EVS_ESK.DOMPLACPR;                      
              WRITE FILE (BE326O) FROM (BE326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              /*DUBLET RECORD*/                                         
              BE326.COMPLA = '2';                                       
              BE326.DOMPLAÅR = GLSLUTÅR - 1;                            
              CPR_PIC = BE321.DCPRCIR;                                  
              BE326.DOMPLACPR = CPR_PIC;                                
              CPR_PIC = EVS_ESK.DOMPLACPR;                              
              BE326.DCPRCIR = CPR_PIC;                                  
              BE326.SORTCPR = CPR_PIC;                                  
              WRITE FILE (BE326O) FROM (BE326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              TV.SKROMPLA = TV.SKROMPLA + 1;                            
              TV.SOMPLA = TV.SOMPLA + 1;                                
            END;                                                        
           OTHER                                                        
            DO;                                                         
              WRITE FILE (BE326O) FROM (BE326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
            END;                                                        
         END;/*SELECT*/                                                 
                                                                        
 END BEHANDL_EJ_PÅ_MT;                                                  
 /********************************************************************* 
 *       BEHANDL EJER SOM FINDES PÅ MINI-MT                           * 
 *********************************************************************/ 
 BEHANDL_MT_OK:                                                         
 PROC;                                                                  
 DCL CPR_PIC PIC '9999999999';                                          
                                                                        
         BE326.SORTCPR = BE321.DCPRCIR;                                 
         BE326.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BE326.CCIVS = BE655.CCIVS;                                     
         BE326.CSBSK = BE655.HIST_OPL(SLUTÅR).CSBSK;                    
                                                                        
         IF BE655.CSTATUS = '2' &                                       
            BE655.HIST_OPL(SLUTÅR).DHENVN > 0                           
         THEN                                                           
            DO;                                                         
               /* PERSONEN ER OMPLACERET JVF. FORSKUD */                
               /* DHENVN INDEHOLDER DET NYE PERSONNR. */                
               BE326.COMPLA = '1';                                      
               BE326.DOMPLAÅR = SLUTÅR;                                 
               CPR_PIC = BE655.HIST_OPL(SLUTÅR).DHENVN;                 
               BE326.DOMPLACPR = CPR_PIC;                               
               WRITE FILE (BE326O) FROM (BE326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
               /*DUBLET RECORD*/                                        
               BE326.COMPLA = '2';                                      
               BE326.DOMPLAÅR = SLUTÅR;                                 
               CPR_PIC = BE321.DCPRCIR;                                 
               BE326.DOMPLACPR = CPR_PIC;                               
               BE326.DCPRCIR = BE655.HIST_OPL(SLUTÅR).DHENVN;           
               BE326.SORTCPR = BE655.HIST_OPL(SLUTÅR).DHENVN;           
               WRITE FILE (BE326O) FROM (BE326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
               TV.FOMPLA = TV.FOMPLA + 1;                               
               TV.SKROMPLA = TV.SKROMPLA + 1;                           
            END;                                                        
         ELSE                                                           
            DO;                                                         
               WRITE FILE (BE326O) FROM (BE326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
            END;                                                        
                                                                        
 END BEHANDL_MT_OK;                                                     
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BE655;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ MINI MT-FIL NYT SLUTÅR    B.H65501D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.NOMPLA;                                     
         DETLIN1.TEKST = '      HERAF OMPLACERET I FORSKUD ';           
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BE321;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ ESR UDTRÆK                B.H32100D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FUNDET;                                     
         DETLIN1.TEKST = '      HERAF EJER FUNDET PÅ MINI MT-FIL  ';    
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKREVET;                                    
         DETLIN1.TEKST = 'SKREVNE INDIVIDER INKL OMPLACEREDE B.H32600D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKROMPLA;                                   
         DETLIN1.TEKST = '        HERAF OMPLACEREDE';                   
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FOMPLA;                                     
         DETLIN1.TEKST = '              HERAF JVF. FORSKUD';            
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SOMPLA;                                     
         DETLIN1.TEKST = '              HERAF JVF. SLUT';               
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
                                                                        
         CLOSE FILE (BE321I),                                           
               FILE (BE655I),                                           
               FILE (BE326O);                                           
                                                                        
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
                                                                        
 %INCLUDE BE32601M; /* SQL DEL*/                                        
                                                                        
 END  BE32625;                                                          
