 BH38900: PROCEDURE OPTIONS (MAIN);                                     
 /********************************************************************/ 
 /* Projekt:     EVS Forskud / EVS Slut                              */ 
 /* Programnavn: BH38900                                             */ 
 /* Programtype: Batch                                               */ 
 /* Funktion:    Udvælg 2-Familieshuse                               */ 
 /*                                                                  */ 
 /* Input:       BH389I   - Grund- eller supplementsleverancen       */ 
 /*                         fra ESR/VUR (CN57000)                    */ 
 /*                         + felt EENHEDLBNR                        */ 
 /*                         + felt TEKFEJLNR                         */ 
 /*                         + felt ENHEDBELIG                        */ 
 /*                         + felt VEJR_ENHED                        */ 
 /*                         + felt HUSNR_ENHED                       */ 
 /*                         + felt ETAGE_ENHED                       */ 
 /*                         + felt SDØR_ENHED                        */ 
 /*                                                                  */ 
 /*              DEBUGI   - Skal programmet debugge (put skip):      */ 
 /*                            Hvis  (filen DEBUGI ikke findes     ) */ 
 /*                            eller (DEBUG_FROM = 0 og DEBU_TO = 0) */ 
 /*                               Ingen debug                        */ 
 /*                            ellers                                */ 
 /*                               Debug alt for de valgte            */ 
 /*                               record numre i BH389I              */ 
 /*                                                                  */ 
 /* Output:      BH389O   - Indhold lig BH389I                       */ 
 /*                                                                  */ 
 /*------------------------------------------------------------------*/ 
 /*                                                                  */ 
 /* 2018.12.05   Gitte Jensen, GJN                                   */ 
 /*              Nyt program                                         */ 
 /********************************************************************/ 
                                                                        
 DCL   COPYRIGHT       CHAR(30)       INIT ('(C) KMD A/S 2018');        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT KALD AF EKSTERNE MODULER */ 
                                                                        
 /********************************************************************/ 
 /* Builtin functions                                                */ 
 /********************************************************************/ 
 DCL   ADDR            BUILTIN;                                         
 DCL   NULL            BUILTIN;                                         
 DCL   PLIDUMP         BUILTIN;                                         
                                                                        
 /********************************************************************/ 
 /* BH389I - Grund- eller supplementsleverancen fra ESR/VUR (CN57000)*/ 
 /*          + felt EENHEDLBNR                                       */ 
 /*          + felt TEKFEJLNR                                        */ 
 /*          + felt ENHEDBELIG                                       */ 
 /*          + felt VEJR_ENHED                                       */ 
 /*          + felt HUSNR_ENHED                                      */ 
 /*          + felt ETAGE_ENHED                                      */ 
 /*          + felt SDØR_ENHED                                       */ 
 /*                                                                  */ 
 /* BH338O - Indhold lig BH389I                                      */ 
 /********************************************************************/ 
 DCL   BH389I          FILE RECORD INPUT;                               
 DCL   BH389O          FILE RECORD OUTPUT;                              
                                                                        
 DCL   BH389_AREA      CHAR(2000) VAR;                                  
                                                                        
 DCL 1 BH389        BASED(ADDR(BH389_AREA)),                            
       4 BH389_LENGTH  FIXED BIN(15),                                   
       %INCLUDE BH33700N;                                               
                                                                        
 /********************************************************************/ 
 /* DEBUGI - Skal programmet debugge (put skip)                      */ 
 /********************************************************************/ 
 DCL   DEBUGI          FILE RECORD INPUT;                               
                                                                        
 DCL 1 DEBUG_REC,                                                       
       3 DEBUG_FROM    PIC'(7)9',                                       
       3 DEBUG_FILL1   CHAR(1),                                         
       3 DEBUG_TO      PIC'(7)9',                                       
       3 DEBUG_FILL2   CHAR(65);                                        
                                                                        
 /********************************************************************/ 
 /* Interne arbejdsfelter                                            */ 
 /********************************************************************/ 
 DCL JA                BIT(01)   INIT('1'B);                            
 DCL NEJ               BIT(01)   INIT('0'B);                            
 DCL FLERE_BH389I      BIT(01)   INIT('1'B);                            
 DCL PUTSKIP_THIS      BIT(01)   INIT('0'B);                            
                                                                        
 DCL PROGRAM_TXT       CHAR(15)  INIT('Program BH38900');               
 DCL START_TXT         CHAR(08)  INIT(' startet');                      
 DCL SLUT_TXT          CHAR(08)  INIT(' sluttet');                      
 DCL DEBUG_TXT         CHAR(12)  INIT('*** DEBUG - ');                  
 DCL REC_TXT           CHAR(13)  INIT(' - Record nr ');                 
 DCL ENHED_TXT         CHAR(13)  INIT(' - ENHEDLBNR ');                 
                                                                        
 DCL DISP_REC          PIC 'Z.ZZZ.ZZ9';                                 
 DCL DISP_ENHED        PIC'ZZZ9';                                       
                                                                        
 DCL BH389I_READ       PIC'(7)9' INIT(0);                               
 DCL BH389I_READ_0     PIC'(7)9' INIT(0);                               
 DCL BH389I_READ_1     PIC'(7)9' INIT(0);                               
 DCL BH389I_READ_2     PIC'(7)9' INIT(0);                               
 DCL BH389O_WRITE      PIC'(7)9' INIT(0);                               
 DCL BH389O_WRITE_0    PIC'(7)9' INIT(0);                               
 DCL BH389O_WRITE_1    PIC'(7)9' INIT(0);                               
 DCL BH389O_WRITE_2    PIC'(7)9' INIT(0);                               
                                                                        
 /********************************************************************/ 
 /* Standard rutiner                                                 */ 
 /********************************************************************/ 
 DCL   ZS61900         ENTRY;          /* Udskrivning af SSI         */ 
 DCL   ZS30101         ENTRY;          /* Abendrutine                */ 
 DCL   ZS67000         ENTRY;          /* Hent sql tekster           */ 
                                                                        
 DCL   1 BH65300M,                     /* Ajourfør mini MT-fil til   */ 
         %INCLUDE BH65300M;            /* EVS forskud eller EVS slut */ 
                                                                        
 DCL  SYSPRINT FILE;                                                    
                                                                        
 /********************************************************************/ 
 /* On Conditions                                                    */ 
 /********************************************************************/ 
 CALL ZS61900;                                                          
                                                                        
 ON ERROR BEGIN;                                                        
   ON ERROR SYSTEM;                                                     
   PUT SKIP LIST('PROGRAMFEJL');                                        
   CALL PLIDUMP('SNHOB','PROGRAM BH38900L');                            
 END;                                                                   
                                                                        
 ON ENDFILE(BH389I) BEGIN;                                              
   FLERE_BH389I = NEJ;                                                  
 END;                                                                   
                                                                        
 /********************************************************************/ 
 /* Main proces                                                      */ 
 /********************************************************************/ 
 CALL HOUSEKEEPING;                                                     
 CALL READ_BH389I;                                                      
                                                                        
 DO WHILE(FLERE_BH389I);                                                
   SELECT(BH389.EENHEDLBNR);                                            
     WHEN(1)   CALL WRITE_BH389O;                                       
     WHEN(2)   CALL WRITE_BH389O;                                       
     OTHERWISE ;                                                        
   END;                                                                 
                                                                        
   CALL READ_BH389I;                                                    
 END;                                                                   
                                                                        
 CALL AFSLUT;                                                           
                                                                        
 /********************************************************************/ 
 /* Housekeeping                                                     */ 
 /********************************************************************/ 
 HOUSEKEEPING: PROC;                                                    
                                                                        
 DCL PUTSKIP_FROM    CHAR(17) INIT('   PUTSKIP_FROM: ');                
 DCL PUTSKIP_TO      CHAR(17) INIT('   PUTSKIP_TO  : ');                
                                                                        
 DCL DISP_FROM       PIC'Z.ZZZ.ZZ9';                                    
 DCL DISP_TO         PIC'Z.ZZZ.ZZ9';                                    
                                                                        
 PUT SKIP LIST(PROGRAM_TXT !! START_TXT);                               
 PUT SKIP LIST('');                                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Skal programmet fortage debug (put skip)                         */ 
 /*------------------------------------------------------------------*/ 
 ON UNDEFINEDFILE(DEBUGI) BEGIN;                                        
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 OPEN FILE(DEBUGI) TITLE('DEBUG00I');                                   
                                                                        
 ON ENDFILE(DEBUGI) BEGIN;                                              
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 READ FILE(DEBUGI) INTO(DEBUG_REC);                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* HOUSEKEEPING_PUTSKIP_FOUND                                       */ 
 /*------------------------------------------------------------------*/ 
 HOUSEKEEPING_PUTSKIP_FOUND:                                            
                                                                        
 DISP_FROM = DEBUG_REC.DEBUG_FROM;                                      
 DISP_TO   = DEBUG_REC.DEBUG_TO;                                        
                                                                        
 PUT SKIP LIST(PUTSKIP_FROM !! DISP_FROM);                              
 PUT SKIP LIST(PUTSKIP_TO !! DISP_TO);                                  
                                                                        
 CLOSE FILE(DEBUGI);                                                    
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Open filer                                                       */ 
 /*------------------------------------------------------------------*/ 
 OPEN FILE(BH389I) TITLE('BH38900I');                                   
 OPEN FILE(BH389O) TITLE('BH38900O');                                   
                                                                        
 END HOUSEKEEPING;                                                      
                                                                        
 /********************************************************************/ 
 /* Læs næste CN570I record                                          */ 
 /********************************************************************/ 
 READ_BH389I: PROC;                                                     
                                                                        
 PUTSKIP_THIS = NEJ;                                                    
                                                                        
 READ FILE(BH389I) INTO(BH389_AREA);                                    
                                                                        
 IF FLERE_BH389I THEN DO;                                               
   BH389I_READ = BH389I_READ + 1;                                       
                                                                        
   SELECT(BH389.EENHEDLBNR);                                            
     WHEN(0)   BH389I_READ_0 = BH389I_READ_0 + 1;                       
     WHEN(1)   BH389I_READ_1 = BH389I_READ_1 + 1;                       
     WHEN(2)   BH389I_READ_2 = BH389I_READ_2 + 1;                       
     OTHERWISE ;                                                        
   END;                                                                 
                                                                        
   IF (DEBUG_REC.DEBUG_TO    > 0                   )                    
    & (  BH389I_READ         > DEBUG_REC.DEBUG_FROM                     
       ! BH389I_READ         = DEBUG_REC.DEBUG_FROM)                    
    & (  BH389I_READ         < DEBUG_REC.DEBUG_TO                       
       ! BH389I_READ         = DEBUG_REC.DEBUG_TO  ) THEN DO;           
     PUTSKIP_THIS = JA;                                                 
   END;                                                                 
 END;                                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   DISP_REC    = BH389I_READ;                                           
   DISP_ENHED  = BH389.EENHEDLBNR;                                      
                                                                        
   PUT SKIP LIST(DEBUG_TXT   !! PROCNAME                                
                !! REC_TXT   !! DISP_REC                                
                !! ENHED_TXT !! DISP_ENHED);                            
 END;                                                                   
                                                                        
 END READ_BH389I;                                                       
                                                                        
 /********************************************************************/ 
 /* Skriv BH389O record                                              */ 
 /********************************************************************/ 
 WRITE_BH389O: PROC;                                                    
                                                                        
 WRITE FILE(BH389O) FROM(BH389_AREA);                                   
                                                                        
 SELECT(BH389.EENHEDLBNR);                                              
   WHEN(0)   BH389O_WRITE_0 = BH389O_WRITE_0 + 1;                       
   WHEN(1)   BH389O_WRITE_1 = BH389O_WRITE_1 + 1;                       
   WHEN(2)   BH389O_WRITE_2 = BH389O_WRITE_2 + 1;                       
   OTHERWISE ;                                                          
 END;                                                                   
                                                                        
 BH389O_WRITE = BH389O_WRITE + 1;                                       
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   DISP_REC    = BH389O_WRITE;                                          
   DISP_ENHED  = BH389.EENHEDLBNR;                                      
                                                                        
   PUT SKIP LIST(DEBUG_TXT   !! PROCNAME                                
                !! REC_TXT   !! DISP_REC                                
                !! ENHED_TXT !! DISP_ENHED);                            
 END;                                                                   
                                                                        
 END WRITE_BH389O;                                                      
                                                                        
 /********************************************************************/ 
 /* Put skip tællere                                                 */ 
 /********************************************************************/ 
 AFSLUT: PROC;                                                          
                                                                        
 DCL DISP_CNT_1 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_2 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_3 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_4 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_5 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_6 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_7 PIC'Z.ZZZ.ZZ9';                                         
 DCL DISP_CNT_8 PIC'Z.ZZZ.ZZ9';                                         
                                                                        
 CLOSE FILE(BH389I),                                                    
       FILE(BH389O);                                                    
                                                                        
 PUT SKIP LIST('');                                                     
 PUT SKIP LIST('Resultat for BH38900:');                                
                                                                        
 DISP_CNT_1 = BH389I_READ;                                              
 DISP_CNT_2 = BH389I_READ_0;                                            
 DISP_CNT_3 = BH389I_READ_1;                                            
 DISP_CNT_4 = BH389I_READ_2;                                            
 DISP_CNT_5 = BH389O_WRITE;                                             
 DISP_CNT_6 = BH389O_WRITE_0;                                           
 DISP_CNT_7 = BH389O_WRITE_1;                                           
 DISP_CNT_8 = BH389O_WRITE_2;                                           
                                                                        
 PUT SKIP LIST('   Read  BH389I - Ialt     :' !! DISP_CNT_1);           
 PUT SKIP LIST('         Enhedsløbenummer 0:' !! DISP_CNT_2);           
 PUT SKIP LIST('         Enhedsløbenummer 1:' !! DISP_CNT_3);           
 PUT SKIP LIST('         Enhedsløbenummer 2:' !! DISP_CNT_4);           
 PUT SKIP LIST('   Write BH389O - Ialt     :' !! DISP_CNT_5);           
 PUT SKIP LIST('         Enhedsløbenummer 0:' !! DISP_CNT_6);           
 PUT SKIP LIST('         Enhedsløbenummer 1:' !! DISP_CNT_7);           
 PUT SKIP LIST('         Enhedsløbenummer 2:' !! DISP_CNT_8);           
                                                                        
 PUT SKIP LIST('');                                                     
 PUT SKIP LIST(PROGRAM_TXT !! SLUT_TXT);                                
                                                                        
 END AFSLUT;                                                            
                                                                        
 END BH38900;                                                           
