 BH94600: /* BEREGNINGSBÅND FLETTES MED ESR_DATA          */            
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2002');       
 /**********************************************************************
 *                                                                     *
 * FORMÅL :  AT FLETTE BEREGNINGSBÅND MED ESR-DATA                     *
 *                                                                     *
 * INDDATA:  B.H90X01D - BEREGNINGSBÅND ENDELIGT.                      *
 *           X = 0 FOR EVS SLUT                                        *
 *           X = 1 FOR EVS FORSKUD                                     *
 *                                                                     *
 * OUTPUT : B.H94600D  BEREGNINGSBÅND ALLE EJENDOMME                   *
 *          BH94601Y   KØRSELSKONTROLLISTE                             *
 *                                                                     *
 * PROGRAMMØR: HANS ERIK KJELDSEN  DECEMBER 2001                       *
 * RETTET:                                                             *
 * FORSKUD06   SUSANNE KROGH VILLUMSEN            02-06-2005           *
 *             TILRETTET FORSKUD 2006                                  *
 *                                                                     *
 * SLUT05      SUSANNE KROGH VILLUMSEN            08-11-2005           *
 *             TILRETTET SLUT 2005                                     *
 *                                                                     *
 * FORSKUD07   BO SCHMIDT                         14-05-2006           *
 *             TILRETTET FORSKUD 2007                                  *
 *                                                                     *
 * SLUT06      BO SCHMIDT                         04-10-2006           *
 *             TILRETTET SLUT 2006                                     *
 *                                                                     *
 * FORSKUD08   BO SCHMIDT                         23-05-2007           *
 *             TILRETTET FORSKUD 2008                                  *
 *                                                                     *
 * SLUT07      BO SCHMIDT                         19-11-2007           *
 *             TILRETTET SLUT 2007                                     *
 *                                                                     *
 * FORSKUD09   BO SCHMIDT                         23-06-2008           *
 *             TILRETTET FORSKUD 2009                                  *
 *                                                                     *
 * SLUT08      BO SCHMIDT                         19-11-2008           *
 *             TILRETTET SLUT 2008                                     *
 *                                                                     *
 * FORSKUD10   GERD HOLM-PEDERSEN                 20-04-2009           *
 *             TILRETTET FORSKUD 2010                                  *
 *                                                                     *
 * SLUT09      GERD HOLM-PEDERSEN                 13-10-2009           *
 *             TILRETTET SLUT 2009                                     *
 *                                                                     *
 * FORSKUD11   GERD HOLM-PEDERSEN                 03-06-2010           *
 *             TILRETTET FORSKUD 2011                                  *
 *                                                                     *
 * SLUT10      GERD HOLM-PEDERSEN                 08-10-2010           *
 *             TILRETTET SLUT 2010                                     *
 *                                                                     *
 * FORSKUD12   GERD HOLM-PEDERSEN                 03-06-2011           *
 *             TILRETTET FORSKUD 2012                                  *
 *                                                                     *
 * SLUT11      GERD HOLM-PEDERSEN                 22-09-2011           *
 *             TILRETTET SLUT 2011                                     *
 *                                                                     *
 * FORSKUD13   LARS KAAE HARRITSHØJ               30-05-2012           *
 *             TILRETTET FORSKUD 2013                                  *
 *             BH90000N => BH90100N                                    *
 *             DER ER FORETAGET ÆNDRING I BH90100N                     *
 *                                                                     *
 * SLUT12      LARS KAAE HARRITSHØJ               OKT 2012             *
 *             TILRETTET SLUT 2012                                     *
 *             KRAV 18-01 NYT FELT CEANV I STRUKTUR BH32202N           *
 *             BENYTTELSESKODE 25                                      *
 * FORSKUD14   LARS KAAE HARRITSHØJ               Maj 2013             *
 *             TILRETTET FORSKUD 2014                                  *
 *             BH90000N => BH90100N                                    *
 *                                                                     *
 * SLUT13      JØRGEN SAUGMANN                    OKT 2013             *
 *             TILRETTET SLUT 2013                                     *
 *                                                                     *
 * FORSKUD15   JØRGEN SAUGMANN                    JUN 2014             *
 *             BH90000N -> BH90100N                                    *
 *                                                                     *
 * SLUT14      JØRGEN SAUGMANN                    OKT 2014             *
 *             TILRETTET SLUT 2013                                     *
 *                                                                     *
 * FORSKUD16   JØRGEN SAUGMANN                    JUN 2015             *
 *             BH90000N -> BH90100N                                    *
 *                                                                     *
 * SLUT15      Z6JNK                              OKT 2015             *
 *             TILRETTET SLUT 2015        (Søg efter RUL)              *
 * FORSKUD17   ESBEN BRODERSEN                    JUN 2016             *
 *             BH90000N -> BH90100N                                    *
 * SLUT16      Lene Maj Jensen                    SEP 2016             *
 *             BH90100N -> BH90000N   søg efter 'RUL'                  *
 * FORSKUD18   ESBEN BRODERSEN                    JUN 2017             *
 *             BH90000N -> BH90100N                                    *
 * SLUT17      Esben Brodersen                    OKT 2017             *
 *             BH90100N -> BH90000N   søg efter 'RUL'                  *
 * FORSKUD19   Esben Brodersen                    MAJ 2018             *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 * SLUT18      Esben Brodersen                    SEP 2018             *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 *FORSKUD20 #02:Edward Laryea, EDL                     22-03-2019      *
 *              Nyt felt STRAKS_EVS i BH32202N                         *
 *             Esben Brodersen                    MAR 2019             *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 *             Esben Brodersen                    OKT 2019             *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 * FORSKUD21   Per Brunk                          Marts 2020           *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 *              Nye felter i BH32202N VURMARK og VUR2001               *
 * SLUT20      Per Brunk PBR                      OKT 2020             *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 * FORSKUD22   Per Brunk                          Januar 2021 MP       *
 *             BH90000N -> BH90100N   søg efter 'RUL'                  *
 *              Nye felter i BH32202N VURMARK og VUR2001               *
 * Slut 2020:  Elena Flygenring EFL               marts 2021           *
 *             - struktur BH90100N erstattes med BH90121N              *
 * FORSKUD22   Per Brunk                          August 2021 KS46     *
 *             Ny struktur BH30100N                                    *
 * SLUT2021    Per Brunk PBR                      September 2021       *
 *             BH90122N -> BH90021N   søg efter 'RUL'                  *
 *             Per Brunk                          November 2021        *
 *             D1081: manglende vejkode                                *
 * FORSKUD23   Per Brunk Årsrul                  08.06.2022            *
 * SLUT 2022      Z6PBR compileret                   Oktober 2022      *
 * SLUT2021    Per Brunk PBR                      September 2021       *
 *             BH90124N -> BH90025N   søg efter 'RUL'                  *
 *             Per Brunk                          januar 2024          *
 **********************************************************************/
         DFT RANGE (*) STATIC;                                          
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH9000I FILE RECORD INPUT;     /* BEREGNINGSBÅND            */ 
 DCL     BH3240I FILE RECORD INPUT;     /* ESR-DATA                  */ 
 DCL     BH9460O FILE RECORD OUTPUT;    /* BEREGNINGSBÅND            */ 
 /********************************************************************* 
 *       DECLARE AF BEREGNINGSBÅND                                    * 
 *********************************************************************/ 
 DCL     BH900        CHAR      (7000) VAR;                             
 DCL     1 BH900I        BASED     (ADDR(BH900)),                       
           2 LÆNGDE      BIN FIXED (15),                                
                                                                        
 /*RUL*/   %INCLUDE BH90025N;;  /* DENNE STRUKTUR NÅR KØRSEL=SLUT  */   
 /*RUL   %INCLUDE BH90124N;;  *//* DENNE STRUKTUR NÅR KØRSEL=FORSKUD */ 
                                                                        
 DCL     1 BH999I        BASED     (ADDR(BH900)),                       
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;;                                          
                                                                        
 DCL     1 BH324I,                                                      
           %INCLUDE BH32410N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    LIN_DEB          CHAR      (133);                               
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH94600L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (                
           'FLET BEREGNINGSBÅND MED ESR-DATA            '),             
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     INDEX           BIN FIXED (31);                                
 DCL     EOF_BH900       BIT (1) INIT ('0'B);                           
 DCL     EOF_BH324       BIT (1) INIT ('0'B);                           
 DCL     FEJLTEKST       CHAR (20)        INIT(' ');                    
 DCL     ABENDMEDD       CHAR (56)        INIT(' ');                    
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     ANTAL_BH900     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_BH324     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_SKRIV     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_OVERFØRTE BIN FIXED (31) INIT(0);                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
                                                                        
         ON ENDFILE (BH9000I) EOF_BH900 = '1'B;                         
         ON ENDFILE (BH3240I) EOF_BH324 = '1'B;                         
                                                                        
         OPEN FILE (BH9000I) TITLE ('BH90001I');                        
         OPEN FILE (BH3240I) TITLE ('BH32411I');                        
         OPEN FILE (BH9460O) TITLE ('BH94600O');                        
                                                                        
         CALL ZZ20390 ('*OPEN','BH94601Y');                             
         DATO1 = '';                                                    
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         DETLIN1 = '';                                                  
         BH324I = '';                                                   
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         READ FILE(BH9000I) INTO (BH900);                               
         READ FILE(BH3240I) INTO (BH324I);                              
         DO WHILE (^EOF_BH900);                                         
            DO WHILE (BH900I.SORT_KOMNR > BH324I.EKOMNR & ^EOF_BH324);  
                  READ FILE(BH3240I) INTO (BH324I);                     
                  ANTAL_BH324 = ANTAL_BH324 +1;                         
            END;/* DO WHILE*/                                           
            DO WHILE (BH900I.SORT_KOMNR = BH324I.EKOMNR                 
                      & BH900I.SORT_EJDNR > BH324I.EEJDNR               
                      & ^EOF_BH324);                                    
               READ FILE(BH3240I) INTO (BH324I);                        
               ANTAL_BH324 = ANTAL_BH324 +1;                            
            END;/* DO WHILE*/                                           
            DO WHILE (BH900I.SORT_KOMNR = BH324I.EKOMNR                 
                      & BH900I.SORT_EJDNR = BH324I.EEJDNR               
                      & BH900I.SORT_ENHEDLBNR > BH324I.EENHEDLBNR       
                      & ^EOF_BH324);                                    
               READ FILE(BH3240I) INTO (BH324I);                        
               ANTAL_BH324 = ANTAL_BH324 +1;                            
            END;/* DO WHILE*/                                           
            IF BH900I.SORT_KOMNR = BH324I.EKOMNR                        
             & BH900I.SORT_EJDNR = BH324I.EEJDNR                        
             & BH900I.SORT_ENHEDLBNR = BH324I.EENHEDLBNR                
             & BH900I.ESR.SORTCPR = 0                                   
            THEN                                                        
               CALL OVERFØR_DATA;                                       
            WRITE FILE (BH9460O) FROM (BH900);                          
            ANTAL_SKRIV = ANTAL_SKRIV +1;                               
            READ FILE(BH9000I) INTO (BH900);                            
                                                                        
 /*         PUT SKIP LIST('BH900I.SORT_EJDNR =',BH900I.SORT_EJDNR);     
            PUT SKIP LIST('BH900I.SORT_ENHEDLBNR=',                     
                                                BH900I.SORT_ENHEDLBNR); 
            IF BH900I.SORT_EJDNR < 9999999                              
            THEN                                                        
              DO;                                                       
                PUT SKIP LIST('BH900I.ESR.SORTCPR =',                   
                                             BH900I.ESR.SORTCPR);       
              END; */                                                   
                                                                        
            ANTAL_BH900 = ANTAL_BH900 +1;                               
         END;  /*DO WHILE*/                                             
                                                                        
         CALL LUK_PROG;                                                 
 /********************************************************************* 
 *                       OVERFØR DATA                                 * 
 *********************************************************************/ 
 OVERFØR_DATA: PROC;                                                    
                                                                        
                                                                        
 /*      PUT SKIP LIST('BH324I.EKOMNR     =', BH324I.EKOMNR);           
         PUT SKIP LIST('BH324I.EEJDNR     =', BH324I.EEJDNR);           
         PUT SKIP LIST('BH324I.EENHEDLBNR =', BH324I.EENHEDLBNR);*/     
                                                                        
         BH900I.ESR.STAMOPL    = BH324I.STAMOPL, BY NAME;               
         BH900I.ESR.EKOMNR     = BH324I.EKOMNR;                         
         BH900I.ESR.EEJDNR     = BH324I.EEJDNR;                         
         BH900I.ESR.EENHEDLBNR = BH324I.EENHEDLBNR;                     
         /* D1081 */                                                    
         BH900I.ESR.STAMOPL    = BH324I.STAMOPL, BY NAME;               
                                                                        
         ANTAL_OVERFØRTE   = ANTAL_OVERFØRTE + 1;                       
                                                                        
 END OVERFØR_DATA;                                                      
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_BH900;                                   
         DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H90X01D........:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_BH324;                                   
         DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H32411D........:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_SKRIV;                                   
         DETLIN1.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H94600D......:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_OVERFØRTE;                               
         DETLIN1.TEKST = 'ANTAL OVERFØRTE INDIVIDER FRA B.H32411D...:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
  END TOTALLISTE;                                                       
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 LUK_PROG: PROC;                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH9000I),                                          
               FILE (BH3240I),                                          
               FILE (BH9460O);                                          
 END LUK_PROG;                                                          
 END  BH94600;                                                          
