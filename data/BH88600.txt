 BH88600: /* Individe 810  EVS SLUT */                                  
       PROC OPTIONS (MAIN) REORDER;                                     
                                                                        
                                                                        
       %include zm2EnvB;                                                
                                                                        
 DCL   COPYRIGHT    CHAR    (30) STATIC INIT ('Copyright KMD A/S 2020');
                                                                        
                                                                        
                                                                        
 /*---------------------------------------------------------------------
       NAVN         Individe 810  EVS SLUT                              
                                                                        
       INDDATA                                                          
                                                                        
       UDDATA                                                           
                                                                        
                                                                        
       PROGRAMMØR   Ole Nielsen                      Oktober 2020       
                                                                        
 ---------------------------------------------------------------------*/
       DEFAULT RANGE (*) STATIC UNALIGNED CONN;                         
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
                                                                        
 /*---------------------------------------------------------------------
       DECLARE AF BUILTIN                                               
 ---------------------------------------------------------------------*/
 DCL   (PLIDUMP,SUBSTR,ADDR)   BUILTIN;                                 
                                                                        
                                                                        
 /*-------------------------------------------------------------------  
       Includes ZM2BATA                                                 
 -------------------------------------------------------------------*/  
       %include ZM2SRXA;           // Standard-rutiner og DUALPLI opsæt 
                                                                        
       zm2Appl    = 'BFSK';                                             
       zm2Program = 'BH88600';                                          
                                                                        
 /*---------------------------------------------------------------------
       FILE-DECLARE                                                     
 ---------------------------------------------------------------------*/
 DCL   BH886I FILE RECORD INPUT;   /* Parameter Forskud-Slut    */      
 DCL   BHX61I FILE RECORD INPUT;   /* EVS DATA 810 HOS DXC */           
                                                                        
 /*---------------------------------------------------------------------
       DECLARE AF UDTRÆK (SLUT PÅ CSC)                                  
 ---------------------------------------------------------------------*/
                                                                        
 DCL   1 BHX61,                                                         
         %INCLUDE BH96110N;;                                            
                                                                        
       BHX61 = '';                                                      
                                                                        
       EXEC SQL INCLUDE BQ73000T;   /* Individ 810            */        
                                                                        
       DCLBQ73000T = '';                                                
 /*---------------------------------------------------------------------
       DECLARE AF DIVERSE FELTER                                        
 ---------------------------------------------------------------------*/
 DCL   JA                      BIT       (1)      INIT ('1'B);          
 DCL   NEJ                     BIT       (1)      INIT ('0'B);          
 DCL   BHX61I_ANTAL            FIXED DEC (11)     INIT (0);             
 DCL   SletAntal               FIXED DEC (11)     INIT (0);             
 DCL   WS_FORSKUD_SLUT_TXT     CHAR      (16)     INIT (' ');           
 DCL   WS_TIL_EVS_AAR          BIN FIXED (15)     INIT (0);             
 DCL   WS_GLSLUTÅR             BIN FIXED (15)     INIT (0);             
 DCL   WS_TIL_EVS_AAR_GL       BIN FIXED (15)     INIT (0);             
 DCL   WS_SLUT_AFVIKLINGS_ID   CHAR      (16)     INIT (' ');           
 DCL   WS_PROGRAMNAVN          CHAR      (8)      INIT ('BH88600');     
 DCL   EVS_AAR_PIC             PIC       '9999';                        
 DCL   UDDATO                  CHAR      (10);                          
 DCL   DATO1                   CHAR      (26);                          
 DCL   1 DATO2  DEF  DATO1,                                             
         2 ÅR                  CHAR      (04),                          
         2 F1                  CHAR      (01),                          
         2 MD                  CHAR      (02),                          
         2 F2                  CHAR      (01),                          
         2 DG                  CHAR      (02);                          
 /*---------------------------------------------------------------------
       PARM FIL                                                         
 ---------------------------------------------------------------------*/
 DCL   EOF_PARMFIL             BIT       (1)      INIT ('0'B);          
 DCL   EOF_BHX61I              BIT       (1)      INIT ('0'B);          
 DCL   BH51300P                FILE RECORD INPUT;                       
 DCL   1 BH865_PARM,                                                    
         %INCLUDE BH54900N;                                             
                                                                        
 DCL   1 BH65300M      ALIGNED,                                         
         %INCLUDE BH65300M;                                             
                                                                        
 /*---------------------------------------------------------------------
       DB2 definitioner                                                 
 ---------------------------------------------------------------------*/
                                                                        
       %include ZM2DB2B;              // Standard til Zm2-programmer    
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH88600'),                                         
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'ESR INPUT TIL BEREGNING '),                                 
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
                                                                        
         % INCLUDE ZZ20301M;                                            
 /*---------------------------------------------------------------------
       HOUSE-KEEPING                                                    
 ---------------------------------------------------------------------*/
                                                                        
       ON ERROR                                                         
          BEGIN;                                                        
            ON ERROR SYSTEM;                                            
            PUT SKIP LIST ('PROGRAMFEJL');                              
            CALL PLIDUMP  ('SHOB');                                     
          END;                                                          
                                                                        
       ON ENDFILE (BH886I)                                              
       BEGIN;                                                           
          EOF_PARMFIL = JA;                                             
       END;                                                             
                                                                        
       ON ENDFILE (BHX61I)                                              
       BEGIN;                                                           
          EOF_BHX61I = JA;                                              
       END;                                                             
                                                                        
                                                                        
       OPEN FILE (BH886I) TITLE ('BH88600P'); //Parm                    
       OPEN FILE (BHX61I) TITLE ('BHX6120I'); //Fil individ 810         
       CALL ZZ20390('*OPEN','BH88601Y');       /* ÅBEN KONTROLLISTE */  
                                                                        
                                                                        
       READ FILE (BH886I) INTO (BH865_PARM);                            
                                                                        
                                                                        
                                                                        
       IF EOF_PARMFIL = NEJ                                             
         THEN DO;                                                       
            SELECT (BH865_PARM.FS_MRK);                                 
               WHEN ('F')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH865_PARM.FORSKUD_TXT;      
                     WS_TIL_EVS_AAR = BH65300M.FORSKÅR;                 
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH865_PARM.SLUT_TXT;       
                     OVERLIN1.TEKST2 = 'EVS Individ 810 FORSKUD';       
                  END;                                                  
               WHEN ('S')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH865_PARM.SLUT_TXT;         
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH865_PARM.SLUT_TXT;       
                     OVERLIN1.TEKST2 = 'EVS Individ 810 SLUT';          
                  END;                                                  
               WHEN ('U')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH865_PARM.SUPPL_TXT;        
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH865_PARM.SLUT_TXT;       
                     OVERLIN1.TEKST2 = 'EVS Individ 810 SUPPLEMENT';    
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                    put skip data(BH865_PARM);                          
                    CALL PROGRAM_FEJL('100','PARAMETERFEJL');           
                  END;                                                  
            END; /* select */                                           
         END;                                                           
                                                                        
          put skip list('BH865_PARM',BH865_PARM);  // TODO              
           put skip list('WS_FORSKUD_SLUT_TXT',WS_FORSKUD_SLUT_TXT);    
            put skip list('WS_TIL_EVS_AAR',WS_TIL_EVS_AAR);             
                                                                        
       CALL ZS38100(DATO1);                                             
       UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;         
       //OVERLIN1.ÅR   = GLSLUTÅR - 1;                                  
       OVERLIN1.ÅR = WS_TIL_EVS_AAR;                                    
       OVERLIN1.DATO = UDDATO;                                          
       SIDELIN.CCA   = ZZIK1;                                           
       OVERLIN1.CCA  = ZZWS2;                                           
       OVERLIN2.CCA  = ZZWS3;                                           
                                                                        
                                                                        
 /*---------------------------------------------------------------------
       Hovedprogram                                                     
 ---------------------------------------------------------------------*/
                                                                        
                                                                        
                                                                        
       READ FILE (BHX61I) INTO (BHX61);                                 
                                                                        
       EVS_AAR_PIC = BHX61.DINDKAAR;                                    
                                                                        
       //--------------------------------------------------------       
       //Vi tjekker om det er en omkørsel.                              
       //Vi bruger datoen BHX61.DINDKAAR fra først læste linie i BHX61I 
                                                                        
       //Antal rækker som der vil blive slettet ved omkørsel            
       SletAntal =                                                      
           FindSekvensNummer(EVS_AAR_PIC,WS_FORSKUD_SLUT_TXT);          
                                                                        
                                                                        
       // Ved omkørsel slettes data                                     
       Call SletData(EVS_AAR_PIC,WS_FORSKUD_SLUT_TXT);                  
                                                                        
       //Evt data er blevet slettet og vi kan nu fortsætte.             
       //--------------------------------------------------------       
                                                                        
                                                                        
       DCLBQ73000T.AFVIKLINGS_ID = WS_FORSKUD_SLUT_TXT;                 
                                                                        
       Do While (^EOF_BHX61I);                                          
          BHX61I_ANTAL = BHX61I_ANTAL + 1;                              
          DCLBQ73000T.INDIVID_SEKVENS_NR  = BHX61I_ANTAL;               
          EVS_AAR_PIC = BHX61.DINDKAAR;                                 
          DCLBQ73000T.TIL_EVS_AAR = EVS_AAR_PIC;                        
          DCLBQ73000T = BHX61, by name;                                 
          Call Insert_BQ730;                                            
          READ FILE (BHX61I) INTO (BHX61);                              
       End; //do while                                                  
                                                                        
       Call KONTROLLISTE;                                               
       Call LukFiler;                                                   
                                                                        
                                                                        
 // Insert række på BQ730 individ 810                                   
 Insert_BQ730: proc;                                                    
                                                                        
       EXEC SQL                                                         
            INSERT INTO BQ73000T                                        
              VALUES ( :DCLBQ73000T.TIL_EVS_AAR,                        
                       :DCLBQ73000T.AFVIKLINGS_ID,                      
                       :DCLBQ73000T.INDIVID_SEKVENS_NR,                 
                       :DCLBQ73000T.INDVNR,                             
                       :DCLBQ73000T.INDVLGD,                            
                       :DCLBQ73000T.FELTLGD,                            
                       :DCLBQ73000T.HEADER,                             
                       :DCLBQ73000T.DINDKAAR,                           
                       :DCLBQ73000T.DCPRN,                              
                       :DCLBQ73000T.EKOMNR,                             
                       :DCLBQ73000T.EEJDNR,                             
                       :DCLBQ73000T.EENHEDLBNR,                         
                       :DCLBQ73000T.ELBNR,                              
                       :DCLBQ73000T.CSLET,                              
                       :DCLBQ73000T.VURDOPRIKOD,                        
                       :DCLBQ73000T.DAFSTAAFOR,                         
                       :DCLBQ73000T.EJDBELIG,                           
                       :DCLBQ73000T.EJDBLIGEKMNR,                       
                       :DCLBQ73000T.VEJKOD,                             
                       :DCLBQ73000T.HUSNR,                              
                       :DCLBQ73000T.HUSBOSTAADR,                        
                       :DCLBQ73000T.ETAGENR,                            
                       :DCLBQ73000T.SIDEDOERNR,                         
                       :DCLBQ73000T.CLANDEKODE,                         
                       :DCLBQ73000T.CEJBEVS,                            
                       :DCLBQ73000T.MANPENSSTI_I,                       
                       :DCLBQ73000T.MANPENSSTI,                         
                       :DCLBQ73000T.BBERGRL_015ANV_I,                   
                       :DCLBQ73000T.BBERGRL_015ANV,                     
                       :DCLBQ73000T.BBERGRL_02ANV_I,                    
                       :DCLBQ73000T.BBERGRL_02ANV,                      
                       :DCLBQ73000T.BBERGRL_AKT_I,                      
                       :DCLBQ73000T.BBERGRL_AKT,                        
                       :DCLBQ73000T.BBER2FAM_015ANV_I,                  
                       :DCLBQ73000T.BBER2FAM_015ANV,                    
                       :DCLBQ73000T.BBER2FAM_02ANV_I,                   
                       :DCLBQ73000T.BBER2FAM_02ANV,                     
                       :DCLBQ73000T.BBER2FAM_AKT_I,                     
                       :DCLBQ73000T.BBER2FAM_AKT,                       
                       :DCLBQ73000T.CEJDTYPE,                           
                       :DCLBQ73000T.CÆGTOPR,                            
                       :DCLBQ73000T.DOVTG,                              
                       :DCLBQ73000T.DAFSTAA,                            
                       :DCLBQ73000T.GPCTEJER_I,                         
                       :DCLBQ73000T.GPCTEJER,                           
                       :DCLBQ73000T.FDAGUDLE,                           
                       :DCLBQ73000T.GPCTUDLE,                           
                       :DCLBQ73000T.FUDL100,                            
                       :DCLBQ73000T.GPCTERHV,                           
                       :DCLBQ73000T.FDAGERHV,                           
                       :DCLBQ73000T.FERHVFLER,                          
                       :DCLBQ73000T.CENKE,                              
                       :DCLBQ73000T.CPTYPSLUT,                          
                       :DCLBQ73000T.CPTYPGL,                            
                       :DCLBQ73000T.FELT759,                            
                       :DCLBQ73000T.FELT760,                            
                       :DCLBQ73000T.BEVSGL_I,                           
                       :DCLBQ73000T.BEVSGL,                             
                       :DCLBQ73000T.BEJDVPUSKTP_I,                      
                       :DCLBQ73000T.BEJDVPUSKTP,                        
                       :DCLBQ73000T.BPENSNEDGL_I,                       
                       :DCLBQ73000T.BPENSNEDGL,                         
                       :DCLBQ73000T.DINDFLYT,                           
                       :DCLBQ73000T.DFRAFLYT,                           
                       :DCLBQ73000T.FUBEBOELIG,                         
                       :DCLBQ73000T.BEJDVÆPUSKP_I,                      
                       :DCLBQ73000T.BEJDVÆPUSKP,                        
                       :DCLBQ73000T.BFASTSTIGBGR_I,                     
                       :DCLBQ73000T.BFASTSTIGBGR,                       
                       :DCLBQ73000T.CINDFLYT,                           
                       :DCLBQ73000T.CBERGRGLOP,                         
                       :DCLBQ73000T.BMANEVSSTI_I,                       
                       :DCLBQ73000T.BMANEVSSTI,                         
                       :DCLBQ73000T.BEVSMAN_I,                          
                       :DCLBQ73000T.BEVSMAN,                            
                       :DCLBQ73000T.BMANNEDSL_I,                        
                       :DCLBQ73000T.BMANNEDSL,                          
                       :DCLBQ73000T.CEKSEMP,                            
                       :DCLBQ73000T.MANNEDSLSTI_I,                      
                       :DCLBQ73000T.MANNEDSLSTI,                        
                       :DCLBQ73000T.BUDLSKC_I,                          
                       :DCLBQ73000T.BUDLSKC,                            
                       :DCLBQ73000T.BEJBOL1_AKT_I,                      
                       :DCLBQ73000T.BEJBOL1_AKT,                        
                       :DCLBQ73000T.BEJBOL2_AKT_I,                      
                       :DCLBQ73000T.BEJBOL2_AKT,                        
                       :DCLBQ73000T.CVURDOPR,                           
                       :DCLBQ73000T.BEJBOL1_02ANV_I,                    
                       :DCLBQ73000T.BEJBOL1_02ANV,                      
                       :DCLBQ73000T.BEJBOL2_02ANV_I,                    
                       :DCLBQ73000T.BEJBOL2_02ANV,                      
                       :DCLBQ73000T.CBENYT,                             
                       :DCLBQ73000T.COMBYG,                             
                       :DCLBQ73000T.CBERGRL,                            
                       :DCLBQ73000T.GPCTEJERFOR,                        
                       :DCLBQ73000T.CERHV,                              
                       :DCLBQ73000T.BLANDVÆR_AKT_I,                     
                       :DCLBQ73000T.BLANDVÆR_AKT,                       
                       :DCLBQ73000T.BLANDVÆR_015ANV_I,                  
                       :DCLBQ73000T.BLANDVÆR_015ANV,                    
                       :DCLBQ73000T.BLANDVÆR_02ANV_I,                   
                       :DCLBQ73000T.BLANDVÆR_02ANV,                     
                       :DCLBQ73000T.BBERGRL_I,                          
                       :DCLBQ73000T.BBERGRL,                            
                       :DCLBQ73000T.CBERGROP,                           
                       :DCLBQ73000T.BFORSKAT_I,                         
                       :DCLBQ73000T.BFORSKAT,                           
                       :DCLBQ73000T.CFORSKAT,                           
                       :DCLBQ73000T.CUDLMAN,                            
                       :DCLBQ73000T.BBER2FAM_I,                         
                       :DCLBQ73000T.BBER2FAM,                           
                       :DCLBQ73000T.FEJLNR,                             
                       :DCLBQ73000T.BEJBOL1_015ANV_I,                   
                       :DCLBQ73000T.BEJBOL1_015ANV,                     
                       :DCLBQ73000T.BEJBOL2_015ANV_I,                   
                       :DCLBQ73000T.BEJBOL2_015ANV,                     
                       :DCLBQ73000T.DFRAGLD,                            
                       :DCLBQ73000T.CNEDSL1,                            
                       :DCLBQ73000T.FLEJL,                              
                       :DCLBQ73000T.DOVTGFOR,                           
                       :DCLBQ73000T.CDATAFRA,                           
                       :DCLBQ73000T.CUDLHEL,                            
                       :DCLBQ73000T.CUDLDEL);                           
                                                                        
       Select(sqlca.sqlcode);                                           
          When(0);  //Det gik godt                                      
          Otherwise                                                     
             Call SQL_FEJL('001','Fejl i insert');                      
       End; //select                                                    
                                                                        
 End Insert_BQ730;                                                      
 //Find højeste sekvensnummer givet år og afviklings id.                
 FindSekvensNummer: Proc(Aar,AfviklingsId) Returns(FIXED DEC (15));     
 dcl Aar          BIN FIXED (15);                                       
 dcl AfviklingsId Char(16);                                             
 dcl MaxNummer    FIXED DEC (15)     INIT (0);                          
                                                                        
                                                                        
       EXEC SQL                                                         
          Select COALESCE(MAX(INDIVID_SEKVENS_NR),0)                    
          Into :MaxNummer                                               
          From BQ73000T                                                 
          Where TIL_EVS_AAR = :Aar                                      
          And AFVIKLINGS_ID = :AfviklingsId                             
       ;                                                                
                                                                        
       Select(sqlca.sqlcode);                                           
          When(0);    //Det gik godt                                    
          When(100);  //Det er ok at den ikke findes                    
          Otherwise                                                     
             Call SQL_FEJL('002','Fejl i FindSekvensNummer');           
       End; //select                                                    
                                                                        
       Return(MaxNummer);                                               
                                                                        
                                                                        
 End FindSekvensNummer;                                                 
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL SLETTEDE RÆKKER PÅ BQ73000T';                           
         DETLIN1.ANTAL = SletAntal;                                     
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL SKREVNE RÆKKER PÅ BQ73000T';                            
         DETLIN1.ANTAL = BHX61I_ANTAL;                                  
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* Ryd db2-tabeller før start                                 RUTINE */
 /*********************************************************************/
 SletData: Proc(Aar,AfviklingsId);                                      
 dcl Aar          BIN FIXED (15);                                       
 dcl AfviklingsId Char(16);                                             
                                                                        
         EXEC SQL                                                       
            Delete from BQ73000T                                        
            Where TIL_EVS_AAR = :Aar                                    
            And   AFVIKLINGS_ID = :AfviklingsId                         
           ;                                                            
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
            END;                                                        
            WHEN(100) DO;                                               
            END;                                                        
            OTHERWISE DO;                                               
               Call SQL_FEJL('003','DELETE BQ73000T');                  
            END;                                                        
         END;                                                           
                                                                        
 END SletData;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: proc (cc, txt);                                              
 dcl cc  char (03);                                                     
 dcl txt char (41);                                                     
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! TXT);            
         PUT SKIP LIST ('*****************************************');   
                                                                        
         Call LukFiler;                                                 
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL zm2Abend(cc,txt);                                         
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC(cc, txt);                                           
 dcl cc  char (03);                                                     
 dcl txt char (41);                                                     
                                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! TXT);            
         PUT SKIP LIST ('*****************************************');   
                                                                        
         Call LukFiler;                                                 
                                                                        
         CALL zm2Abend(cc,txt);                                         
                                                                        
 END PROGRAM_FEJL;                                                      
 LukFiler: PROC;                                                        
                                                                        
         CLOSE FILE (BH886I);                                           
         CLOSE FILE (BHX61I);                                           
         CALL ZZ20300(' ','99');                                        
                                                                        
 End LukFiler;                                                          
                                                                        
 END BH88600;                                                           
