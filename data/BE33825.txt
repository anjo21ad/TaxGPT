 BE33825: PROCEDURE OPTIONS (MAIN);                                     
 /********************************************************************/ 
 /* Projekt:     EVS Forskud / EVS Slut                              */ 
 /* Programnavn: BE33825                                             */ 
 /* Programtype: Batch                                               */ 
 /* Funktion:    Forbrænder program, som valider lejlighedsnummer    */ 
 /*              for 2-familiehuse (ejendomstype 4 eller 5)          */ 
 /*              pr. indkomstår og EVS-kørsel (Slut eller Forskud).  */ 
 /*                                                                  */ 
 /*              De CN337I records, som har fået udfyldt TEKFEJLNR   */ 
 /*              af BE33700, bliver ikke valideret.                  */ 
 /*                                                                  */ 
 /*              Feltet TEKFEJLNR udfyldes med følgende fejlnumre:   */ 
 /*                 3800 - Antal enheder på aktuelt indkomstår,      */ 
 /*                        aktuel EVS-kørsel, kommunenummer og       */ 
 /*                        ejendomsnummer i EVS-enhedsanvendelses-   */ 
 /*                        tabellen er ikke 2 (ejendomstype 4) eller */ 
 /*                        1 (ejendomstype 5)                        */ 
 /*                 3801 - Antal enheder på aktuelt indkomstår +     */ 
 /*                        aktuel EVS-kørsel eller tidligere ind-    */ 
 /*                        komstår + tidligere EVS-kørsel Slut,      */ 
 /*                        kommunenummer og ejendomsnummer i         */ 
 /*                        EVS-enhedsanvendelsestabellen er ikke 2   */ 
 /*                        (ejendomstype 4) eller 1 (ejendomstype 5) */ 
 /*                 3802 - Antal enheder på aktuelt indkomstår,      */ 
 /*                        aktuel EVS-kørsel eller tidligere         */ 
 /*                        EVS-kørsel Forskud, kommunenummer og      */ 
 /*                        ejendomsnummer i                          */ 
 /*                        EVS-enhedsanvendelsestabellen er ikke 2   */ 
 /*                        (ejendomstype 4) eller 1 (ejendomstype 5) */ 
 /*                                                                  */ 
 /* Input:       BE337I   - Indhold lig CN570I                       */ 
 /*                         + felt EENHEDLBNR                        */ 
 /*                         + felt TEKFEJLNR                         */ 
 /*                         + felt ENHEDBELIG                        */ 
 /*                         + felt VEJR_ENHED                        */ 
 /*                         + felt HUSNR_ENHED                       */ 
 /*                         + felt ETAGE_ENHED                       */ 
 /*                         + felt SDØR_ENHED                        */ 
 /*                         (output fra BE33700)                     */ 
 /*                                                                  */ 
 /*              BE338P   - Parameter fil - Find EVS Kørsel type     */ 
 /*                                                                  */ 
 /*              DEBUGI   - Skal programmet debugge (put skip):      */ 
 /*                                                                  */ 
 /*                            Hvis  (filen DEBUGI ikke findes     ) */ 
 /*                            eller (DEBUG_FROM = 0 og DEBU_TO = 0) */ 
 /*                               Ingen debug                        */ 
 /*                            ellers                                */ 
 /*                               Debug alt for de valgte            */ 
 /*                               record numre i BE337I              */ 
 /*                                                                  */ 
 /*              BE80300T - EVS-enhedsanvendelsestabel (DB2)         */ 
 /*                                                                  */ 
 /* I-O          BE80300T - EVS-enhedssporbarhed (DB2)               */ 
 /*                                                                  */ 
 /* Output:      BE338O   - Indhold lig BE337I                       */ 
 /*                         + felt TEKFEJLNR                         */ 
 /*                                                                  */ 
 /*------------------------------------------------------------------*/ 
 /*                                                                  */ 
 /* Oprettet:    Gitte Jensen, GJN                        24-09-2018 */ 
 /*                                                                  */ 
 /* Rettelse #01: Gitte Jensen, GJN                       25-01-2019 */ 
 /*               Nye felter NY_VURDERING & NY_VURDERING i BE33701M  */ 
 /* Rettelse #02: Edward Laryea, EDL                     22-03-2019  */ 
 /*               Nyt felt STRAKS_EVS i BE33700N                     */ 
 /* Forskud2021   Per Brunk                              mar 2020    */ 
 /*               Nye felter i BE33700N VURMARK og VUR2001           */ 
 /* Slut 2020   : Per Brunk PBR Ny struktur BE33700N   Okt 2020      */ 
 /* Slut 2020   : Elena Flygenring EFL                   marts 2021  */ 
 /*             - ny kopi af BE33800                                 */ 
 /*             - struktur BE65300M erstattes med BE55300M           */ 
 /*             - Brug af FILE BE338P og BE338_PARM fjernes          */ 
 /*             - BE338_PARM.FS_MARK fjernes                         */ 
 /*             - BE338_PARM.FORSKUD_TXT, SLUT_TXT, SUPPL_TXT fjernes*/ 
 /*               Erstattes med brug af BE55300M.AFVIKLINGS_ID       */ 
 /*             - BE80300T: nyt felt DATA_ID tilføjet                */ 
 /* Forskud2021   Tina Carstens, TIC                 BF17 15.05.2023 */ 
 /*               Årsrulning                                         */ 
 /* Forskud2025   Jan Jensen, JJJ                    BF17 15.05.2023 */ 
 /*             - BE55600M erstatter BE55300M                        */ 
 /********************************************************************/ 
                                                                        
 DCL   COPYRIGHT       CHAR(30)       INIT ('(C) KMD A/S 2018');        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT KALD AF EKSTERNE MODULER */ 
                                                                        
 /********************************************************************/ 
 /* Builtin functions                                                */ 
 /********************************************************************/ 
 DCL   ADDR            BUILTIN;                                         
 DCL   NULL            BUILTIN;                                         
 DCL   PLIDUMP         BUILTIN;                                         
                                                                        
 /********************************************************************/ 
 /* BE337I - Indhold lig CN570I                                      */ 
 /*          + felt EENHEDLBNR                                       */ 
 /*          + felt TEKFEJLNR                                        */ 
 /*          + felt ENHEDBELIG                                       */ 
 /*          + felt VEJR_ENHED                                       */ 
 /*          + felt HUSNR_ENHED                                      */ 
 /*          + felt ETAGE_ENHED                                      */ 
 /*          + felt SDØR_ENHED                                       */ 
 /*          (output fra BE33700)                                    */ 
 /*                                                                  */ 
 /* BE338O - Indhold lig BE337I                                      */ 
 /*          + felt TEKFEJLNR                                        */ 
 /********************************************************************/ 
 DCL   BE337I          FILE RECORD INPUT;                               
 DCL   BE338O          FILE RECORD OUTPUT;                              
                                                                        
 DCL   BE337_AREA      CHAR(2000) VAR;                                  
                                                                        
 DCL 1 BE337           BASED(ADDR(BE337_AREA)),                         
       4 BE337_LENGTH  FIXED BIN(15),                                   
       %INCLUDE BE33700N;                                               
                                                                        
 DCL 1 CN570           BASED(ADDR(BE337_AREA)),    /* 01 Level CN570 */ 
       4 CN570_LENGTH  FIXED BIN(15),              /* anvendes af    */ 
       %INCLUDE BE33700N;                          /* makro BE33701M */ 
                                                                        
 /********************************************************************/ 
 /* DEBUGI - Skal programmet debugge (put skip)                      */ 
 /********************************************************************/ 
 DCL   DEBUGI          FILE RECORD INPUT;                               
                                                                        
 DCL 1 DEBUG_REC,                                                       
       3 DEBUG_FROM    PIC'(7)9',                                       
       3 DEBUG_FILL1   CHAR(1),                                         
       3 DEBUG_TO      PIC'(7)9',                                       
       3 DEBUG_FILL2   CHAR(65);                                        
                                                                        
 /********************************************************************/ 
 /* Interne arbejdsfelter                                            */ 
 /********************************************************************/ 
 DCL JA                BIT(01)        INIT('1'B);                       
 DCL NEJ               BIT(01)        INIT('0'B);                       
 DCL EVS_FORSKUD       BIT(01)        INIT('0'B);                       
 DCL EVS_SLUT          BIT(01)        INIT('0'B);                       
 DCL EVS_SUPPL         BIT(01)        INIT('0'B);                       
 DCL FLERE_BE337I      BIT(01)        INIT('1'B);                       
 DCL EN_EJERBOLIG      BIT(01)        INIT('0'B);                       
 DCL TO_EJERBOLIG      BIT(01)        INIT('0'B);                       
 DCL GL_VURDERING      BIT(01)        INIT('0'B); /*#01*/               
 DCL NY_VURDERING      BIT(01)        INIT('0'B); /*#01*/               
 DCL INGEN_FEJL        BIT(01)        INIT('1'B);                       
 DCL PUTSKIP_THIS      BIT(01)        INIT('0'B);                       
                                                                        
 DCL EVS_KOERSEL       CHAR(07)       INIT('');                         
 DCL EVS_KOERSEL_F     CHAR(07)       INIT('FORSKUD');                  
 DCL EVS_KOERSEL_S     CHAR(07)       INIT('SLUT   ');                  
 DCL EVS_KOERSEL_U     CHAR(07)       INIT('SUPPL  ');                  
                                                                        
 DCL INDKAAR           CHAR(04)       INIT('');                         
 DCL AFVIKL_ID         CHAR(16)       INIT('');                         
 DCL AFVIKL_ID_F       CHAR(16)       INIT('');                         
 DCL AFVIKL_ID_S       CHAR(16)       INIT('');                         
                                                                        
 DCL CONDCODE          FIXED BIN(31)  INIT(0);                          
 DCL DISP_4            PIC'-(3)9'     INIT(0);                          
                                                                        
 DCL DEBUG_TXT         CHAR(12)       INIT('*** DEBUG - ');             
 DCL VT1_TXT           CHAR(12)       INIT('BEH_VT1     ');             
 DCL VT2_TXT           CHAR(12)       INIT('BEH_VT2     ');             
 DCL VT3_TXT           CHAR(12)       INIT('BEH_VT3     ');             
 DCL CNT_TXT           CHAR(12)       INIT(' - COUNT  - ');             
 DCL WRITE_TXT         CHAR(12)       INIT(' - WRITE  - ');             
                                                                        
 DCL DISP_X            CHAR(35)       INIT('');                         
                                                                        
 DCL 1 DISP_DATA       BASED(ADDR(DISP_X)),                             
       3 DISP_EJERBO   CHAR(02),                                        
       3 DISP_FILLER1  CHAR(01),                                        
       3 DISP_EKOMNR   PIC'(4)9',                                       
       3 DISP_FILLER2  CHAR(01),                                        
       3 DISP_EEJDNR   PIC'(8)9',                                       
       3 DISP_FILLER3  CHAR(01),                                        
       3 DISP_ENHED    PIC'(4)9',                                       
       3 DISP_FILLER4  CHAR(01),                                        
       3 DISP_P1       PIC'9',                                          
       3 DISP_FILLER5  CHAR(01),                                        
       3 DISP_P2       PIC'9',                                          
       3 DISP_FILLER6  CHAR(01),                                        
       3 DISP_CNT      PIC'(4)9',                                       
       3 DISP_FILLER7  CHAR(01),                                        
       3 DISP_STATUS   CHAR(04);                                        
                                                                        
 DCL COMMIT_VALUE      PIC'(7)9'      INIT(10000);                      
 DCL COMMIT_COUNT      PIC'(7)9'      INIT(0);                          
 DCL COMMIT_NR         PIC'(7)9'      INIT(0);                          
                                                                        
 DCL BE337I_READ       PIC'(7)9'      INIT(0);                          
 DCL BE337I_REST       PIC'(7)9'      INIT(0);                          
 DCL BE337I_OK_EN      PIC'(7)9'      INIT(0);                          
 DCL BE337I_OK_TO      PIC'(7)9'      INIT(0);                          
 DCL BE337I_FEJL_EN    PIC'(7)9'      INIT(0);                          
 DCL BE337I_FEJL_TO    PIC'(7)9'      INIT(0);                          
                                                                        
 DCL VT1_CNT           PIC'(7)9'      INIT(0);                          
 DCL VT1_CNT_OK_EN     PIC'(7)9'      INIT(0);                          
 DCL VT1_CNT_OK_TO     PIC'(7)9'      INIT(0);                          
 DCL VT1_CNT_3800_EN   PIC'(7)9'      INIT(0);                          
 DCL VT1_CNT_3800_TO   PIC'(7)9'      INIT(0);                          
                                                                        
 DCL VT2_CNT           PIC'(7)9'      INIT(0);                          
 DCL VT2_CNT_OK_EN     PIC'(7)9'      INIT(0);                          
 DCL VT2_CNT_OK_TO     PIC'(7)9'      INIT(0);                          
 DCL VT2_CNT_3801_EN   PIC'(7)9'      INIT(0);                          
 DCL VT2_CNT_3801_TO   PIC'(7)9'      INIT(0);                          
                                                                        
 DCL VT3_CNT           PIC'(7)9'      INIT(0);                          
 DCL VT3_CNT_OK_EN     PIC'(7)9'      INIT(0);                          
 DCL VT3_CNT_OK_TO     PIC'(7)9'      INIT(0);                          
 DCL VT3_CNT_3802_EN   PIC'(7)9'      INIT(0);                          
 DCL VT3_CNT_3802_TO   PIC'(7)9'      INIT(0);                          
                                                                        
 DCL BE338O_WRITE      PIC'(7)9'      INIT(0);                          
 DCL BE338O_REST       PIC'(7)9'      INIT(0);                          
 DCL BE338O_OK_EN      PIC'(7)9'      INIT(0);                          
 DCL BE338O_OK_TO      PIC'(7)9'      INIT(0);                          
 DCL BE338O_FEJL_EN    PIC'(7)9'      INIT(0);                          
 DCL BE338O_FEJL_TO    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3800_EN    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3800_TO    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3801_EN    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3801_TO    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3802_EN    PIC'(7)9'      INIT(0);                          
 DCL BE338O_3802_TO    PIC'(7)9'      INIT(0);                          
                                                                        
 /********************************************************************/ 
 /* Standard rutiner                                                 */ 
 /********************************************************************/ 
 DCL   ZS61900         ENTRY;          /* Udskrivning af SSI         */ 
 DCL   ZS30101         ENTRY;          /* Abendrutine                */ 
 DCL   ZS67000         ENTRY;          /* Hent sql tekster           */ 
                                                                        
 DCL   1 BE55600M,                     /* Ajourfør mini MT-fil til   */ 
         %INCLUDE BE55600M;            /* EVS forskud eller EVS slut */ 
                                                                        
 DCL   ABEND_KODE      FIXED(2) INIT(5);                                
 DCL   ABEND_RTKODE    FIXED(1) INIT(2);                                
                                                                        
 DCL 1 ABEND_MEDDEL,                                                    
       2 ABEND_MODUL   CHAR(07) INIT('BE33825'),                        
       2 ABEND_FILL1   CHAR(01) INIT(' '),                              
       2 ABEND_MEDNR   CHAR(02) INIT('**'),                             
       2 ABEND_FILL2   CHAR(01) INIT(' '),                              
       2 ABEND_FEJLNR  CHAR(03) INIT(' '),                              
       2 ABEND_FILL3   CHAR(01) INIT(' '),                              
       2 ABEND_TXT     CHAR(41) INIT('');                               
                                                                        
 DCL  SYSPRINT FILE;                                                    
                                                                        
 /********************************************************************/ 
 /* Db2 tabeller                                                     */ 
 /********************************************************************/ 
 EXEC SQL INCLUDE BE80300T;           /* EVS-enhedsanvendelsestabel  */ 
 EXEC SQL INCLUDE SQLCA;              /* SQL kommunikationsareal     */ 
                                                                        
 /********************************************************************/ 
 /* On Conditions                                                    */ 
 /********************************************************************/ 
 CALL ZS61900;                                                          
                                                                        
 ON ERROR BEGIN;                                                        
   ON ERROR SYSTEM;                                                     
   PUT SKIP LIST('PROGRAMFEJL');                                        
   CALL PLIDUMP('SNHOB','PROGRAM BE33825L');                            
 END;                                                                   
                                                                        
 ON ENDFILE(BE337I) BEGIN;                                              
   FLERE_BE337I = NEJ;                                                  
 END;                                                                   
                                                                        
 /********************************************************************/ 
 /* Main proces                                                      */ 
 /********************************************************************/ 
 CALL HOUSEKEEPING;                                                     
 CALL READ_BE337I;                                                      
                                                                        
 DO WHILE (FLERE_BE337I);                                               
   CALL FIND_EJERBOLIG_ANTAL;                                           
                                                                        
   IF EN_EJERBOLIG                                                      
    ! TO_EJERBOLIG THEN DO;                                             
     IF INGEN_FEJL THEN DO;                                             
       CALL BEH_VT1;                                                    
     END;                                                               
                                                                        
     IF INGEN_FEJL THEN DO;                                             
       CALL BEH_VT2;                                                    
     END;                                                               
                                                                        
     IF INGEN_FEJL THEN DO;                                             
       CALL BEH_VT3;                                                    
     END;                                                               
   END;                                                                 
                                                                        
   CALL WRITE_BE338O;                                                   
   CALL DB2_COMMIT;                                                     
   CALL READ_BE337I;                                                    
 END;                                                                   
                                                                        
 IF COMMIT_COUNT > 0 THEN DO;                                           
   COMMIT_COUNT = COMMIT_COUNT + COMMIT_VALUE;                          
   CALL DB2_COMMIT;                                                     
 END;                                                                   
                                                                        
 CALL AFSLUT;                                                           
                                                                        
 /********************************************************************/ 
 /* Housekeeping                                                     */ 
 /********************************************************************/ 
 HOUSEKEEPING: PROC;                                                    
                                                                        
 DCL PUTSKIP_FROM    CHAR(17) INIT('   PUTSKIP_FROM: ');                
 DCL PUTSKIP_TO      CHAR(17) INIT('   PUTSKIP_TO  : ');                
 DCL EVS_KOERSEL_TXT CHAR(19) INIT('   EVS_KOERSEL :   ');              
 DCL INDKAAR_TXT     CHAR(22) INIT('   INDKOMSTÅR  :      ');           
 DCL COMMIT_TXT      CHAR(17) INIT('   COMMIT_VALUE: ');                
                                                                        
 DCL DISP_FROM       PIC'Z.ZZZ.ZZ9';                                    
 DCL DISP_TO         PIC'Z.ZZZ.ZZ9';                                    
 DCL DISP_COMMIT     PIC'Z.ZZZ.ZZ9';                                    
                                                                        
 PUT SKIP LIST('Program BE33825 startet');                              
 PUT SKIP LIST('');                                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Skal programmet fortage debug (put skip)                         */ 
 /*------------------------------------------------------------------*/ 
 ON UNDEFINEDFILE(DEBUGI) BEGIN;                                        
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 OPEN FILE(DEBUGI) TITLE('DEBUG00I');                                   
                                                                        
 ON ENDFILE(DEBUGI) BEGIN;                                              
   DEBUG_REC.DEBUG_FROM = 0;                                            
   DEBUG_REC.DEBUG_TO   = 0;                                            
   GO TO HOUSEKEEPING_PUTSKIP_FOUND;                                    
 END;                                                                   
                                                                        
 READ FILE(DEBUGI) INTO(DEBUG_REC);                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* HOUSEKEEPING_PUTSKIP_FOUND                                       */ 
 /*------------------------------------------------------------------*/ 
 HOUSEKEEPING_PUTSKIP_FOUND:                                            
                                                                        
 PUT SKIP LIST('BE33825 parametre:');                                   
                                                                        
 DISP_FROM = DEBUG_REC.DEBUG_FROM;                                      
 DISP_TO   = DEBUG_REC.DEBUG_TO;                                        
                                                                        
 PUT SKIP LIST(PUTSKIP_FROM !! DISP_FROM);                              
 PUT SKIP LIST(PUTSKIP_TO !! DISP_TO);                                  
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Find EVS Kørsel type                                             */ 
 /*------------------------------------------------------------------*/ 
                                                                        
 AFVIKL_ID_F = 'OPRET_EVS_FORSKUD';                                     
 AFVIKL_ID_S = 'OPRET_EVS_SLUT';                                        
                                                                        
 SELECT (BE55600M.AFVIKLINGS_ID);                                       
   WHEN('OPRET_EVS_FORSK') DO;                                          
     EVS_FORSKUD = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_F;                                       
     AFVIKL_ID   = BE55600M.AFVIKLINGS_ID;                              
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   WHEN('OPRET_EVS_SLUT') DO;                                           
     EVS_SLUT    = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_S;                                       
     AFVIKL_ID   = BE55600M.AFVIKLINGS_ID;                              
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   WHEN('SUPPL_EVS_SLUT') DO;                                           
     EVS_SUPPL   = JA;                                                  
     EVS_KOERSEL = EVS_KOERSEL_U;                                       
     AFVIKL_ID   = BE55600M.AFVIKLINGS_ID;                              
     PUT SKIP LIST(EVS_KOERSEL_TXT !! EVS_KOERSEL                       
                  !! ' - ' !! AFVIKL_ID);                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     ABEND_FEJLNR = '101';                                              
     ABEND_TXT    = 'Afviklings_id skal være OPRET_EVS_FORSK, ' !!      
                    'OPRET_EVS_SLUT eller SUPPL_EVS_SLUT';              
     CALL PROGRAM_FEJL;                                                 
   END;                                                                 
 END;                                                                   
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Find Indkomstår afhængig af EVS kørsel type                      */ 
 /*------------------------------------------------------------------*/ 
 IF EVS_KOERSEL = EVS_KOERSEL_S                                         
  ! EVS_KOERSEL = EVS_KOERSEL_U THEN DO;                                
    INDKAAR = BE55600M.INDKÅR1;                                         
 END;                                                                   
 ELSE DO;                                                               
    INDKAAR = BE55600M.INDKÅR2;                                         
 END;                                                                   
                                                                        
 DISP_COMMIT = COMMIT_VALUE;                                            
                                                                        
 PUT SKIP LIST(INDKAAR_TXT !! INDKAAR);                                 
 PUT SKIP LIST(COMMIT_TXT !! DISP_COMMIT);                              
 PUT SKIP LIST('');                                                     
                                                                        
 /*------------------------------------------------------------------*/ 
 /* Open filer                                                       */ 
 /*------------------------------------------------------------------*/ 
 OPEN FILE(BE337I) TITLE('BE33700I');                                   
 OPEN FILE(BE338O) TITLE('BE33800O');                                   
                                                                        
 END HOUSEKEEPING;                                                      
                                                                        
 /********************************************************************/ 
 /* Læs næste BE337I record                                          */ 
 /********************************************************************/ 
 READ_BE337I: PROC;                                                     
                                                                        
 DCL DISP_X           CHAR(9);                                          
 DCL DISP_9       BASED(ADDR(DISP_X))                                   
                      PIC 'Z.ZZZ.ZZ9';                                  
                                                                        
 DCL DISP_BE337I      CHAR(9);                                          
                                                                        
 PUTSKIP_THIS = NEJ;                                                    
                                                                        
 READ FILE(BE337I) INTO(BE337_AREA);                                    
                                                                        
 IF FLERE_BE337I THEN DO;                                               
   BE337I_READ = BE337I_READ + 1;                                       
                                                                        
   SELECT(BE337.TEKFEJLNR);                                             
     WHEN(0)   INGEN_FEJL = JA;                                         
     OTHERWISE INGEN_FEJL = NEJ;                                        
   END;                                                                 
                                                                        
   IF (DEBUG_REC.DEBUG_TO > 0                   )                       
    & (  BE337I_READ      > DEBUG_REC.DEBUG_FROM                        
       ! BE337I_READ      = DEBUG_REC.DEBUG_FROM)                       
    & (  BE337I_READ      < DEBUG_REC.DEBUG_TO                          
       ! BE337I_READ      = DEBUG_REC.DEBUG_TO  ) THEN DO;              
     PUTSKIP_THIS = JA;                                                 
   END;                                                                 
 END;                                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   DISP_9      = BE337I_READ;                                           
   DISP_BE337I = TRIM(DISP_X,' ');                                      
                                                                        
   PUT SKIP LIST(DEBUG_TXT !! PROCNAME                                  
                 !! ' - record nr ' !! DISP_BE337I);                    
 END;                                                                   
                                                                        
 END READ_BE337I;                                                       
                                                                        
 /********************************************************************/ 
 /* Initier felterne EN_EJERBOLIG & TO_EJERBOLIG                     */ 
 /********************************************************************/ 
 FIND_EJERBOLIG_ANTAL: PROC;                                            
                                                                        
 %INCLUDE BE33701M;      /* Kode skal ligge her før koden i proc     */ 
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   SELECT(BE337.TEKFEJLNR);                                             
     WHEN(0)   BE337I_OK_EN   = BE337I_OK_EN   + 1;                     
     OTHERWISE BE337I_FEJL_EN = BE337I_FEJL_EN + 1;                     
   END;                                                                 
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   SELECT(BE337.TEKFEJLNR);                                             
     WHEN(0)   BE337I_OK_TO   = BE337I_OK_TO   + 1;                     
     OTHERWISE BE337I_FEJL_TO = BE337I_FEJL_TO + 1;                     
   END;                                                                 
 END;                                                                   
                                                                        
 IF ^EN_EJERBOLIG                                                       
  & ^TO_EJERBOLIG THEN DO;                                              
   BE337I_REST = BE337I_REST + 1;                                       
 END;                                                                   
                                                                        
 END FIND_EJERBOLIG_ANTAL;                                              
                                                                        
 /********************************************************************/ 
 /* VT.1 - Såfremt antal enheder på aktuelt indkomstår, aktuel       */ 
 /*        EVS-kørsel, kommunenummer og ejendomsnummer i             */ 
 /*        EVS-enhedsanvendelsestabellen (BE803) ikke er             */ 
 /*        2 (ejendomstype 4) eller 1 (ejendomstype 5),              */ 
 /*        så er det en fejl (TEKFEJLNR = 3800)                      */ 
 /********************************************************************/ 
 BEH_VT1: PROC;                                                         
                                                                        
 DCL CNT_ANTAL FIXED BIN(15) INIT(0);                                   
 DCL P1        FIXED BIN(15) INIT(0);                                   
 DCL P2        FIXED BIN(15) INIT(0);                                   
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   P1 = 1;                                                              
   P2 = 1;                                                              
 END;                                                                   
 ELSE DO;                                                               
   P1 = 1;                                                              
   P2 = 2;                                                              
 END;                                                                   
                                                                        
 DCLBE80300T                   = '';                                    
 DCLBE80300T.TIL_EVS_AAR       = INDKAAR;                               
 DCLBE80300T.AFVIKLINGS_ID     = AFVIKL_ID;                             
 DCLBE80300T.DATA_ID           = BE55600M.DATA_ID;                      
 DCLBE80300T.EKOMNR            = BE337.EKOMNR;                          
 DCLBE80300T.EEJDNR            = BE337.EEJDNR;                          
                                                                        
 EXEC SQL SELECT COUNT(*)                                               
          INTO  :CNT_ANTAL                                              
          FROM   BE80300T                                               
          WHERE  TIL_EVS_AAR    = :DCLBE80300T.TIL_EVS_AAR              
          AND    AFVIKLINGS_ID  = :DCLBE80300T.AFVIKLINGS_ID            
          AND    DATA_ID        = :DCLBE80300T.DATA_ID                  
          AND    EKOMNR         = :DCLBE80300T.EKOMNR                   
          AND    EEJDNR         = :DCLBE80300T.EEJDNR                   
          AND    EENHEDLBNR    IN(:P1,:P2);                             
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     VT1_CNT = VT1_CNT + 1;                                             
                                                                        
     IF EN_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(1) DO;                                                    
           VT1_CNT_OK_EN = VT1_CNT_OK_EN + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT1_CNT_3800_EN = VT1_CNT_3800_EN + 1;                       
           BE337.TEKFEJLNR = 3800;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF TO_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(2) DO;                                                    
           VT1_CNT_OK_TO = VT1_CNT_OK_TO + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT1_CNT_3800_TO = VT1_CNT_3800_TO + 1;                       
           BE337.TEKFEJLNR = 3800;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBE80300T.EKOMNR;                                
       DISP_EEJDNR = DCLBE80300T.EEJDNR;                                
       DISP_P1     = P1;                                                
       DISP_P2     = P2;                                                
       DISP_CNT    = CNT_ANTAL;                                         
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! VT1_TXT !! CNT_TXT !! DISP_X);        
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBE80300T);                                        
     DISP_4 = SQLCA.SQLCODE;                                            
                                                                        
     ABEND_FEJLNR = '102';                                              
     ABEND_TXT    = 'Fejl i BEH_VT1 - Sqlcode ' !! DISP_4;              
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END BEH_VT1;                                                           
                                                                        
 /********************************************************************/ 
 /* VT.2 - Såfremt antal enheder på aktuelt indkomstår + aktuel      */ 
 /*        EVS-kørsel eller tidligere indkomstår + tidligere         */ 
 /*        EVS-kørsel Slut, kommunenummer og ejendomsnummer i         */
 /*        EVS-enhedsanvendelsestabellen (BE803) ikke er             */ 
 /*        2 (ejendomstype 4) eller 1 (ejendomstype 5),              */ 
 /*        så er det en fejl (TEKFEJLNR = 3801)                      */ 
 /********************************************************************/ 
 BEH_VT2: PROC;                                                         
                                                                        
 DCL CNT_ANTAL FIXED BIN(15) INIT(0);                                   
 DCL P1        FIXED BIN(15) INIT(0);                                   
 DCL P2        FIXED BIN(15) INIT(0);                                   
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   P1 = 1;                                                              
   P2 = 1;                                                              
 END;                                                                   
 ELSE DO;                                                               
   P1 = 1;                                                              
   P2 = 2;                                                              
 END;                                                                   
                                                                        
 DCLBE80300T                   = '';                                    
 DCLBE80300T.TIL_EVS_AAR       = INDKAAR;                               
 DCLBE80300T.AFVIKLINGS_ID     = AFVIKL_ID;                             
 DCLBE80300T.DATA_ID           = BE55600M.DATA_ID;                      
 DCLBE80300T.EKOMNR            = BE337.EKOMNR;                          
 DCLBE80300T.EEJDNR            = BE337.EEJDNR;                          
                                                                        
 EXEC SQL SELECT COUNT(*)                                               
          INTO  :CNT_ANTAL                                              
          FROM   BE80300T                                               
          WHERE (   (    TIL_EVS_AAR    = :DCLBE80300T.TIL_EVS_AAR      
                     AND AFVIKLINGS_ID  = :DCLBE80300T.AFVIKLINGS_ID    
                     AND DATA_ID        = :DCLBE80300T.DATA_ID)         
                 OR (    TIL_EVS_AAR    < :DCLBE80300T.TIL_EVS_AAR      
                     AND AFVIKLINGS_ID  = :AFVIKL_ID_S                  
                     AND DATA_ID        = :DCLBE80300T.DATA_ID))        
          AND    EKOMNR                 = :DCLBE80300T.EKOMNR           
          AND    EEJDNR                 = :DCLBE80300T.EEJDNR           
          AND    EENHEDLBNR            IN(:P1,:P2);                     
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     VT2_CNT = VT2_CNT + 1;                                             
                                                                        
     IF EN_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(1) DO;                                                    
           VT2_CNT_OK_EN = VT2_CNT_OK_EN + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT2_CNT_3801_EN = VT2_CNT_3801_EN + 1;                       
           BE337.TEKFEJLNR = 3801;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF TO_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(2) DO;                                                    
           VT2_CNT_OK_TO = VT2_CNT_OK_TO + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT2_CNT_3801_TO = VT2_CNT_3801_TO + 1;                       
           BE337.TEKFEJLNR = 3801;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBE80300T.EKOMNR;                                
       DISP_EEJDNR = DCLBE80300T.EEJDNR;                                
       DISP_P1     = P1;                                                
       DISP_P2     = P2;                                                
       DISP_CNT    = CNT_ANTAL;                                         
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! VT2_TXT !! CNT_TXT !! DISP_X);        
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBE80300T);                                        
     DISP_4 = SQLCA.SQLCODE;                                            
                                                                        
     ABEND_FEJLNR = '103';                                              
     ABEND_TXT    = 'Fejl i BEH_VT2 - Sqlcode ' !! DISP_4;              
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END BEH_VT2;                                                           
                                                                        
 /********************************************************************/ 
 /* VT.3 - Såfremt antal enheder på aktuelt indkomstår, aktuel       */ 
 /*        EVS-kørsel eller tidligere EVS-kørsel Forskud,            */ 
 /*        kommunenummer og ejendomsnummer i                         */ 
 /*        EVS-enhedsanvendelsestabellen (BE803) ikke er             */ 
 /*        2 (ejendomstype 4) eller 1 (ejendomstype 5),              */ 
 /*        så er det en fejl (TEKFEJLNR = 3802)                      */ 
 /********************************************************************/ 
 BEH_VT3: PROC;                                                         
                                                                        
 DCL CNT_ANTAL FIXED BIN(15) INIT(0);                                   
 DCL P1        FIXED BIN(15) INIT(0);                                   
 DCL P2        FIXED BIN(15) INIT(0);                                   
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   P1 = 1;                                                              
   P2 = 1;                                                              
 END;                                                                   
 ELSE DO;                                                               
   P1 = 1;                                                              
   P2 = 2;                                                              
 END;                                                                   
                                                                        
 DCLBE80300T                   = '';                                    
 DCLBE80300T.TIL_EVS_AAR       = INDKAAR;                               
 DCLBE80300T.AFVIKLINGS_ID     = AFVIKL_ID;                             
 DCLBE80300T.DATA_ID           = BE55600M.DATA_ID;                      
 DCLBE80300T.EKOMNR            = BE337.EKOMNR;                          
 DCLBE80300T.EEJDNR            = BE337.EEJDNR;                          
                                                                        
 EXEC SQL SELECT COUNT(*)                                               
          INTO  :CNT_ANTAL                                              
          FROM   BE80300T                                               
          WHERE (   (    TIL_EVS_AAR    = :DCLBE80300T.TIL_EVS_AAR      
                     AND AFVIKLINGS_ID  = :DCLBE80300T.AFVIKLINGS_ID    
                     AND DATA_ID        = :DCLBE80300T.DATA_ID)         
                 OR (    TIL_EVS_AAR    < :DCLBE80300T.TIL_EVS_AAR      
                     AND AFVIKLINGS_ID  = :AFVIKL_ID_F                  
                     AND DATA_ID        = :DCLBE80300T.DATA_ID))        
          AND    EKOMNR                 = :DCLBE80300T.EKOMNR           
          AND    EEJDNR                 = :DCLBE80300T.EEJDNR           
          AND    EENHEDLBNR            IN(:P1,:P2);                     
                                                                        
 SELECT (SQLCA.SQLCODE);                                                
   WHEN(0) DO;                                                          
     VT3_CNT = VT3_CNT + 1;                                             
                                                                        
     IF EN_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(1) DO;                                                    
           VT3_CNT_OK_EN = VT3_CNT_OK_EN + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT3_CNT_3802_EN = VT3_CNT_3802_EN + 1;                       
           BE337.TEKFEJLNR = 3802;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF TO_EJERBOLIG THEN DO;                                           
       SELECT(CNT_ANTAL);                                               
         WHEN(2) DO;                                                    
           VT3_CNT_OK_TO = VT3_CNT_OK_TO + 1;                           
         END;                                                           
         OTHERWISE DO;                                                  
           INGEN_FEJL      = NEJ;                                       
           VT3_CNT_3802_TO = VT3_CNT_3802_TO + 1;                       
           BE337.TEKFEJLNR = 3802;                                      
         END;                                                           
       END;                                                             
     END;                                                               
                                                                        
     IF PUTSKIP_THIS THEN DO;                                           
       CALL INIT_DISP_X;                                                
                                                                        
       DISP_EKOMNR = DCLBE80300T.EKOMNR;                                
       DISP_EEJDNR = DCLBE80300T.EEJDNR;                                
       DISP_P1     = P1;                                                
       DISP_P2     = P2;                                                
       DISP_CNT    = CNT_ANTAL;                                         
                                                                        
       PUT SKIP LIST(DEBUG_TXT !! VT3_TXT !! CNT_TXT !! DISP_X);        
     END;                                                               
   END;                                                                 
                                                                        
   OTHERWISE DO;                                                        
     PUT SKIP DATA(DCLBE80300T);                                        
     DISP_4 = SQLCA.SQLCODE;                                            
                                                                        
     ABEND_FEJLNR = '104';                                              
     ABEND_TXT    = 'Fejl i BEH_VT3 - Sqlcode ' !! DISP_4;              
                                                                        
     CALL SQL_FEJL;                                                     
   END;                                                                 
 END;                                                                   
                                                                        
 END BEH_VT3;                                                           
                                                                        
 /********************************************************************/ 
 /* Skriv BE338O record                                              */ 
 /********************************************************************/ 
 WRITE_BE338O: PROC;                                                    
                                                                        
 WRITE FILE(BE338O) FROM(BE337_AREA);                                   
                                                                        
 BE338O_WRITE = BE338O_WRITE + 1;                                       
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   SELECT(BE337.TEKFEJLNR);                                             
     WHEN(0)    BE338O_OK_EN   = BE338O_OK_EN   + 1;                    
     WHEN(3800) BE338O_3800_EN = BE338O_3800_EN + 1;                    
     WHEN(3801) BE338O_3801_EN = BE338O_3801_EN + 1;                    
     WHEN(3802) BE338O_3802_EN = BE338O_3802_EN + 1;                    
     OTHERWISE  BE338O_FEJL_EN = BE338O_FEJL_EN + 1;                    
   END;                                                                 
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   SELECT(BE337.TEKFEJLNR);                                             
     WHEN(0)    BE338O_OK_TO   = BE338O_OK_TO   + 1;                    
     WHEN(3800) BE338O_3800_TO = BE338O_3800_TO + 1;                    
     WHEN(3801) BE338O_3801_TO = BE338O_3801_TO + 1;                    
     WHEN(3802) BE338O_3802_TO = BE338O_3802_TO + 1;                    
     OTHERWISE  BE338O_FEJL_TO = BE338O_FEJL_TO + 1;                    
   END;                                                                 
 END;                                                                   
                                                                        
 IF ^EN_EJERBOLIG                                                       
  & ^TO_EJERBOLIG THEN DO;                                              
   BE338O_REST = BE338O_REST + 1;                                       
 END;                                                                   
                                                                        
 IF PUTSKIP_THIS THEN DO;                                               
   CALL INIT_DISP_X;                                                    
                                                                        
   DISP_EKOMNR = BE337.EKOMNR;                                          
   DISP_EEJDNR = BE337.EEJDNR;                                          
   DISP_ENHED  = BE337.EENHEDLBNR;                                      
                                                                        
   PUT SKIP LIST(DEBUG_TXT !! PROCNAME !! WRITE_TXT !! DISP_X);         
 END;                                                                   
                                                                        
 END WRITE_BE338O;                                                      
                                                                        
 /********************************************************************/ 
 /* Luk filer og put skip tællere                                    */ 
 /********************************************************************/ 
 AFSLUT: PROC;                                                          
                                                                        
 DCL DISP_CNT_1  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_2  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_3  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_4  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_5  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_6  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_7  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_8  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_9  PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_10 PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_11 PIC'Z.ZZZ.ZZ9';                                        
 DCL DISP_CNT_12 PIC'Z.ZZZ.ZZ9';                                        
                                                                        
 CLOSE FILE(BE337I),                                                    
       FILE(BE338O),                                                    
       FILE(DEBUGI);                                                    
                                                                        
 PUT SKIP LIST('');                                                     
 PUT SKIP LIST('Resultat for BE33825:');                                
                                                                        
 DISP_CNT_1 = BE337I_READ;                                              
 DISP_CNT_2 = BE337I_REST;                                              
 DISP_CNT_3 = BE337I_OK_EN;                                             
 DISP_CNT_4 = BE337I_OK_TO;                                             
 DISP_CNT_5 = BE337I_FEJL_EN;                                           
 DISP_CNT_6 = BE337I_FEJL_TO;                                           
                                                                        
 PUT SKIP LIST('Read BE337I  - Ialt                  :' !! DISP_CNT_1); 
 PUT SKIP LIST('          Alle andre                 :' !! DISP_CNT_2); 
 PUT SKIP LIST('          En ejerbolig - OK          :' !! DISP_CNT_3); 
 PUT SKIP LIST('          To ejerbolig - OK          :' !! DISP_CNT_4); 
 PUT SKIP LIST('          En ejerbolig - BE33700 FEJL:' !! DISP_CNT_5); 
 PUT SKIP LIST('          To ejerbolig - BE33700 FEJL:' !! DISP_CNT_6); 
                                                                        
 DISP_CNT_1 = VT1_CNT;                                                  
 DISP_CNT_2 = VT1_CNT_OK_EN;                                            
 DISP_CNT_3 = VT1_CNT_OK_TO;                                            
 DISP_CNT_4 = VT1_CNT_3800_EN;                                          
 DISP_CNT_5 = VT1_CNT_3800_TO;                                          
                                                                        
 PUT SKIP LIST('VT1 Count    - Ialt                  :' !! DISP_CNT_1); 
 PUT SKIP LIST('             En ejerbolig - OK       :' !! DISP_CNT_2); 
 PUT SKIP LIST('             To ejerbolig - OK       :' !! DISP_CNT_3); 
 PUT SKIP LIST('             En ejerbolig - FEJL 3800:' !! DISP_CNT_4); 
 PUT SKIP LIST('             To ejerbolig - FEJL 3800:' !! DISP_CNT_5); 
                                                                        
 DISP_CNT_1 = VT2_CNT;                                                  
 DISP_CNT_2 = VT2_CNT_OK_EN;                                            
 DISP_CNT_3 = VT2_CNT_OK_TO;                                            
 DISP_CNT_4 = VT2_CNT_3801_EN;                                          
 DISP_CNT_5 = VT2_CNT_3801_TO;                                          
                                                                        
 PUT SKIP LIST('VT2 Count    - Ialt                  :' !! DISP_CNT_1); 
 PUT SKIP LIST('             En ejerbolig - OK       :' !! DISP_CNT_2); 
 PUT SKIP LIST('             To ejerbolig - OK       :' !! DISP_CNT_3); 
 PUT SKIP LIST('             En ejerbolig - FEJL 3801:' !! DISP_CNT_4); 
 PUT SKIP LIST('             To ejerbolig - FEJL 3801:' !! DISP_CNT_5); 
                                                                        
 DISP_CNT_1 = VT3_CNT;                                                  
 DISP_CNT_2 = VT3_CNT_OK_EN;                                            
 DISP_CNT_3 = VT3_CNT_OK_TO;                                            
 DISP_CNT_4 = VT3_CNT_3802_EN;                                          
 DISP_CNT_5 = VT3_CNT_3802_TO;                                          
                                                                        
 PUT SKIP LIST('VT3 Count    - Ialt                  :' !! DISP_CNT_1); 
 PUT SKIP LIST('             En ejerbolig - OK       :' !! DISP_CNT_2); 
 PUT SKIP LIST('             To ejerbolig - OK       :' !! DISP_CNT_3); 
 PUT SKIP LIST('             En ejerbolig - FEJL 3802:' !! DISP_CNT_4); 
 PUT SKIP LIST('             To ejerbolig - FEJL 3802:' !! DISP_CNT_5); 
                                                                        
 DISP_CNT_1  = BE338O_WRITE;                                            
 DISP_CNT_2  = BE338O_REST;                                             
 DISP_CNT_3  = BE338O_OK_EN;                                            
 DISP_CNT_4  = BE338O_OK_TO;                                            
 DISP_CNT_5  = BE338O_FEJL_EN;                                          
 DISP_CNT_6  = BE338O_FEJL_TO;                                          
 DISP_CNT_7  = BE338O_3800_EN;                                          
 DISP_CNT_8  = BE338O_3800_TO;                                          
 DISP_CNT_9  = BE338O_3801_EN;                                          
 DISP_CNT_10 = BE338O_3801_TO;                                          
 DISP_CNT_11 = BE338O_3802_EN;                                          
 DISP_CNT_12 = BE338O_3802_TO;                                          
                                                                        
 PUT SKIP LIST('Write BE338O - Ialt                  :' !! DISP_CNT_1); 
 PUT SKIP LIST('          Alle andre                 :' !! DISP_CNT_2); 
 PUT SKIP LIST('          En ejerbolig - OK          :' !! DISP_CNT_3); 
 PUT SKIP LIST('          To ejerbolig - OK          :' !! DISP_CNT_4); 
 PUT SKIP LIST('          En ejerbolig - BE33700 FEJL:' !! DISP_CNT_5); 
 PUT SKIP LIST('          To ejerbolig - BE33700 FEJL:' !! DISP_CNT_6); 
 PUT SKIP LIST('          En ejerbolig - FEJL 3800   :' !! DISP_CNT_7); 
 PUT SKIP LIST('          To ejerbolig - FEJL 3800   :' !! DISP_CNT_8); 
 PUT SKIP LIST('          En ejerbolig - FEJL 3801   :' !! DISP_CNT_9); 
 PUT SKIP LIST('          To ejerbolig - FEJL 3801   :' !! DISP_CNT_10);
 PUT SKIP LIST('          En ejerbolig - FEJL 3802   :' !! DISP_CNT_11);
 PUT SKIP LIST('          To ejerbolig - FEJL 3802   :' !! DISP_CNT_12);
                                                                        
 PUT SKIP LIST('');                                                     
 PUT SKIP LIST('Program BE33825 sluttet');                              
                                                                        
 END AFSLUT;                                                            
                                                                        
 /********************************************************************/ 
 /* Init DISP_X strengen                                             */ 
 /********************************************************************/ 
 INIT_DISP_X: PROC;                                                     
                                                                        
 DCL EN_TXT CHAR(02) INIT('EN');                                        
 DCL TO_TXT CHAR(02) INIT('TO');                                        
 DCL OK_TXT CHAR(04) INIT('OK  ');                                      
                                                                        
 DISP_X = '';                                                           
                                                                        
 IF EN_EJERBOLIG THEN DO;                                               
   DISP_EJERBO = EN_TXT;                                                
 END;                                                                   
                                                                        
 IF TO_EJERBOLIG THEN DO;                                               
   DISP_EJERBO = TO_TXT;                                                
 END;                                                                   
                                                                        
 SELECT(BE337.TEKFEJLNR);                                               
   WHEN(0)   DISP_STATUS = OK_TXT;                                      
   OTHERWISE DISP_STATUS = BE337.TEKFEJLNR;                             
 END;                                                                   
                                                                        
 END INIT_DISP_X;                                                       
                                                                        
 /********************************************************************/ 
 /* Commit og put skip                                               */ 
 /********************************************************************/ 
 DB2_COMMIT: PROC;                                                      
                                                                        
 COMMIT_COUNT = COMMIT_COUNT + 1;                                       
                                                                        
 IF COMMIT_COUNT > COMMIT_VALUE                                         
  ! COMMIT_COUNT = COMMIT_VALUE THEN DO;                                
   EXEC SQL COMMIT;                                                     
                                                                        
   COMMIT_COUNT = 0;                                                    
   COMMIT_NR     = COMMIT_NR + 1;                                       
                                                                        
   PUT SKIP LIST('Commit nr ' !! COMMIT_NR);                            
 END;                                                                   
                                                                        
 END DB2_COMMIT;                                                        
                                                                        
 /********************************************************************/ 
 /* Udskriv sql fejl                                                 */ 
 /********************************************************************/ 
 PROGRAM_FEJL: PROC;                                                    
                                                                        
 PUT SKIP LIST ('*****************************************');           
 PUT SKIP LIST ('Der abendes med programfejl');                         
 PUT SKIP LIST ('ABEND_TXT:                 ' !! ABEND_TXT);            
 PUT SKIP LIST ('*****************************************');           
 CALL ZS30101(ABEND_KODE,ABEND_MEDDEL,ABEND_RTKODE);                    
                                                                        
 END PROGRAM_FEJL;                                                      
                                                                        
 /********************************************************************/ 
 /* Udskriv sql fejl                                                 */ 
 /********************************************************************/ 
 SQL_FEJL: PROC;                                                        
                                                                        
 PUT SKIP LIST ('*****************************************');           
 PUT SKIP LIST ('Der abendes med sqlcode: ' !! SQLCA.SQLCODE);          
 PUT SKIP LIST ('ABEND_TXT:               ' !! ABEND_TXT);              
 PUT SKIP LIST ('*****************************************');           
                                                                        
 CALL ZS67000(SQLCA);                                                   
 CALL ZS30101(ABEND_KODE,ABEND_MEDDEL,ABEND_RTKODE);                    
                                                                        
 CONDCODE = 99;                                                         
                                                                        
 END SQL_FEJL;                                                          
                                                                        
 END BE33825;                                                           
