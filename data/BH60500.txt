  PROCESS OPT(TIME);                                                    
 BH60500: /***       OPSPLITNING AF BEREGNINGSBÅND PR. RC       ***/    
         PROCEDURE(EXECPARM) OPTIONS (MAIN) REORDER;                    
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 1997');       
 /*********************************************************************/
 /*                                                                   */
 /*      FUNKTION   :   PROGRAMMET OPSPLITTER DET SORTEREDE OG AJOUR- */
 /*                     FØRTE BEREGNINGSBÅND PR. CENTRAL.             */
 /*                     DER UDSKRIVES KØRSELSKONTROLLISTE.            */
 /*                                                                   */
 /*      INDDATA    :   BH61101D          BEREGNET UDTRÆK             */
 /*                                                                   */
 /*      UDDATA     :   BH60510D          BEREGNET UDTRÆK  KKE        */
 /*                     BH60530D          BEREGNET UDTRÆK  A.C        */
 /*                     BH60540D          BEREGNET UDTRÆK  Å.C        */
 /*                     BH60550D          BEREGNET UDTRÆK  O.C        */
 /*                     BH60560D          BEREGNET UDTRÆK  B.C        */
 /*                     BH60501Y          KØRSELSKONTROL              */
 /*                     BH60502Y          FEJLLISTE AFSTEMNING        */
 /*                                                                   */
 /*      PROGRAMMØR :   JMØ               UC-P   SEPTEMBER 85         */
 /*      RETTET     :   MARGIT PEDERSBÆK  UC-P   OKTOBER 86           */
 /*      RETTET     :   JESPER OLSEN      UC-P   AUGUST 1987          */
 /*      RETTET     :   MARGIT PEDERSBÆK  UC-P   SEPTEMBER 1988       */
 /*      RETTET     :   JESPER OLSEN    UC-SKAT  SEPTEMBER 1989       */
 /*      RETTET     :   MARGIT PEDERSBÆK DIV SKAT OKTOBER  1990       */
 /*      RETTET     :   AAGE RASMUSSEN   DIV SKAT OKTOBER  1991       */
 /*      RETTET     :   AAGE RASMUSSEN   DIV SKAT OKTOBER  1992       */
 /*      RETTET     :   AAGE RASMUSSEN   PUS     DECEMBER  1993       */
 /*      RETTET     :   AAGE RASMUSSEN   PUS     DECEMBER  1994       */
 /*      RETTET     :   AAGE RASMUSSEN   PFB     JANUAR    1997       */
 /*      RETTET     :   AAGE RASMUSSEN   PFB     JANUAR    1998       */
 /*      RETTET     :   AAGE RASMUSSEN   PFB     JANUAR    1999       */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      ÅRSTILRETNING VIL 'NORMALT' KUN BESTÅ AF REKOMPILERING       */
 /*      PÅ GRUND AF ÆNDRING(ER) I INPUT-STRUKTURER.                  */
 /*                                                                   */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF GENEREL VÆRDI TIL INDSÆTTELSE I   */
 /*      CSMUDS I FORBINDELSE MED TEST.                               */
 /*      VED ÆNDRING ANGIVES PARM POS 1-4 = TEST(ÆNDRING)             */
 /*                               POS 5   = GL. CSMUDS NR. 1          */
 /*                               POS 6   = NY  CSMUDS NR. 1          */
 /*                               POS 7   = GL. CSMUDS NR. 2          */
 /*                               POS 8   = NY  CSMUDS NR. 2          */
 /*      HVIS POS 1-4 = TEST OG PSO 5-8 ER BLANKE OG KØRSLEN VEDRØRER */
 /*      EJENDOMSSKEMAER TIL PSA EJ PLADS (D.V.S. BH611.CAJOUR = 'P') */
 /*      SÅ ÆNDRES CSMUDS = 'M' TIL CSMUDS = 'N' HVIS EJDNR >= 110000 */
 /*      SÅ SÆTTES CSMUDS = 'O' HVIS CSÆREJD = 2 OG                   */
 /*      EJDNR >= 050000 & <= 112999                                  */
 /*      (D.V.S. HVIS EJ PÅ MANDTAL)                                  */
 /*      SÅ SÆTTES CSMUDS = 'P' HVIS CSÆREJD = 1 OG                   */
 /*      EJDNR >= 050000 & <= 112999                                  */
 /*      (D.V.S. HVIS DØD IFLG. MANDTAL)                              */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (10)   VAR;                          
 DCL     EX_PARM         CHAR      (8);                                 
         EX_PARM = EXECPARM;                                            
 /*********************************************************************/
 /*      DECLARE AF INPUT OG OUTPUT                                   */
 /*********************************************************************/
 DCL     BH611I          FILE RECORD INPUT;                             
 DCL     BH605O          FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      DECLARE AF STRUCTURE                                         */
 /*********************************************************************/
 DCL     BH611_AR        CHAR      (4000) VAR;                          
 DCL     1 BH611                          BASED(ADDR(BH611_AR)),        
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60100N;                                           
 DCL     1 BH61199                        BASED(ADDR(BH611_AR)),        
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60199N;                                           
 DCL     1 BH61101                        BASED(ADDR(BH611_AR)),        
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60101N;                                           
 DCL     1 BH60599,                                                     
           %INCLUDE BH60199N;                                           
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH60500L'),                                        
           2 F1          CHAR      (06)      INIT      (' '),           
           2 TEKST2      CHAR      (51)      INIT      (                
           'OPSPLITNING AF EJENDOMSREGISTER PR. CENTRAL KØRSEL '),      
           2 KØRSELSNR   CHAR      (34),                                
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
 DCL     1 OVERLIN3,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (54)      INIT      (' '),           
           2 IALT        CHAR      (05)      INIT      ('I ALT'),       
           2 F2          CHAR      (10)      INIT      (' '),           
           2 KBA         CHAR      (04)      INIT      ('KBH.'),        
           2 F3          CHAR      (11)      INIT      (' '),           
           2 AC          CHAR      (03)      INIT      ('A.C'),         
           2 F4          CHAR      (11)      INIT      (' '),           
           2 ÅC          CHAR      (03)      INIT      ('Å.C'),         
           2 F5          CHAR      (11)      INIT      (' '),           
           2 OC          CHAR      (03)      INIT      ('O.C'),         
           2 F6          CHAR      (11)      INIT      (' '),           
           2 BC          CHAR      (03)      INIT      ('B.C'),         
           2 F7          CHAR      (03)      INIT      (' ');           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (48),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (73)      INIT      (' ');           
 DCL     1 DETLIN2,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (45),                                
           2 ANT(6)      PIC       'BBBZZZ.ZZZ.ZZ9',                    
           2 F2          CHAR      (03)      INIT      (' ');           
 /*********************************************************************/
 /*      DECLARE AF OPTÆLLINGS FELTER                                 */
 /*********************************************************************/
 DCL     ANT_KOM (6)     DEC FIXED (09);                                
 DCL     ANT_OVF (6)     DEC FIXED (09);                                
 DCL     ANT_UDSKR (6)   DEC FIXED (09);                                
 DCL     ANT_EJUDSKR (6) DEC FIXED (09);                                
 DCL     ANT_EJSKEMA (6) DEC FIXED (09);                                
 DCL     ANT_SKM (6)     DEC FIXED (09);                                
 DCL     ANT_USA (6)     DEC FIXED (09);                                
 DCL     ANT_BSA (6)     DEC FIXED (09);                                
 DCL     ANT_ØVR (6)     DEC FIXED (09);                                
 DCL     ANT_PSA (6)     DEC FIXED (09);                                
 DCL     ANT_PSAD (6)    DEC FIXED (09);                                
 DCL     ANT_PSAK (6)    DEC FIXED (09);                                
 DCL     ANT_PSAO (6)    DEC FIXED (09);                                
 DCL     ANT_PSAP (6)    DEC FIXED (09);                                
 DCL     ANT_LÆS         DEC FIXED (09)      INIT      (0);             
 DCL     ANT_LÆSKOM      DEC FIXED (09)      INIT      (0);             
 /*********************************************************************/
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*********************************************************************/
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     GEM_CRCKO       PIC       '9';                                 
 DCL     EOF_BH611       BIT       (1)       INIT       ('0'B);         
 DCL     INDEX           BIN FIXED (15);                                
 DCL     I               BIN FIXED (15);                                
  %INCLUDE ZZ20301M;                                                    
 /*********************************************************************/
 /*       DECLARE AF ENTRIES                                          */
 /*********************************************************************/
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM); /* SKRIV SSI-DATO */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;        /* PROGRAM-KOMMUNIKATION       */
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
         ON ENDFILE (BH611I)                                            
            EOF_BH611 = '1'B;                                           
         CALL ZS61900;                      /* UDSKRIVN. AF SSI-DATO  */
         CALL ZZ20390 ('*OPEN','BH60501Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
         OVERLIN3.CCA  = ZZWS2;                                         
         OPEN FILE (BH611I) TITLE ('BH61101I');                         
         CALL LÆS;                                                      
         GEM_CRCKO = BH611.CRCKO;                                       
         IF BH611.CAJOUR = 'S'                                          
         THEN                                                           
            OVERLIN1.KØRSELSNR = '1 (USA ELLER BSA)';                   
         ELSE                                                           
            OVERLIN1.KØRSELSNR = '2 (PSA EJ PLADS, DØDE, EJ PÅ MT)';    
         OPEN FILE (BH605O) TITLE ('BH605' !! BH611.CRCKO !! '0O');     
         ANT_KOM (*)  = 0;                                              
         ANT_OVF (*)  = 0;                                              
         ANT_EJUDSKR (*) = 0;                                           
         ANT_UDSKR (*) = 0;                                             
         ANT_USA (*)  = 0;                                              
         ANT_BSA (*)  = 0;                                              
         ANT_ØVR (*)  = 0;                                              
         ANT_PSA (*)  = 0;                                              
         ANT_PSAD (*) = 0;                                              
         ANT_PSAK (*) = 0;                                              
         ANT_EJSKEMA (*) = 0;                                           
         ANT_PSAO (*) = 0;                                              
         ANT_PSAP (*) = 0;                                              
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
         DO WHILE ( ^EOF_BH611 );                                       
            CALL BEHANDL_INDIVID;                                       
            CALL LÆS;                                                   
         END;     /*  DO WHILE */                                       
         CALL KØRSELSKONTROL;                                           
         CALL MASK_AFSTEMNING;                                          
         CALL AFSLUT;                                                   
 /*********************************************************************/
 /*      LÆSNING AF BEREGNINGSBÅND                                    */
 /*********************************************************************/
 LÆS :                                                                  
         PROC;                                                          
         READ FILE(BH611I) INTO (BH611_AR);                             
         IF BH611.EKOMNR ^= 999 & ^EOF_BH611                            
         THEN                                                           
            DO;                                                         
               IF BH611.DCPRCIR = 0  /* KOMMUNEINDIVID */               
               THEN                                                     
                  ANT_LÆSKOM = ANT_LÆSKOM + 1;                          
               ELSE                                                     
                  ANT_LÆS  = ANT_LÆS + 1;                               
               IF SUBSTR(EX_PARM,1,4) = 'TEST' &                        
                  BH611.EKOMNR ^= 0                                     
               THEN                                                     
                  DO;                                                   
                     IF SUBSTR(EX_PARM,5,1) = BH611.CSMUDS              
                     THEN                                               
                        BH611.CSMUDS = SUBSTR(EX_PARM,6,1);             
                     IF SUBSTR(EX_PARM,7,1) = BH611.CSMUDS              
                     THEN                                               
                        BH611.CSMUDS = SUBSTR(EX_PARM,8,1);             
                     IF BH611.CAJOUR = 'P' &                            
                        BH611.CSMUDS = 'M' &                            
                        BH611.EEJDNR >= 110000                          
                     THEN                                               
                        BH611.CSMUDS = 'N';                             
                     IF BH611.CAJOUR = 'P' &                            
                        BH611.CSÆREJD = 1  &  /* DØD IFLG. MT */        
                       (BH611.EEJDNR >= 50000 &                         
                        BH611.EEJDNR <= 112999)                         
                     THEN                                               
                        BH611.CSMUDS = 'P';                             
                     IF BH611.CAJOUR = 'P' &                            
                        BH611.CSÆREJD = 2  &  /* FØR EJ PÅ MT */        
                       (BH611.EEJDNR >= 50000 &                         
                        BH611.EEJDNR <= 112999)                         
                     THEN                                               
                        BH611.CSMUDS = 'O';                             
                  END;                                                  
               IF BH611.CRCKO = 1                                       
               THEN                                                     
                  INDEX = 2;                                            
               ELSE                                                     
                  INDEX = BH611.CRCKO;                                  
            END;                                                        
 END LÆS;                                                               
 /*********************************************************************/
 /*      BEHANDLING AF INDIVIDER SOM SKAL OVERFØRES TIL RC            */
 /*********************************************************************/
 BEHANDL_INDIVID:                                                       
         PROC;                                                          
         IF BH611.EKOMNR = 999                                          
         THEN                                                           
            RETURN;                                                     
         IF BH611.CRCKO ^= GEM_CRCKO                                    
         THEN                                                           
             CALL REGISTER_SKIFT;                                       
         ANT_OVF(INDEX) = ANT_OVF(INDEX)+ 1;                            
         ANT_OVF(1)     = ANT_OVF(1) + 1;                               
         IF BH611.DCPRCIR = 0                                           
         THEN                                                           
            DO;                                                         
               /* KOMMUNEINDIVID */                                     
               ANT_KOM(1) = ANT_KOM(1) + 1;                             
               ANT_KOM(INDEX) = ANT_KOM(INDEX) + 1;                     
            END;                                                        
         ELSE                                                           
            CALL OPTÆL;                                                 
         WRITE FILE(BH605O) FROM (BH611_AR);                            
 END BEHANDL_INDIVID;                                                   
 /*********************************************************************/
 /*      CLOSE OG OPEN AF FILER PR. CENTRAL                           */
 /*      DAN SLUTINDIVID PR. CENTRAL                                  */
 /*********************************************************************/
 REGISTER_SKIFT:                                                        
         PROC;                                                          
         CALL DAN_OPT_INDV;                                             
         CLOSE FILE (BH605O);                                           
         OPEN  FILE (BH605O) TITLE ('BH605' !! BH611.CRCKO !! '0O');    
         GEM_CRCKO = BH611.CRCKO;                                       
 END REGISTER_SKIFT;                                                    
 /*********************************************************************/
 /*      DER DANNES ET SLUTINDIVID PR. CENTRAL TIL BRUG FOR MASKINEL  */
 /*      AFSTEMNING                                                   */
 /*********************************************************************/
 DAN_OPT_INDV:                                                          
         PROC;                                                          
         BH60599 = '';                                                  
         BH60599.EKOMNR = 999;                                          
         BH60599.EEJDNR = 8888888;                                      
         BH60599.DCPRCIR= 99999999999;                                  
         BH60599.CITYP  = 9;                                            
         BH60599.EÆKNR  = 999;                                          
         BH60599.DSORP  = 99999999999;                                  
         BH60599.CRCKO  = GEM_CRCKO;                                    
         IF GEM_CRCKO = 1                                               
         THEN                                                           
            GEM_CRCKO = 2;                                              
         BH60599.ANT_INDV  = ANT_OVF(GEM_CRCKO) - ANT_KOM(GEM_CRCKO);   
         BH60599.ANT_SKEMA = ANT_UDSKR(GEM_CRCKO) +                     
                             ANT_EJSKEMA(GEM_CRCKO);                    
         BH60599.ANT_KOM   = ANT_KOM(GEM_CRCKO);                        
         WRITE FILE (BH605O) FROM (BH60599);                            
 END DAN_OPT_INDV;                                                      
 /*********************************************************************/
 /*      OPTÆLLING AF INDIVIDER PR. CENTRAL OG IALT                   */
 /*********************************************************************/
 OPTÆL:                                                                 
         PROC;                                                          
         IF BH611.CUDEJD = 0                                            
         THEN                                                           
            DO;                                                         
               ANT_EJUDSKR(1) = ANT_EJUDSKR(1)+ 1;                      
               ANT_EJUDSKR(INDEX) = ANT_EJUDSKR(INDEX)+ 1;              
               RETURN;                                                  
            END;                                                        
         IF (BH611.CSMUDS = 'H' !                                       
             BH611.CSMUDS = 'O' !                                       
             BH611.CSMUDS = 'P')                                        
         THEN                                                           
            DO;                                                         
               ANT_EJSKEMA(INDEX) = ANT_EJSKEMA(INDEX)+ 1;              
               ANT_EJSKEMA(1)     = ANT_EJSKEMA(1) + 1;                 
            END;                                                        
         ELSE                                                           
            DO;                                                         
               ANT_UDSKR(INDEX) = ANT_UDSKR(INDEX)+ 1;                  
               ANT_UDSKR(1)     = ANT_UDSKR(1) + 1;                     
            END;                                                        
         IF BH611.CSMUDS >= 'B' &                                       
            BH611.CSMUDS <= 'D'                                         
         THEN                                                           
           DO;                                                          
            ANT_USA(1) = ANT_USA(1) + 1;                                
            ANT_USA(INDEX) =                                            
            ANT_USA(INDEX) + 1;                                         
           END;                                                         
         ELSE                                                           
            IF BH611.CSMUDS >= 'E' &                                    
               BH611.CSMUDS <= 'G'                                      
            THEN                                                        
              DO;                                                       
               ANT_BSA(1) = ANT_BSA(1) + 1;                             
               ANT_BSA(INDEX) =                                         
               ANT_BSA(INDEX) + 1;                                      
              END;                                                      
            ELSE                                                        
               IF BH611.CSMUDS = 'M'                                    
               THEN                                                     
                 DO;                                                    
                  ANT_PSAD(1) = ANT_PSAD(1) + 1;                        
                  ANT_PSAD(INDEX) =                                     
                  ANT_PSAD(INDEX) + 1;                                  
                 END;                                                   
               ELSE                                                     
                  IF BH611.CSMUDS = 'N'                                 
                  THEN                                                  
                    DO;                                                 
                     ANT_PSAK(1) = ANT_PSAK(1) + 1;                     
                     ANT_PSAK(INDEX) =                                  
                     ANT_PSAK(INDEX) + 1;                               
                    END;                                                
                    ELSE                                                
                       IF BH611.CSMUDS = 'H'                            
                       THEN                                             
                         DO;                                            
                          ANT_PSA(1) = ANT_PSA(1) + 1;                  
                          ANT_PSA(INDEX) =                              
                          ANT_PSA(INDEX) + 1;                           
                         END;                                           
                        ELSE                                            
                           IF BH611.CSMUDS = 'O'                        
                           THEN                                         
                             DO;                                        
                              ANT_PSAO(1) = ANT_PSAO(1) + 1;            
                              ANT_PSAO(INDEX) =                         
                              ANT_PSAO(INDEX) + 1;                      
                             END;                                       
                           ELSE                                         
                              IF BH611.CSMUDS = 'P'                     
                              THEN                                      
                                DO;                                     
                                 ANT_PSAP(1) = ANT_PSAP(1) + 1;         
                                 ANT_PSAP(INDEX) =                      
                                 ANT_PSAP(INDEX) + 1;                   
                                END;                                    
                              ELSE                                      
                                DO;                                     
                                 ANT_ØVR(1) = ANT_ØVR(1) + 1;           
                                 ANT_ØVR(INDEX) =                       
                                 ANT_ØVR(INDEX) + 1;                    
                                END;                                    
 END OPTÆL;                                                             
 /*********************************************************************/
 /*      UDSKRIVNING AF KØRSELSKONTROLLISTE                           */
 /*********************************************************************/
 KØRSELSKONTROL:                                                        
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST  = 'ANTAL LÆSTE INDIVIDER EXCL KOM PÅ B.H61101D';
         DETLIN1.ANTAL = ANT_LÆS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.CCA   = ZZWS3;                                         
         DETLIN1.TEKST  = 'ANTAL LÆSTE KOMMUNEINDIVIDER   PÅ B.H61101D';
         DETLIN1.ANTAL = ANT_LÆSKOM;                                    
         CALL ZZ20300(DETLIN1,'01');                                    
         CALL ZZ20300(OVERLIN3,'01');                                   
         DETLIN2.TEKST = 'ANTAL OVERFØRTE INDIVIDER';                   
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_OVF(I);                                
         END;                                                           
         DETLIN2.CCA   = ZZWS3;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '      HERAF KOMMUNEINDIVIDER';                
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_KOM(I);                                
         END;                                                           
         DETLIN2.CCA   = ZZWS3;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '      HERAF EJD EJ TIL UDSKRIV AF EJDOPLYSN'; 
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_EJUDSKR(I);                            
         END;                                                           
         DETLIN2.CCA   = ZZWS3;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = 'ANTAL INDIVIDER TIL UDSKRIVNING (EXCL. PSA)'; 
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_UDSKR(I);                              
         END;                                                           
         DETLIN2.CCA   = ZZWS1;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF PÅ USA (HVIS PLADS)';                
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_USA(I);                                
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF PÅ BSA (HVIS PLADS)';                
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_BSA(I);                                
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF ESKEMA (PSA EJ PLADS - DIREKTE UD)'; 
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_PSAD(I);                               
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF ESKEMA (PSA EJ PLADS - KOMMUNEN)';   
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_PSAK(I);                               
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF ESKEMA (ØVRIGE - DØDE, EJ PÅ MT)';   
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_ØVR(I);                                
         END;                                                           
         DETLIN2.CCA   = ZZWS3;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = 'ANTAL INDIVIDER TIL UDSKRIVNING (KUN PÅ PSA)';
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_EJSKEMA(I);                            
         END;                                                           
         DETLIN2.CCA   = ZZWS1;                                         
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF PÅ PSA';                             
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_PSA(I);                                
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF DØDE UDSKREVET PÅ EFTERLEV. (PSA)';  
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_PSAP(I);                               
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
         DETLIN2.TEKST = '   HERAF SENERE OPTAGET PÅ MANDTAL (PSA)   '; 
         DO I = 1 TO 6;                                                 
            DETLIN2.ANT(I) = ANT_PSAO(I);                               
         END;                                                           
         CALL ZZ20300(DETLIN2,'01');                                    
 END KØRSELSKONTROL;                                                    
 /********************************************************************* 
 *            MASKINEL AFSTEMNING                                     * 
 *********************************************************************/ 
 MASK_AFSTEMNING:                                                       
         PROC;                                                          
         IF ^EOF_BH611 !                /* OPTÆLLINGSINDIVID MANGLER */ 
            BH611.EKOMNR ^= 999                                         
         THEN                                                           
            DO;                                                         
               CALL AFSTEM_FEJL('1');                                   
               CALL ZS30101(5,'BH60500 ** 251 ' !!                      
                        'KONSTATERET AFSTEMNINGSFEJL              ',2); 
            END;                                                        
         IF BH61199.ANT_INDV ^= ANT_LÆS                                 
         THEN                                                           
            DO;                                                         
               CALL AFSTEM_FEJL('2');                                   
               CALL ZS30101(5,'BH60500 ** 251 ' !!                      
                        'KONSTATERET AFSTEMNINGSFEJL              ',2); 
            END;                                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      UDSKRIV FEJLLISTE HVIS DER ER KONSTATERET FEJL VED           */
 /*      MASKINEL AFSTEMNING                                          */
 /*********************************************************************/
 AFSTEM_FEJL:                                                           
         PROC(FEJL);                                                    
 DCL     FEJL            CHAR      (01);                                
 DCL     1 FLIN1,                                                       
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (44)      INIT      (' '),           
           2 F2          CHAR      (88)      INIT      (                
           'FEJLLISTE VEDR. MASKINEL AFSTEMNING');                      
 DCL     1 FLIN2,                                                       
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (55),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (68)      INIT      (' ');           
         CALL ZZ20390 ('*OPEN','BH60502Y');                             
         SIDELIN.CCA = ZZIK1;                                           
         CALL ZZ20300 (SIDELIN,'02');                                   
         OVERLIN1.CCA = ZZWS2;                                          
         CALL ZZ20300 (OVERLIN1,'02');                                  
         FLIN1.CCA = ZZWS2;                                             
         CALL ZZ20300 (FLIN1,'02');                                     
         SELECT (FEJL);                                                 
            WHEN('1')                   /* OPTÆLLINGSINDIVID MANGLER */ 
            DO;                                                         
               SIDELIN.CCA = ZZWS2;                                     
               SIDELIN.FILLER= 'OPTÆLLINGSINDIVID MANGLER';             
               CALL ZZ20300 (SIDELIN,'02');                             
            END;                                                        
            WHEN('2')                   /* ANTAL LÆSTE STEMMER IKKE */  
            DO;                                                         
               FLIN2.CCA   = ZZWS2;                                     
               FLIN2.TEKST = 'ANTAL LÆSTE RECORDS PÅ B.H61101D';        
               FLIN2.ANTAL = ANT_LÆS;                                   
               CALL ZZ20300 (FLIN2,'02');                               
               FLIN2.CCA   = ZZWS2;                                     
               FLIN2.TEKST = 'ANTAL RECORDS PÅ B.H61101D IFØLGE OPTÆLLIN
 GSINDIVID';                                                            
               FLIN2.ANTAL = BH61199.ANT_INDV;                          
               CALL ZZ20300 (FLIN2,'02');                               
            END;                                                        
         END;    /* SELECT */                                           
 END AFSTEM_FEJL;                                                       
 /*********************************************************************/
 /*      LUKNING AF FILER                                             */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
         CALL ZZ20300(' ','99');                                        
         CALL DAN_OPT_INDV;                                             
         CLOSE FILE (BH605O);                                           
         CLOSE FILE (BH611I);                                           
 END AFSLUT;                                                            
 END BH60500;                                                           
