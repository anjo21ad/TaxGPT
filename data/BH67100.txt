  PROCESS OPT(TIME);                                                    
 BH67100: /* OPDEL BEREGNINGSBÅND I SAMBESKATTEDE OG ØVRIGE EJENDOMME */
         PROC OPTIONS (MAIN) REORDER;                                   
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');       
 /*********************************************************************/
 /*                                                                   */
 /*      FORMÅL       FIND 'MODEREJENDOM' TIL 'TILSTØDENDE GRUND' OG  */
 /*                   OPDEL BEREGNINGSBÅND I SAMBESKATTEDE OG         */
 /*                   ØVRIGE EJENDOMME                                */
 /*                                                                   */
 /*      INDDATA      B.H67000D BEREGNINGSBÅND INCL. TO-EJERE         */
 /*                                                                   */
 /*      UDDATA       B.H67100D EJENDOMSOPLYSNINGER FOR SAMBESKATTEDE */
 /*                   B.H67110D EJENDOMSOPLYSNINGER FOR ØVRIGE EJD.   */
 /*                   B.H67101Y KØRSELSKONTROLLISTE                   */
 /*                                                                   */
 /*      PROGRAMMØR   AAGE RASMUSSEN              PFB      JAN 1998   */
 /*                                                                   */
 /*      ÅRSTILRET    AAGE RASMUSSEN              PFB, JULI    1998   */
 /*      ÅRSTILRET    AAGE RASMUSSEN              PFB, JANUAR  2000   */
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*                                                                   */
 /*      FILE-DECLARE                                                 */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH670I          FILE RECORD INPUT;                             
 DCL     BH6710O         FILE RECORD OUTPUT;                            
 DCL     BH6711O         FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*                                                                   */
 /*      ENTRY-DECLARE                                                */
 /*                                                                   */
 /*********************************************************************/
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM);   /* SSI-DATO     */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*                                                                   */
 /*      STRUCTUR-DECLARE  INPUT                                      */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH601_AR CHAR (4000) VAR;                                      
 DCL     1 BH601 BASED(ADDR(BH601_AR)),                                 
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60100N;                                           
 DCL     1 BH60199,                                                     
           %INCLUDE BH60199N;                                           
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF PRINTLINIER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 LIN1,                                                        
          2 CTL          CHAR      (1),                                 
          2 TX1          CHAR      (47)      INIT                       
                         ('PROGRAM: BH67100L'),                         
          2 TX2          CHAR      (59)      INIT                       
                         ('OPDEL BEREGNINGSBÅND I SAMBESKAT OG ØVRIGE'),
          2 TX3          CHAR      (14)      INIT                       
                         ('UDSKREVET DEN '),                            
          2 DATO         CHAR      (10),                                
          2 REST         CHAR      (02)      INIT      (' ');           
 DCL     1 LIN2,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (54)      INIT      ('  '),          
          2 TX1          CHAR      (20)      INIT                       
                         ('KØRSELSKONTROLLISTE'),                       
          2 REST         CHAR      (58)      INIT      (' ');           
 DCL     1 LIN3,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (2)       INIT      (' '),           
          2 TEKST        CHAR      (52),                                
          2 ANT          PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZZ',               
          2 REST         CHAR      (59)      INIT      (' ');           
 DCL     1 LIN_BLANK,                                                   
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (132)     INIT      (' ');           
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     TV_LÆS          FIXED     (9)       INIT      (0);             
 DCL     TV_KOM          FIXED     (9)       INIT      (0);             
 DCL     TV_SKRIV_ØVR    FIXED     (9)       INIT      (0);             
 DCL     TV_SKRIV_SAM    FIXED     (9)       INIT      (0);             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF BUILTIN                                           */
 /*                                                                   */
 /*********************************************************************/
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*                                                                   */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2                              BASED(ADDR(DATO1)),       
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     EOF             PIC       '9'       INIT (0);                  
                                                                        
 DCL     1 GEM           STATIC,    /* GEM FOR SAMME PERSON           */
         2 DCPRCIR       FIXED (10),/* CPRNR.                         */
         2 FLIN          FIXED (5), /* IALT ANTAL 'FASTE' LINIER      */
         2 FEJD          FIXED (5), /* ANTAL EJEDE EJENDOMME          */
         2 CUDLEJNING    CHAR  (1), /* EJER MINDST EEN UDLEJNINGSEJD. */
         2 CRENERKL      PIC   '9', /* EJER KUN ERKLÆRBARE            */
         2 CLIN16        PIC   '9', /* SLUTLINIE TIL USA              */
         2 FANT_SAM      FIXED (3), /* ANTAL SAMBESKATNINGSNUMRE      */
         2 SAMTAB(25),                                                  
           3 SKOM        FIXED (3), /* SAMBESKATNINGSNUMMER FOR MODER-*/
           3 SEJD        FIXED (7), /* EJENDOMMEN(E)                  */
           3 SKOMPONEN   CHAR  (1), /* KOMPONENTKODE TIL SLS          */
           3 SCERKLÆR    PIC   '9', /* KODE ERKLÆRBAR EJENDOM         */
           3 SCLEJE      CHAR  (1), /* LEJEVÆRDIKODE TIL BEREGNING    */
           3 SCEJEV      CHAR  (1), /* KODE FOR EJ VURDERET           */
           3 SBENYT      CHAR  (2), /* BENYTTELSESKODE                */
           3 SFLEJL      FIXED (5), /* ANTAL LEJLIGHEDER              */
           3 SCOVG1998   FIXED (1), /* KODE FOR OVERGANGSORDNING 1998 */
           3 SGANE       FIXED (5), /* EJERANDEL                      */
           3 SFRADAG     FIXED (9), /* STARTDATO FOR BEREGNING        */
           3 STILDAG     FIXED (9), /* SLUTDATO  FOR BEREGNING        */
           3 ANTMOD      FIXED (3), /* ANTAL MODEJD MED DETTE NUMMER  */
           3 GKOM        FIXED (3), /* EKOMNR+EEJDNR FOR  FØRSTE  BK09*/
           3 GEJD        FIXED (7), /* TIL SAMBESKATN. MED OVENNÆVNTE */
           3 GKOMPONEN   CHAR  (1), /* KOMPONENTKODE TIL SLS          */
           3 GCERKLÆR    PIC   '9', /* KODE ERKLÆRBAR EJENDOM         */
           3 GCLEJE      CHAR  (1), /* LEJEVÆRDIKODE TIL BEREGNING    */
           3 GCEJEV      CHAR  (1), /* KODE FOR EJ VURDERET           */
           3 GEJDVÆR     FIXED (11),/* EJENDOMSVÆRDI                  */
           3 GLEJE       FIXED (11),/* LEJEV. NORMAL                  */
           3 GLEJEFP     FIXED (11),/* LEJEV. FØRTIDSPENS./EFTERL.    */
           3 GLEJE67     FIXED (11),/* LEJEVÆRDI PENSIONIST           */
           3 GOVLEJE     FIXED (11),/* LEJEV. NORMAL                  */
           3 GOVLEJEFP   FIXED (11),/* LEJEV. FØRTIDSPENS./EFTERL.    */
           3 GOVLEJE67   FIXED (11),/* LEJEVÆRDI PENSIONIST           */
           3 GGANE       FIXED (5), /* EJERANDEL                      */
           3 GFRADAG     FIXED (9), /* STARTDATO FOR BEREGNING        */
           3 GTILDAG     FIXED (9), /* SLUTDATO  FOR BEREGNING        */
           3 ANT09       FIXED (3); /* ANTAL BK 09 MED DETTE NUMMER   */
 /*********************************************************************/
 /*                                                                   */
 /*      HOUSE-KEEPING                                                */
 /*                                                                   */
 /*********************************************************************/
         ON ERROR CALL PLIDUMP('SNHOB');                                
         CALL ZS61900;                 /*   UDSKRIVNING AF SSI-DATO   */
         LIN_BLANK.CTL = ZZIK1;                                         
         LIN1.CTL = ZZWS2;                                              
         LIN2.CTL = ZZWS3;                                              
         LIN3.CTL = ZZWS2;                                              
         OPEN FILE (BH670I)  TITLE ('BH67000I');                        
         OPEN FILE (BH6710O) TITLE ('BH67100O');                        
         OPEN FILE (BH6711O) TITLE ('BH67110O');                        
         CALL ZZ20390 ('*OPEN','BH67101Y');                             
         ON ENDFILE (BH670I)                                            
         BEGIN;                                                         
            EOF = 1;                                                    
         END;                                                           
         GEM = '';                                                      
         DO I = 1 TO 25;                                                
            GEM.SAMTAB(I) = '';                                         
         END;                                                           
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         LIN1.DATO = UDDATO;                                            
 /*********************************************************************/
 /*                                                                   */
 /*      PROCES                                                       */
 /*                                                                   */
 /*********************************************************************/
                                                                        
         READ FILE (BH670I) INTO (BH601_AR);                            
                                                                        
         DO WHILE (EOF = 0);                                            
            TV_LÆS = TV_LÆS + 1;                                        
            IF BH601.SORT_KOMNR = 999 /* KOMMUNE-INDIVID        */      
            THEN                                                        
               TV_KOM = TV_KOM + 1;                                     
            ELSE                                                        
               DO;                                                      
                  IF BH601.CINVPNR ^= '0'   /* KUN VALIDE CPRNR */      
                  THEN                      /* FÅR FORSØGT      */      
                     DO;                    /* SAMBESKATNING    */      
                        BH601.CSAMBSKAT = ' ';                          
                        BH601.FANT_SAM = 0;                             
                     END;                                               
                  ELSE                                                  
                     DO;                                                
                        CALL OPSPLIT_BEREGNINGSBÅND;                    
                        CALL OVF_SAMLET_STATUS;                         
                     END;                                               
                  CALL SKRIV_DATASÆT;                                   
               END;                                                     
                                                                        
            READ FILE (BH670I) INTO (BH601_AR);                         
         END;                                                           
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*    AFSLUT PROGRAM                                                 */
 /*                                                                   */
 /*********************************************************************/
                                                                        
         CALL DAN_OPT_INDV;                                             
                                                                        
         CALL SKRIV_TOTALER;                                            
                                                                        
         CALL ZZ20300(LIN_BLANK,'99');                                  
                                                                        
         CLOSE FILE (BH670I),                                           
               FILE (BH6710O),                                          
               FILE (BH6711O);                                          
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*    UNDERSØG OM INDIVID ER TIL SAMBESKATNING ELLER ØVRIGE          */
 /*                                                                   */
 /*********************************************************************/
 OPSPLIT_BEREGNINGSBÅND:                                                
 PROC;                                                                  
                                                                        
         IF GEM.DCPRCIR ^= BH601.DCPRCIR                                
         THEN                                                           
            DO;                                                         
               /* NY PERSON SÅ TAG LIGE OG INITIER DIV. TÆLLEVÆRKER */  
               /* M.V. TIL STYRING AF SLUTLINIE-REDIGERING          */  
               GEM = '';                                                
               DO I = 1 TO 25;                                          
                  GEM.SAMTAB(I) = '';                                   
               END;                                                     
               GEM.DCPRCIR    = BH601.DCPRCIR;                          
               GEM.FLIN       = BH601.FLIN;                             
               GEM.FEJD       = BH601.FEJD; /* KUN HVIS NY PERSON */    
               GEM.CUDLEJNING = BH601.CUDLEJNING;                       
               GEM.CRENERKL   = BH601.CRENERKL;                         
               GEM.CLIN16     = BH601.CLIN16;                           
               GEM.FANT_SAM   = BH601.FANT_SAM;                         
               IF BH601.FANT_SAM > 0                                    
               THEN                                                     
                  DO;                                                   
                     DO I = 1 TO BH601.FANT_SAM;                        
                        GEM.SAMTAB(I) = BH601.SAMTAB(I), BY NAME;       
                     END;                                               
                  END;                                                  
            END;                                                        
                                                                        
         IF GEM.FANT_SAM > 0 &                                          
            BH601.CBENYT ^= '09'                                        
         THEN                                                           
            DO;                                                         
               DO I = 1 TO GEM.FANT_SAM;                                
                  IF (GEM.SAMTAB(I).SKOM = BH601.EKOMNR &               
                      GEM.SAMTAB(I).SEJD = BH601.EEJDNR)                
                  THEN                                                  
                     DO;                                                
                        CALL AJF_MODEREJD;                              
                        GEM.SAMTAB(I).ANTMOD =                          
                        GEM.SAMTAB(I).ANTMOD + 1;                       
                        BH601.UDSKRLBNR = I;                            
                        BH601.CSAMTYP   = 2;                            
                        I = GEM.FANT_SAM;                               
                     END;                                               
               END;                                                     
            END;                                                        
                                                                        
 END OPSPLIT_BEREGNINGSBÅND;                                            
 /*********************************************************************/
 /*                                                                   */
 /*      MODEREJD FUNDET OG GEM AJOURFØRES MED DATA HERFRA, MEN KUN   */
 /*      FRA DET FØRST MØDTE INDIVID MED SAMME KOMMUNE-/EJENDOMSNUM-  */
 /*      MER. DET AKTUELLE MODEREJD-INDIVID AJOURFØRES MED DATA FRA   */
 /*      DEN I BH60100 FØRST MØDTE TILSTØDENDE GRUND.                 */
 /*                                                                   */
 /*********************************************************************/
 AJF_MODEREJD:                                                          
 PROC;                                                                  
                                                                        
         IF GEM.SAMTAB(I).ANTMOD = 0                                    
         THEN                                                           
            DO;                                                         
               /* AJOURFØR GEM MED OPLYSNINGER FRA DET 'FØRSTE'  */     
               /* MODEREJD-INDIVID HVIS EKOMNR+EEJDNR ER LIG DET */     
               /* AKTUELLE GEM.SAMTAB(I).SKOM+SEJD               */     
               GEM.SAMTAB(I).SKOMPONEN = BH601.CKOMPONEN;               
               GEM.SAMTAB(I).SCERKLÆR  = BH601.CERKLÆR;                 
               GEM.SAMTAB(I).SCLEJE    = BH601.CLEJE;                   
               GEM.SAMTAB(I).SCEJEV    = BH601.CEJEV;                   
               GEM.SAMTAB(I).SBENYT    = BH601.CBENYT;                  
               GEM.SAMTAB(I).SFLEJL    = BH601.FLEJL;                   
               IF BH601.COVG_1998 = '2'                                 
               THEN                                                     
                  GEM.SAMTAB(I).SCOVG1998 = 2;           /* HELÅRSSOM */
               GEM.SAMTAB(I).SGANE     = BH601.GANE;                    
               GEM.SAMTAB(I).SFRADAG   = BH601.FRADAG;                  
               GEM.SAMTAB(I).STILDAG   = BH601.TILDAG;                  
            END;                                                        
                                                                        
         /* GEM OPLYSNINGER FRA 'OPRINDELIG BEREGNING' A.H.T. */        
         /* UDSKRIVNING AF LINIER VEDR. LEJEVÆRDI SELVOM SAM- */        
         /* BESKATNING MED TILSTØDENDE GRUND IKKE KAN GENNEM- */        
         /* FØRES MASKINELT.                                  */        
                                                                        
         BH601.MLEJE1  = BH601.BLEJE1;                                  
         BH601.MLEJE2  = BH601.BLEJE2;                                  
         BH601.MSTDF   = BH601.BSTDF;                                   
         BH601.MREDIG3 = BH601.CREDIG3;                                 
         BH601.MLIN03  = BH601.CLIN03;                                  
         BH601.MLIN06  = BH601.CLIN06;                                  
                                                                        
         /* RESÆT REDIGERINGSKODE FOR MLIN03(LEJEVÆRDI) */              
         /* DA DEN IKKE MÅ ANGIVE UDSKRIVNING AF MNETTO */              
         /* HVIS SAMBESKATNINGS-BEREGNING IKKE UDFØRES  */              
         /* (EJENDOMMEN ER DA IKKE ERKLÆRBAR)           */              
                                                                        
         SELECT ( BH601.MREDIG3 );                                      
            WHEN ('3','4')                   /* IKKE PENSIONIST */      
               BH601.MREDIG3 = '2';          /* MED STD.FRAD.   */      
            WHEN ('5')                       /* IKKE PENSIONIST */      
               BH601.MREDIG3 = '1';          /* UDEN STD.FRAD.  */      
            WHEN ('8','9')                   /* PENSIONIST      */      
               BH601.MREDIG3 = '7';          /* MED STD.FRAD.   */      
            WHEN ('A')                       /* PENSIONIST      */      
               BH601.MREDIG3 = '6';          /* UDEN STD.FRAD.  */      
            OTHERWISE;                                                  
         END;                                                           
                                                                        
         /* INITIER DIVERSE FELTER A.H.T. TIL EVT. MASKINEL */          
         /* SAMBESKATNING MED TILSTØDENDE GRUND.            */          
                                                                        
         BH601.CSAMBSKAT = '2';                   /* DEFAULT=EJ BEREGN*/
         BH601.ESAMBKNR = GEM.SAMTAB(I).GKOM;                           
         BH601.ESAMBENR = GEM.SAMTAB(I).GEJD;                           
         BH601.BERGRL2  = GEM.SAMTAB(I).GEJDVÆR;                        
                                                                        
         /* RESÆT REDIGERINGSKODE FOR CLIN03(LEJEVÆRDI) */              
         /* DA DEN SKAL GENOPSÆTTES I BH67500 EFTER     */              
         /* EVT. GENNEMFØRT SAMBESKATNINGS-BEREGNING.   */              
                                                                        
         SELECT ( BH601.CREDIG3 );                                      
            WHEN ('2','3','4','5')  /* IKKE PENSIONIST*/                
               BH601.CREDIG3 = '1';                                     
            WHEN ('7','8','9','A')  /* PENSIONIST */                    
               BH601.CREDIG3 = '6';                                     
            OTHERWISE;                                                  
         END;                                                           
                                                                        
         BH601.MODEJD_1999(*) =                                         
         BH601.EJD_1999(*), BY NAME;                                    
         BH601.MODSOM_1999(*) =                                         
         BH601.SOM_1999(*), BY NAME;                                    
         BH601.EJD_1999(1).BLEJE   =                                    
         GEM.SAMTAB(I).GLEJE;                                           
         BH601.EJD_1999(1).BLEJEFP =                                    
         GEM.SAMTAB(I).GLEJEFP;                                         
         BH601.EJD_1999(1).BLEJE67 =                                    
         GEM.SAMTAB(I).GLEJE67;                                         
         IF BH601.COVG_1998 = '1'                                       
         THEN                                                           
            DO;                                                         
               BH601.COVG_1998 = '*';                                   
            END;                                                        
         ELSE                                                           
            IF BH601.COVG_1998 = '2'                                    
            THEN                                                        
               DO;                                                      
                  BH601.OVG_1998.BLEJE =                                
                  GEM.SAMTAB(I).GOVLEJE;                                
                  BH601.OVG_1998.BLEJEFP =                              
                  GEM.SAMTAB(I).GOVLEJEFP;                              
                  BH601.OVG_1998.BLEJE67 =                              
                  GEM.SAMTAB(I).GOVLEJE67;                              
               END;                                                     
                                                                        
 END AJF_MODEREJD;                                                      
 /********************************************************************/ 
 /*                                                                  */ 
 /*      OVERFØR DEN SAMLEDE STATUS FOR PERSONEN VEDR. AFGØRELSE OM  */ 
 /*      ØNSKE OM SAMBESKATNING ER MULIGT AT UDFØRE SAMT DIVERSE OP- */ 
 /*      LYSNINGER TIL BRUG VED STYRING AF UDSKRIVNING AF EJENDOMS-  */ 
 /*      OPLYSNINGER PÅ HENHOLDSVIS PSA, USA, BSA ELLER EJENDOMSSKE- */ 
 /*      MA.                                                         */ 
 /*                                                                  */ 
 /********************************************************************/ 
 OVF_SAMLET_STATUS:                                                     
 PROC;                                                                  
                                                                        
         BH601.FLIN       = GEM.FLIN;                                   
         BH601.FEJD       = GEM.FEJD;                                   
         BH601.CUDLEJNING = GEM.CUDLEJNING;                             
         BH601.CRENERKL   = GEM.CRENERKL;                               
         BH601.CLIN16     = GEM.CLIN16;                                 
                                                                        
         /* OVERFØR STATUS VEDR. SAMBESKATNING TIL AKTUELT INDIVID */   
                                                                        
         IF GEM.FANT_SAM > 0                                            
         THEN                                                           
            DO;                                                         
               BH601.FANT_SAM = GEM.FANT_SAM;                           
               DO I = 1 TO GEM.FANT_SAM;                                
                  BH601.SAMTAB(I) = GEM.SAMTAB(I), BY NAME;             
               END;                                                     
            END;                                                        
                                                                        
 END OVF_SAMLET_STATUS;                                                 
 /*********************************************************************/
 /*                                                                   */
 /*    SKRIV DATASÆT                                                  */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_DATASÆT:                                                         
 PROC;                                                                  
                                                                        
         /* OPSÆT LÆNGDE PÅ AKTUELT INDIVID JVF. BH601.FANT_SAM */      
                                                                        
         BH601.LÆNGDE = 1250 + (BH601.FANT_SAM * 98);                   
                                                                        
         IF BH601.CSAMBSKAT < '1'                                       
         THEN                                                           
            DO;                                                         
               WRITE FILE (BH6711O) FROM (BH601_AR);                    
               TV_SKRIV_ØVR = TV_SKRIV_ØVR + 1;                         
            END;                                                        
         ELSE                                                           
            DO;                                                         
               WRITE FILE (BH6710O) FROM (BH601_AR);                    
               TV_SKRIV_SAM = TV_SKRIV_SAM + 1;                         
            END;                                                        
                                                                        
 END SKRIV_DATASÆT;                                                     
 /*********************************************************************/
 /*      DER DANNES ET SLUTINDIVID TIL BRUG FOR MASKINEL AFSTEMNING   */
 /*********************************************************************/
 DAN_OPT_INDV:                                                          
 PROC;                                                                  
                                                                        
         BH60199          = '';                                         
         BH60199.SORT_KOMNR = 999;                                      
         BH60199.SORT_EJDNR = 8888888;                                  
         BH60199.SORT_CPR = 99999999999;                                
         BH60199.SORT_CITYP = 9;                                        
         BH60199.SORT_EÆKNR = 999;                                      
         BH60199.ANT_INDV = TV_SKRIV_SAM;                               
         WRITE FILE (BH6710O) FROM (BH60199);                           
                                                                        
         BH60199          = '';                                         
         BH60199.SORT_KOMNR = 999;                                      
         BH60199.SORT_EJDNR = 8888888;                                  
         BH60199.SORT_CPR = 99999999999;                                
         BH60199.SORT_CITYP = 9;                                        
         BH60199.SORT_EÆKNR = 999;                                      
         BH60199.ANT_INDV = TV_SKRIV_ØVR;                               
         WRITE FILE (BH6711O) FROM (BH60199);                           
                                                                        
 END DAN_OPT_INDV;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_TOTALER:                                                         
 PROC;                                                                  
                                                                        
         CALL ZZ20300(LIN_BLANK,'01');                                  
         CALL ZZ20300(LIN1,'01');                                       
         CALL ZZ20300(LIN2,'01');                                       
         LIN3.TEKST ='ANTAL LÆSTE INDIVIDER PÅ BH67000D BEREGNINGSBÅND';
         LIN3.ANT = TV_LÆS;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.TEKST = 'ANTAL FRASORTEREDE KOMMUNEHEADERE';              
         LIN3.ANT = TV_KOM;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS2;                                              
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER VEDR. SAMBESKATNING    ';
         LIN3.ANT = TV_SKRIV_SAM;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS2;                                              
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER VEDR. ØVRIGE EJENDOMME ';
         LIN3.ANT = TV_SKRIV_ØVR;                                       
         CALL ZZ20300(LIN3,'01');                                       
                                                                        
 END SKRIV_TOTALER;                                                     
                                                                        
                                                                        
 END BH67100;                                                           
