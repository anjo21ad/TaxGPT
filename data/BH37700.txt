 BH37700: /* OPRETNING AF OMBEREGNEDE VURDERING FORSKUD 2016 */         
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
         INIT ('COPYRIGHT KMD A/S');                                    
 /********************************************************************* 
 *                                                                     *
 * FORMÅL : DER ER OPSAT FORKERTE VÆRDIER TIL EBS BEREGNINGEN.         *
 *                                                                     *
 *  Da der på BN00600T kan være være flere ejerforhold for samme CPRNR *
 *  på samme ejendom laves der en cursor til læsning af BN00600T       *
 *  Såfremt der fremkommer flere ejerforhold vil de alle blive         *
 *  opdateret med de samme værdier for 761, idet forskudsbereg-        *
 *  ningen efterfølgende vil redusere værdien til det der svare til    *
 *  ejerforholdet.                                                     *
 *                                                                     *
 *  Læse beregningsbåndet fra Forskud 2016 og                          *
 *  finde de personer som har felt SKATSTOP.DSTOPÅR udfyldt.           *
 *                                                                     *
 *  Følgende felter indlæses fra BN00600T:                             *
 *  BEVSGL,  feltet kan være NULL hvorfor der er indikator variabel    *
 *                                                                     *
 *  Fra BQ31000 behandles følgende felt:                               *
 *  761: Feltkode 37                                                   *
 *       Feltet sammenlignes det med BEVSGL på BN00600T                *
 *       Hvis der er forskel flyttes værdi til BN00600T                *
 *       indikatorvariabel sættes til 0                                *
 *       Ændring flaget sættes til JA                                  *
 *                                                                     *
 *  Hvis ændrings flaget er sat opdateres BN00600T og ejerforholdet    *
 *  markeres på kontroldatasettet med teksten 'OPDAT'.                 *
 *                                                                     *
 *  Hvis ændrings flaget er sat undersøges det om personen selv eller  *
 *  personens evt. ægtefælle findes på Sags- og Fejl registeret.       *
 *  Hvis dette ikke er tilfældet dannes der en omberegnings transaktion*
 *                                                                     *
 *  Aht. udskrift på forskudsopgørelserne skal følgende udføres:       *
 *  Fra beregningsbåndet læses SKATSTOP.OMBER02(1).BEJDVÆR             *
 *  Feltet sammenligned med BBERGRL_02ANV på BN00600T                  *
 *  Hvis ens OK                                                        *
 *                                                                     *
 * INPUT  : B.H95000D BEREGNINGSBÅND                                   *
 *          BN0600T, EVS Forskud tabel                                 *
 *                                                                     *
 * OUTPUT : BN00600T OPDATERET                                         *
 *          BH37700O TRANSAKTIONER TIL FORSKUD BEREGNING               *
 *                                                                     *
 * PROGRAMMØR: HANS ERIK KJELDSEN  NOV. 2015                        *   
 *                                                                     *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
                                                                        
 /**********************************************************************
 *                       DECLARE AF REGISTRE                           *
 **********************************************************************/
 DCL     BH950I FILE RECORD INPUT;      /* BEREGNINGSBÅND             */
 DCL     BH377O FILE RECORD OUTPUT;     /* FORSKUD BEREGN. TRANS.     */
 DCL     BH3772O FILE RECORD OUTPUT;    /* KONTROL DATASÆT.           */
 DCL     BH377I FILE RECORD INPUT;      /* PARMKORT                   */
 /**********************************************************************
 *       DECLARE AF UDTRÆK                                             *
 **********************************************************************/
 DCL     BH950_AR        CHAR      (6000) VAR;                          
 DCL     1 BH950         BASED     (ADDR(BH950_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90100N;;                                          
                                                                        
 DCL     1 BH95099       BASED     (ADDR(BH950_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;                                           
 DCL     1 PARMI,                                                       
         2 KØRSW    CHAR(1),          /* Blank = uden opdtering       */
                                      /* X     = med opdatering       */
         2 TESTP    CHAR(1),          /* X = PUT SKIP                 */
         2 FILLER   CHAR(78);                                           
 DCL   1 KONTROLFIL,                                                    
        2 FPERSONNUMMER  CHAR(6),                                       
        2 PERSONNUMMER   PIC '9999999999',                              
        2 FEKOMNR        CHAR(8),                                       
        2 EKOMNR         PIC '999',                                     
        2 FEEJDNR        CHAR(8),                                       
        2 EEJDNR         PIC '9999999',                                 
        2 FELBNR         CHAR(7),                                       
        2 ELBNR          PIC '999',                                     
        2 FBEVSGL        CHAR(8),                                       
        2 BEVSGL         PIC '99999999999V,99',                         
        2 FCTYPFOR       CHAR(9),                                       
        2 CTYPFOR        CHAR(1),                                       
        2 FCTYPSLUT      CHAR(10),                                      
        2 CTYPSLUT       CHAR(1);                                       
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DB2                                               */
 /*********************************************************************/
 DCL 1 GEM_PERSONEJENDOMSOPL,                                           
       5 PERSONNUMMER    DEC FIXED(11,0),                               
       5 ELBNR           BIN FIXED(15),                                 
       5 DINDKAAR        BIN FIXED(15),                                 
       5 EKOMNR          BIN FIXED(15),                                 
       5 EEJDNR          BIN FIXED(31),                                 
       5 BEVSGL          DEC FIXED(13,2),                               
       5 CTYPFOR         CHAR(1),                                       
       5 CTYPSLUT        CHAR(1);                                       
 DCL 1 NULL_INDK,                                                       
       5 BEVSGL          FIXED BIN(15,0);                               
 %INCLUDE SQLCA;                                                        
 %INCLUDE BN00600T;                                                     
                                                                        
                                                                        
     EXEC SQL                                                           
        DECLARE PERS_CUR CURSOR WITH HOLD FOR                           
           SELECT T1.PERSONNUMMER,                                      
                  T1.ELBNR,                                             
                  T1.DINDKAAR,                                          
                  T1.EKOMNR,                                            
                  T1.EEJDNR,                                            
                  T1.BEVSGL,                                            
                  T1.CTYPFOR,                                           
                  T1.CTYPSLUT                                           
              FROM BN00600T T1                                          
              WHERE T1.PERSONNUMMER = :PERSONEJENDOMSOPL.PERSONNUMMER   
                AND T1.DINDKAAR     = :PERSONEJENDOMSOPL.DINDKAAR       
                AND T1.EKOMNR       = :PERSONEJENDOMSOPL.EKOMNR         
                AND T1.EEJDNR       = :PERSONEJENDOMSOPL.EEJDNR         
                AND T1.DKØRT        = '2015-12-02-18.20.00.000001'      
                AND T1.CTYPFOR = '3'                                    
                AND T1.CTYPSLUT IN ('1', '2')                           
                AND T1.DFRAGLD =                                        
                     (SELECT MAX(T2.DFRAGLD)                            
                       FROM BN00600T T2                                 
                       WHERE T2.DINDKAAR = T1.DINDKAAR                  
                         AND T2.EKOMNR = T1.EKOMNR                      
                         AND T2.EEJDNR = T1.EEJDNR                      
                         AND T2.PERSONNUMMER = T1.PERSONNUMMER          
                         AND T2.ELBNR = T1.ELBNR                        
                         AND T2.DKØRT = '2015-12-02-18.20.00.000001'    
                         AND T2.CTYPFOR = '3'                           
                         AND T2.CTYPSLUT IN ('1', '2')                  
                      )                                                 
                    ;                                                   
                                                                        
 EXEC SQL INCLUDE BN00000T;                                             
 EXEC SQL INCLUDE BN00200T;                                             
 EXEC SQL DECLARE FEJLSAG_CUR CURSOR FOR                                
    SELECT T1.PERSONNR                                                  
       FROM  BN00000T T1,  BN00200T T2                                  
       WHERE   T1.INDKOMSTÅR = 2016                                     
         AND   T1.PERSONNR   = :DCLBN00000T.PERSONNR                    
         AND   T2.INK_KOMNR  = T1.INK_KOMNR                             
         AND   T2.PERSONNR   = T1.PERSONNR                              
         AND   T2.INDKOMSTÅR = T1.INDKOMSTÅR                            
         AND   T2.LØBE_NR    = T1.LØBE_NR                               
         AND   T1.HISTORIK IN(' ','P','G')                              
         AND  (T2.FEJLTYPE   = 'B' OR T2.FEJLNR = 9077);                
                                                                        
 /**********************************************************************
 *       DECLARE AF INDTÆGTSREGISTERET                                 *
 **********************************************************************/
 DCL     BR601           %INCLUDE BR601VLN;                             
 DCL     1 BR601I        BASED (ADDR(BR601)),                           
           2 LGD         BIN FIXED(15),                                 
              %INCLUDE BR60100N;                                        
                                                                        
 DCL     HJ_DCPRN       DEC FIXED  (11);                                
 DCL     NØGLE          BASED(ADDR(HJ_DCPRN)) CHAR (06);                
                                                                        
 DCL     1 PARM, %INCLUDE BR67501N;                                     
                                                                        
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     STG             BUILTIN;                                       
 DCL     PROCNAME        BUILTIN;                                       
 DCL     TIME            BUILTIN;                                       
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP   */ 
 DCL     BR67500         ENTRY;                       /* læserutine */  
                                                                        
 /**********************************************************************
 *               DECLARE AF DIVERSE                                    *
 **********************************************************************/
 DCL     JA              BIT(1)          INIT('1'B);                    
 DCL     NEJ             BIT(1)          INIT('0'B);                    
 DCL     TFLAG           BIT(1)          INIT('0'B);                    
 DCL     ÆND_MINDSTE_BEL BIT(1)          INIT('0'B);                    
 DCL     PARM_EOF        BIT(1)          INIT('0'B);                    
 DCL     OPDAT_SW        BIT(1)          INIT('0'B);                    
 DCL     CUR_EOF         BIT(1)          INIT('0'B);                    
 DCL     FEJLTEKST       CHAR (20)       INIT('FEJL BN00600T');         
 DCL     ABENDMEDD       CHAR (56)       INIT(' ');                     
 DCL     PLIXOPT         CHAR      (40)  VAR STATIC                     
                                       EXTERNAL INIT ('NOTEST(ALL,,;)');
 DCL     EOF_BH950       BIT(1)          INIT('0'B);                    
 DCL     FDATO           FIXED DEC (9)   INIT(0);                       
 DCL     FTID            FIXED DEC (7)   INIT(0);                       
 DCL     MARKERING       CHAR(1)         INIT(' ');                     
 DCL     CPRNR_GEM       FIXED DEC(11,0) INIT(0);                       
 DCL     OMBEREGN_FLAG   BIT(1)          INIT('0'B);                    
 DCL     ÆNDRING_FLAG    BIT(1)          INIT('0'B);                    
 DCL     BN00600T_NF     BIT(1)          INIT('0'B);                    
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLERE                                           */
 /*********************************************************************/
 DCL     BH950_ANT_LÆST         FIXED DEC(7)   INIT(0);                 
 DCL     BH950_ANT_UDEN_SKTSTOP FIXED DEC(7)   INIT(0);                 
 DCL     BH377_ANT_SKRIV        FIXED DEC(7)   INIT(0);                 
 DCL     BH377_ANT_EJOP         FIXED DEC(7)   INIT(0);                 
 DCL     DB_ANT_EJ_FUNDET       FIXED DEC(7)   INIT(0);                 
 DCL     DB_ANT_OPDATERET       FIXED DEC(7)   INIT(0);                 
 DCL     CUR_LÆS_ANT            FIXED DEC(7)   INIT(0);                 
 DCL     COMMIT_ANT             FIXED DEC(7)   INIT(0);                 
 DCL     OPDAT_ANT              FIXED DEC(7)   INIT(0);                 
 DCL     ANT_FEJLSAG            FIXED DEC(7)   INIT(0);                 
 DCL     ANT_EJ_IREG            FIXED DEC(7)   INIT(0);                 
 DCL     ANT_ÆND_SKAT           FIXED DEC(7)   INIT(0);                 
 DCL     ANT_OPDAT_BEVSGL       FIXED DEC(7)   INIT(0);                 
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH950 = NEJ;                                               
         ON CONVERSION                                                  
            BEGIN;                                                      
              PUT SKIP LIST ('CPR-NR = ',BH950.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH950.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH950.SORT_EJDNR);             
              PUT SKIP LIST ('CPR-NR = ',BH950.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH950.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH950.SORT_EJDNR);             
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
         ON ERROR                                                       
            BEGIN;                                                      
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
         ON ENDFILE (BH377I)                                            
            PARM_EOF = '1'B;                                            
         OPEN FILE (BH950I) TITLE ('BH95000I');                         
         OPEN FILE (BH377O) TITLE ('BH37700O');                         
         OPEN FILE (BH3772O) TITLE ('BH37701O');                        
                                                                        
         ON ENDFILE (BH950I)                                            
            EOF_BH950 = JA;                                             
                                                                        
                                                                        
         FTID = 182000;                                                 
                                                                        
                                                                        
 /*-----------------------------------------------------------*/        
 /* ÅBEN FILER, SAMT 1. KALD TIL LÆSRUTINE                    */        
 /*-----------------------------------------------------------*/        
 /* ÅBEN IREG */                                                        
       PARM.NØGLE.DCPRN  = 0;           /* CPRNUMMERET I NØGLEN     */  
       PARM.NØGLE.RECART = 0;           /* RECORD-ARTEN I NØGLEN    */  
       PARM.SLAGS        = 'KUNAKT';    /* = 'KUNAKT' EL. 'BEGGE'   */  
       PARM.HEADER       = 'NEJ';       /* = 'JA' ELLER 'NEJ'       */  
       PARM.SPECIEL      = 'JA';        /* 'NEJ'=UDEN 'JA'=TOMT SPEC*/  
       PARM.BEHANDLING   = '1';         /* = 1=ÅBEN,2=ÅBEN+LÆS      */  
                                        /* = 3=LÆS, 4=LÆS+LUK, 5=LUK*/  
       PARM.FILNAVN      = 'BR601I';    /* ='BR601I', 'BR601U'      */  
                                        /*  'BR603I'ELLER 'BR603U'  */  
       CALL BR67500(PARM,BR601);                                        
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
         CALL SKAF_PARM;                                                
         CALL LÆS_BH950;                                                
         DO WHILE(^EOF_BH950);                                          
           CALL FETCH_CUR;                                              
           DO WHILE (^CUR_EOF);                                         
              IF ^BN00600T_NF                                           
              THEN                                                      
                 CALL BEHANDL;                                          
              CALL FETCH_CUR;                                           
           END;                                                         
           CALL LÆS_BH950;                                              
         END;                                                           
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS BEREGNINGSBÅND                                           * 
 *********************************************************************/ 
 LÆS_BH950: PROC;                                                       
                                                                        
         READ FILE (BH950I) INTO (BH950_AR);                            
                                                                        
         IF BH950.SORT_CPR > 99999999999     /* OPTÆLL.INDV. 1 */       
         THEN                                                           
           EOF_BH950 = JA;                                              
         ELSE                                                           
           DO;                                                          
             BH950_ANT_LÆST = BH950_ANT_LÆST + 1;                       
                                                                        
             IF CPRNR_GEM ^= BH950.SORT_CPR                             
             THEN                                                       
               DO;                                                      
                 IF OMBEREGN_FLAG                                       
                 THEN                                                   
                   DO;                                                  
                     IF ^FINDES_FEJLSAG                                 
                     THEN                                               
                       CALL SKRIV_OMBEREGN_TRANS;                       
                     ELSE                                               
                       ANT_FEJLSAG = ANT_FEJLSAG + 1;                   
                   END;                                                 
                 ELSE                                                   
                   BH377_ANT_EJOP= BH377_ANT_EJOP + 1;                  
                 CPRNR_GEM = BH950.SORT_CPR;                            
                 OMBEREGN_FLAG = NEJ;                                   
               END;                                                     
             CUR_EOF     = NEJ;                                         
             CUR_LÆS_ANT = 0;                                           
                                                                        
             PERSONEJENDOMSOPL              = '';                       
             PERSONEJENDOMSOPL.PERSONNUMMER = BH950.SORT_CPR;           
             PERSONEJENDOMSOPL.DINDKAAR     = 2016;                     
             PERSONEJENDOMSOPL.EKOMNR       = BH950.SORT_KOMNR;         
             PERSONEJENDOMSOPL.EEJDNR       = BH950.SORT_EJDNR;         
                                                                        
             EXEC SQL OPEN PERS_CUR;                                    
                                                                        
             SELECT (SQLCA.SQLCODE);                                    
               WHEN (0);                                                
               OTHERWISE                                                
                 DO;                                                    
                   FEJLTEKST = 'FEJL VED OPEN AF CURSOR: '!!            
                             SQLCA.SQLCODE;                             
                   CALL SQL_FEJL('001');                                
                END;                                                    
             END;                                                       
           END;                                                         
                                                                        
 END LÆS_BH950;                                                         
 /********************************************************************* 
 *       PERSONER SOM EVT. SKAL BEREGNES                              * 
 *********************************************************************/ 
 BEHANDL: PROC;                                                         
 DCL FORSKEL  FIXED (13,2);                                             
 DCL ANVEND   FIXED (11,2);                                             
       FORSKEL = 0;                                                     
       ANVEND = 0;                                                      
       ÆNDRING_FLAG = NEJ;                                              
       IF BH950.SKATSTOP.DSTOPÅR > '    '      /* SKATTESTOP FINDES */  
       THEN                                                             
         DO;                                                            
           IF BH950.FRA_EVS_FOR.BSUM2 < BH950.FRA_EVS_SLUT.BGLSTITIL    
           THEN                                                         
             ANVEND = BH950.FRA_EVS_FOR.BSUM2;                          
           ELSE                                                         
             ANVEND = BH950.FRA_EVS_SLUT.BGLSTITIL;                     
           /* OPSÆTNING AF 761 BEVSGL        */                         
           IF NULL_INDK.BEVSGL ^= -1                                    
           THEN                                                         
             DO;                                                        
               IF BH950.FRA_EVS_FOR.BSUM2 ^= PERSONEJENDOMSOPL.BEVSGL   
               THEN                                                     
                 DO;                                                    
                   FORSKEL = ANVEND                                     
                               - PERSONEJENDOMSOPL.BEVSGL;              
                   IF FORSKEL > 1                                       
                   !  FORSKEL <-1                                       
                   THEN                                                 
                     DO;                                                
                       GEM_PERSONEJENDOMSOPL.BEVSGL = ANVEND;           
                       ÆNDRING_FLAG = JA;                               
                       ANT_OPDAT_BEVSGL  = ANT_OPDAT_BEVSGL + 1;        
                     END;                                               
                 END;                                                   
             END;                                                       
           ELSE                                                         
             DO;                                                        
               GEM_PERSONEJENDOMSOPL.BEVSGL = ANVEND;                   
               NULL_INDK.BEVSGL = 0;                                    
               ÆNDRING_FLAG = JA;                                       
               ANT_OPDAT_BEVSGL  = ANT_OPDAT_BEVSGL + 1;                
             END;                                                       
           /* ØVRIGE BEHANDLING                              */         
           IF ÆNDRING_FLAG                                              
           THEN                                                         
             DO;                                                        
               CALL OPDATER_BN00600T();                                 
               OPDAT_ANT     = OPDAT_ANT + 1;                           
               OMBEREGN_FLAG = JA;                                      
             END;                                                       
         END;                                                           
                                                                        
 END BEHANDL;                                                           
                                                                        
 /*********************************************************************/
 /*      FETCH CURSOR                                                 */
 /*********************************************************************/
 FETCH_CUR: PROC;                                                       
                                                                        
         EXEC SQL                                                       
            FETCH PERS_CUR                                              
               INTO :PERSONEJENDOMSOPL.PERSONNUMMER                     
                   ,:PERSONEJENDOMSOPL.ELBNR                            
                   ,:PERSONEJENDOMSOPL.DINDKAAR                         
                   ,:PERSONEJENDOMSOPL.EKOMNR                           
                   ,:PERSONEJENDOMSOPL.EEJDNR                           
                   ,:PERSONEJENDOMSOPL.BEVSGL:NULL_INDK.BEVSGL          
                   ,:PERSONEJENDOMSOPL.CTYPFOR                          
                   ,:PERSONEJENDOMSOPL.CTYPSLUT                         
                                                                        
          ;                                                             
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0)                                                     
              DO;                                                       
                CUR_LÆS_ANT = CUR_LÆS_ANT + 1;                          
                GEM_PERSONEJENDOMSOPL  = PERSONEJENDOMSOPL, BY NAME;    
                BN00600T_NF = NEJ;                                      
              END;                                                      
            WHEN(100)                                                   
              DO;                                                       
                IF CUR_LÆS_ANT = 0                                      
                THEN                                                    
                  DO;                                                   
                    DB_ANT_EJ_FUNDET = DB_ANT_EJ_FUNDET + 1;            
                    BN00600T_NF = JA;                                   
                  END;                                                  
                CUR_EOF = JA;                                           
                EXEC SQL                                                
                  CLOSE PERS_CUR;                                       
                SELECT (SQLCA.SQLCODE);                                 
                  WHEN (0);                                             
                  OTHERWISE                                             
                    DO;                                                 
                      FEJLTEKST = 'FEJL VED CLOSE PÅ CURSOR: '!!        
                                 SQLCA.SQLCODE;                         
                      CALL SQL_FEJL('002');                             
                    END;                                                
                END; /* SELECT (SQLCA.SQLCODE) */                       
              END;                                                      
            OTHERWISE                                                   
              DO;                                                       
                CALL SQL_FEJL('003');                                   
              END;                                                      
         END;                                                           
 END FETCH_CUR;                                                         
                                                                        
 /*********************************************************************/
 /*      OPDATER BN00600T                                             */
 /*********************************************************************/
 OPDATER_BN00600T: PROC;                                                
     CALL SKRIV_KONTROLFIL;                                             
     IF OPDAT_SW                                                        
     THEN                                                               
       DO;                                                              
         EXEC SQL                                                       
           UPDATE BN00600T                                              
              SET BEVSGL         = :GEM_PERSONEJENDOMSOPL.BEVSGL        
                                   :NULL_INDK.BEVSGL                    
                 ,COPR_BEVSGL    = 'L'                                  
                 ,CEVSBENY       = '1'                                  
              WHERE PERSONNUMMER = :GEM_PERSONEJENDOMSOPL.PERSONNUMMER  
                AND ELBNR        = :GEM_PERSONEJENDOMSOPL.ELBNR         
                AND DINDKAAR     = :GEM_PERSONEJENDOMSOPL.DINDKAAR      
                AND EKOMNR       = :GEM_PERSONEJENDOMSOPL.EKOMNR        
                AND EEJDNR       = :GEM_PERSONEJENDOMSOPL.EEJDNR        
                AND DTILGLD      = '9999-12-31-23.59.59.999999'         
         ;                                                              
         SELECT(SQLCA.SQLCODE);                                         
           WHEN(0)                                                      
             DO;                                                        
               COMMIT_ANT = COMMIT_ANT + 1;                             
               IF COMMIT_ANT > 9999                                     
               THEN                                                     
                 DO;                                                    
                   EXEC SQL COMMIT;                                     
                   IF SQLCA.SQLCODE ^= 0                                
                   THEN                                                 
                     DO;                                                
                       CALL SQL_FEJL('004');                            
                     END;                                               
                   COMMIT_ANT = 0;                                      
                 END;                                                   
             END;                                                       
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST = 'FEJL VED opdate: '!!                        
                                 SQLCA.SQLCODE;                         
               CALL SQL_FEJL('005');                                    
             END;                                                       
         END;                                                           
       END;                                                             
     DB_ANT_OPDATERET = DB_ANT_OPDATERET + 1;                           
                                                                        
 END OPDATER_BN00600T;                                                  
                                                                        
 /*********************************************************************/
 /*      SKRIV OMBEREGNINGS TRANSAKTION                               */
 /*********************************************************************/
 SKRIV_OMBEREGN_TRANS: PROC;                                            
                                                                        
 DCL START       BIN FIXED(15);                                         
 DCL EKOM        PIC '999';                                             
 DCL HJ_FLYD     CHAR(900) VAR;                                         
 DCL UDREC       CHAR(1000) VAR;                                        
 DCL 1 UD        BASED(ADDR(UDREC)),                                    
       2 LÆNGDE  BIN FIXED  (15),                                       
           % INCLUDE BS38900N;                                          
    IF LÆS_IREG(CPRNR_GEM)                                              
    THEN                                                                
      DO;                                                               
        HJ_FLYD   = '';                                                 
        UDREC     = '';                                                 
        EKOM      = BR601I.EÆKNR;                                       
        UD.DKØRT  = 20151205;                                           
        UD.DKLOK  = FTID;                                               
        UD.CTYPE  = 3;                                                  
        UD.CSORR  = '7';                                                
        UD.CSORP  = BR601I.DCPRN;                                       
        UD.CGENK  = 0;                                                  
        HJ_FLYD   = '&911' !! CSORP                                     
                           !! '&374' !! EKOM                            
                           !! '&30022'                                  
                           !! '&790TS05'                                
                           !! '&9711'                                   
                           !! '&-\';                                    
        UD.HFLYD  = HJ_FLYD;                                            
        UD.LÆNGDE = 150;  /*LENGHT*/                                    
        WRITE FILE (BH377O) FROM (UDREC);                               
        BH377_ANT_SKRIV = BH377_ANT_SKRIV + 1;                          
      END;                                                              
    ELSE                                                                
      ANT_EJ_IREG = ANT_EJ_IREG + 1;                                    
 END SKRIV_OMBEREGN_TRANS;                                              
 /*********************************************************************/
 /* HER UDSKRIVES kontrolfil                                   RUTINE */
 /*********************************************************************/
 SKRIV_KONTROLFIL: PROC;                                                
                                                                        
         KONTROLFIL.FPERSONNUMMER = 'CPRNR.';                           
         KONTROLFIL.PERSONNUMMER  = GEM_PERSONEJENDOMSOPL.PERSONNUMMER; 
         KONTROLFIL.FEKOMNR       = ' EKOMNR.';                         
         KONTROLFIL.EKOMNR        = GEM_PERSONEJENDOMSOPL.EKOMNR;       
         KONTROLFIL.FEEJDNR       = ' EEJDNR.';                         
         KONTROLFIL.EEJDNR        = GEM_PERSONEJENDOMSOPL.EEJDNR;       
         KONTROLFIL.FELBNR        = ' ELBNR,';                          
         KONTROLFIL.ELBNR         = GEM_PERSONEJENDOMSOPL.ELBNR;        
         KONTROLFIL.FBEVSGL       = ' BEVSGL.';                         
         KONTROLFIL.BEVSGL        = GEM_PERSONEJENDOMSOPL.BEVSGL;       
         KONTROLFIL.FCTYPFOR      = ' CTYPFOR.';                        
         KONTROLFIL.CTYPFOR       = GEM_PERSONEJENDOMSOPL.CTYPFOR;      
         KONTROLFIL.FCTYPSLUT     = ' CTYPSLUT.';                       
         KONTROLFIL.CTYPSLUT      = GEM_PERSONEJENDOMSOPL.CTYPSLUT;     
                                                                        
          WRITE FILE (BH3772O) FROM (KONTROLFIL);                       
 END SKRIV_KONTROLFIL;                                                  
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC(SF_SEQNR);                                              
 DCL SF_SEQNR            CHAR(3);                                       
                                                                        
         CALL ZS67000(SQLCA);                                           
         ABENDMEDD =  FEJLTEKST;                                        
         CALL ZS30101 (4, ABENDMEDD, 2);                                
         CALL PLIDUMP ('SNHOB');                                        
 END SQL_FEJL;                                                          
                                                                        
 /*********************************************************************/
 /* KONTROLLER SAGS- OG FEJLSYSTEM                                    */
 /*********************************************************************/
 FINDES_FEJLSAG: PROC RETURNS(BIT (1));                                 
 DCL HJ_DCPRN DEC(11);                                                  
 DCL FEJSAG_FUNDET  BIT (1);                                            
                                                                        
    FEJSAG_FUNDET = '0'B;                                               
    DCLBN00000T.PERSONNR = CPRNR_GEM;                                   
    EXEC SQL OPEN FEJLSAG_CUR;                                          
    IF SQLCODE = 0 THEN DO;                                             
       CALL FETCH_FEJLSAG_CUR;                                          
       SELECT(SQLCODE);                                                 
          WHEN(0) DO; /*EN ELLER FLERE SAGER PÅ TABEL*/                 
             FEJSAG_FUNDET = '1'B;                                      
             MARKERING = '%';                                           
          END;                                                          
          WHEN(100) DO;  /*INGEN SAG PÅ FEJL TABEL*/                    
             EXEC SQL CLOSE FEJLSAG_CUR;                                
             IF SQLCA.SQLCODE = 0 THEN DO;                              
                IF LÆS_IREG(BH950.SORT_CPR) THEN DO;                    
                   IF BR601I.DHENV > 0    /*UNDERSØG ÆGTEFÆLLE*/        
                    & (BR601I.CSBSK = 2                                 
                     ! BR601I.CSBSK = 3                                 
                     ! BR601I.CSBSK = 6) THEN DO;                       
                      DCLBN00000T.PERSONNR = BR601I.DHENV;              
                      EXEC SQL OPEN FEJLSAG_CUR;                        
                      IF SQLCODE = 0 THEN DO;                           
                         CALL FETCH_FEJLSAG_CUR;                        
                         SELECT(SQLCODE);                               
                            WHEN(0) DO;/*EN ELLER FLERE SAGER PÅ TABEL*/
                               FEJSAG_FUNDET = '1'B;                    
                               MARKERING = 'Æ';                         
                            END;                                        
                            WHEN(100) DO; /*INGEN SAG PÅ FEJL TABEL*/   
                               FEJSAG_FUNDET = '0'B;                    
                            END;                                        
                            OTHER DO;                                   
                               CALL SQL_FEJL('007');                    
                           END;                                         
                         END; /*SELECT*/                                
                      END;                                              
                   END;                                                 
                END;                                                    
             END;                                                       
          END;                                                          
          OTHER DO;                                                     
             CALL SQL_FEJL('008');                                      
          END;                                                          
       END; /*SELECT*/                                                  
       EXEC SQL CLOSE FEJLSAG_CUR;                                      
    END;                                                                
    ELSE DO;                                                            
       CALL SQL_FEJL('009');                                            
    END;                                                                
                                                                        
    RETURN(FEJSAG_FUNDET);                                              
                                                                        
 /********************************************************************/ 
 FETCH_FEJLSAG_CUR: PROC;                                               
                                                                        
    DCLBN00000T.PERSONNR = 0;                                           
    EXEC SQL FETCH FEJLSAG_CUR                                          
       INTO :DCLBN00000T.PERSONNR;                                      
    IF SQLCODE = 0 THEN                                                 
       PUT SKIP LIST('FEJLSAG PERSONNR =',DCLBN00000T.PERSONNR);        
                                                                        
 END FETCH_FEJLSAG_CUR;                                                 
                                                                        
 END FINDES_FEJLSAG;                                                    
                                                                        
 /*********************************************************************/
 /*   LÆS INDTÆGTS REGISTER                                           */
 /*********************************************************************/
 LÆS_IREG: PROC(LI_CPRNR) RETURNS(BIT(1));                              
 DCL LI_CPRNR            FIXED(11,0);                                   
 DCL RTC                 BIT(1)        INIT('0'B);                      
                                                                        
    MARKERING        = '';                                              
    RTC              = NEJ;                                             
    PARM.NØGLE.DCPRN = LI_CPRNR;                                        
    PARM.BEHANDLING  = '3';  /*LÆS*/                                    
    CALL BR67500(PARM,BR601);                                           
    SELECT (PARM.SVAR);                                                 
       WHEN (00) DO;                                                    
          RTC = JA;                                                     
       END;                                                             
       WHEN (51) DO;                                                    
          ANT_EJ_IREG = ANT_EJ_IREG + 1;                                
          RTC = NEJ;                                                    
       END;                                                             
       OTHERWISE DO;                                                    
          PUT SKIP LIST ('FEJL I PARM SVAR VED LÆS PÅ IREG ' !!         
                         PARM.NØGLE.DCPRN);                             
          RTC = NEJ;                                                    
       END;                                                             
    END; /* END-SELECT */                                               
                                                                        
    RETURN(RTC);                                                        
                                                                        
 END LÆS_IREG;                                                          
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE: PROC;                                                      
     PUT SKIP LIST('ANTAL LÆSTE PÅ BH950..:',BH950_ANT_LÆST);           
     PUT SKIP LIST('ANTAL OPDATEREDE......:',OPDAT_ANT);                
     PUT SKIP LIST('ANTAL EJ OPDATEREDE...:',BH377_ANT_EJOP);           
     PUT SKIP LIST('ANTAL PÅ FEJL.........:',ANT_FEJLSAG);              
     PUT SKIP LIST('ANTAL OPDATEREDE......:',ANT_OPDAT_BEVSGL);         
     PUT SKIP LIST('ANTAL IKKE PÅ IREG....:',ANT_EJ_IREG);              
     PUT SKIP LIST('ANTAL transer.........:',BH377_ANT_SKRIV);          
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /*      SKAF PARM                                                    */
 /*********************************************************************/
 SKAF_PARM: PROC;                                                       
                                                                        
    OPEN FILE(BH377I) TITLE('BH37700I');                                
    READ FILE(BH377I) INTO(PARMI);                                      
    IF ^PARM_EOF                                                        
    THEN                                                                
      DO;                                                               
        IF PARMI.KØRSW ^= ' '                                           
        THEN                                                            
           OPDAT_SW = JA;                                               
        ELSE                                                            
           OPDAT_SW = NEJ;                                              
        IF PARMI.TESTP = 'X'                                            
        THEN                                                            
          TFLAG = JA;                                                   
        ELSE                                                            
          TFLAG = NEJ;                                                  
      END;                                                              
    ELSE                                                                
      DO;                                                               
        PUT SKIP LIST('DEFAULT OPDATERING OPSAT');                      
        OPDAT_SW = NEJ;                                                 
        TFLAG    = NEJ;                                                 
      END;                                                              
                                                                        
    IF OPDAT_SW                                                         
    THEN                                                                
       PUT SKIP LIST('Der sker opdatering af BN00600T');                
    ELSE                                                                
       PUT SKIP LIST('Ingen opdatering af BN00600T');                   
 END SKAF_PARM;                                                         
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT: PROC;                                                          
         IF TFLAG THEN PUT SKIP LIST('*** '!!PROCNAME!!' ***');         
         CALL TOTALLISTE;                                               
         CLOSE FILE (BH950I),                                           
               FILE (BH377O),                                           
               FILE (BH3772O),                                          
               FILE (BH377I);                                           
                                                                        
 END AFSLUT;                                                            
 /*********************************************************************/
 /*      DIVERSE INCLUDE                                              */
 /*********************************************************************/
    %INCLUDE BS36103M;     /* RETURNERE DATO OG TID I FIXED TID       */
    %INCLUDE ZZ20301M;     /* PRINT STYRE TEGN                        */
                                                                        
 END  BH37700;                                                          
