 BE86200: PROCEDURE OPTIONS(MAIN) REORDER;                              
         DFT RANGE (*) STATIC;                                          
 /*-------------------------------------------------------------------* 
  *                                                                   * 
  *  Danne tabel med GS oplysninger på Døde personer                  * 
  *                                                                   * 
  *  Der læses på mandstalstabel BE86000T, for at finde evt. døde i   * 
  *  indkosmtåret.                                                    * 
  *  For dissse personer, hentes der GS oplysninger og disse gemmes   * 
  *  på tabel BE61000T.                                               * 
  *                                                                   * 
  *  PROGRAMMØR: LAC   Sep/Okt 2023                                   * 
  *                                                                   * 
  *  Rettet af :                                                      * 
  *  SLUT 2024   PEN                                     12-02-2024   * 
  *              Rettede opslag på diverse BE/BS tabeller             * 
  *  SLUT 2024   PEN                                     15-03-2024   * 
  *              Rettet udregning af EP_ANTALDAGE_P                   * 
  *              Formel fra BT51910M implementeret (Defect 2447)      * 
  *              Rettet udregning af EP_ANTALDAGE_P. HJ_RES og        * 
  *              HJ_RES4 ændret fra DEC FIXED (20,16)                 * 
  *              til DEC FIXED (20,4) - (Defect 2440)                 * 
  *  SLUT 2024   JJJ                                     21-06-2024   * 
  *              BE55600M erstatter BE55300M                          * 
  *              PBR                                     29-10-2024   * 
  *              Tilføjet data_id ved opslag på BE66000T              * 
  *-------------------------------------------------------------------*/
 /*-------------------------------------------------------------------* 
  *       INCLUDE SQL COMMUNICATION AREA                              * 
  *-------------------------------------------------------------------*/
   EXEC SQL INCLUDE SQLCA;                                              
   %INCLUDE BS03100T;      // E&E modtagetabel                          
   %include BS03400T;      // Ejendomstabel                             
   %include BS03500T;      // Ejendomsvurdering                         
   dcl 1 BS03500T,                                                      
       3 I_VUR_ÆNDRDATO      bin fixed (15) init (0),                   
       3 I_VUR_OPRKODE       bin fixed (15) init (0);                   
                                                                        
   %include BS03600T;      // Ejerforhold                               
   dcl 1 BS03600T,                                                      
       3 I_FEJLKODE            bin fixed (15) init (0),                 
       3 I_FEJLTEKST           bin fixed (15) init (0),                 
       3 I_EJERANDEL           bin fixed (15) init (0),                 
       3 I_EP_EJERSLUTDATO     bin fixed (15) init (0),                 
       3 I_EGS_RABATBELØB      bin fixed (15) init (0);                 
                                                                        
   %include BS03700T;      // BFE nummer                                
                                                                        
   %include BS04500T;      // Printopgør id                             
                                                                        
                                                                        
   %include BE66000T;      // Mandtalstabel                             
                                                                        
   %include BE61000T;      // GS oplysninger på døde i indkomståret     
   dcl 1 BE61000T,                                                      
       3 I_VUR_ÆNDRDATO        bin fixed (15) init (0),                 
       3 I_VUR_OPRKODE         bin fixed (15) init (0),                 
       3 I_EJERANDEL           bin fixed (15) init (0),                 
       3 I_EP_EJERSLUTDATO     bin fixed (15) init (0),                 
       3 I_EP_EJERSLUTDATO_P   bin fixed (15) init (0),                 
       3 I_EGS_RABATBELØB      bin fixed (15) init (0),                 
       3 I_EGS_RABATBELØB_P    bin fixed (15) init (0),                 
       3 I_ADR_POSTNR          bin fixed (15) init (0),                 
       3 I_ADR_POSTDISTRIKT    bin fixed (15) init (0),                 
       3 I_ADR_BYNAVN          bin fixed (15) init (0);                 
                                                                        
                                                                        
 DEFAULT RANGE (*) STATIC;                                              
                                                                        
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
 DCL     WS_FORSKUD_SLUT_TXT            CHAR(16) INIT (' ');            
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     WS_KØRSELS_ID                  CHAR(5);                        
                                                                        
 DCL     WS_GLSLUTÅR                    BIN FIXED (15);                 
                                                                        
 DCL     WS_DATA_ID                     BIN FIXED (15);                 
                                                                        
 DCL     WS_TIL_EVS_AAR_GL              BIN FIXED (15);                 
 DCL     WS_AAR_NAESTE                  BIN FIXED (15);                 
                                                                        
 DCL     WS_PROGRAMNAVN                CHAR(8)  INIT ('BE86200');       
                                                                        
 DCL     WS_EP_EJERSLUTDATO_P          DEC FIXED(9,0);                  
 DCL     WS_EP_ANTALDAGE_P             BIN FIXED (15);                  
 DCL     WS_EP_DAGE                    DEC FIXED (9,6);                 
 DCL     WS_EGS_BELØB_P                DEC FIXED(13,2);                 
 DCL     WS_EGS_RABATBELØB_P           DEC FIXED(13,2);                 
 DCL     WS_EGS_BELØBEFTERRABAT_P      DEC FIXED(13,2);                 
                                                                        
 DCL     WS_START_DATO                 PIC       '(8)9';                
 DCL     WS_SLUT_DATO                  PIC       '(8)9';                
 DCL     HJDAGE_EJET                   FIXED     (3);                   
 DCL     HJDAGE_PLUS                   FIXED     (3) INIT (1);          
                                                                        
 DCL     HJ_EJET_FRA                   PIC       '(8)9';                
 DCL     HJ_EJET_TIL                   PIC       '(8)9';                
 DCL     HJ_TID                        Char       (26);                 
                                                                        
 DCL     1 BE55600M,                                                    
           %INCLUDE BE55600M;                                           
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     WS_DCPRN        DECIMAL(11,0);                                 
                                                                        
 DCL     PERSON_FUNDET      BIT(1)     INIT('0'B);                      
                                                                        
 DCL     ID_FUNDET          BIT(1)     INIT('0'B);                      
 DCL     PERIODISER_GS      BIT(1)     INIT('0'B);                      
 DCL     GS_GEM             BIT(1)     INIT('0'B);                      
 DCL     FØRSTE_LÆS         BIT(1)     INIT('0'B);  /* LÆS BS04500T   */
 DCL     FØRSTE_LÆS_3600    BIT(1)     INIT('0'B);  /* LÆS BS04500T   */
 DCL     FEJLTEKST              CHAR(40)   INIT(' ');                   
                                                                        
 DCL     AAR_SIDSTE   pic '9999';  /* benyttes i sidste_ */             
 DCL     AAR_DETTE    pic '9999';  /* benyttes i dette_ */              
 DCL     AAR_NAESTE   pic '9999';  /* benyttes i naeste_ */             
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BE86200'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BE86200L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'GS grundlag for døde  '),                                   
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
  DCL     1 BLKLIN,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (132)     INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
                                                                        
 /*------------------------------------------------------------------*/ 
 /*   Cursor til mandtal BE66000T (finde døde personer )             */ 
 /*------------------------------------------------------------------*/ 
                                                                        
      exec sql declare BE66000_cur cursor with hold for select          
               DCPRN                                                    
              ,HIST_OPD_DETTE_DCIÆN                                     
              ,PRINTOPGØRID                                             
        from  BE66000T                                                  
         where CCIVS  = 'D'                                             
           and (substr(HIST_OPD_DETTE_DCIÆN,1,4) = :WS_TIL_EVS_AAR      
            or  substr(HIST_OPD_DETTE_DCIÆN,1,4) = :WS_AAR_NAESTE)      
           And AFVIKLINGS_ID        = :WS_FORSKUD_SLUT_TXT              
           and DATA_ID = :WS_DATA_ID                                    
           And KØRSELS_ID = 'MT2'                                       
           And CMFRG      = '0'                                         
           and PRINTOPGØRID > 0                                         
        group by  DCPRN                                                 
                 ,HIST_OPD_DETTE_DCIÆN                                  
                 ,PRINTOPGØRID                                          
        order by DCPRN                                                  
      ;                                                                 
                                                                        
        SELECT (SQLCODE);                                               
        WHEN (0);                                                       
        OTHER                                                           
            DO;                                                         
               FEJLTEKST = 'FEJL v/cursor BS66000T';                    
               CALL SQL_FEJL;                                           
            END;                                                        
         END; /* SELECT*/                                               
                                                                        
                                                                        
 /*------------------------------------------------------------------*/ 
 /*   Cusor til Printopgør_id BS045000T                              */ 
 /*------------------------------------------------------------------*/ 
                                                                        
      exec sql declare BS04500_cur cursor with hold for                 
      select DINDKAAR                                                   
            ,PRINTOPGØRID                                               
            ,PERSONNUMMER                                               
            ,GRUNDLAGSTYPE                                              
            ,EKOMNR                                                     
            ,EEJDNR                                                     
            ,EENHEDLBNR                                                 
            ,EPLBNR                                                     
            ,DFRAGLD                                                    
         from  BS04500T                                                 
        WHere personnummer  = :DCLBE66000T.DCPRN                        
          and DINDKAAR      = :WS_TIL_EVS_AAR                           
          and GRUNDLAGSTYPE = 'GSG'                                     
          and PRINTOPGØRID  = :DCLBE66000T.PRINTOPGØRID                 
         order by EKOMNR, EEJDNR, EPLBNR                                
        ;                                                               
                                                                        
                                                                        
      SELECT (SQLCODE);                                                 
         WHEN (0);                                                      
         OTHER                                                          
            DO;                                                         
              FEJLTEKST = 'FEJL v/cursor BS04500T';                     
              CALL SQL_FEJL;                                            
            END;                                                        
       END;                                                             
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
                                                                        
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /* DECLARE AF TÆLLERE                                                */
 /*********************************************************************/
                                                                        
 DCL     ANT_INS_PERSON     BIN FIXED (31) INIT(0);  /* INSERTED      */
 DCL     ANT_INS_GS         BIN FIXED (31) INIT(0);  /* INS rækker    */
 DCL     ANT_LÆS_BE6600T    BIN FIXED (31) INIT(0);  /* LÆST BE66000T */
 DCL     ANT_GS_EJ_FUNDET   BIN FIXED (31) INIT(0);  /* LÆST BS03600T */
 DCL     ANT_GS_FUNDET      BIN FIXED (31) INIT(0);  /* LÆST BS03600T */
 DCL     ANT_ID_EJ_FUNDET   BIN FIXED (31) INIT(0);  /* LÆST BS04500T */
 DCL     ANT_ID_FUNDET      BIN FIXED (31) INIT(0);  /* LÆST BS04500T */
                                                                        
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BE86200L');             
            END;                                                        
                                                                        
                                                                        
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
            EXEC SQL SET :HJ_TID = CURRENT TIMESTAMP;                   
                                                                        
            SELECT (BE55600M.AFVIKLINGS_ID);                            
               WHEN ('OPRET_EVS_FORSK')                                 
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR      = BE55600M.TIL_EVS_AAR;        
                     WS_DATA_ID          = BE55600M.DATA_ID;            
                     OVERLIN1.TEKST2 = 'GS data FORSKUD';               
                     AAR_SIDSTE = WS_TIL_EVS_AAR - 2;                   
                     AAR_DETTE  = WS_TIL_EVS_AAR - 1;                   
                     AAR_NAESTE = WS_TIL_EVS_AAR;                       
                  END;                                                  
               WHEN ('OPRET_EVS_SLUT')                                  
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR      = BE55600M.TIL_EVS_AAR;        
                     WS_DATA_ID          = BE55600M.DATA_ID;            
                     OVERLIN1.TEKST2 = 'GS data SLUT';                  
                     AAR_SIDSTE = WS_TIL_EVS_AAR - 1;                   
                     AAR_DETTE = WS_TIL_EVS_AAR;                        
                     AAR_NAESTE = WS_TIL_EVS_AAR + 1;                   
                  END;                                                  
               WHEN ('SUPPL_EVS_SLUT')                                  
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;      
                     WS_TIL_EVS_AAR      = BE55600M.TIL_EVS_AAR;        
                     WS_DATA_ID          = BE55600M.DATA_ID;            
                     OVERLIN1.TEKST2 = 'GS data SUPPLEMENT';            
                     AAR_SIDSTE = WS_TIL_EVS_AAR - 1;                   
                     AAR_DETTE = WS_TIL_EVS_AAR;                        
                     AAR_NAESTE = WS_TIL_EVS_AAR + 1;                   
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     FEJLTEKST = 'PARAMETERFEJL';                       
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
                                                                        
         WS_AAR_NAESTE = WS_TIL_EVS_AAR + 1;                            
                                                                        
         CALL ZZ20390('*OPEN','BE86201Y');       /* ÅBEN KONTROLLISTE */
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.ÅR   = WS_TIL_EVS_AAR;                                
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
                                                                        
         CALL OPEN_BE66000_CUR;                                         
                                                                        
         CALL FETCH_BE66000_CUR;                                        
                                                                        
         DO WHILE (Person_fundet);                                      
                                                                        
            Call SKAF_GS_DATA;                                          
                                                                        
            CALL FETCH_BE66000_CUR;                                     
                                                                        
         END;                                                           
                                                                        
        CALL CLOSE_BE66000_CUR;                                         
                                                                        
    //     exec sql commit;                                             
                                                                        
       CALL AFSLUT;                                                     
                                                                        
 /*********************************************************************/
 /* Hent data fra GS tabeller og periodiser                           */
 /*********************************************************************/
 SKAF_GS_DATA: PROC;                                                    
                                                                        
        /* første gang der læses med en person fra BE66000T */          
        FØRSTE_LÆS =  '1'B;                                             
                                                                        
        /* første gang der læses med en person fra BS0E6000T */         
        FØRSTE_LÆS_3600 = '1'B;                                         
                                                                        
        CALL OPEN_BS04500_CUR;                                          
                                                                        
        CALL FETCH_BS04500_CUR;                                         
                                                                        
        DO WHILE (ID_FUNDET);                                           
                                                                        
            CALL SKAF_BS03600T;                                         
                                                                        
            CALL SKAF_BS03500T;                                         
                                                                        
            CALL SKAF_BS03400T;                                         
                                                                        
            CALL SKAF_GSBEREGNID;                                       
                                                                        
            CALL SKAF_BS03700T;                                         
                                                                        
            CALL PERIODISER;                                            
                                                                        
            IF GS_GEM                                                   
            THEN DO;                                                    
                  /* flyt data GS data til db2-struktur */              
                  CALL FLYT_FELTER_BE61000T;                            
                  CALL INSERT_GS_OPL;                                   
                 END;                                                   
                                                                        
            CALL FETCH_BS04500_CUR;                                     
         END;                                                           
                                                                        
         CALL CLOSE_BS04500_CUR;                                        
                                                                        
 END SKAF_GS_DATA;                                                      
 /*********************************************************************/
 /* Hent GSBEREGNID fra modtagetabel BS03100T                         */
 /*********************************************************************/
 SKAF_GSBEREGNID: PROC;                                                 
                                                                        
   EXEC SQL                                                             
   SELECT GSBEREGNID                                                    
     INTO :DCLBS03100T.GSBEREGNID                                       
     FROM BS03100T                                                      
     WHere personnummer = :DCLBS03600T.PERSONNUMMER                     
       and DINDKAAR     = :WS_TIL_EVS_AAR                               
       and LEVERANCETID     = :DCLBS03400T.LEVERANCETID                 
       and EKOMNR           = :DCLBS04500T.EKOMNR                       
       and EEJDNR           = :DCLBS04500T.EEJDNR                       
       AND EP_EJERSTARTDATO = :DCLBS03600T.EP_EJERSTARTDATO             
       ;                                                                
                                                                        
     IF SQLCA.SQLCODE ^= 0                                              
     THEN                                                               
        DO;                                                             
         FEJLTEKST = 'SKAF_GSBEREGNID';                                 
         CALL SQL_FEJL;                                                 
       END;                                                             
                                                                        
 END SKAF_GSBEREGNID;                                                   
                                                                        
 /*********************************************************************/
 /* Hent data fra GS tabel BS036000T  (Ejerforhold m.m.)              */
 /*********************************************************************/
 SKAF_BS03600T: PROC;                                                   
                                                                        
   EXEC SQL                                                             
   SELECT                                                               
               "DINDKAAR"                                               
              ,"PERSONNUMMER"                                           
              ,"EKOMNR"                                                 
              ,"EEJDNR"                                                 
              ,"EPLBNR"                                                 
              ,"DFRAGLD"                                                
              ,"GSBEREGNTID"                                            
              ,"FEJLKODE"                                               
              ,"FEJLTEKST"                                              
              ,"DTILGLD"                                                
              ,"EJERANDEL"                                              
              ,"EP_EJERSTARTDATO"                                       
              ,"EP_EJERSLUTDATO"                                        
              ,"EP_ANTALDAGE"                                           
              ,"EGS_OPKRPSKAT3112"                                      
              ,"EGS_BELØB"                                              
              ,"EGS_RABATBELØB"                                         
              ,"EGS_BELØBEFTERRABAT"                                    
              ,"OPRET_AF"                                               
              ,"OPRET_TS"                                               
         into                                                           
              :DCLBS03600T.DINDKAAR                                     
             ,:DCLBS03600T.PERSONNUMMER                                 
             ,:DCLBS03600T.EKOMNR                                       
             ,:DCLBS03600T.EEJDNR                                       
             ,:DCLBS03600T.EPLBNR                                       
             ,:DCLBS03600T.DFRAGLD                                      
             ,:DCLBS03600T.GSBEREGNTID                                  
             ,:DCLBS03600T.FEJLKODE                                     
              :BS03600T.I_FEJLKODE                                      
             ,:DCLBS03600T.FEJLTEKST                                    
              :BS03600T.I_FEJLTEKST                                     
             ,:DCLBS03600T.DTILGLD                                      
             ,:DCLBS03600T.EJERANDEL                                    
              :BS03600T.I_EJERANDEL                                     
             ,:DCLBS03600T.EP_EJERSTARTDATO                             
             ,:DCLBS03600T.EP_EJERSLUTDATO                              
              :BS03600T.I_EP_EJERSLUTDATO                               
             ,:DCLBS03600T.EP_ANTALDAGE                                 
             ,:DCLBS03600T.EGS_OPKRPSKAT3112                            
             ,:DCLBS03600T.EGS_BELØB                                    
             ,:DCLBS03600T.EGS_RABATBELØB                               
              :BS03600T.I_EGS_RABATBELØB                                
             ,:DCLBS03600T.EGS_BELØBEFTERRABAT                          
             ,:DCLBS03600T.OPRET_AF                                     
             ,:DCLBS03600T.OPRET_TS                                     
     FROM BS03600T                                                      
     WHere personnummer = :DCLBE66000T.DCPRN                            
       and DINDKAAR     = :WS_TIL_EVS_AAR                               
       and DFRAGLD      = :DCLBS04500T.DFRAGLD                          
       and EKOMNR       = :DCLBS04500T.EKOMNR                           
       and EEJDNR       = :DCLBS04500T.EEJDNR                           
       AND EPLBNR       = :DCLBS04500T.EPLBNR                           
       ;                                                                
                                                                        
                                                                        
      SELECT (SQLCODE);                                                 
         WHEN (0)                                                       
         DO;                                                            
           FØRSTE_LÆS_3600    =  '0'B;                                  
           ANT_GS_FUNDET = ANT_GS_FUNDET + 1;                           
         END;                                                           
         OTHER                                                          
            DO;                                                         
              put skip list ('Person   = ' !! DCLBE66000T.DCPRN);       
              put skip list ('årstal   = ' !! WS_TIL_EVS_AAR);          
              put skip list ('Fra dato = ' !! DCLBS04500T.DFRAGLD);     
              put skip list ('kommune  = ' !! DCLBS04500T.EKOMNR);      
              put skip list ('ejendom  = ' !! DCLBS04500T.EEJDNR);      
                                                                        
              FEJLTEKST = 'Skaf BS03600';                               
              CALL SQL_FEJL;                                            
            END;                                                        
       END;                                                             
                                                                        
 END SKAF_BS03600T;                                                     
                                                                        
 /*********************************************************************/
 /* Hent data fra GS tabel BS034000 (Adr, GS grundlag m.m.)           */
 /*********************************************************************/
 SKAF_BS03400T: PROC;                                                   
                                                                        
   EXEC SQL                                                             
   SELECT *                                                             
     INTO :DCLBS03400T                                                  
     FROM BS03400T                                                      
     WHere DINDKAAR     = :WS_TIL_EVS_AAR                               
       and DFRAGLD      = :DCLBS04500T.DFRAGLD                          
       and EKOMNR       = :DCLBS04500T.EKOMNR                           
       and EEJDNR       = :DCLBS04500T.EEJDNR                           
       ;                                                                
                                                                        
     IF SQLCA.SQLCODE ^= 0                                              
     THEN                                                               
        DO;                                                             
         FEJLTEKST = 'SKAF_BS03400T';                                   
         CALL SQL_FEJL;                                                 
       END;                                                             
                                                                        
                                                                        
 END SKAF_BS03400T;                                                     
 /*********************************************************************/
 /* Hent data fra GS tabel BS035000T  ( vurderingsår, m.m.)           */
 /*********************************************************************/
 SKAF_BS03500T: PROC;                                                   
                                                                        
   EXEC SQL                                                             
   SELECT *                                                             
     INTO :DCLBS03500T                                                  
     FROM BS03500T                                                      
     WHere personnummer = :DCLBS03600T.PERSONNUMMER                     
       and DINDKAAR     = :WS_TIL_EVS_AAR                               
       and DFRAGLD      = :DCLBS04500T.DFRAGLD                          
       and EKOMNR       = :DCLBS04500T.EKOMNR                           
       and EEJDNR       = :DCLBS04500T.EEJDNR                           
       ;                                                                
                                                                        
     IF SQLCA.SQLCODE ^= 0                                              
     THEN                                                               
        DO;                                                             
         put skip list ('DCLBE66000T.DCPRN= ',DCLBE66000T.DCPRN);       
         put skip list ('Person   = ' !! DCLBS03600T.PERSONNUMMER);     
         put skip list ('årstal   = ' !! WS_TIL_EVS_AAR);               
         put skip list ('Fra dato = ' !! DCLBS04500T.DFRAGLD);          
         put skip list ('kommune  = ' !! DCLBS04500T.EKOMNR);           
         put skip list ('ejendom  = ' !! DCLBS04500T.EEJDNR);           
                                                                        
         FEJLTEKST = 'SKAF_BS03500T';                                   
         CALL SQL_FEJL;                                                 
       END;                                                             
                                                                        
 END SKAF_BS03500T;                                                     
 /*********************************************************************/
 /* Hent data fra GS tabel BS037000T                                  */
 /*********************************************************************/
 SKAF_BS03700T: PROC;                                                   
                                                                        
   EXEC SQL                                                             
   SELECT *                                                             
     INTO :DCLBS03700T                                                  
     FROM BS03700T                                                      
     WHere personnummer = :DCLBS03600T.PERSONNUMMER                     
       and DINDKAAR     = :WS_TIL_EVS_AAR                               
       and DFRAGLD       = :DCLBS04500T.DFRAGLD                         
       and EKOMNR       = :DCLBS04500T.EKOMNR                           
       and EEJDNR       = :DCLBS04500T.EEJDNR                           
       FETCH FIRST ROW ONLY                                             
       ;                                                                
                                                                        
     IF SQLCA.SQLCODE ^= 0                                              
     THEN                                                               
        DO;                                                             
         FEJLTEKST = 'SKAF_BS037000T';                                  
         CALL SQL_FEJL;                                                 
       END;                                                             
 END SKAF_BS03700T;                                                     
                                                                        
 /*********************************************************************/
 /* Beregn periodisering af GS data                                   */
 /* Herunder undersøg om GS skal gemmes eller ej                      */
 /* og om periodisering skal foretages                                */
 /*********************************************************************/
 PERIODISER: PROC;                                                      
                                                                        
   PERIODISER_GS   = '0'B;                                              
   GS_GEM          = '0'B;                                              
                                                                        
   /* Hvis start før indkomstår skal start være 01/01 i indkomstår */   
   IF SUBSTR(DCLBS03600T.EP_EJERSTARTDATO,5,4) <  WS_TIL_EVS_AAR        
   THEN                                                                 
      WS_Start_dato   =  WS_TIL_EVS_AAR !! '0101';                      
   ELSE                                                                 
      WS_Start_dato   =  DCLBS03600T.EP_EJERSTARTDATO;                  
                                                                        
   WS_SLUT_DATO       =  DCLBS03600T.EP_EJERSLUTDATO;                   
                                                                        
  /* Døds dato ligger efter start dato og før slut dato på ejerforhold*/
  /* Der skal gemmes og PERIODISERES                                  */
   IF DCLBE66000T.HIST_OPD_DETTE_DCIÆN > WS_Start_dato                  
    & DCLBE66000T.HIST_OPD_DETTE_DCIÆN <= WS_SLUT_dato                  
   Then DO;                                                             
         PERIODISER_GS = '1'B;                                          
         GS_GEM        = '1'B;                                          
        END;                                                            
                                                                        
                                                                        
   /* Døds dato ligger efter slutdato på ejerforhold      */            
   /* Der skal gemmes men ikke periodiseres               */            
   IF DCLBE66000T.HIST_OPD_DETTE_DCIÆN >  WS_Start_dato                 
    & DCLBE66000T.HIST_OPD_DETTE_DCIÆN > WS_SLUT_dato                   
   Then DO;                                                             
         PERIODISER_GS = '0'B;                                          
         GS_GEM        = '1'B;                                          
        END;                                                            
                                                                        
                                                                        
  /* Døds dato ligger på eller før start dato på ejerforhold          */
  /* Der skal ikke gemmes og periodiseres                             */
   IF DCLBE66000T.HIST_OPD_DETTE_DCIÆN <= WS_START_dato                 
   Then  DO;                                                            
         PERIODISER_GS = '0'B;                                          
         GS_GEM        = '0'B;                                          
        END;                                                            
                                                                        
  /* Døds dato ligger efter start dato og før slut dato på ejerforhold*/
  /* Der skal gemmes og periodiseres                                  */
  /*                                                                    
  IF DCLBE66000T.HIST_OPD_DETTE_DCIÆN > WS_START_dato                   
   Then DO;                                                             
         PERIODISER_GS = '0'B;                                          
         GS_GEM        = '0'B;                                          
        END;                                                            
  */                                                                    
                                                                        
                                                                        
 DCL     HJ_FLOAT1      DEC FLOAT (16);                                 
 DCL     HJ_FLOAT2      DEC FLOAT (16);                                 
 DCL     HJ_FLOAT_res   DEC FLOAT (16);                                 
 DCL     HJ_RES         DEC FIXED (20,4);                               
 DCL     HJ_RES4        DEC FIXED (20,4);                               
                                                                        
   IF PERIODISER_GS                                                     
   THEN DO;                                                             
          /* Beregn antal dage mellem EP_EJERSTARTDATO og         */    
          /* DØDSDATO  (DCLBE66000T.HIST_OPD_DETTE_DCIÆN)         */    
          WS_EP_EJERSLUTDATO_P      = 0;                                
          WS_EP_ANTALDAGE_P         = 0;                                
          HJDAGE_EJET               = 0;                                
          HJ_EJET_FRA               = 0;                                
          HJ_EJET_TIL               = 0;                                
                                                                        
          CALL BEREGN_DAGE;                                             
                                                                        
                                                                        
          WS_EP_EJERSLUTDATO_P      = DCLBE66000T.HIST_OPD_DETTE_DCIÆN; 
          WS_EP_ANTALDAGE_P         = HJDAGE_EJET;                      
                                                                        
          HJ_FLOAT1                 = HJDAGE_EJET/360;                  
                                                                        
          HJ_FLOAT2                = DCLBS03600T.EGS_BELØB;             
          HJ_FLOAT_res             = HJ_FLOAT1*HJ_FLOAT2;               
          HJ_RES                   = ROUND(HJ_FLOAT_res,4);             
          HJ_RES4                  = ROUND(HJ_RES,4);                   
                                                                        
          WS_EGS_BELØB_P           = ROUND(HJ_RES4,2);                  
                                                                        
          HJ_FLOAT2                = DCLBS03600T.EGS_RABATBELØB;        
          HJ_FLOAT_res             = HJ_FLOAT1*HJ_FLOAT2;               
          HJ_RES                   = ROUND(HJ_FLOAT_res,4);             
          HJ_RES4                  = ROUND(HJ_RES,4);                   
                                                                        
          WS_EGS_RABATBELØB_P      = ROUND(HJ_RES4,2);                  
                                                                        
                                                                        
          HJ_FLOAT2                = DCLBS03600T.EGS_BELØBEFTERRABAT;   
          HJ_FLOAT_res             = HJ_FLOAT1*HJ_FLOAT2;               
          HJ_RES                   = ROUND(HJ_FLOAT_res,4);             
          HJ_RES4                  = ROUND(HJ_RES,4);                   
                                                                        
          WS_EGS_BELØBEFTERRABAT_P = ROUND(HJ_RES4,2);                  
                                                                        
          /*                                                            
          WS_EGS_BELØB_P           =                                    
             ROUND(MULTIPLY(DIVIDE(HJDAGE_EJET,360,18,17),              
                   DCLBS03600T.EGS_BELØB,13,5),2);                      
                                                                        
          WS_EGS_RABATBELØB_P      =                                    
             ROUND(MULTIPLY(DIVIDE(HJDAGE_EJET,360,18,17),              
                   DCLBS03600T.EGS_RABATBELØB,13,5),2);                 
                                                                        
          WS_EGS_BELØBEFTERRABAT_P           =                          
             ROUND(MULTIPLY(DIVIDE(HJDAGE_EJET,360,18,17),              
                DCLBS03600T.EGS_BELØBEFTERRABAT,13,5),2);               
                                                                        
          */                                                            
            END;                                                        
   ELSE DO;                                                             
         /* oprindelige beløb m.m. fra ejerforhold BS03600 gemmes    */ 
          WS_EP_EJERSLUTDATO_P      = DCLBS03600T.EP_EJERSLUTDATO;      
          WS_EP_ANTALDAGE_P         = DCLBS03600T.EP_ANTALDAGE;         
          WS_EGS_BELØB_P            = DCLBS03600T.EGS_BELØB;            
          WS_EGS_RABATBELØB_P       = DCLBS03600T.EGS_RABATBELØB;       
          WS_EGS_BELØBEFTERRABAT_P  = DCLBS03600T.EGS_BELØBEFTERRABAT;  
        END;                                                            
                                                                        
 END PERIODISER;                                                        
                                                                        
 /*********************************************************************/
 /*     Beregn dage fra ejerstart til død                             */
 /*********************************************************************/
 BEREGN_DAGE:                                                           
 PROC;                                                                  
                                                                        
 DCL     FRADATO         PIC       '(8)9'    STATIC;                    
 DCL     1 FRA           BASED(ADDR(FRADATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
 DCL     TILDATO         PIC       '(8)9'    STATIC;                    
 DCL     1 TIL           BASED(ADDR(TILDATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
 DCL     PLUS_DAG          BIT(1)     INIT('0'B);                       
 DCL     WK_SKUD           FIXED BIN(15,1);                             
 DCL     SKUD_AAR          BIT(1)     INIT('0'B);                       
                                                                        
        WK_SKUD = (WS_TIL_EVS_AAR / 4);                                 
        WK_SKUD = (WK_SKUD * 4) - WS_TIL_EVS_AAR;                       
        IF WK_SKUD = 0 THEN                                             
           SKUD_AAR = '1'b;                                             
                                                                        
        HJ_EJET_FRA = WS_Start_dato;                                    
        HJ_EJET_TIL = DCLBE66000T.HIST_OPD_DETTE_DCIÆN;                 
                                                                        
        FRADATO = HJ_EJET_FRA;                                          
        TILDATO = HJ_EJET_TIL;                                          
                                                                        
        IF FRA.MD = 02 &                                                
           ((^SKUD_AAR &                                                
           FRA.DG = 28) !                                               
           (SKUD_AAR &                                                  
           FRA.DG = 29))                                                
         THEN                                                           
           FRA.DG = 30;                                                 
                                                                        
        IF FRA.DG > 30                                                  
         THEN                                                           
           FRA.DG = 30;                                                 
                                                                        
        IF TIL.MD = 02 &                                                
           ((^SKUD_AAR &                                                
           TIL.DG = 28) !                                               
           (SKUD_AAR &                                                  
           TIL.DG = 29)) THEN                                           
           TIL.DG = 30;                                                 
                                                                        
         IF TIL.DG > 30                                                 
           THEN DO;                                                     
           TIL.DG   = 30;                                               
        END;                                                            
                                                                        
        HJDAGE_EJET = (((TIL.MD -1) * 30) + TIL.DG) -                   
                      (((FRA.MD -1) * 30) + FRA.DG);                    
                                                                        
        IF TIL.ÅR > WS_TIL_EVS_AAR THEN                                 
            PLUS_DAG = '1'b;                                            
                                                                        
        IF PLUS_DAG                                                     
          THEN                                                          
           HJDAGE_EJET += HJDAGE_PLUS;                                  
                                                                        
 END BEREGN_DAGE;                                                       
                                                                        
 /*********************************************************************/
 /* FLYT FELTER TIL DB2-STRUKTUR GS oplysninger                       */
 /*********************************************************************/
 FLYT_FELTER_BE61000T: PROC;                                            
                                                                        
                                                                        
    DCLBE61000T = '';                                                   
                                                                        
    DCLBE61000T.TIL_EVS_AAR          = WS_TIL_EVS_AAR;                  
    DCLBE61000T.AFVIKLINGS_ID        = WS_FORSKUD_SLUT_TXT;             
    DCLBE61000T.DATA_ID              = BE55600M.DATA_ID;                
    DCLBE61000T.KØRSELS_ID           = '1';                             
    DCLBE61000T.PERSONNUMMER         = DCLBS03600T.PERSONNUMMER;        
    DCLBE61000T.EKOMNR               = DCLBS03600T.EKOMNR;              
    DCLBE61000T.EEJDNR               = DCLBS03600T.EEJDNR;              
    DCLBE61000T.EPLBNR               = DCLBS03600T.EPLBNR;              
    DCLBE61000T.DFRAGLD              = DCLBS03600T.DFRAGLD;             
    DCLBE61000T.EVUREJDID            = DCLBS03500T.EVUREJDID;           
    DCLBE61000T.CAJOURKODE           = DCLBS03500T.CAJOURKODE;          
    DCLBE61000T.BEJDGRUNDSKYLD       = DCLBS03500T.BEJDGRUNDSKYLD;      
    DCLBE61000T.VUR_VURDERINGSID     = DCLBS03500T.VUR_VURDERINGSID;    
    DCLBE61000T.VUR_ÆNDRDATO         = DCLBS03500T.VUR_ÆNDRDATO;        
    DCLBE61000T.VUR_ÅR               = DCLBS03500T.VUR_ÅR;              
    DCLBE61000T.VUR_OPRKODE          = DCLBS03500T.VUR_OPRKODE;         
    DCLBE61000T.VUR_GSBESKATGRL      = DCLBS03500T.VUR_GSBESKATGRL;     
    DCLBE61000T.GSBEREGNID           = DCLBS03100T.GSBEREGNID;          
    DCLBE61000T.DTILGLD              = DCLBS03600T.DTILGLD;             
    DCLBE61000T.GSBEREGNTID          = DCLBS03600T.GSBEREGNTID;         
    DCLBE61000T.EJERANDEL            = DCLBS03600T.EJERANDEL;           
    DCLBE61000T.EP_EJERSTARTDATO     = DCLBS03600T.EP_EJERSTARTDATO;    
    DCLBE61000T.EP_EJERSLUTDATO      = DCLBS03600T.EP_EJERSLUTDATO;     
    DCLBE61000T.EP_EJERSLUTDATO_P    = WS_EP_EJERSLUTDATO_P;            
    DCLBE61000T.EP_ANTALDAGE         = DCLBS03600T.EP_ANTALDAGE;        
    DCLBE61000T.EP_ANTALDAGE_P       = WS_EP_ANTALDAGE_P;               
    DCLBE61000T.EGS_OPKRPSKAT3112    = DCLBS03600T.EGS_OPKRPSKAT3112;   
    DCLBE61000T.EGS_BELØB            = DCLBS03600T.EGS_BELØB;           
    DCLBE61000T.EGS_BELØB_P          = WS_EGS_BELØB_P;                  
    DCLBE61000T.EGS_RABATBELØB       = DCLBS03600T.EGS_RABATBELØB;      
    DCLBE61000T.EGS_RABATBELØB_P     = WS_EGS_RABATBELØB_P;             
    DCLBE61000T.EGS_BELØBEFTERRABAT  = DCLBS03600T.EGS_BELØBEFTERRABAT; 
    DCLBE61000T.EGS_BELØBEFTERRABAT_P= WS_EGS_BELØBEFTERRABAT_P;        
    DCLBE61000T.BFENUMMER            = DCLBS03700T.BFENUMMER ;          
    DCLBE61000T.LEVERANCETID         = DCLBS03400T.LEVERANCETID;        
    DCLBE61000T.ADR_KORT             = DCLBS03400T.ADR_KORT;            
    DCLBE61000T.ADR_POSTNR           = DCLBS03400T.ADR_POSTNR;          
    DCLBE61000T.ADR_POSTDISTRIKT     = DCLBS03400T.ADR_POSTDISTRIKT;    
    DCLBE61000T.ADR_BYNAVN           = DCLBS03400T.ADR_BYNAVN;          
    DCLBE61000T.DØDS_DATO  =                                            
                   substr(DCLBE66000T.HIST_OPD_DETTE_DCIÆN,5,4)!! '-' !!
                   substr(DCLBE66000T.HIST_OPD_DETTE_DCIÆN,9,2)!! '-' !!
                   substr(DCLBE66000T.HIST_OPD_DETTE_DCIÆN,11,2);       
    DCLBE61000T.OPRET_AF             = WS_PROGRAMNAVN;                  
    DCLBE61000T.OPRET_TS             = HJ_TID;                          
                                                                        
 END FLYT_FELTER_BE61000T;                                              
                                                                        
 /*********************************************************************/
 /* INSERT række på DB2-tabel GS OPLYSNINGER TIL slut                 */
 /*********************************************************************/
 INSERT_GS_OPl: PROC;                                                   
                                                                        
                                                                        
        EXEC SQL                                                        
            INSERT INTO BE61000T                                        
            (                                                           
               TIL_EVS_AAR                                              
              ,AFVIKLINGS_ID                                            
              ,DATA_ID                                                  
              ,KØRSELS_ID                                               
              ,PERSONNUMMER                                             
              ,EKOMNR                                                   
              ,EEJDNR                                                   
              ,EPLBNR                                                   
              ,DFRAGLD                                                  
              ,EVUREJDID                                                
              ,CAJOURKODE                                               
              ,BEJDGRUNDSKYLD                                           
              ,VUR_VURDERINGSID                                         
              ,VUR_ÆNDRDATO                                             
              ,VUR_ÅR                                                   
              ,VUR_OPRKODE                                              
              ,VUR_GSBESKATGRL                                          
              ,GSBEREGNID                                               
              ,DTILGLD                                                  
              ,GSBEREGNTID                                              
              ,EJERANDEL                                                
              ,EP_EJERSTARTDATO                                         
              ,EP_EJERSLUTDATO                                          
              ,EP_EJERSLUTDATO_P                                        
              ,EP_ANTALDAGE                                             
              ,EP_ANTALDAGE_P                                           
              ,EGS_OPKRPSKAT3112                                        
              ,EGS_BELØB                                                
              ,EGS_BELØB_P                                              
              ,EGS_RABATBELØB                                           
              ,EGS_RABATBELØB_P                                         
              ,EGS_BELØBEFTERRABAT                                      
              ,EGS_BELØBEFTERRABAT_P                                    
              ,BFENUMMER                                                
              ,LEVERANCETID                                             
              ,ADR_KORT                                                 
              ,ADR_POSTNR                                               
              ,ADR_POSTDISTRIKT                                         
              ,ADR_BYNAVN                                               
              ,DØDS_DATO                                                
              ,OPRET_AF                                                 
              ,OPRET_TS                                                 
                     )                                                  
          Values                                                        
          (                                                             
            :DCLBE61000T.TIL_EVS_AAR                                    
           ,:DCLBE61000T.AFVIKLINGS_ID                                  
           ,:DCLBE61000T.DATA_ID                                        
           ,:DCLBE61000T.KØRSELS_ID                                     
           ,:DCLBE61000T.PERSONNUMMER                                   
           ,:DCLBE61000T.EKOMNR                                         
           ,:DCLBE61000T.EEJDNR                                         
           ,:DCLBE61000T.EPLBNR                                         
           ,:DCLBE61000T.DFRAGLD                                        
           ,:DCLBE61000T.EVUREJDID                                      
           ,:DCLBE61000T.CAJOURKODE                                     
           ,:DCLBE61000T.BEJDGRUNDSKYLD                                 
           ,:DCLBE61000T.VUR_VURDERINGSID                               
           ,:DCLBE61000T.VUR_ÆNDRDATO                                   
            :BE61000T.I_VUR_ÆNDRDATO                                    
           ,:DCLBE61000T.VUR_ÅR                                         
           ,:DCLBE61000T.VUR_OPRKODE                                    
            :BE61000T.I_VUR_OPRKODE                                     
           ,:DCLBE61000T.VUR_GSBESKATGRL                                
           ,:DCLBE61000T.GSBEREGNID                                     
           ,:DCLBE61000T.DTILGLD                                        
           ,:DCLBE61000T.GSBEREGNTID                                    
           ,:DCLBE61000T.EJERANDEL                                      
            :BE61000T.I_EJERANDEL                                       
           ,:DCLBE61000T.EP_EJERSTARTDATO                               
           ,:DCLBE61000T.EP_EJERSLUTDATO                                
            :BE61000T.I_EP_EJERSLUTDATO                                 
           ,:DCLBE61000T.EP_EJERSLUTDATO_P                              
            :BE61000T.I_EP_EJERSLUTDATO_P                               
           ,:DCLBE61000T.EP_ANTALDAGE                                   
           ,:DCLBE61000T.EP_ANTALDAGE_P                                 
           ,:DCLBE61000T.EGS_OPKRPSKAT3112                              
           ,:DCLBE61000T.EGS_BELØB                                      
           ,:DCLBE61000T.EGS_BELØB_P                                    
           ,:DCLBE61000T.EGS_RABATBELØB                                 
            :BE61000T.I_EGS_RABATBELØB                                  
           ,:DCLBE61000T.EGS_RABATBELØB_P                               
            :BE61000T.I_EGS_RABATBELØB_P                                
           ,:DCLBE61000T.EGS_BELØBEFTERRABAT                            
           ,:DCLBE61000T.EGS_BELØBEFTERRABAT_P                          
           ,:DCLBE61000T.BFENUMMER                                      
           ,:DCLBE61000T.LEVERANCETID                                   
           ,:DCLBE61000T.ADR_KORT                                       
           ,:DCLBE61000T.ADR_POSTNR                                     
            :BE61000T.I_ADR_POSTNR                                      
           ,:DCLBE61000T.ADR_POSTDISTRIKT                               
            :BE61000T.I_ADR_POSTDISTRIKT                                
           ,:DCLBE61000T.ADR_BYNAVN                                     
            :BE61000T.I_ADR_BYNAVN                                      
           ,:DCLBE61000T.DØDS_DATO                                      
           ,:DCLBE61000T.OPRET_AF                                       
           ,:DCLBE61000T.OPRET_TS                                       
           );                                                           
                                                                        
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
               ANT_INS_GS   = ANT_INS_GS + 1;                           
            END;                                                        
            OTHERWISE DO;                                               
               PUT SKIP LIST('DCLBE61000T.PERSONNUMMER:',               
                   DCLBE61000T.PERSONNUMMER);                           
               PUT SKIP LIST('DCLBS04500T.PRINTOPGØRID:',               
                   DCLBS04500T.PRINTOPGØRID);                           
                                                                        
               FEJLTEKST  = 'INSERT BE61000T';                          
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
 END INSERT_GS_OPl;                                                     
                                                                        
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF BE660000T                          RUTINE */
 /*********************************************************************/
 FETCH_BE66000_CUR: PROC;                                               
                                                                        
        exec sql fetch BE66000_CUR                                      
         into                                                           
          :DCLBE66000T.DCPRN,                                           
          :DCLBE66000T.HIST_OPD_DETTE_DCIÆN,                            
          :DCLBE66000T.PRINTOPGØRID                                     
         ;                                                              
                                                                        
       SELECT (SQLCODE);                                                
         WHEN (0)                                                       
         DO;                                                            
           ANT_LÆS_BE6600T = ANT_LÆS_BE6600T + 1;                       
           Person_fundet   = '1'B;                                      
         END;                                                           
         WHEN (100)                                                     
            Person_fundet   = '0'B;                                     
                                                                        
         OTHER                                                          
            DO;                                                         
              FEJLTEKST = 'FETCH BE66000_CUR';                          
              CALL SQL_FEJL;                                            
            END;                                                        
       END;                                                             
                                                                        
 END FETCH_BE66000_CUR;                                                 
                                                                        
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF BS04500T (Printopgør id)                  */
 /*********************************************************************/
 FETCH_BS04500_CUR: PROC;                                               
                                                                        
        exec sql fetch BS04500_CUR                                      
         into                                                           
              :DCLBS04500T.DINDKAAR                                     
             ,:DCLBS04500T.PRINTOPGØRID                                 
             ,:DCLBS04500T.PERSONNUMMER                                 
             ,:DCLBS04500T.GRUNDLAGSTYPE                                
             ,:DCLBS04500T.EKOMNR                                       
             ,:DCLBS04500T.EEJDNR                                       
             ,:DCLBS04500T.EENHEDLBNR                                   
             ,:DCLBS04500T.EPLBNR                                       
             ,:DCLBS04500T.DFRAGLD                                      
             ;                                                          
       SELECT (SQLCODE);                                                
         WHEN (0)                                                       
         DO;                                                            
           ID_FUNDET     = '1'B;                                        
           FØRSTE_LÆS    =  '0'B;                                       
           ANT_ID_FUNDET = ANT_ID_FUNDET + 1;                           
         END;                                                           
         WHEN (100)                                                     
         do;                                                            
             ID_FUNDET = '0'B;                                          
             IF FØRSTE_LÆS                                              
             THEN DO;                                                   
                   ANT_ID_EJ_FUNDET = ANT_ID_EJ_FUNDET + 1;             
                   FØRSTE_LÆS       =  '0'B;                            
                  END;                                                  
         END;                                                           
         OTHER                                                          
            DO;                                                         
              FEJLTEKST = 'FETCH BS04500_CUR';                          
              CALL SQL_FEJL;                                            
            END;                                                        
       END;                                                             
                                                                        
 END FETCH_BS04500_CUR;                                                 
                                                                        
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF BS04500T                            RUTINE */
 /*********************************************************************/
 OPEN_BS04500_CUR: PROC;                                                
                                                                        
         EXEC SQL OPEN BS04500_CUR;                                     
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'OPEN BS04500_CUR';                           
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_BS04500_CUR;                                                  
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF BS04500T                           RUTINE */
 /*********************************************************************/
 CLOSE_BS04500_CUR: PROC;                                               
                                                                        
         EXEC SQL CLOSE BS04500_CUR;                                    
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'CLOSE BS04500_CUR';                          
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END CLOSE_BS04500_CUR;                                                 
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF BE660000T                           RUTINE */
 /*********************************************************************/
 OPEN_BE66000_CUR: PROC;                                                
                                                                        
         EXEC SQL OPEN BE66000_CUR;                                     
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'OPEN BE66000_CUR';                           
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_BE66000_CUR;                                                  
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF BE66000T                           RUTINE */
 /*********************************************************************/
 CLOSE_BE66000_CUR: PROC;                                               
                                                                        
         EXEC SQL CLOSE BE66000_CUR;                                    
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'CLOSE BE66000_CUR';                          
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END CLOSE_BE66000_CUR;                                                 
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('*****************************************');   
                                                                        
         ABENDTXT = '101 ' !! FEJLTEKST;                                
         PUT SKIP LIST('ABENDTXT: ',ABENDTXT);                          
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         ABENDTXT = '101 ' !! FEJLTEKST;                                
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         BLKLIN.CCA = ZZWS1;                                            
         CALL ZZ20300(BLKLIN,'01');                                     
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL LÆSTE RECORDS PÅ BE66000T   ';                          
         DETLIN1.ANTAL = ANT_LÆS_BE6600T;                               
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL SKREVNE RÆKKER PÅ BE61000T  ';                          
         DETLIN1.ANTAL = ANT_INS_GS;                                    
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         CALL ZZ20300(BLKLIN,'01');                                     
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL PRINTOPGØRID LÆST PÅ BS04500T ';                        
         DETLIN1.ANTAL = ANT_ID_FUNDET;                                 
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL IKKE FUNDNE PRINTOPGØRID på BS04500T  ';                
         DETLIN1.ANTAL = ANT_ID_EJ_FUNDET;                              
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         CALL ZZ20300(BLKLIN,'01');                                     
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL GS LÆST PÅ B03600T   ';                                 
         DETLIN1.ANTAL = ANT_GS_FUNDET;                                 
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
         DETLIN1.TEKST =                                                
         'ANTAL IKKE FUNDNE GS på BS03600T  ';                          
         DETLIN1.ANTAL = ANT_GS_EJ_FUNDET;                              
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL KONTROLLISTE;                                             
                                                                        
         CALL ZZ20300(' ','99');                                        
                                                                        
 END AFSLUT;                                                            
 END BE86200;                                                           
