                                                                        
 /*********************************************************************/
 /*      BE60105M MACRO FOR ESKEMA                                    */
 /*                                                                   */
 /*      OPSÆT KODE FOR OM DEN AKTUELLE EJER HAR ET VALIDT CPRNR,     */
 /*      ET VALIDT SE-NR, ET LØBENR ELLER ET INVALIDT CPRNR.          */
 /*                                                                   */
 /*      RETURKODE = '0' HVIS VALIDT CPRNR                            */
 /*      RETURKODE = '1' HVIS INVALIDT CPR- ELLER SE-NR (=LØBENR)     */
 /*      RETURKODE = '2' HVIS VALIDT SE-NR                            */
 /*      RETURKODE = '3' HVIS CPRNR LIG LØBENR                        */
 /*      RETURKODE = '4' HVIS IKKE ET VALIDT CPR- ELLER SE-NR  O G    */
 /*                      EJERFORHOLDSKODE ER FORSKELLIG FRA 10 OG 41  */
 /*                                                                   */
 /*      INDGÅR I FØLGENDE PROGRAMMER:                                */
 /*                                                                   */
 /*      BE90000 ORDINÆR ESKEMA                                       */
 /*      BE90100 FORSKUD ESKEMA                                       */
 /*      BE94200 FORSKUD/ORDINÆR ESKEMA                               */
 /*                                                                   */
 /*********************************************************************/
 DAN_CPRNR_STATUS: PROC(CPRNR,EJERFORHOLD,RETURKODE);                   
                                                                        
 DCL     CPRNR          PIC       '(10)9';                              
                                                                        
 DCL     CHCPR          CHAR      (10) BASED(ADDR(CPRNR));              
                                                                        
 DCL     1 HJCPR        BASED(ADDR(CPRNR)),                             
           2 CIF1_9     PIC       '(9)9',                               
           2 CHECK      PIC       '9';                                  
                                                                        
 DCL     RETURKODE      CHAR      (1);                                  
                                                                        
 DCL     EJERFORHOLD    CHAR      (2);                                  
                                                                        
 DCL     ZS11201        ENTRY;                                          
 DCL     ZS32101        ENTRY;                                          
                                                                        
         RETURKODE = ' ';                                               
                                                                        
         IF CPRNR = 0                                                   
         THEN                                                           
            DO;                                                         
               RETURKODE = '4';                                         
               RETURN;                                                  
            END;                                                        
                                                                        
         IF CPRNR >= 10000000 &                                         
            CPRNR <= 99999999                                           
         THEN                                                           
            DO;                                                         
               RETURKODE = '2';                                         
               CALL ZS32101 (CIF1_9,CHECK,RETURKODE);                   
               IF RETURKODE = 'X'                                       
               THEN                                                     
                  RETURKODE = '1';               /* INVALIDT SE-NR    */
               ELSE                                                     
                  RETURKODE = '2';               /* CPRNR LIG SE-NR   */
            END;                                                        
         ELSE                                                           
            IF CPRNR < 0101000000 !                                     
               CPRNR > 3112999999                                       
            THEN                                                        
               RETURKODE = '3';                  /* CPRNR LIG LØBENR  */
            ELSE                                                        
               DO;                                                      
                  CALL ZS11201(CHCPR,RETURKODE);                        
                  IF RETURKODE = '0'                                    
                  THEN;                         /* VALIDT CPRNR      */ 
                  ELSE                                                  
                     RETURKODE = '1';           /* INVALIDT CPRNR    */ 
               END;                                                     
                                                                        
         IF RETURKODE = '1' !                                           
            RETURKODE = '3'                                             
         THEN                                                           
            DO;                                                         
               IF EJERFORHOLD = '10' !                                  
                  EJERFORHOLD = '41'                                    
               THEN;                                                    
               ELSE                                                     
                  RETURKODE = '4';                                      
            END;                                                        
                                                                        
 END DAN_CPRNR_STATUS;                                                  
