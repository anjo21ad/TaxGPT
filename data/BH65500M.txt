 /**********************************************************************
 * BH65500M                                                            *
 *                                                                     *
 * DENNE MACRO BRUGES VED EJENDOMSVÆRDISKATTE-BEREGNING TIL HENHOLDVIS *
 * SLUT OG FORSKUD TIL BEREGNING AF EN PERSONS ALDER PR. EN DATO I ET  *
 * AKTUELT ÅR (REGNSKABSÅR).                                           *
 *                                                                     *
 * OBS.: 'PR-DAGEN' ER PR. 31/12 I DET AKTUELLE ÅR F.EKS. 20041231.    *
 *                                                                     *
 *       FOR 'DØDE' PERSONER SKAL DOG  A L T I D  BENYTTES 'DØDSDAGEN' *
 *       D.V.S. CIVILSTANDSÆNDRINGSDATOEN  U N D T A G E N  HVIS DENNE *
 *       ER  E F T E R  SLUTDATOEN FOR DET AKTUELLE REGNSKABSÅR.       *
 *                                                                     *
 *       'PR-DAGEN' OPSÆTTES I DET KALDENDE PROGRAM.                   *
 *                                                                     *
 * DER KAN UDFØRES 4 FUNKTIONER:                                       *
 *                                                                     *
 *     1. DET UNDERSØGES OM EN PERSON ER 67 ÅR ELLER DEROVER.          *
 *                                                                     *
 *     2. DET UNDERSØGES OM EN PERSON ER FRA 60 TIL 66 ÅR INCL..       *
 *                                                                     *
 *     3. DET UNDERSØGES OM EN PERSON ENDNU IKKE ER FYLDT 60 ÅR.       *
 *                                                                     *
 *     4. DET UNDERSØGES OM EN  L E V E N D E  PERSON ER FYLDT 65 ÅR   *
 *        TIDLIGST PR. 1/7-2004 OG SENEST PÅ 'PR-DAGEN'.               *
 *        E L L E R                                                    *
 *        DET UNDERSØGES OM EN  D Ø D  PERSON ER FYLDT 65 ÅR I LEVENDE *
 *        LIVE PR. 1/7-2004 OG SENEST PÅ 'PR-DAGEN' SOM SKAL VÆRE SAT  *
 *        LIG CIVILSTANDSÆNDRINGSDATOEN JVF. OVENFOR.                  *
 *                                                                     *
 **********************************************************************/
                                                                        
 PERSON_ALDER_OK: PROC (PRDATO,CPRNR,FUNKTION) RETURNS (BIT(1));        
                                                                        
 DCL     RETURBIT        BIT       (1);                                 
                                                                        
 DCL     FUNKTION        CHAR      (1);                                 
                                                                        
 DCL     PRDATO          FIXED     (9);                                 
                                                                        
 DCL     CPRNR           PIC       '(10)9';                             
                                                                        
 DCL     1 CPRNUM        BASED     (ADDR(CPRNR)),                       
           2 DG          PIC       '99',                                
           2 MD          PIC       '99',                                
           2 ÅR          PIC       '99',                                
           2 LBNR        PIC       '9',                                 
           2 NR          CHAR      (2),                                 
           2 CHCIF       CHAR      (1);                                 
                                                                        
 DCL     FØDSDAG         FIXED     (9);                                 
                                                                        
         RETURBIT = '0'B;                                               
                                                                        
         IF CPRNR < 0101000000 !       /* AKT. PERSON EJ MED CPR.NR. */ 
            CPRNR > 3112999999                                          
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
                                                                        
                /* PERSONENS FØDSELSÅRHUNDREDE OPSÆTTES JVF. */         
                /* 1. CIFFER AF LØBENUMMER + ÅR I CPRNR      */         
                                                                        
                FØDSDAG = 19000000 +        /* DEFAULT FØDT 19XX */     
                          (CPRNUM.ÅR * 10000) +                         
                          (CPRNUM.MD * 100) +                           
                          CPRNUM.DG;                                    
                                                                        
                SELECT (CPRNUM.LBNR);                                   
                                                                        
                  WHEN (5,6,7,8)                                        
                    DO;                                                 
                      IF CPRNUM.ÅR >= 58 & CPRNUM.ÅR <= 99              
                      THEN                                              
                         FØDSDAG = 18000000 +                           
                                   (CPRNUM.ÅR * 10000) +                
                                   (CPRNUM.MD * 100) +                  
                                   CPRNUM.DG;                           
                      ELSE                                              
                         IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36           
                         THEN                                           
                            FØDSDAG = 20000000 +                        
                                      (CPRNUM.ÅR * 10000) +             
                                      (CPRNUM.MD * 100) +               
                                      CPRNUM.DG;                        
                    END;                                                
                                                                        
                  WHEN (4)                                              
                    DO;                                                 
                      IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36              
                      THEN                                              
                         FØDSDAG = 20000000 +                           
                                   (CPRNUM.ÅR * 10000) +                
                                   (CPRNUM.MD * 100) +                  
                                   CPRNUM.DG;                           
                    END;                                                
                                                                        
                  WHEN (9)                                              
                    DO;                                                 
                      IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36              
                      THEN                                              
                         FØDSDAG = 20000000 +                           
                                   (CPRNUM.ÅR * 10000) +                
                                   (CPRNUM.MD * 100) +                  
                                   CPRNUM.DG;                           
                    END;                                                
                                                                        
                  OTHERWISE;                                            
                                                                        
                END;                                                    
                                                                        
                /* BEREGN PERSONENS ALDER */                            
                                                                        
                SELECT (FUNKTION);                                      
                                                                        
                  WHEN ('1')                                            
                    DO;                                                 
                       IF (PRDATO - FØDSDAG) >= 670000                  
                       THEN                                             
                          RETURBIT = '1'B;                              
                    END;                                                
                                                                        
                  WHEN ('2')                                            
                    DO;                                                 
                       IF (((PRDATO - FØDSDAG) >= 600000) &             
                           ((PRDATO - FØDSDAG)  < 670000))              
                       THEN                                             
                          RETURBIT = '1'B;                              
                    END;                                                
                                                                        
                  WHEN ('3')                                            
                    DO;                                                 
                       IF (PRDATO - FØDSDAG) < 600000                   
                       THEN                                             
                          RETURBIT = '1'B;                              
                    END;                                                
                                                                        
                  WHEN ('4')                                            
                    DO;                                                 
                       IF (20040630 - FØDSDAG) >= 650000                
                       THEN;                                            
                       ELSE                                             
                          IF (PRDATO - FØDSDAG) >= 650000               
                          THEN                                          
                             RETURBIT = '1'B;                           
                    END;                                                
                                                                        
                  OTHERWISE;                                            
                                                                        
                END;                                                    
                                                                        
            END;                                                        
                                                                        
         RETURN (RETURBIT);                                             
                                                                        
 END PERSON_ALDER_OK;                                                   
