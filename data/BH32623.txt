 BH32623: /* SKAF HENVISNINGSNR. FRA NYT SLUTÅR TIL ESR-UDTRÆK */       
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /********************************************************************/ 
  DCL COPYRIGHT  CHAR  (30) STATIC     INIT ('COPYRIGHT KMD A/S 2004'); 
 /********************************************************************* 
 *                                                                   *  
 * FORMÅL : AT UNDERSØGE OM DET ER SKET EN OMPLACERING                * 
 *                                                                    * 
 * INPUT  : B.H32100D  UDTRÆK FRA ESR EFTER 2-EJER-BEHANDLING         * 
 *          B.H65501D  MINI MT-FIL FRA EVS NYT SLUTÅR (HOVEDLEV.).    * 
 *                                                                    * 
 * OUTPUT : B.H32600D  ESR UDTRÆK MED OMPLACERINGS DATA BL.A SOM      * 
 *                     DUBLETTER.                                     * 
 *          BH32601Y   KØRSELSKONTROLLISTE                            * 
 *                                                                    * 
 * PROGRAMMØR: GGP              LSU T&S EVS            NOV 2003       * 
 * RETTET:     BO SCHMIDT       LSU T&S EVS            JUN 2004       * 
 * SLUT 2012:  KRAV 18-01 - BENYTTELSESKODE 25, LKA, OKTOBER 2012     * 
 *             OMCOMPILERET P.G.A.                                    * 
 *             NYT FELT CEANV I STRUKTUR BH31800N                     * 
 *             OG NYT FELT CEANV I STRUKTUR BH32200N (BH32202N)       * 
 *             DE TO FELTER SÆTTES LIG HINANDEN                       * 
 *                                                                    * 
 * FORSKUD2014 Lars Harritshøj                           Maj 2013     * 
 *             Compileret Ingen ændringer                             * 
 *                                                                    * 
 * SLUT 2013   Jørgen Saugmann                           okt 2013     * 
 *             Compileret Ingen ændringer                             * 
 *                                                                    * 
 * FORSKUD2015 Jørgen Saugmann                           jun 2014     * 
 *             Compileret Ingen ændringer                             * 
 * SLUT 2014   Jørgen Saugmann                           okt 2014     * 
 *             Compileret Ingen ændringer                             * 
 * FORSKUD2016 Z6JNK                                     08.06.2015   * 
 *             Compileret. Ingen ændringer                            * 
 * FORSKUD2017 Z8QEB                                     16.08.2016   * 
 *             Indlagt EXECPARM                                       * 
 *             Så omplacerings check kan spriges over i test. Da det  * 
 *             kører meget længe. Hvis der angives TEST springes      * 
 *             undersøgelsen af omplacering over                      * 
 *                                                                    * 
 * SLUT 2018   Esben Brodersen                           12.09.2018   * 
 *             ændret struktur BH31800N -> BH33700N                   * 
 *             ændret struktur BH32200N                               * 
 *             ændret struktur BQ30000N samt udtræk pga. af           * 
 *             udvidelse til 2familiebolig                            * 
 * Rettelse #02: Edward Laryea, EDL                     22-03-2019    * 
 *               Nyt felt STRAKS_EVS i BH33700N                       * 
 * Forskud2021 Per Brunk                               Marts 2020     * 
 *               Nye felter i BH32202N/BH33700N VURMARK og VUR2001    * 
 * Slut 2020 : Per Brunk PBR Ny struktur BH33700N/BH31800N   Okt 2020 * 
 * SLUT 2021   Z6SEP                                   Marts 2021     * 
 *             BH65300M rettet til BH55300M                           * 
 *             BH65300N rettet til BH65321N                           * 
 * Slut 2021 : Per Brunk PBR Defect 889                      Jun 2021 * 
 *             Flyt BFENR og VUREJENDOMID fra BH321 til BH326         * 
 * SLUT 2023   Z6PBR                                   Oktober 2023   * 
 *             Kopieret fra BH32600                                   * 
 *             BH65300N rettet til BH65323N                           * 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************  
 *                       DECLARE AF REGISTRE                          * 
 **********************************************************************/
 DCL     BH655I FILE RECORD INPUT;     /* MINI MT-FIL */                
 DCL     BH321I FILE RECORD INPUT;     /* ESR UDTRÆK */                 
 DCL     BH326O FILE RECORD OUTPUT;    /* BEHANDLET ESR UDTRÆK */       
 /********************************************************************* 
 *       DECLARE AF MINI MT-FIL FRA NYT SLUTÅR                        * 
 *********************************************************************/ 
 DCL     BH655_AR CHAR (200) VAR;                                       
 DCL     1 BH655 BASED(ADDR(BH655_AR)),                                 
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH65323N;;                                          
                                                                        
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER TESTKØRSEL              */
 /*      FOR AT KUNNE AFVIKLE KØRSLER HURTIGER I TEST.                */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
                                                                        
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     BH321_AR        CHAR      (2000) VAR;                          
 DCL     1 BH321         BASED     (ADDR(BH321_AR)),                    
           4 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH33700N;                                           
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK OUTPUT                                 * 
 *********************************************************************/ 
 DCL     1 BH326,                                                       
           %INCLUDE BH32200N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                        /* TÆLLEVÆRKER TIL TOTALLISTEN */ 
           2 LÆS_BH655   FIXED DEC (7),                                 
           2 LÆS_BH321   FIXED DEC (7),                                 
           2 FUNDET      FIXED DEC (7),                                 
           2 NOMPLA      FIXED DEC (7),                                 
           2 SKROMPLA    FIXED DEC (7),                                 
           2 FOMPLA      FIXED DEC (7),                                 
           2 SOMPLA      FIXED DEC (7),                                 
           2 SKREVET     FIXED DEC (7);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     1 BH55300M,                                                    
           %INCLUDE BH55300M; /* OPSÆT AKTUELT INDKOMSTÅR M.V. */       
                                                                        
 DCL     HJSLUTÅR        BIN FIXED (15);                                
 DCL     EOF_BH655       BIT       (1);                                 
 DCL     EOF_BH321       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     FEJLTEKST         CHAR (80);                                   
 DCL     ICOMPLA           BIT;                                         
 DCL     IICOMPLA          BIT;                                         
 DCL     ZS67000        ENTRY;                                          
 DCL     ZS30101        ENTRY;                                          
 /***************************************************************       
 /*      DECLARE AF ABEND-RUTINE                                        
 /**************************************************************/       
 DCL     ABENDKODE    FIXED       (2);                                  
 DCL     1 ABENDMEDD,                                                   
           2 ABENDMODUL CHAR      (7),                                  
           2 ABENDFILL1 CHAR      (1),                                  
           2 ABENDMEDNR CHAR      (2),                                  
           2 ABENDFILL2 CHAR      (1),                                  
           2 ABENDTXT   CHAR      (45);                                 
 DCL     ABENDRTKODE    FIXED     (2);                                  
                                                                        
 DCL    SQLKODE         FIXED     (5);                                  
 DCL    SQLCODE_CH      CHAR      (5);                                  
 DCL    SQLCODE_PIC     PIC       'SSSS9' DEF SQLCODE_CH;               
 /*******************************************************************/  
                                                                        
  EXEC SQL INCLUDE BQ30000T;                                            
  EXEC SQL INCLUDE SQLCA;                                               
                                                                        
 DCL     1 EVS_ESK,                     /* EVS EJERSKABSTABELEN       */
         %INCLUDE BQ30000N;;                                            
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH32623L'),                                        
           2 F1          CHAR      (21)      INIT      (' '),           
           2 TEKST2      CHAR      (52)      INIT      (                
           'BEHANDL EVT. OMPLACEREDE FRA ESR UDTRÆK'),                  
           2 F2          CHAR      (21)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (63)      INIT      ((63)' ');       
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH655 = NEJ;                                               
         EOF_BH321 = NEJ;                                               
                                                                        
         ON ERROR CALL PLIDUMP ('SNHOB');                               
         OPEN FILE (BH655I) TITLE ('BH65501I');                         
         OPEN FILE (BH321I) TITLE ('BH32100I');                         
         OPEN FILE (BH326O) TITLE ('BH32600O');                         
         ON ENDFILE (BH655I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
         END;                                                           
         ON ENDFILE (BH321I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
            EOF_BH321 = JA;                                             
         END;                                                           
         CALL ZZ20390 ('*OPEN','BH32601Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         TV = '';                                                       
         FEJLTEKST = ' ';                                               
         ABENDMEDD = ' ';                                               
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
         CALL LÆS_BH655;                                                
         CALL LÆS_BH321;                                                
                                                                        
         DO WHILE (EOF_BH655 = NEJ ! EOF_BH321 = NEJ);                  
            SELECT;                                                     
               WHEN (BH321.DCPRCIR > BH655.DCPRN & EOF_BH655 = NEJ)     
                  CALL LÆS_BH655;                                       
               WHEN (BH321.DCPRCIR < BH655.DCPRN & EOF_BH321 = NEJ)     
                  DO;                                                   
                     ICOMPLA = '0'B;                                    
                     IICOMPLA = '0'B;                                   
                     CALL BEHANDL_EJ_PÅ_MT;                             
                     CALL LÆS_BH321;                                    
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     TV.FUNDET = TV.FUNDET + 1;                         
                     CALL BEHANDL_MT_OK;                                
                     CALL LÆS_BH321;                                    
                  END;                                                  
            END;                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS ESR UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_BH321:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH321I) INTO (BH321_AR);                            
                                                                        
         IF EOF_BH321 = NEJ                                             
         THEN                                                           
            DO;                                                         
               BH326 = '';                                              
               BH326.CEANV = ' ';          /* SLUT12 LKA, NOV 2012 */   
               BH326.CEANV = BH321.CEANV;  /* SLUT12 LKA, NOV 2012 */   
                                                                        
               BH326.EKOMNR       = BH321.EKOMNR;                       
               BH326.EEJDNR       = BH321.EEJDNR;                       
               BH326.EENHEDLBNR   = BH321.EENHEDLBNR;                   
               BH326.BFENR        = BH321.BFENR;         /*D889*/       
               BH326.VUREJENDOMID = BH321.VUREJENDOMID;  /*D889*/       
                                                                        
                                                                        
               BH326.EJER = BH321, BY NAME;                             
                                                                        
               IF BH326.EJER.DAFSTÅ > 0                                 
               THEN                                                     
                  BH326.SORTDATO = BH326.EJER.DAFSTÅ;                   
               ELSE                                                     
                  BH326.SORTDATO = 99991231;                            
                                                                        
               BH326.STAMOPL = BH321, BY NAME;                          
                                                                        
               TV.LÆS_BH321 = TV.LÆS_BH321 + 1;                         
            END;                                                        
                                                                        
 END LÆS_BH321;                                                         
 /********************************************************************* 
 *       LÆS MINI MT-FIL                                              * 
 *********************************************************************/ 
 LÆS_BH655:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH655I) INTO (BH655_AR);                            
                                                                        
         IF EOF_BH655 = NEJ & BH655.DCPRN < 9999999999                  
         THEN                                                           
            DO;                                                         
               TV.LÆS_BH655 = TV.LÆS_BH655 + 1;                         
               IF BH655.CSTATUS = '2'                                   
               THEN                                                     
                  TV.NOMPLA = TV.NOMPLA + 1;                            
            END;                                                        
                                                                        
 END LÆS_BH655;                                                         
                                                                        
 /********************************************************************* 
 *       BEHANDL EJER SOM IKKE FINDES PÅ MINI-MT                      * 
 *********************************************************************/ 
 BEHANDL_EJ_PÅ_MT:                                                      
 PROC;                                                                  
 DCL STOPOP BIT;                                                        
 DCL CPR_PIC PIC '9999999999';                                          
                                                                        
         STOPOP = '0'B;                                                 
         CPR_PIC = 0;                                                   
         BH326.SORTCPR  = BH321.DCPRCIR;                                
         BH326.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BH326.CCIVS = ' ';                                             
         BH326.CSBSK = ' ';                                             
                                                                        
         IF EX_PARM ^= 'TEST'                                           
         THEN                                                           
           DO;                                                          
                                                                        
             CALL DECLARE_CURSOR;                                       
             CALL OPEN_BQ300_Å1_CURSOR;                                 
             DO UNTIL (STOPOP = '1'B);                                  
               CALL FETCH_BQ300_Å1_CURSOR;                              
               IF SQLCA.SQLCODE ^= 0                                    
                  ! ICOMPLA = '1'B                                      
               THEN                                                     
                 STOPOP = '1'B;                                         
             END;                                                       
             IF SQLCA.SQLCODE ^= 0                                      
              & SQLCA.SQLCODE ^= 100                                    
             THEN                                                       
               DO;                                                      
                 SQLCODE_PIC = SQLCODE;                                 
                 ABENDTXT   =  '281 FEJL FETCH_BQ300_Å1,SQLCODE= '      
                               !! SQLCODE_CH;                           
                 CALL SQL_FEJL;                                         
               END;                                                     
             CALL CLOSE_BQ300_Å1_CURSOR;                                
                                                                        
             IF ICOMPLA = '0'B                                          
             THEN                                                       
               DO;                                                      
                  STOPOP = '0'B;                                        
                  EVS_ESK.DCPRN = BH321.DCPRCIR;                        
                  CALL OPEN_BQ300_Å2_CURSOR;                            
                  DO UNTIL (SQLCA.SQLCODE ^= 0);                        
                    CALL FETCH_BQ300_Å2_CURSOR;                         
                    IF SQLCA.SQLCODE ^= 0                               
                     ! IICOMPLA = '1'B                                  
                    THEN                                                
                      STOPOP = '1'B;                                    
                  END;                                                  
                  IF SQLCA.SQLCODE ^= 0                                 
                   & SQLCA.SQLCODE ^= 100                               
                  THEN                                                  
                    DO;                                                 
                      SQLCODE_PIC = SQLCODE;                            
                      ABENDTXT   =  '282 FEJL FETCH_BQ300_Å2,' !!       
                                    'SQLCODE= ' !! SQLCODE_CH;          
                      CALL SQL_FEJL;                                    
                    END;                                                
                  CALL CLOSE_BQ300_Å2_CURSOR;                           
               END;                                                     
           END; /* EX_PARM ^= 'TEST' */                                 
                                                                        
                                                                        
         SELECT;                                                        
           WHEN (ICOMPLA = '1'B)                                        
            DO;                                                         
              /*BEVAR RECORD*/                                          
              BH326.COMPLA = '1';                                       
              BH326.DOMPLAÅR = GLSLUTÅR;                                
              BH326.DOMPLACPR = EVS_ESK.DOMPLACPR;                      
              WRITE FILE (BH326O) FROM (BH326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              /*DUBLET RECORD*/                                         
              BH326.COMPLA = '2';                                       
              BH326.DOMPLAÅR = GLSLUTÅR;                                
              CPR_PIC = BH321.DCPRCIR;                                  
              BH326.DOMPLACPR = CPR_PIC;                                
              CPR_PIC = EVS_ESK.DOMPLACPR;                              
              BH326.DCPRCIR = CPR_PIC;                                  
              BH326.SORTCPR = CPR_PIC;                                  
              WRITE FILE (BH326O) FROM (BH326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              TV.SKROMPLA = TV.SKROMPLA + 1;                            
              TV.SOMPLA = TV.SOMPLA + 1;                                
            END;                                                        
           WHEN (IICOMPLA = '1'B)                                       
            DO;                                                         
              /*BEVAR RECORD*/                                          
              BH326.COMPLA = '1';                                       
              BH326.DOMPLAÅR = GLSLUTÅR - 1;                            
              BH326.DOMPLACPR = EVS_ESK.DOMPLACPR;                      
              WRITE FILE (BH326O) FROM (BH326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              /*DUBLET RECORD*/                                         
              BH326.COMPLA = '2';                                       
              BH326.DOMPLAÅR = GLSLUTÅR - 1;                            
              CPR_PIC = BH321.DCPRCIR;                                  
              BH326.DOMPLACPR = CPR_PIC;                                
              CPR_PIC = EVS_ESK.DOMPLACPR;                              
              BH326.DCPRCIR = CPR_PIC;                                  
              BH326.SORTCPR = CPR_PIC;                                  
              WRITE FILE (BH326O) FROM (BH326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
              TV.SKROMPLA = TV.SKROMPLA + 1;                            
              TV.SOMPLA = TV.SOMPLA + 1;                                
            END;                                                        
           OTHER                                                        
            DO;                                                         
              WRITE FILE (BH326O) FROM (BH326);                         
              TV.SKREVET = TV.SKREVET + 1;                              
            END;                                                        
         END;/*SELECT*/                                                 
                                                                        
 END BEHANDL_EJ_PÅ_MT;                                                  
 /********************************************************************* 
 *       BEHANDL EJER SOM FINDES PÅ MINI-MT                           * 
 *********************************************************************/ 
 BEHANDL_MT_OK:                                                         
 PROC;                                                                  
 DCL CPR_PIC PIC '9999999999';                                          
                                                                        
         BH326.SORTCPR = BH321.DCPRCIR;                                 
         BH326.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BH326.CCIVS = BH655.CCIVS;                                     
         BH326.CSBSK = BH655.HIST_OPL(SLUTÅR).CSBSK;                    
                                                                        
         IF BH655.CSTATUS = '2' &                                       
            BH655.HIST_OPL(SLUTÅR).DHENVN > 0                           
         THEN                                                           
            DO;                                                         
               /* PERSONEN ER OMPLACERET JVF. FORSKUD */                
               /* DHENVN INDEHOLDER DET NYE PERSONNR. */                
               BH326.COMPLA = '1';                                      
               BH326.DOMPLAÅR = SLUTÅR;                                 
               CPR_PIC = BH655.HIST_OPL(SLUTÅR).DHENVN;                 
               BH326.DOMPLACPR = CPR_PIC;                               
               WRITE FILE (BH326O) FROM (BH326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
               /*DUBLET RECORD*/                                        
               BH326.COMPLA = '2';                                      
               BH326.DOMPLAÅR = SLUTÅR;                                 
               CPR_PIC = BH321.DCPRCIR;                                 
               BH326.DOMPLACPR = CPR_PIC;                               
               BH326.DCPRCIR = BH655.HIST_OPL(SLUTÅR).DHENVN;           
               BH326.SORTCPR = BH655.HIST_OPL(SLUTÅR).DHENVN;           
               WRITE FILE (BH326O) FROM (BH326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
               TV.FOMPLA = TV.FOMPLA + 1;                               
               TV.SKROMPLA = TV.SKROMPLA + 1;                           
            END;                                                        
         ELSE                                                           
            DO;                                                         
               WRITE FILE (BH326O) FROM (BH326);                        
               TV.SKREVET = TV.SKREVET + 1;                             
            END;                                                        
                                                                        
 END BEHANDL_MT_OK;                                                     
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BH655;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ MINI MT-FIL NYT SLUTÅR    B.H65501D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.NOMPLA;                                     
         DETLIN1.TEKST = '      HERAF OMPLACERET I FORSKUD ';           
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BH321;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ ESR UDTRÆK                B.H32100D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FUNDET;                                     
         DETLIN1.TEKST = '      HERAF EJER FUNDET PÅ MINI MT-FIL  ';    
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKREVET;                                    
         DETLIN1.TEKST = 'SKREVNE INDIVIDER INKL OMPLACEREDE B.H32600D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKROMPLA;                                   
         DETLIN1.TEKST = '        HERAF OMPLACEREDE';                   
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FOMPLA;                                     
         DETLIN1.TEKST = '              HERAF JVF. FORSKUD';            
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SOMPLA;                                     
         DETLIN1.TEKST = '              HERAF JVF. SLUT';               
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
                                                                        
         CLOSE FILE (BH321I),                                           
               FILE (BH655I),                                           
               FILE (BH326O);                                           
                                                                        
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
                                                                        
 %INCLUDE BH32601M; /* SQL DEL*/                                        
                                                                        
 END  BH32623;                                                          
