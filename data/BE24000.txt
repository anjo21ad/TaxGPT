 BE24000:    /* Check versioner på individ 129 og 132         #01     */
 PROC OPTIONS (MAIN) REORDER;                                           
 /*********************************************************************/
 /* FUNKTION:                                                         */
 /*                                                                   */
 /* BEHANDLING AF 0080 3132 INDIVIDER                                 */
 /* 1. LÆSER HEADER PÅ INDIVID 129                                    */
 /*                                                                   */
 /* 2. LÆSER HEADER PÅ INDIVID 132                                    */
 /*                                                                   */
 /* 3. CHECKER AT KØRSELSNUMMER OG KØRSELSDATO ER ENS PÅ DE TO HEADERE*/
 /*                                                                   */
 /* 4. HVIS DER ER FEJL I INDDATA DUMPES PROGRAMMET                   */
 /*                                                                   */
 /*  INDDATA:        FLYDENDE DATA FRA DXC MED FØLGENDE INDIVIDER:    */
 /*                  -  INDIVID 129 Ejendomsdata                      */
 /*                  -  INDIVID 132 Ejendomsdata                      */
 /*                                                                   */
 /*  UDDATA                                                           */
 /*                                                                   */
 /*  EXTERNE REF.    ZS30101   ABENDRUTINE                            */
 /*                                                                   */
 /*********************************************************************/
 /*      RETTET AF   :  MAY BRITT SIMONSEN OKT 2023                   */
 /*      VEDRØRENDE  :                                                */
 /*      HENVISNING  :                                                */
 /*                                                                   */
 /*    Kan ikke håndtere et tomt inddatasæt BE19400J abend !!!!       */
 /*                                                                   */
 /*********************************************************************/
 /*********************************************************************/
 /*      DECLARES AF FILES                                            */
 /*********************************************************************/
  DCL BE2401I FILE RECORD INPUT;  /*INDIVID 129 EJENDOMSDATA*//*ÅR-RET*/
  DCL BE2402I FILE RECORD INPUT;  /*INDIVID 132 EJENDOMSDATA*//*ÅR-RET*/
  DCL SYSPRINT EXTERNAL FILE PRINT;                                     
 /*********************************************************************/
 /*      DECLARES AF BUILTIN-FUNKTIONER                               */
 /*********************************************************************/
  DCL (ADDR,                                                            
       CSTG,                                                            
       NULL,                                                            
       STG,                                                             
       INDEX,                                                           
       SUBSTR,                                                          
       LBOUND,                                                          
       HBOUND,                                                          
       VERIFY,                                                          
       DIVIDE,                                                          
       LENGTH,                                                          
       MOD,                                                             
       PLIRETC,                                                         
       PLIDUMP,                                                         
       LOW,                                                             
       TIMESTAMP)       BUILTIN;                                        
 /*********************************************************************/
 /*      DECLARES                                                     */
 /*********************************************************************/
 DCL ZS61900            ENTRY;                                          
 /*********************************************************************/
 /*      DECLARES AF STRUKTURER  INPUT                                */
 /*********************************************************************/
 /*********************************************************************/
 /* Input FILER                                                       */
 /*********************************************************************/
 DCL LGD_INPUTFIL       BIN FIXED(15);                                  
 DCL 1 INPUTFIL         CHAR(20004) VAR; /* MED LIDT EKSTRA PLADS */    
 DCL 1 INPUTFIL1_IND,                                                   
       2 DATA           CHAR(20000);                          /*ÅR-RET*/
 DCL 1 INDV129_HEAD BASED (ADDR(INPUTFIL1_IND.DATA)),                   
       %INCLUDE BE24000N;                                    /*ÅR-RET*/ 
 DCL 1 INPUTFIL2_IND,                                                   
       2 DATA           CHAR(20000);                          /*ÅR-RET*/
 DCL 1 INDV132_HEAD BASED (ADDR(INPUTFIL2_IND.DATA)),                   
       %INCLUDE BE24000N;                                    /*ÅR-RET*/ 
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
    ON ERROR                                                            
       BEGIN;                                                           
          ON ERROR SYSTEM;                                              
          PUT SKIP LIST ('PROGRAMFEJL');                                
          CALL PLIDUMP  ('SBKO','MODUL BE24000');       /*ÅR-RET*/      
       END;                                                             
                                                                        
    ON CONVERSION                                                       
       BEGIN;                                                           
          PUT SKIP LIST(' BE24000: CONVERSION FEJL');      /*ÅR-RET*/   
       END;                                                             
                                                                        
    CALL ZS61900;/* SKRIV SSI-DATO */                                   
 /*------------------------------------------------------------------*/ 
 /* HOUSEKEEPING_SLUT                                                */ 
 /*------------------------------------------------------------------*/ 
 HOUSEKEEPING_SLUT:                                                     
    CALL INIT;                                                          
    CALL CHECK_INDIVIDTYPER;                                            
    CALL AFSLUT;                                                        
    CALL INIT;                                                          
 /*********************************************************************/
 /*      HOVEDSTYRING                                                 */
 /*********************************************************************/
    CALL BEHANDLING;                                                    
    CALL AFSLUT;                                                        
 /*********************************************************************/
 /*      INIT                                                         */
 /*********************************************************************/
 INIT: PROC;                                                            
    OPEN FILE (BE2401I) TITLE ('BE24029I'); /* individ 129 */ /*ÅR-RET*/
    OPEN FILE (BE2402I) TITLE ('BE24032I'); /* individ 132 */ /*ÅR-RET*/
 END INIT;                                                              
 /*********************************************************************/
 /*      CHECK_INDIVIDTYPER                                           */
 /*********************************************************************/
 CHECK_INDIVIDTYPER: PROC;                                              
    CALL LÆS_INPUTFIL; /* LÆS HEADER */                                 
    CALL LÆS_INPUTFIL; /* LÆS NÆSTE RECORD MED IND.TYPE 129 ELLER 132 */
    IF INDV129_HEAD.INDVNR ^= '0129'                                    
    THEN DO;                                                            
       PUT SKIP LIST ('DATASÆT BE24029I ER IKKE INDIVID 129');          
       ABENDTXT = '250 DATASÆT BE24029I ER IKKE INDIVID 129';           
       CALL ABEND_MODUL('BE24000'); /*ÅR-RET*/                          
    END;                                                                
    IF INDV132_HEAD.INDVNR ^= '0132'                                    
    THEN DO;                                                            
       PUT SKIP LIST ('DATASÆT BE24032I ER IKKE INDIVID 132');          
       ABENDTXT = '250 DATASÆT BE24032I ER IKKE INDIVID 132';           
       CALL ABEND_MODUL('BE24000'); /*ÅR-RET*/                          
    END;                                                                
  END CHECK_INDIVIDTYPER;                                               
 /*********************************************************************/
 /*      BEHANDLING                                                   */
 /*********************************************************************/
 BEHANDLING: PROC;                                                      
    CALL LÆS_INPUTFIL;                                                  
    IF INDV129_HEAD.INDVNR = '0001'                                     
     & INDV132_HEAD.INDVNR = '0001' /* BEGGE ER HEADERE */              
    THEN DO;                                                            
       CALL CHECK_HEADERE;                                              
    END;                                                                
    ELSE DO; /* FEJL*/                                                  
       IF INDV129_HEAD.INDVNR ^= '0001'                                 
       THEN DO;                                                         
         PUT SKIP LIST                                                  
                     ('FØRSTE RECORD PÅ INDIVID 129 ER IKKE EN HEADER');
       END;                                                             
       IF INDV132_HEAD.INDVNR ^= '0001'                                 
       THEN DO;                                                         
         PUT SKIP LIST                                                  
                     ('FØRSTE RECORD PÅ INDIVID 132 ER IKKE EN HEADER');
       END;                                                             
       ABENDTXT = '250 FØRSTE RECORD ER IKKE EN HEADER';                
       CALL ABEND_MODUL('BE24000');                           /*ÅR-RET*/
    END;                                                                
 END BEHANDLING;                                                        
 /*********************************************************************/
 /*      LÆSE INPUT FIL                                               */
 /*********************************************************************/
 LÆS_INPUTFIL: PROC;                                                    
    READ FILE (BE2401I) INTO (INPUTFIL);                      /*ÅR-RET*/
    INPUTFIL1_IND = INPUTFIL;                                           
    READ FILE (BE2402I) INTO (INPUTFIL);                      /*ÅR-RET*/
    INPUTFIL2_IND = INPUTFIL;                                           
 END LÆS_INPUTFIL;                                                      
 /*********************************************************************/
 /*      CHECK HEADERE                                                */
 /*********************************************************************/
 CHECK_HEADERE: PROC;                                                   
    IF INDV129_HEAD.KOERNR = INDV132_HEAD.KOERNR /* KØRSELSNUMMER */    
    THEN; /* ALT ER OK */                                               
    ELSE DO;                                                            
       PUT SKIP LIST                                                    
                ('KØRSELSNUMMER ER IKKE ENS PÅ INDIVID 129 OG 132');    
       ABENDTXT = '251 KØRSELSNUMRE ER IKKE ENS';                       
       CALL ABEND_MODUL('BE24000');                                     
    END;                                                                
    IF INDV129_HEAD.KOERDTOD = INDV132_HEAD.KOERDTOD /* KØRSELSDATO */  
    THEN; /* ALT ER OK */                                               
    ELSE DO;                                                            
       PUT SKIP LIST                                                    
                ('KØRSELSDATO ER IKKE ENS PÅ INDIVID 129 OG 132');      
       ABENDTXT = '252 KØRSELSDATOER ER IKKE ENS';                      
       CALL ABEND_MODUL('BE24000');                                     
    END;                                                                
 END CHECK_HEADERE;                                                     
 /*********************************************************************/
 /*      AFSLUT                                                       */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
    CLOSE FILE (BE2401I),                                     /*ÅR-RET*/
          FILE (BE2402I);                                     /*ÅR-RET*/
 END AFSLUT;                                                            
 /*********************************************************************/
 %INCLUDE BE24004M; /*ABEND-MODUL */                                    
 END BE24000;                                                    /*#01*/
