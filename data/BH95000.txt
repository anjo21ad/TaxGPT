  PROCESS OPT(TIME);                                                    
 BH95000: /* BEREGN EVS TIL FORSKUD 2015 */                             
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
         INIT ('COPYRIGHT KMD A/S 2016');                               
 /**********************************************************************
 *                                                                     *
 * FORMÅL : BEREGNING AF EVS TIL FORSKUD 2018                          *
 *                                                                     *
 * INPUT  : B.H94500D BEREGNINGSBÅND DANNET PÅ GRUNDLAG AF UDTRÆK FRA  *
 *          ESR SAMT EVS SLUT OG FORSKUD SUPPLERET MED DIVERSE DATA FRA*
 *          MINI MT-FIL FRA FORSKUD +                                  *
 *          1/7-98-STATUS OPLYSNINGER SAMT HELÅRSSOMMERHUSE + OPSATTE  *
 *          PERSONTYPER M.V. TIL BRUG VED BEREGNINGERNE.               *
 *                                                                     *
 * OUTPUT : B.H95000D  BEREGNET UDTRÆK - ALLE EJENDOMME                *
 *          BH95001Y   KØRSELSKONTROLLISTE                             *
 *          BH95002Y   FEJLLISTE FRA MASKINEL AFSTEMNING               *
 *                                                                     *
 * PROGRAMMØR:          AAGE RASMUSSEN  AUG       1999                 *
 * ÅRSTILRETTET:        AAGE RASMUSSEN  AUG       2000                 *
 * ÅRSTILRETTET:        AAGE RASMUSSEN  AUG       2001                 *
 * ÅRSTILRETTET:        AAGE RASMUSSEN  AUG       2002                 *
 * ÅRSTILRETTET:        AAGE RASMUSSEN  AUG       2003                 *
 * ÅRSTILRETTET:        BO SCHMIDT      AUG       2004                 *
 * ÅRSTILRETTET:        SUSANNE VILLUMSEN    JUL  2005                 *
 * ÅRSTILRETTET:        BO SCHMIDT      JUL       2006                 *
 * ÅRSTILRETTET:        BO SCHMIDT      JUL       2007                 *
 * ÅRSTILRETTET:        BO SCHMIDT      JUL       2008                 *
 * ÅRSTILRETTET:        GERD HOLM-PEDERSEN   APR  2009   RUL           *
 * SAMT KRAVNR.#0602:   GERD HOLM-PEDERSEN   JUN  2009                 *
 *                      NYT FELT BUDLSKC FRA BH30000L OG 2 NYE FELTER, *
 *                      BBERGRL OG BBER2FAM, FRA  BH30100L             *
 * SAMT KRAVNR.#0904:   GERD HOLM-PEDERSEN   JUN  2009                 *
 *                      NYT FELT CEKSEMP FRA  BH30100L M.M.            *
 *                      HVIS FELTET ER BLANKT, OPSÆTTES DET I BEREGNING*
 * ÅRSTILRETTET F2011:  GERD HOLM-PEDERSEN   JUN  2010:          RUL   *
 *                      SAMT KRAVNR. 6.1                               *
 *  13-08-2010 DEF.40   FOR UDENLANDSKE OG DANSKE EJENDOMME SKAL DER   *
 *                      IKKE OPSÆTTES FEJLNUMMER PÅ EVS NÅR FELT 158=A *
 *             DEF.41   NÅR 158 = 2,3 SKAL FEJLNR OGSÅ OPS. FOR UDL    *
 *                      GERD HOLM-PEDERSEN   APR  2011                 *
 *                      ÆNDRET BEREGN_EVS PROCEDURERNE TIL AT VÆRE     *
 *                      ÅRSUAFHÆNGIGE                                  *
 * ÅRSTILRETTET F2012:  GERD HOLM-PEDERSEN   MAJ  2011:          RUL   *
 * ÅRSTILRETTET F2013:  LARS KAAE HARRITSHØJ JUNI 2012:          RUL   *
 *                      ÆNDRINGER FORETAGET I BH90100N                 *
 *      HUSK HUSK    :  BT*1610 SKAL ÆNDRES I PLC-VERSIONEN AF BH95000 *
 *      HUSK HUSK    :  OG DEN SKAL VÆRE COMP. FRA BFSK'S PAKKER       *
 *      HUSK HUSK    :  OG DENNE PAKKE SKAL VÆRE EN PART. PAKKE (4)    *
 *      HUSK HUSK    :  OG DENNE PAKKE SKAL TILFØJES I BFSK'S COMPLEX  *
 *                                                                     *
 * FORSKUD2014:      Lars Kaae Harritshøj                  Maj 2013    *
 *                   Årstal                                            *
 * FORSKUD2015:      Jørgen Saugmann                       Juni 2014   *
 *                   Årstal                                            *
 *                   Ændring af beregning af pensionist nedslags       *
 *                   beregning i relation til persontype 4   #001      *
 * FORSKUD2016:      Jørgen Saugmann                       Juni 2015   *
 *                   Årstal                                            *
 * SLUT2015          Esben Brodersen                       Marts 2016  *
 *                   Udviddet brug af BH65300M dato macro  #01         *
 *                   Udviddet betingelse med CEJDTYPE = 9  #02         *
 *                   Proceduren OPSÆT                                  *
 * FORSKUD2017       Esben Brodersen                       Juni 2016   *
 *                   Kompileret til Forskud 2017 plus kommentare       *
 *                   Defect 151 Forskud fra 2016 fejlnummer 5152       *
 *                   skal ikke sættes op når ejerforholdet er slettet  *
 * FORSKUD2018       Esben Brodersen                       Juni 2017   *
 *                   Kompileret til Forskud 2017 plus kommentare       *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH945I FILE RECORD INPUT;      /* FORELØBIGT BEREGN.BÅND    */ 
 DCL     BH950O FILE RECORD OUTPUT;     /* BEREGN.BÅND EFTER BEREGN. */ 
 /********************************************************************* 
 *       DECLARE AF UDTRÆK                                            * 
 *********************************************************************/ 
 DCL     BH945_AR        CHAR      (6000) VAR;                          
 DCL     1 BH945         BASED     (ADDR(BH945_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90100N;;                                          
 DCL     1 BH94599       BASED     (ADDR(BH945_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;                                           
 /********************************************************************* 
 *       DECLARE AF UDTRÆK (BEREGNINGSBÅND)                           * 
 *********************************************************************/ 
 DCL     1 BH95099,                         /* BEREGNINGSBÅND         */
           %INCLUDE BH90099N;               /* OPTÆLLINGSINDIVID      */
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                   /* TÆLLEVÆRKER TIL TOTALLISTEN */      
           2 LÆS_BH945   FIXED DEC (7),                                 
           2 OVERLÆS     FIXED DEC (7),   /*FORSKUD06*/                 
           2 BK16        FIXED DEC (7),   /*FORSKUD06*/                 
           2 SKREVET     FIXED DEC (7);                                 
 /********************************************************************* 
 *       DECLARE AF DIV. FÆLLESMACROER (EVS/BOY/FORSKUD)              * 
 *********************************************************************/ 
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;                                           
 DCL     1 BH60101M      ALIGNED,                                       
           %INCLUDE BH60101M;                                           
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     MULTIPLY        BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     DIVIDE          BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     STG             BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     BH95010         ENTRY;                                         
 DCL     BH95020         ENTRY;                                         
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH95000L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (' '),           
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN0,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (41),                                
           2 CPUÅR       PIC       '99',                                
           2 TEKST2      CHAR      (11),                                
           2 START       PIC       '999999',                            
           2 TEKST3      CHAR      (9),                                 
           2 SLUT        PIC       '999999',                            
           2 TEKST4      CHAR      (57);                                
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
 DCL     1 DETLIN2,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (33),                                
           2 RCKOD       CHAR      (01)      INIT      (' '),           
           2 F1          CHAR      (19)      INIT      (' '),           
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (68)      INIT      (' ');           
 DCL     1 DETLIN3,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (10)      INIT      (' '),           
           2 TEKST2      CHAR      (07)      INIT      ('BELØB: '),     
           2 BELØB       PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZ9',               
           2 F3          CHAR      (25)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
 EXTERNAL INIT ('NOTEST(ALL,,;)');                                      
 DCL     EOF_BH945       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     HJVÆRDI2        FIXED     (13,3);                              
 DCL     HJVÆRDI3        FIXED     (13,0);                              
 DCL     HJVÆRDI4        FIXED     (15,1);                              
 DCL     HJDAGE          FIXED     (3);                                 
 DCL     HJPCT           FIXED     (5,4);                               
                                                                        
 DCL     FRADATO         PIC       '(8)9';                              
 DCL     1 FRA           BASED(ADDR(FRADATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
 DCL     TILDATO         PIC       '(8)9';                              
 DCL     1 TIL           BASED(ADDR(TILDATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
 DCL     1 INP_OPSÆT,                                                   
     6 CANVEBOL      CHAR  (1),    /* FRA EVS SLUT EJER SIDSTE        */
     6 ESR_EKOMNR    FIXED (3),    /* BELIGGENHEDSKOMMUNE             */
     6 BOPÆLSKODE    CHAR  (1),    /* BOPÆLSKODE                      */
     6 CBENYT        CHAR  (2),    /* BENYTTELSESKODE                 */
     6 FLEJL         FIXED (5),    /* ANTAL LEJLIGHEDER               */
     6 BEJDVÆR       FIXED (11),   /* EJD.VÆRDI                       */
     6 EJEBOL(2),                  /*                                 */
      7 VEJK_EJEB    FIXED (15),   /* VEJKODE      (FELT 452)         */
      7 HUSNR_EJEB   CHAR  (4),    /* HUSNR.+BOGST.(FELT 453)         */
      7 BOGST_EJEB   CHAR  (1),    /* BOGSTAV      (FELT 454)         */
      7 ETAGE_EJEB   CHAR  (2),    /* ETAGE        (FELT 455)         */
      7 SDØR_EJEB    CHAR  (4),    /* SIDE/DØRNR   (FELT 455)         */
      7 EJBOLTYP     CHAR  (1),    /* EJERBOLIGTYP (FELT 450)         */
      7 BEJEBOL      FIXED (11),   /* EJERBOLIG-/STUEHUSVÆRD          */
     6 ANT_FOR_1     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_2     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_4     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_5     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_6     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_8     FIXED (5),    /* ANTAL EJERBOLIGFORDELINGER IALT */
     6 ANT_FOR_9     FIXED (5);    /* ANTAL EJERBOLIGFORDELINGER IALT */
                                                                        
 DCL     TFLAG       BIT(1)        INIT('0'B);                          
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /********************************************************************* 
 *       DECLARE AF HJÆLPEFELTER TIL MASKINEL AFSTEMNING              * 
 *********************************************************************/ 
 DCL     BH945_ANT_INDV  FIXED     (7)       INIT      (0);             
 DCL     (I,J,K,N)       BIN FIXED (15)      INIT      (0);             
 DCL     SWFEJL          CHAR      (1)       INIT      (' ');           
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH945 = NEJ;                                               
         ON CONVERSION                                                  
            BEGIN;                                                      
              PUT SKIP LIST ('CPR-NR = ',BH945.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH945.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH945.SORT_EJDNR);             
              PUT SKIP LIST ('CPR-NR = ',BH945.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH945.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH945.SORT_EJDNR);             
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
         ON ERROR                                                       
            BEGIN;                                                      
              PUT SKIP LIST ('PROGRAM FEJL');                           
              PUT SKIP LIST ('CPR-NR = ',BH945.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH945.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH945.SORT_EJDNR);             
              PUT SKIP LIST ('CPR-NR = ',BH945.SORT_CPR);               
              PUT SKIP LIST ('KOM-NR = ',BH945.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH945.SORT_EJDNR);             
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
         OPEN FILE (BH945I) TITLE ('BH94500I');                         
         OPEN FILE (BH950O) TITLE ('BH95000O');                         
                                                                        
         ON ENDFILE (BH945I)                                            
            EOF_BH945 = JA;                                             
                                                                        
         CALL ZZ20390 ('*OPEN','BH95001Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         TV = 0;                                                        
                                                                        
         OVERLIN1.TEKST2 = 'BEREGNING AF EJENDOMSVÆRDISKAT TIL ' !!     
                           'FORSKUD ' !! INDKÅR2; /*#01*/               
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH945;                                                
                                                                        
         DO WHILE ( EOF_BH945 = NEJ );                                  
                                                                        
            CALL BEHANDL;                                               
            CALL LÆS_BH945;                                             
                                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS UDTRÆK FRA ESR                                           * 
 *********************************************************************/ 
 LÆS_BH945:                                                             
         PROC;                                                          
                                                                        
         READ FILE (BH945I) INTO (BH945_AR);                            
                                                                        
         IF BH945.SORT_CPR = 99999999999     /* OPTÆLL.INDV. 1 */       
         THEN                                                           
            DO;                                                         
               BH945_ANT_INDV = BH94599.ANT_INDV;                       
               READ FILE (BH945I) INTO (BH945_AR);                      
               IF BH945.SORT_CPR = 99999999999     /* OPTÆLL.INDV. 2 */ 
               THEN                                                     
                  DO;                                                   
                     PUT SKIP LIST('BH945_ANT_INDV =',BH945_ANT_INDV ); 
                     BH945_ANT_INDV =                                   
                     BH945_ANT_INDV + BH94599.ANT_INDV;                 
                     READ FILE (BH945I) INTO (BH945_AR);                
                  END;                                                  
            END;                                                        
                                                                        
         IF EOF_BH945 = NEJ                                             
         THEN                                                           
            TV.LÆS_BH945 = TV.LÆS_BH945 + 1;                            
                                                                        
   /*   IF BH945.SORT_CPR = 101060438                                   
        THEN                                                            
          TFLAG = '1'B;                                                 
        ELSE                                                            
          TFLAG = '0'B; */                                              
                                                                        
                                                                        
                                                                        
         IF TFLAG                                                       
         THEN DO;                                                       
            PUT SKIP LIST('BH95000: SORT-CPRNR         = '!!            
                           BH945.SORT_CPR);                             
            PUT SKIP LIST('         SORT_KOMNR         = '!!            
                           BH945.SORT_KOMNR);                           
            PUT SKIP LIST('         SORT_EJDNR         = '!!            
                           BH945.SORT_EJDNR);                           
            PUT SKIP LIST('         EJER.DCPRCIR       = '!!            
                           BH945.EJER.DCPRCIR);                         
            PUT SKIP LIST('         EJER.CPENS         = '!!            
                           BH945.EJER.CPENS);                           
            PUT SKIP LIST('         EJER.FEJER         = '!!            
                           BH945.EJER.FEJER);                           
            PUT SKIP LIST('         EJER.GANE          = '!!            
                           BH945.EJER.GANE);                            
            PUT SKIP LIST('         ÆGTF.DCPRCIR       = '!!            
                           BH945.ÆGTF.DCPRCIR);                         
            PUT SKIP LIST('         ÆGTF.CPENS         = '!!            
                           BH945.ÆGTF.CPENS);                           
            PUT SKIP LIST('         ÆGTF.FEJER         = '!!            
                           BH945.ÆGTF.FEJER);                           
            PUT SKIP LIST('         ÆGTF.GANE          = '!!            
                           BH945.ÆGTF.GANE);                            
  /*#01*/   PUT SKIP LIST('         CSKPLOM(GLSLUTÅR)      = '!!        
                           BH945.MT.HIST_OPL(GLSLUTÅR).CSKPLOM);        
  /*#01*/   PUT SKIP LIST('         CSKPLOM(SLUTÅR)      = '!!          
                           BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM);          
  /*#01*/   PUT SKIP LIST('         CSKPLOM(FORSKÅR)      = '!!         
                           BH945.MT.HIST_OPL(FORSKÅR).CSKPLOM);         
            PUT SKIP LIST('         TIL_EVS.CSKPLOMF   = '!!            
                           BH945.TIL_EVS.CSKPLOMF);                     
            PUT SKIP LIST('         TIL_EVS.CSKPLOMFGL = '!!            
                           BH945.TIL_EVS.CSKPLOMFGL);                   
            PUT SKIP LIST('          TIL_EVS.CNEDSL1   = '!!            
                           BH945.TIL_EVS.CNEDSL1);                      
            PUT SKIP LIST('          TIL_EVS.CNEDSL2   = '!!            
                           BH945.TIL_EVS.CNEDSL2);                      
            PUT SKIP LIST('          TIL_EVS.CENKE     = '!!            
                           BH945.TIL_EVS.CENKE);                        
            PUT SKIP LIST('          TIL_EVS.CPTYPSLUT = '!!            
                           BH945.TIL_EVS.CPTYPSLUT);                    
            PUT SKIP LIST('          TIL_EVS.CPTYPFOR  = '!!            
                           BH945.TIL_EVS.CPTYPFOR);                     
         END;                                                           
                                                                        
      /* PUT SKIP LIST('ANTAL LÆST =',TV.LÆS_BH945); */                 
                                                                        
 END LÆS_BH945;                                                         
 /********************************************************************* 
 *       PERSONER SOM EVT. SKAL BEREGNES                              * 
 *********************************************************************/ 
 BEHANDL:                                                               
         PROC;                                                          
                                                                        
         IF BH945.SORT_KOMNR < 999 &             /*FORSKUD06* HERFRA*/  
            BH945.ESR.SORTCPR = 0 &              /*HENTET FRA BH96000*/ 
            BH945.EVS_FORSK_EJER.DCPRN = 0 &                            
           (BH945.EVS_SLUT_EJER.DCPRN = 0 !                             
            BH945.EVS_SLUT_EJER.SIDSTE.CSLET = '1')                     
         THEN                                                           
            DO;                                                         
               TV.OVERLÆS = TV.OVERLÆS + 1;                             
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH945.AKT_VURD.CBENYT = '16'                                
         THEN                                                           
            DO;                                                         
               /* NYT FRA OG MED EVS SLUT 2004       */                 
               /* FOR AT UNDGÅ HANSTHOLM-SITUATIONER */                 
               /* D.V.S. VIDEREFØRSEL AF EJENDOMME   */                 
               /* HVOR BYGNINGEN/BEBOELSEN ER        */                 
               /* OVERFØRT TIL GRUNDENS EJENDOMSNR.  */                 
               TV.BK16 = TV.BK16 + 1;                                   
               RETURN;                                                  
            END;                                /*FORSKUD06* *HERTIL*/  
                                                                        
         /* HEREFTER INITIERES DIVERSE INDIKATOR-FELTER  */             
         /* I BEREGNINGSBÅNDET DA DET IKKE ER GIVENT AT  */             
         /* ALLE DE TILHØRENDE DATA-FELTERNE FÅR INDHOLD */             
         /* EFTERFØLGENDE.                               */             
                                                                        
         BH945.TIL_EVS.BBERGRL_SW  = -1;                                
         BH945.TIL_EVS.BBER2FAM_SW = -1;                                
         BH945.TIL_EVS.BPENSIND_SW = -1;                                
         BH945.TIL_EVS.BEVSMAN_SW = -1;                                 
         BH945.TIL_EVS.BUDLSKC_SW = -1;                                 
         BH945.TIL_EVS.BEJERAN_SW = -1;                                 
         BH945.TIL_EVS.BNYMAN_SW = -1;                                  
         BH945.TIL_EVS.BBRUTTO_SW = -1;                                 
         BH945.EJERSKAB.BBERGRLGL_SW = -1;                              
         BH945.EJERSKAB.BBER2FAMGL_SW = -1;                             
         BH945.EJERSKAB.BBERGRL_SW = -1;                                
         BH945.EJERSKAB.BBER2FAM_SW = -1;                               
         BH945.EJERSKAB.BEJBOLANV_SW = -1;                              
                                                                        
         BH945.FRA_EVS_SLUT.BEVSGL_SW = -1;                             
         BH945.FRA_EVS_SLUT.BPENSNEDGL_SW = -1;                         
                                                                        
         BH945.FRA_EVS_FOR.BGLSUM_SW = -1;                              
                                                                        
         BH945.TIL_EVS_FOR.BEVSFREM_SW = -1;                            
         BH945.TIL_EVS_FOR.BUDLSKC_SW = -1;                             
                                                                        
         /* SKAF DIVERSE DATA FRA EVS UDTRÆKKENE      */                
         /* TIL BRUG VED OPSÆTNING AF UDGANGSPUNKT    */                
         /* FOR BEREGNING (STIGNINGSBEGRÆNSNING) M.V. */                
                                                                        
         SELECT;                                                        
                                                                        
           WHEN (BH945.EVS_SLUT_EJER.DCPRN > 0 &                        
                 BH945.EVS_SLUT_EJER.SIDSTE.CSLET < '1')                
            DO;                                                         
               IF BH945.EVS_SLUT_EJER.SIDSTE.BEJERAN_SW = 0             
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BEJERAN =                            
                     BH945.EVS_SLUT_EJER.SIDSTE.BEJERAN;                
                     BH945.TIL_EVS.BEJERAN_SW = 0;                      
                                                                        
                     BH945.TIL_EVS.BBERGRL =                            
                     BH945.EVS_SLUT_EJER.SIDSTE.BBERGRL;                
                     BH945.TIL_EVS.BBERGRL_SW =                         
                     BH945.EVS_SLUT_EJER.SIDSTE.BBERGRL_SW;             
                     BH945.TIL_EVS.BBER2FAM =                           
                     BH945.EVS_SLUT_EJER.SIDSTE.BBER2FAM;               
                     BH945.TIL_EVS.BBER2FAM_SW =                        
                     BH945.EVS_SLUT_EJER.SIDSTE.BBER2FAM_SW;            
                  END;                                                  
                                                                        
               IF BH945.EVS_UDTRÆK.EKOMNUM = 999                        
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.CEKSEMP =                            
                                    BH945.EVS_SLUT_EJER.SIDSTE.CEKSEMP; 
                     BH945.TIL_EVS_FOR.CEKSEMP =                        
                                   BH945.EVS_SLUT_EJER.SIDSTE.CEKSEMP;  
                     BH945.TIL_FORSKUD.COPR_EKSEMP = 'S';    /* #0904 */
                  END;                                                  
                                                                        
               IF BH945.EVS_SLUT_EJER.SIDSTE.BEVSMAN_SW = 0             
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BEVSMAN =                            
                     BH945.EVS_SLUT_EJER.SIDSTE.BEVSMAN;                
                     BH945.TIL_EVS.BEVSMAN_SW = 0;                      
                  END;                                                  
                                                                        
               IF BH945.ESR.EKOMNR = 0                                  
               THEN                                                     
                  DO;                                                   
                     IF BH945.EVS_SLUT_EJER.SIDSTE.FELT760 = '1'        
                     THEN                                               
                        DO;                                             
                           BH945.TIL_EVS.CFRED =                        
                           BH945.EVS_SLUT_EJER.SIDSTE.FELT760;          
                           BH945.TIL_FORSKUD.COPR_CFRED = 'S';          
                        END;                                            
                     IF BH945.EVS_SLUT_EJER.SIDSTE.FELT759 >= '1'       
                     THEN                                               
                        DO;                                             
                           BH945.TIL_EVS_FOR.CPENSNED1 =                
                           BH945.EVS_SLUT_EJER.SIDSTE.FELT759;          
                           BH945.TIL_FORSKUD.COPR_CPENSNED1 = 'S';      
                        END;                                            
                  END;                                                  
            END;                                                        
                                                                        
           WHEN (BH945.EVS_SLUT_ÆGTF.DCPRN > 0 &                        
                 BH945.EVS_SLUT_ÆGTF.SIDSTE.CSLET < '1')                
            DO;                                                         
               IF BH945.EVS_SLUT_ÆGTF.SIDSTE.BEJERAN_SW = 0             
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BEJERAN =                            
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BEJERAN;                
                     BH945.TIL_EVS.BEJERAN_SW = 0;                      
                                                                        
                     BH945.TIL_EVS.BBERGRL =                            
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BBERGRL;                
                     BH945.TIL_EVS.BBERGRL_SW =                         
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BBERGRL_SW;             
                     BH945.TIL_EVS.BBER2FAM =                           
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BBER2FAM;               
                     BH945.TIL_EVS.BBER2FAM_SW =                        
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BBER2FAM_SW;            
                  END;                                                  
                                                                        
               IF BH945.EVS_SLUT_ÆGTF.SIDSTE.BEVSMAN_SW = 0             
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BEVSMAN =                            
                     BH945.EVS_SLUT_ÆGTF.SIDSTE.BEVSMAN;                
                     BH945.TIL_EVS.BEVSMAN_SW = 0;                      
                  END;                                                  
            END;                                                        
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BH945.EVS_FORSK_EJER.DCPRN > 0                              
         THEN                                                           
            DO;                                                         
               IF BH945.EVS_FORSK_EJER.BBRUTTO_SW = 0                   
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BBRUTTO =                            
                     BH945.EVS_FORSK_EJER.BBRUTTO;                      
                     BH945.TIL_EVS.BBRUTTO_SW = 0;                      
                  END;                                                  
                                                                        
               IF BH945.EVS_FORSK_EJER.BNYMAN_SW = 0                    
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.BNYMAN =                             
                     BH945.EVS_FORSK_EJER.BNYMAN;                       
                     BH945.TIL_EVS.BNYMAN_SW = 0;                       
                  END;                                                  
                                                                        
 /*#0904 OPSÆTNING AF FELT 816, OGSÅ KALDET LEMPELSESKODE/CEKSEMP */    
 /*#GÆLDER KUN FOR UDENLANDSKE EJENDOMMER: KOMMUNNR 999           */    
 /* HVIS SLUT FINDES ER DENNES BRUGT ELLERS FORSKUD */                  
               IF BH945.EVS_UDTRÆK.EKOMNUM = 999                        
               &  (BH945.EVS_SLUT_EJER.DCPRN = 0 !                      
                  BH945.EVS_SLUT_EJER.SIDSTE.CSLET = '1')               
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.CEKSEMP =                            
                          BH945.EVS_UDTRÆK.EVS_FORSK_EJER.CEKSEMP;      
                     BH945.TIL_EVS_FOR.CEKSEMP =                        
                          BH945.EVS_UDTRÆK.EVS_FORSK_EJER.CEKSEMP;      
                     BH945.TIL_FORSKUD.COPR_EKSEMP = 'F';               
                  END;                               /* #0904 HERTIL */ 
                                                                        
               IF BH945.ESR.EKOMNR = 0 &                                
                 (BH945.EVS_SLUT_EJER.DCPRN = 0 !                       
                  BH945.EVS_SLUT_EJER.SIDSTE.CSLET = '1')               
               THEN                                                     
                  DO;                                                   
                     IF BH945.EVS_FORSK_EJER.CEJDTYPE > '0'             
                     THEN                                               
                        DO;                                             
                           BH945.EJENDOM.CEJDTYPE =                     
                           BH945.EVS_FORSK_EJER.CEJDTYPE;               
                           BH945.TIL_FORSKUD.COPR_CEJDTYPE = 'F';       
                        END;                                            
                     IF BH945.EVS_FORSK_EJER.CFRED = '1'                
                     THEN                                               
                        DO;                                             
                           BH945.TIL_EVS.CFRED =                        
                           BH945.EVS_FORSK_EJER.CFRED;                  
                           BH945.TIL_FORSKUD.COPR_CFRED = 'F';          
                        END;                                            
                  END;                                                  
            END;                                                        
                                                                        
         IF BH945.ESR.EKOMNR > 0                                        
         THEN                                                           
            DO;                                                         
               IF BH945.ESR.CFRED = '1'                                 
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS.CFRED = BH945.ESR.CFRED;             
                     BH945.TIL_FORSKUD.COPR_CFRED = 'E';                
                  END;                                                  
                                                                        
               CALL BEREGN_EVS_SIDSTE_ÅR;                               
               CALL BEREGN_EVS_AKT_ÅR;                                  
                                                                        
               IF BH945.FEJLNR > 0 &                                    
                  BH945.EVS_UDTRÆK.DCPRNUM > 0                          
               THEN                                                     
                  DO;                                                   
                     CALL BEHANDL_EVS_FREMSKRIVNING;                    
                                                                        
                     IF BH945.TIL_EVS.CEJBEVS = '0'                     
                     THEN                                               
                        BH945.TIL_EVS.CEJBEVS = '2';                    
                     IF BH945.TIL_FORSKUD.COPR_CEJBEVS = ' '            
                     THEN                                               
                        BH945.TIL_FORSKUD.COPR_CEJBEVS = 'F';           
                  END;                                                  
            END;                                                        
         ELSE                                                           
            IF BH945.EVS_UDTRÆK.DCPRNUM > 0                             
            THEN                                                        
               DO;                                                      
                  IF BH945.TIL_EVS.CEJBEVS = '7' !                      
                     BH945.TIL_EVS.CEJBEVS = '8' !                      
                     BH945.TIL_EVS.CEJBEVS = '9'                        
                  THEN;                                                 
                  ELSE  /* FORDI DEN IKKE KAN BEREGNES (EJ I ESR) */    
                     BH945.TIL_EVS.CEJBEVS = '2';                       
                                                                        
                  BH945.TIL_FORSKUD.COPR_CEJBEVS = 'F';                 
                  BH945.EJENDOM.CBERGRL = '9';                          
                                                                        
                  IF BH945.EVS_UDTRÆK.EKOMNUM = 999                     
                   & BH945.TIL_EVS.CEJBEVS = '2'            /* DEF.41 */
                  THEN                                                  
                     DO;                                                
                        /* UDENLANDSK */ /*DEFECT 151 F2016 */          
                        IF BH945.EVS_UDTRÆK.CUDLAND = '4' &             
                           BH945.EVS_SLUT_EJER.SIDSTE.CSLET < '1'       
                        THEN                                            
                           BH945.FEJLNR = 5152;                         
                        ELSE                                            
                           BH945.FEJLNR = 5021;             /* DEF.41 */
                     END;                                               
                  ELSE                                                  
                  IF BH945.TIL_EVS.CEJBEVS = '2'                        
                  THEN                                                  
                     DO;                                                
                        /* IKKE UDENLANDSK */                           
                        BH945.FEJLNR = 5022;                            
                        IF BH945.EVS_SLUT_EJER.DCPRN > 0 &              
                           BH945.EVS_SLUT_EJER.SIDSTE.CSLET < '1' &     
                           BH945.EVS_SLUT_EJER.SIDSTE.CÆGTOPR = '3'     
                        THEN                                            
                           DO;                                          
                              /* OPRETTET ÆGTF. */                      
                              BH945.FEJLNR = 5027;                      
                              BH945.TIL_EVS.CÆGTOPR = '3';              
                           END;                                         
                        IF BH945.EVS_SLUT_EJER.DCPRN > 0 &              
                           BH945.EVS_SLUT_ÆGTF.SIDSTE.CSLET < '1' &     
                           BH945.EVS_SLUT_ÆGTF.SIDSTE.CÆGTOPR = '3'     
                        THEN                                            
                           DO;                                          
                              /* ÆGTEFÆLLE TIL 'OPRETTET ÆGTF.' */      
                              BH945.FEJLNR = 5027;                      
                              BH945.TIL_EVS.CÆGTOPR = '4';              
                           END;                                         
                     END;                                               
                                                                        
                  /* OPSÆT CPENSNED1 TIL FORSKUD */                     
                                                                        
                  IF BH945.TIL_EVS_FOR.CPENSNED1 = ' '                  
                  THEN                                                  
                   DO;                                                  
                     SELECT (BH945.EJENDOM.CEJDTYPE);                   
                        WHEN ('1','4','5','7','9') /*#02*/              
                          DO;                                           
                             IF BH945.TIL_EVS.CPTYPFOR = '3'            
                              ! BH945.TIL_EVS.CPTYPFOR = '4'  /* #001 */
                             THEN                                       
                                BH945.TIL_EVS_FOR.CPENSNED1 = '1';      
                          END;                                          
                        WHEN ('3','8')                                  
                          DO;                                           
                             IF BH945.TIL_EVS.CPTYPFOR = '3'            
                              ! BH945.TIL_EVS.CPTYPFOR = '4'  /* #001 */
                             THEN                                       
                                BH945.TIL_EVS_FOR.CPENSNED1 = '2';      
                          END;                                          
                        OTHERWISE;                                      
                     END; /* SELECT */                                  
                   END;                                                 
                                                                        
                  CALL BEHANDL_EVS_FREMSKRIVNING;                       
                                                                        
         /*       IF BH945.EVS_UDTRÆK.EKOMNUM ^= 999 &                  
                     BH945.TIL_EVS_FOR.BEVSFREM_SW = 0                  
                  THEN                                                  
                     BH945.FEJLNR = 5121;             DEF. 40    #6.1 */
                                                                        
               END; /* BEHANDL OPRETTELSER FRA EVS SLUT ELLER FORSKUD */
                                                                        
         IF BH945.AKT_EJER.DCPRCIR = 0 &                                
            BH945.TIL_EVS.CDATAFRA = ' ' &                              
            BH945.EVS_UDTRÆK.DCPRNUM > 0                                
         THEN                                                           
            DO;                                                         
               IF BH945.EVS_SLUT_EJER.DCPRN > 0 &                       
                  BH945.EVS_SLUT_EJER.SIDSTE.CSLET < '1' &              
                  BH945.EVS_SLUT_EJER.SIDSTE.CEJBEVS ^= '9'             
               THEN                                                     
                  DO;                                                   
                    IF BH945.EVS_SLUT_EJER.SIDSTE.CDATAFRA = '1'        
                    THEN                                                
                       BH945.TIL_EVS.CDATAFRA = '5';                    
                    ELSE                                                
                       BH945.TIL_EVS.CDATAFRA = '1';                    
                  END;                                                  
               ELSE                                                     
                  BH945.TIL_EVS.CDATAFRA = '2';                         
            END;                                                        
                                                                        
         IF BH945.TIL_EVS.CEJBEVS = '9'                                 
         THEN                                                           
            DO;                                                         
               BH945.TIL_EVS.GPCTEJER_SW = 0;                           
               BH945.TIL_EVS.GPCTEJER = 0;                              
               BH945.TIL_FORSKUD.COPR_GPCTEJER = 'S';                   
            END;                                                        
                                                                        
 /*#0904 HERFRA */                                                      
 /*#     ØSTRIG(AT), MALAYSIA(MY)  SKAL ALTID HAVE SAT CEKSEMP TIL 2*/  
 /*      F2 ER FRANSKE EJENDOMME KØBT EFTER 28/11-2007              */  
 /*      NÅR CEKSEMP = '2' NEDSÆTTES DEN BEREGNEDE EJENDOMSVÆRDI-   */  
 /*      SKAT MED BELØBET I FELT 910 (BUDLSKC)                      */  
                                                                        
         IF BH945.SORT_KOMNR = 999                                      
         THEN                                                           
            DO;                                                         
               IF BH945.EJENDOM.CLANDEKODE = 'AT'                       
               !  BH945.EJENDOM.CLANDEKODE = 'MY'                       
               !  BH945.EJENDOM.CLANDEKODE = 'F2'                       
               THEN                                                     
                  BH945.TIL_EVS_FOR.CEKSEMP   = '2';                    
            END;                                                        
                                                                        
 /*#0904 HERTIL */                                                      
                                                                        
         CALL SKRIV_PERSON;                                             
                                                                        
 END BEHANDL;                                                           
                                                                        
 /********************************************************************* 
 *       HER BEREGNES EJENDOMSVÆRDISKAT-KOMPONENTER VEDR. SIDSTE ÅR   * 
 *********************************************************************/ 
 BEREGN_EVS_SIDSTE_ÅR:                                                  
         PROC;                                                          
                                                                        
         IF TFLAG                                                       
         THEN DO;                                                       
            PUT SKIP LIST ('BH95000: BEREGN_EVS_SIDSTE_ÅR');            
            PUT SKIP LIST ('         CPR-NR = ',BH945.SORT_CPR);        
            PUT SKIP LIST ('         KOM-NR = ',BH945.SORT_KOMNR);      
            PUT SKIP LIST ('         EJD-NR = ',BH945.SORT_EJDNR);      
         END;                                                           
                                                                        
         CALL BH95010 (BH945_AR,BH60101M);                              
                                                                        
 END BEREGN_EVS_SIDSTE_ÅR;                                              
                                                                        
 /********************************************************************* 
 *       HER BEREGNES EJENDOMSVÆRDISKAT TIL FORSKUD AKT_ÅR            * 
 *********************************************************************/ 
 BEREGN_EVS_AKT_ÅR:                                                     
         PROC;                                                          
                                                                        
         IF TFLAG                                                       
         THEN DO;                                                       
            PUT SKIP LIST ('BH95000: BEREGN_EVS_AKT_ÅR');               
            PUT SKIP LIST ('         CPR-NR = ',BH945.SORT_CPR);        
            PUT SKIP LIST ('         KOM-NR = ',BH945.SORT_KOMNR);      
            PUT SKIP LIST ('         EJD-NR = ',BH945.SORT_EJDNR);      
         END;                                                           
                                                                        
         CALL BH95020 (BH945_AR);   /*BEREGN VIA C-VERSION*/            
                                                                        
         IF TFLAG                                                       
         THEN DO;                                                       
              PUT SKIP LIST ('FRA_EVS_FOR.BGLSUM: ' !!                  
              BH945.FRA_EVS_FOR.BGLSUM);                                
              PUT SKIP LIST ('FRA_EVS_FOR.BGLSUM_SW: ' !!               
              BH945.FRA_EVS_FOR.BGLSUM_SW);                             
              PUT SKIP LIST ('FRA_EVS_FOR.BGRPROM1: ' !!                
              BH945.FRA_EVS_FOR.BGRPROM1);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BGRPROM2: ' !!                
              BH945.FRA_EVS_FOR.BGRPROM2);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BSTITIL: ' !!                 
              BH945.FRA_EVS_FOR.BSTITIL);                               
              PUT SKIP LIST ('FRA_EVS_FOR.CSTIBLB: ' !!                 
              BH945.FRA_EVS_FOR.CSTIBLB);                               
              PUT SKIP LIST ('FRA_EVS_FOR.FDAGEJ: ' !!                  
              BH945.FRA_EVS_FOR.FDAGEJ);                                
              PUT SKIP LIST ('FRA_EVS_FOR.FDAGEJEJ: ' !!                
              BH945.FRA_EVS_FOR.FDAGEJEJ);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BPROM1: ' !!                  
              BH945.FRA_EVS_FOR.BPROM1);                                
              PUT SKIP LIST ('FRA_EVS_FOR.BPROM2: ' !!                  
              BH945.FRA_EVS_FOR.BPROM2);                                
              PUT SKIP LIST ('FRA_EVS_FOR.BNEDSL1: ' !!                 
              BH945.FRA_EVS_FOR.BNEDSL1);                               
              PUT SKIP LIST ('FRA_EVS_FOR.CNEDSL2: ' !!                 
              BH945.FRA_EVS_FOR.CNEDSL2);                               
              PUT SKIP LIST ('FRA_EVS_FOR.BNEDSL2: ' !!                 
              BH945.FRA_EVS_FOR.BNEDSL2);                               
              PUT SKIP LIST ('FRA_EVS_FOR.BPENSNED: ' !!                
              BH945.FRA_EVS_FOR.BPENSNED);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BSUM2: ' !!                   
              BH945.FRA_EVS_FOR.BSUM2);                                 
              PUT SKIP LIST ('FRA_EVS_FOR.BSUM3: ' !!                   
              BH945.FRA_EVS_FOR.BSUM3);                                 
              PUT SKIP LIST ('FRA_EVS_FOR.BSUM1: ' !!                   
              BH945.FRA_EVS_FOR.BSUM1);                                 
              PUT SKIP LIST ('FRA_EVS_FOR.BSTIBGR: ' !!                 
              BH945.FRA_EVS_FOR.BSTIBGR);                               
              PUT SKIP LIST ('FRA_EVS_FOR.BNETTO: ' !!                  
              BH945.FRA_EVS_FOR.BNETTO);                                
              PUT SKIP LIST ('FRA_EVS_FOR.BPENSRED: ' !!                
              BH945.FRA_EVS_FOR.BPENSRED);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BBRUTTO: ' !!                 
              BH945.FRA_EVS_FOR.BBRUTTO);                               
              PUT SKIP LIST ('FRA_EVS_FOR.BEJERED: ' !!                 
              BH945.FRA_EVS_FOR.BEJERED);                               
              PUT SKIP LIST ('FRA_EVS_FOR.BUDLERED: ' !!                
              BH945.FRA_EVS_FOR.BUDLERED);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BERHVRED: ' !!                
              BH945.FRA_EVS_FOR.BERHVRED);                              
              PUT SKIP LIST ('FRA_EVS_FOR.BUBEBOLRED: ' !!              
              BH945.FRA_EVS_FOR.BUBEBOLRED);                            
              PUT SKIP LIST ('FRA_EVS_FOR.B100: ' !!                    
              BH945.FRA_EVS_FOR.B100);                                  
              PUT SKIP LIST ('FRA_EVS_FOR.BEJERAN: ' !!                 
              BH945.FRA_EVS_FOR.BEJERAN);                               
              PUT SKIP LIST ('FRA_EVS_FOR.RETURKODE: ' !!               
              BH945.FRA_EVS_FOR.RETURKODE);                             
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(1): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(1));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(2): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(2));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(3): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(3));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(4): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(4));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(5): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(5));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(6): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(6));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(7): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(7));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(8): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(8));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(9): ' !!              
              BH945.FRA_EVS_FOR.FEJLTAB(9));                            
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(10): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(10));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(11): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(11));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(12): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(12));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(13): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(13));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(14): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(14));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(15): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(15));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(16): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(16));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(17): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(17));                           
              PUT SKIP LIST ('FRA_EVS_FOR.FEJLTAB(18): ' !!             
              BH945.FRA_EVS_FOR.FEJLTAB(18));                           
         END;                                                           
                                                                        
 END BEREGN_EVS_AKT_ÅR;                                                 
                                                                        
 /********************************************************************* 
 * HER FREMSKRIVES EJENDOMME SOM IKKE BLEV BEREGNET MASKINELT HVIS DER* 
 * FRA EVS FORSKUD 2009 ELLER EVS SLUT 2008 ER MODTAGET MANUELT ELLER * 
 * MASKINELT BEREGNET EVS.                                            * 
 *********************************************************************/ 
 BEHANDL_EVS_FREMSKRIVNING:                                             
         PROC;                                                          
                                                                        
         /* EFTERFØLGENDE SITUATIONER SKAL IKKE FREMSKRIVES */          
                                                                        
         IF BH945.TIL_EVS.CEJBEVS = '7' !                               
            BH945.TIL_EVS.CEJBEVS = '8' !                               
            BH945.TIL_EVS.CEJBEVS = '9'                                 
         THEN                                                           
            RETURN;                                                     
                                                                        
         /* FØRST PRØVES FRA SLUT */                                    
                                                                        
         SELECT;                                                        
                                                                        
            WHEN (BH945.TIL_EVS.BEVSMAN_SW = 0)                         
             DO;                                                        
               IF BH945.EVS_UDTRÆK.EKOMNUM = 999                        
               THEN                                                     
                  BH945.TIL_EVS_FOR.BEVSFREM = BH945.TIL_EVS.BEVSMAN;   
               ELSE                                                     
                  BH945.TIL_EVS_FOR.BEVSFREM = MULTIPLY /* 0% REGU- */  
                 (BH945.TIL_EVS.BEVSMAN,1.00,11,2);     /* LERING   */  
               BH945.TIL_EVS_FOR.BEVSFREM_SW = 0;                       
               BH945.TIL_FORSKUD.COPR_BEVSFREM = 'S';                   
               BH945.TIL_EVS.CEJBEVS = 'A';                             
               BH945.FEJLNR = 0;                     /* DEF. 40  #6.1 */
               BH945.TIL_FORSKUD.COPR_CEJBEVS = 'S';                    
               CALL BEREGN_DAGE_EJET_GL_SLUT;                           
             END;                                                       
                                                                        
            WHEN (BH945.TIL_EVS.BEJERAN_SW = 0)                         
             DO;                                                        
               IF BH945.EVS_UDTRÆK.EKOMNUM = 999                        
               THEN                                                     
                  BH945.TIL_EVS_FOR.BEVSFREM = BH945.TIL_EVS.BEJERAN;   
               ELSE                                                     
                  BH945.TIL_EVS_FOR.BEVSFREM = MULTIPLY /* 0% REGU- */  
                 (BH945.TIL_EVS.BEJERAN,1.00,11,2);     /* LERING   */  
               BH945.TIL_EVS_FOR.BEVSFREM_SW = 0;                       
               BH945.TIL_FORSKUD.COPR_BEVSFREM = 'S';                   
                                                                        
               BH945.TIL_EVS.CEJBEVS = 'A';                             
               BH945.FEJLNR = 0;                     /* DEF. 40  #6.1 */
               BH945.TIL_FORSKUD.COPR_CEJBEVS = 'S';                    
                                                                        
               CALL BEREGN_DAGE_EJET_GL_SLUT;                           
                                                                        
 /*#0602 HERFRA. KUN NÅR MASKINELT BEREGNET */                          
                                                                        
               IF BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BBERGRL_SW = 0  
               THEN                                                     
                  DO;                                                   
                     BH945.EJERSKAB.BBERGRL =                           
                     BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BBERGRL;     
                     BH945.TIL_FORSKUD.COPR_BBERGRL = 'S';              
                  END;                                                  
                                                                        
               IF BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BBER2FAM_SW = 0 
               THEN                                                     
                  DO;                                                   
                     BH945.EJERSKAB.BBER2FAM =                          
                     BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BBER2FAM;    
                     BH945.TIL_FORSKUD.COPR_BBER2FAM = 'S';             
                  END;                                                  
                                                                        
               IF BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BUDLSKC_SW = 0  
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS_FOR.BUDLSKC =                        
                     BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.BUDLSKC;     
                     BH945.TIL_FORSKUD.COPR_BUDLSKC = 'S';              
                  END;                                                  
 /*#0602 HERTIL */                                                      
             END;                                                       
                                                                        
            OTHERWISE;                                                  
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BH945.TIL_EVS_FOR.BEVSFREM_SW = 0                           
         THEN                                                           
            RETURN;                                                     
                                                                        
         /* SÅ PRØVES FRA FORSKUD HVIS IKKE MULIGT FRA SLUT */          
                                                                        
         SELECT;                                                        
                                                                        
            WHEN (BH945.TIL_EVS.BNYMAN_SW = 0 &                         
                  BH945.EVS_FORSK_EJER.CEJBEVS = '6')                   
             DO;                                                        
               BH945.TIL_EVS_FOR.BEVSFREM =                             
               BH945.TIL_EVS.BNYMAN;                                    
               BH945.TIL_EVS_FOR.BEVSFREM_SW = 0;                       
               BH945.TIL_FORSKUD.COPR_BEVSFREM = 'F';                   
               BH945.TIL_EVS.CEJBEVS = 'A';                             
               BH945.FEJLNR = 0;                     /* DEF. 40  #6.1 */
               BH945.TIL_FORSKUD.COPR_CEJBEVS = 'F';                    
               CALL BEREGN_DAGE_EJET_SLUT;                              
             END;                                                       
                                                                        
            /* DEFECT 521 FORSKUD 2013 BUDLSKC TRÆKKES FRA BBRUTTO*/    
            WHEN (BH945.TIL_EVS.BBRUTTO_SW = 0)                         
             DO;                                                        
               IF BH945.EVS_FORSK_EJER.BUDLSKC >= BH945.TIL_EVS.BBRUTTO 
               THEN                                                     
                 BH945.TIL_EVS_FOR.BEVSFREM = 0;                        
               ELSE                                                     
                 BH945.TIL_EVS_FOR.BEVSFREM =                           
                   BH945.TIL_EVS.BBRUTTO - BH945.EVS_FORSK_EJER.BUDLSKC;
                                                                        
               /* UD IGEN/ SAT TILBAGE                                  
               BH945.TIL_EVS_FOR.BEVSFREM =                             
                 BH945.TIL_EVS.BBRUTTO;   */                            
               BH945.TIL_EVS_FOR.BEVSFREM_SW = 0;                       
               BH945.TIL_FORSKUD.COPR_BEVSFREM = 'F';                   
                                                                        
               IF BH945.EVS_UDTRÆK.EVS_FORSK_EJER.BUDLSKC > 0           
               THEN                                                     
                  DO;                                                   
                     BH945.TIL_EVS_FOR.BUDLSKC =                        
                            BH945.EVS_UDTRÆK.EVS_FORSK_EJER.BUDLSKC;    
                     BH945.TIL_FORSKUD.COPR_BUDLSKC = 'F';              
                  END;                                                  
                                                                        
               BH945.TIL_EVS.CEJBEVS = 'A';                             
               BH945.FEJLNR = 0;                     /* DEF. 40  #6.1 */
               BH945.TIL_FORSKUD.COPR_CEJBEVS = 'F';                    
                                                                        
               IF BH945.TIL_EVS.GPCTEJER > 0 &                          
                  BH945.TIL_EVS.GPCTEJER < 100.00                       
               THEN                                                     
                  DO;                                                   
                     HJPCT =                                            
                     BH945.TIL_EVS.GPCTEJER / 100.00;                   
                     BH945.TIL_EVS_FOR.BEVSFREM = MULTIPLY              
                    (BH945.TIL_EVS_FOR.BEVSFREM,HJPCT,11,4);            
                  END;                                                  
                                                                        
 /*#0602 HERFRA. KUN NÅR MASKINELT BEREGNET */                          
               BH945.EJERSKAB.BBERGRL          =                        
                     BH945.EVS_UDTRÆK.EVS_FORSK_EJER.BBERGRL;           
               BH945.TIL_FORSKUD.COPR_BBERGRL  = 'F';                   
                                                                        
               BH945.EJERSKAB.BBER2FAM         =                        
                     BH945.EVS_UDTRÆK.EVS_FORSK_EJER.BBER2FAM;          
               BH945.TIL_FORSKUD.COPR_BBER2FAM = 'F';                   
 /*#0602 HERTIL */                                                      
                                                                        
             END;                                                       
                                                                        
            OTHERWISE;                                                  
                                                                        
         END; /* SELECT */                                              
                                                                        
 END BEHANDL_EVS_FREMSKRIVNING;                                         
 /*********************************************************************/
 /*      BEREGN ANTAL DAGE EJET I INDKOMSTÅRET (AKTUELT GL. SLUTÅR)   */
 /*      MÅNEDER BEREGNES SOM VÆRENDE PÅ 30 DAGE                      */
 /*      D.V.S. ET  H E L T  ÅR ER PÅ 360 DAGE                        */
 /*********************************************************************/
 BEREGN_DAGE_EJET_GL_SLUT:                                              
 PROC;                                                                  
                                                                        
         BH60101M = '';                                                 
         KØBT_I_ÅR  = NEJ;                                              
         SOLGT_I_ÅR = NEJ;                                              
                                                                        
         IF BH945.EVS_SLUT_EJER.SIDSTE.DINDFLYT > 0 &                   
           (BH945.EVS_SLUT_EJER.SIDSTE.DINDFLYT >                       
            BH945.EVS_SLUT_EJER.SIDSTE.DOVTG)                           
         THEN                                                           
            FRADATO = BH945.EVS_SLUT_EJER.SIDSTE.DINDFLYT;              
         ELSE                                                           
            FRADATO = BH945.EVS_SLUT_EJER.SIDSTE.DOVTG;                 
                                                                        
         IF BH945.EVS_SLUT_EJER.SIDSTE.DFRAFLYT > 0 &                   
           (BH945.EVS_SLUT_EJER.SIDSTE.DFRAFLYT <                       
            BH945.EVS_SLUT_EJER.SIDSTE.DAFSTAA)                         
         THEN                                                           
            TILDATO = BH945.EVS_SLUT_EJER.SIDSTE.DFRAFLYT;              
         ELSE                                                           
            TILDATO = BH945.EVS_SLUT_EJER.SIDSTE.DAFSTAA;               
                                                                        
         /* UNDERSØG OM EJEREN HAR HANDLET I GL. SLUTÅR */              
         /* AF HENSYN TIL BEREGNING AF HELÅRLIG EVS     */              
         /* TIL BRUG VED FREMSKRIVNING.                 */              
                                                                        
         IF FRADATO >  BH65300M.GLÅR0101 &           /*#01*/            
            FRADATO <= BH65300M.GLÅR1231                                
         THEN                                                           
            KØBT_I_ÅR = JA;                                             
                                                                        
         IF TILDATO >= BH65300M.GLÅR0101 &           /*#01*/            
            TILDATO <= BH65300M.GLÅR1231                                
         THEN                                                           
            SOLGT_I_ÅR = JA;                                            
                                                                        
         IF KØBT_I_ÅR = NEJ & SOLGT_I_ÅR = NEJ                          
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF FRADATO < BH65300M.GLÅR0101             /*#01*/             
         THEN                                                           
            FRADATO = BH65300M.GLÅR0101;                                
                                                                        
         IF TILDATO > BH65300M.GLÅR1231 !           /*#01*/             
            TILDATO = 0                                                 
         THEN                                                           
            TILDATO = BH65300M.GLÅR1231;            /*#01*/             
                                                                        
         HJDAGE = (((TIL.MD - 1) * 30) + TIL.DG) -                      
                  (((FRA.MD - 1) * 30) + FRA.DG);                       
                                                                        
         IF HJDAGE = 0                                                  
         THEN                                                           
            HJDAGE = 1;    /* KØB OG SALG SAMME DATO */                 
                                                                        
         HJVÆRDI3 = BH945.TIL_EVS_FOR.BEVSFREM * 1000;                  
                                                                        
         HJVÆRDI4 = (MULTIPLY((DIVIDE(HJVÆRDI3,HJDAGE,13,4)),           
                      360,14,2));                                       
                                                                        
         BH945.TIL_EVS_FOR.BEVSFREM =                                   
         (DIVIDE(HJVÆRDI4,1000,14,3)) + 0.005;                          
                                                                        
 END BEREGN_DAGE_EJET_GL_SLUT;                                          
 /*********************************************************************/
 /*      BEREGN ANTAL DAGE EJET I INDKOMSTÅRET (AKTUELT SLUTÅR)       */
 /*      MÅNEDER BEREGNES SOM VÆRENDE PÅ 30 DAGE                      */
 /*      D.V.S. ET  H E L T  ÅR ER PÅ 360 DAGE                        */
 /*********************************************************************/
 BEREGN_DAGE_EJET_SLUT:                                                 
 PROC;                                                                  
                                                                        
         BH60101M = '';                                                 
         KØBT_I_ÅR  = NEJ;                                              
         SOLGT_I_ÅR = NEJ;                                              
                                                                        
         IF BH945.EVS_FORSK_EJER.DINDFLYT > 0 &                         
           (BH945.EVS_FORSK_EJER.DINDFLYT >                             
            BH945.EVS_FORSK_EJER.DOVTG)                                 
         THEN                                                           
            FRADATO = BH945.EVS_FORSK_EJER.DINDFLYT;                    
         ELSE                                                           
            FRADATO = BH945.EVS_FORSK_EJER.DOVTG;                       
                                                                        
         IF BH945.EVS_FORSK_EJER.DFRAFLYT > 0 &                         
           (BH945.EVS_FORSK_EJER.DFRAFLYT <                             
            BH945.EVS_FORSK_EJER.DAFSTAA)                               
         THEN                                                           
            TILDATO = BH945.EVS_FORSK_EJER.DFRAFLYT;                    
         ELSE                                                           
            TILDATO = BH945.EVS_FORSK_EJER.DAFSTAA;                     
                                                                        
         /* UNDERSØG OM EJEREN HAR HANDLET I SLUTÅRET   */              
         /* AF HENSYN TIL BEREGNING AF HELÅRLIG EVS     */              
         /* TIL BRUG VED FREMSKRIVNING.                 */              
                                                                        
         IF FRADATO >  BH65300M.ÅR0101  &          /*#01*/              
            FRADATO <= BH65300M.GLÅR1231                                
         THEN                                                           
            KØBT_I_ÅR = JA;                                             
                                                                        
         IF TILDATO >= BH65300M.ÅR0101  &          /*#01*/              
            TILDATO <= BH65300M.GLÅR1231                                
         THEN                                                           
            SOLGT_I_ÅR = JA;                                            
                                                                        
         IF KØBT_I_ÅR = NEJ & SOLGT_I_ÅR = NEJ                          
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF FRADATO < BH65300M.ÅR0101             /*#01*/               
         THEN                                                           
            FRADATO = BH65300M.ÅR0101;            /*#01*/               
                                                                        
         IF TILDATO > BH65300M.ÅR1231 !           /*#01*/               
            TILDATO = 0                                                 
         THEN                                                           
            TILDATO = BH65300M.ÅR1231;            /*#01*/               
                                                                        
         HJDAGE = (((TIL.MD - 1) * 30) + TIL.DG) -                      
                  (((FRA.MD - 1) * 30) + FRA.DG);                       
                                                                        
         IF HJDAGE = 0                                                  
         THEN                                                           
            HJDAGE = 1;    /* KØB OG SALG SAMME DATO */                 
                                                                        
         HJVÆRDI3 = BH945.TIL_EVS_FOR.BEVSFREM * 1000;                  
                                                                        
         HJVÆRDI4 = (MULTIPLY((DIVIDE(HJVÆRDI3,HJDAGE,13,4)),           
                      360,14,2));                                       
                                                                        
         BH945.TIL_EVS_FOR.BEVSFREM =                                   
                         (DIVIDE(HJVÆRDI4,1000,14,3)) + 0.005;          
                                                                        
 END BEREGN_DAGE_EJET_SLUT;                                             
 /********************************************************************* 
 *       HER SKRIVES PERSONEN                                         * 
 *********************************************************************/ 
 SKRIV_PERSON:                                                          
         PROC;                                                          
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
         WRITE FILE (BH950O) FROM (BH945_AR);                           
                                                                        
 END SKRIV_PERSON;                                                      
                                                                        
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BH945;                                  
         DETLIN1.TEKST = 'ANTAL LÆSTE PÅ UDTRÆK             B.H94500D'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                            /*FORSKUD06 HERFRA*/        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.OVERLÆS;                                    
         DETLIN1.TEKST = '      HERAF OVERLÆSTE EVSUDTRÆK UDEN ESR-OPL';
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.BK16;                                       
         DETLIN1.TEKST = '      HERAF OVERLÆSTE EVSUDTRÆK BK=16 I ESR'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                          /*FORSKUD06 HERTIL*/          
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKREVET;                                    
         DETLIN1.TEKST = 'ANTAL SKREVNE INDIVIDER           B.H95000D'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
  END TOTALLISTE;                                                       
 /********************************************************************* 
 *       SKRIVNING AF OPTÆLLINGSINDIVID                               * 
 *********************************************************************/ 
 SKRIV_OPTÆLLING:                                                       
         PROC;                                                          
                                                                        
         BH95099            = '';                                       
         BH95099.SORT_CPR   = 99999999999;                              
         BH95099.SORT_KOMNR = 999;                                      
         BH95099.SORT_EJDNR = 9999999;                                  
         BH95099.ANT_INDV   = TV.SKREVET;                               
         WRITE FILE (BH950O) FROM (BH95099);                            
                                                                        
 END SKRIV_OPTÆLLING;                                                   
 /********************************************************************* 
 *       MASKINEL AFSTEMNING                                          * 
 *********************************************************************/ 
 MASK_AFSTEMNING:                                                       
         PROC;                                                          
                                                                        
         IF BH94599.SORT_KOMNR ^= 999                                   
         THEN                                                           
            CALL FEJLLISTE('1');                                        
         IF BH945_ANT_INDV ^= TV.LÆS_BH945 &                            
            EX_PARM ^= 'TEST'                                           
         THEN                                                           
            CALL FEJLLISTE('2');                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      UDSKRIV FEJLLISTE VEDR. KONSTATERET AFSTEMNINGFEJL PÅ ???    */
 /*********************************************************************/
 FEJLLISTE:                                                             
         PROC(FEJL);                                                    
 DCL     FEJL            CHAR      (01);                                
                                                                        
         CALL ZZ20390 ('*OPEN','BH95002Y');                             
         CALL ZZ20300(SIDELIN,'02');                                    
         CALL ZZ20300(OVERLIN1,'02');                                   
         OVERLIN2.CCA = ZZWS3;                                          
         OVERLIN2.TEKST1 = 'FEJLLISTE VEDR. MASKINEL AFSTEMNING';       
         CALL ZZ20300(OVERLIN2,'02');                                   
         IF FEJL = '1'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST  =                                         
               'AFSTEMNINGSINDIVID MANGLER PÅ B.H94500D';               
               DETLIN1.ANTAL = '';                                      
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
         IF FEJL = '2'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN1.CCA   = ZZWS3;                                   
               DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H94500D';    
               DETLIN1.ANTAL = TV.LÆS_BH945;                            
               CALL ZZ20300(DETLIN1,'02');                              
               DETLIN1.TEKST =                                          
               'ANTAL IFØLGE AFSTEMNINGSINDIVID IALT  :';               
               DETLIN1.ANTAL = BH945_ANT_INDV;                          
               CALL ZZ20300(DETLIN1,'02');                              
            END;                                                        
            SWFEJL = '1';                                               
 END FEJLLISTE;                                                         
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
         CALL SKRIV_OPTÆLLING;                                          
         CALL MASK_AFSTEMNING;                                          
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH945I),                                           
               FILE (BH950O);                                           
                                                                        
         IF SWFEJL = '1'                                                
         THEN                                                           
            CALL ZS30101(4,'BH95000 ** 251 ' !!                         
                 'MASKINEL KONSTATERET AFSTEMNINGSFEJL',2);             
 END AFSLUT;                                                            
 END  BH95000;                                                          
