 /*********************************************************************/
 BH37200: PROCEDURE OPTIONS (MAIN) REORDER;                             
         DCL COPYRIGHT   CHAR      (30) STATIC                          
         INIT ('COPYRIGHT KMD A/S');                                    
 /********************************************************************* 
 * FORMÅL : Omkørsel EvSFREM                                           *
 * INPUT  : B.H95001D BEREGNINGSBÅND                                   *
 *          BN00600T, EVS EJER TABEL                                   *
 * OUTPUT : BN00600T OPDATERET                                         *
 * PROGRAMMØR: HANS ERIK KJELDSEN  DEC. 2015                           *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
                                                                        
 /**********************************************************************
 *                       DECLARE AF REGISTRE                           *
 **********************************************************************/
 DCL     BH950I FILE RECORD INPUT;      /* BEREGNINGSBÅND             */
 DCL     BH372O FILE RECORD OUTPUT;     /* FORSKUD BEREGN. TRANS.     */
 DCL     BH3722O FILE RECORD OUTPUT;    /* KONTROL DATASÆT.           */
 DCL     BH372I FILE RECORD INPUT;      /* PARMKORT                   */
 /**********************************************************************
 *       DECLARE AF UDTRÆK                                             *
 **********************************************************************/
 DCL     BH950_AR        CHAR      (6000) VAR;                          
 DCL     1 BH950         BASED     (ADDR(BH950_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90100N;;                                          
                                                                        
 DCL     1 BH95099       BASED     (ADDR(BH950_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;                                           
                                                                        
 DCL     1 PARMI,                                                       
         2 KØRSW    CHAR(1),          /* Blank = uden opdtering       */
                                      /* X     = med opdatering       */
         2 TESTP    CHAR(1),          /* X = PUT SKIP                 */
         2 FILLER   CHAR(78);                                           
                                                                        
 /**********************************************************************
 *       DECLARE AF INDTÆGTSREGISTERET                                 *
 **********************************************************************/
 DCL     BR601           %INCLUDE BR601VLN;                             
 DCL     1 BR601I        BASED (ADDR(BR601)),                           
           2 LGD         BIN FIXED(15),                                 
              %INCLUDE BR60100N;                                        
                                                                        
 DCL     HJ_DCPRN       DEC FIXED  (11);                                
 DCL     NØGLE          BASED(ADDR(HJ_DCPRN)) CHAR (06);                
                                                                        
 DCL     1 PARM, %INCLUDE BR67501N;                                     
 /*********************************************************************/
 /*      DECLARE AF DB2                                               */
 /*********************************************************************/
                                                                        
 EXEC SQL INCLUDE SQLCA;                                                
 EXEC SQL INCLUDE BN00600T;                                             
 EXEC SQL INCLUDE BN00000T;                                             
 EXEC SQL INCLUDE BN00200T;                                             
                                                                        
 DCL   1 BN006_LÆS   LIKE PERSONEJENDOMSOPL;                            
                                                                        
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     STG             BUILTIN;                                       
 DCL     PROCNAME        BUILTIN;                                       
 DCL     TIME            BUILTIN;                                       
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP   */ 
 DCL     BR67500         ENTRY;                       /* læserutine */  
 /**********************************************************************
 *               DECLARE AF DIVERSE                                    *
 **********************************************************************/
 DCL     JA              BIT(1)          VALUE('1'B);                   
 DCL     NEJ             BIT(1)          VALUE('0'B);                   
 DCL     TFLAG           BIT(1)          INIT('0'B);                    
 DCL     PARM_EOF        BIT(1)          INIT('0'B);                    
 DCL     OPDAT_SW        BIT(1)          INIT('0'B);                    
 DCL     FEJLTEKST       CHAR (40)       INIT(' ');                     
 DCL     ABENDMEDD       CHAR (56)       INIT(' ');                     
 DCL     PLIXOPT         CHAR      (40)  VAR STATIC                     
                                       EXTERNAL INIT ('NOTEST(ALL,,;)');
 DCL     EOF_BH950       BIT(1)          INIT('0'B);                    
 DCL     FDATO           FIXED DEC (9)   INIT(0);                       
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     GEM_CPRNR       FIXED DEC(11,0) INIT(0);                       
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
 /*********************************************************************/
 /*      DECLARE AF TÆLLERE                                           */
 /*********************************************************************/
 DCL     BH950_ANT_LÆST         FIXED DEC(7)   INIT(0);                 
 DCL     OPDAT_ANT              FIXED DEC(7)   INIT(0);                 
 DCL     ANT_FEJLSAG            FIXED DEC(7)   INIT(0);                 
 DCL     ANT_EJ_IREG            FIXED DEC(7)   INIT(0);                 
 DCL     COMMIT_ANT             FIXED DEC(7)   INIT(0);                 
 DCL     DB_ANT_OPDATERET       FIXED DEC(7)   INIT(0);                 
 DCL     ANT_SKRIV_TRANS        FIXED DEC(7)   INIT(0);                 
 DCL     ANT_BPENS              FIXED DEC(7)   INIT(0);                 
                                                                        
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH950 = NEJ;                                               
         ON CONVERSION                                                  
            BEGIN;                                                      
              PUT SKIP LIST ('KOM-NR = ',BH950.SORT_KOMNR);             
              PUT SKIP LIST ('EJD-NR = ',BH950.SORT_EJDNR);             
              FLUSH FILE(*);                                            
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
              FLUSH FILE(*);                                            
              CALL PLIDUMP ('SNHOB');                                   
            END;                                                        
                                                                        
         ON ENDFILE (BH372I)                                            
            PARM_EOF = '1'B;                                            
                                                                        
         OPEN FILE (BH950I) TITLE ('BH95000I');                         
         OPEN FILE (BH372O) TITLE ('BH37200O');                         
         OPEN FILE (BH3722O) TITLE ('BH37201O');                        
                                                                        
         ON ENDFILE (BH950I)                                            
            EOF_BH950 = JA;                                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
                                                                        
         FDATO = FIXED_DAGSDATO + 1;                                    
 /*********************************************************************/
 /* ÅBEN FILER, SAMT 1. KALD TIL LÆSRUTINE                            */
 /*********************************************************************/
 /* ÅBEN IREG */                                                        
       PARM.NØGLE.DCPRN  = 0;           /* CPRNUMMERET I NØGLEN     */  
       PARM.NØGLE.RECART = 0;           /* RECORD-ARTEN I NØGLEN    */  
       PARM.SLAGS        = 'KUNAKT';    /* = 'KUNAKT' EL. 'BEGGE'   */  
       PARM.HEADER       = 'NEJ';       /* = 'JA' ELLER 'NEJ'       */  
       PARM.SPECIEL      = 'JA';        /* 'NEJ'=UDEN 'JA'=TOMT SPEC*/  
       PARM.BEHANDLING   = '1';         /* = 1=ÅBEN,2=ÅBEN+LÆS      */  
                                        /* = 3=LÆS, 4=LÆS+LUK, 5=LUK*/  
       PARM.FILNAVN      = 'BR601I';    /* ='BR601I', 'BR601U'      */  
                                        /*  'BR603I'ELLER 'BR603U'  */  
       CALL BR67500(PARM,BR601);                                        
                                                                        
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
       CALL SKAF_PARM;                                                  
       CALL LÆS_BH950();                                                
       DO WHILE(^EOF_BH950);                                            
          CALL BEHANDL();                                               
          CALL LÆS_BH950();                                             
       END;                                                             
       CALL AFSLUT;                                                     
                                                                        
 /********************************************************************* 
 *       LÆS BEREGNINGSBÅND                                           * 
 *********************************************************************/ 
 LÆS_BH950: PROC();                                                     
                                                                        
      READ FILE (BH950I) INTO (BH950_AR);                               
                                                                        
      IF BH950.SORT_CPR > 99999999999     /* OPTÆLL.INDV. 1 */          
      THEN                                                              
         EOF_BH950 = JA;                                                
      ELSE                                                              
         BH950_ANT_LÆST = BH950_ANT_LÆST + 1;                           
                                                                        
 END LÆS_BH950;                                                         
 /********************************************************************* 
 *       EJENDOMME SOM EVT. SKAL OPDATEREES                           * 
 *********************************************************************/ 
 BEHANDL: PROC;                                                         
                                                                        
       IF BH950.SORT_CPR = 201481155 THEN                               
       DO;                                                              
 /* Aftalt at denne ikke medtages                   */                  
          RETURN;                                                       
       END;                                                             
                                                                        
       IF ^SKAL_MEDTAGES() THEN                                         
       DO;                                                              
          RETURN;                                                       
       END;                                                             
                                                                        
       IF BH950.BEVSFREM_SW = 0 &                                       
          BH950.TIL_EVS.CEJBEVS = 'A' THEN                              
       DO;                                                              
  /* Skal stadig kontrolleres */                                        
       END;                                                             
       ELSE DO;                                                         
          RETURN;                                                       
       END;                                                             
                                                                        
       IF ^ER_KANDIDAT() THEN                                           
       DO;                                                              
          RETURN;                                                       
       END;                                                             
                                                                        
       CALL OPDATER_BN00600T();                                         
                                                                        
       IF FINDES_FEJLSAG() THEN                                         
       DO;                                                              
          ANT_FEJLSAG = ANT_FEJLSAG + 1;                                
       END;                                                             
       ELSE DO;                                                         
          CALL SKRIV_OMBEREGN_TRANS;                                    
       END;                                                             
                                                                        
 END BEHANDL;                                                           
                                                                        
 /*********************************************************************/
 /*      OPDATER BN00600T                                             */
 /*********************************************************************/
 OPDATER_BN00600T: PROC;                                                
                                                                        
       CALL SKRIV_KONTROLFIL;                                           
                                                                        
       IF OPDAT_SW THEN                                                 
       DO;                                                              
          EXEC SQL                                                      
             UPDATE BN00600T                                            
             SET COPR_BEVSFREM  =  :BH950.COPR_BEVSFREM                 
               , CEVSBENY       =  '1'                                  
               , BEVSFREM       =  :BH950.BEVSFREM                      
             WHERE PERSONNUMMER    =  :PERSONEJENDOMSOPL.PERSONNUMMER   
               AND EKOMNR          =  :PERSONEJENDOMSOPL.EKOMNR         
               AND EEJDNR          =  :PERSONEJENDOMSOPL.EEJDNR         
               AND DINDKAAR        =  :PERSONEJENDOMSOPL.DINDKAAR       
               AND BEVSFREM        <> :PERSONEJENDOMSOPL.BEVSFREM       
               AND CEJBEVS         =  'A'                               
               AND DTILGLD         =  '9999-12-31-23.59.59.999999';     
                                                                        
          SELECT(SQLCA.SQLCODE);                                        
          WHEN(0)                                                       
             DO;                                                        
                COMMIT_ANT = COMMIT_ANT + 1;                            
                IF COMMIT_ANT > 50 THEN                                 
                DO;                                                     
                   EXEC SQL COMMIT;                                     
                   IF SQLCA.SQLCODE ^= 0 THEN                           
                   DO;                                                  
                      CALL SQL_FEJL('004');                             
                   END;                                                 
                   COMMIT_ANT = 0;                                      
                END;                                                    
             END;                                                       
          OTHERWISE                                                     
             DO;                                                        
                FEJLTEKST = 'FEJL VED opdate: '!!                       
                                  SQLCA.SQLCODE;                        
                CALL SQL_FEJL('005');                                   
             END;                                                       
          END;                                                          
       END;                                                             
       DB_ANT_OPDATERET = DB_ANT_OPDATERET + 1;                         
                                                                        
 END OPDATER_BN00600T;                                                  
 /*********************************************************************/
 /*      SKRIV OMBEREGNINGS TRANSAKTION                               */
 /*********************************************************************/
                                                                        
 SKRIV_OMBEREGN_TRANS: PROC;                                            
                                                                        
 DCL START       BIN FIXED(15);                                         
 DCL EKOM        PIC '999';                                             
 DCL HJ_FLYD     CHAR(900) VAR;                                         
 DCL UDREC       CHAR(1000) VAR;                                        
 DCL 1 UD        BASED(ADDR(UDREC)),                                    
       2 LÆNGDE  BIN FIXED  (15),                                       
           % INCLUDE BS38900N;                                          
    IF LÆS_IREG(BH950.SORT_CPR)                                         
    THEN                                                                
      DO;                                                               
        HJ_FLYD   = '';                                                 
        UDREC     = '';                                                 
        EKOM      = BR601I.EÆKNR;                                       
        UD.DKØRT  = FDATO;                                              
        UD.DKLOK  = 182000;                                             
        UD.CTYPE  = 3;                                                  
        UD.CSORR  = '7';                                                
        UD.CSORP  = BR601I.DCPRN;                                       
        UD.CGENK  = 0;                                                  
        HJ_FLYD   = '&911' !! CSORP                                     
                           !! '&374' !! EKOM                            
                           !! '&30022'                                  
                           !! '&790TS06'                                
                           !! '&9711'                                   
                           !! '&-\';                                    
        UD.HFLYD  = HJ_FLYD;                                            
        UD.LÆNGDE = 150;  /*LENGHT*/                                    
        WRITE FILE (BH372O) FROM (UDREC);                               
        GEM_CPRNR = BH950.SORT_CPR;                                     
        ANT_SKRIV_TRANS = ANT_SKRIV_TRANS + 1;                          
      END;                                                              
    ELSE                                                                
      ANT_EJ_IREG = ANT_EJ_IREG + 1;                                    
 END SKRIV_OMBEREGN_TRANS;                                              
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES kontrolfil                                   RUTINE */
 /*********************************************************************/
 SKRIV_KONTROLFIL: PROC;                                                
                                                                        
 DCL  CSV_HEADER_SKREVET    BIT(1) STATIC  INIT(NEJ);                   
 DCL  CSV_TEXT              CHAR(132) VAR;                              
                                                                        
      IF ^CSV_HEADER_SKREVET THEN                                       
      DO;                                                               
         CSV_TEXT = '';                                                 
         CSV_TEXT = CSV_TEXT !! '"CPRNR";';                             
         CSV_TEXT = CSV_TEXT !! '"KOMMUNENR";';                         
         CSV_TEXT = CSV_TEXT !! '"EJENDOMSNR";';                        
         CSV_TEXT = CSV_TEXT !! '"NY COPR_BEVSFREM";';                  
         CSV_TEXT = CSV_TEXT !! '"GL. COPR_BEVSFREM";';                 
         CSV_TEXT = CSV_TEXT !! '"NY BEVSFREM";';                       
         CSV_TEXT = CSV_TEXT !! '"GL. BEVSFREM";';                      
         WRITE FILE (BH3722O) FROM (CSV_TEXT);                          
         CSV_HEADER_SKREVET = JA;                                       
      END;                                                              
                                                                        
      CSV_TEXT = '';                                                    
      CSV_TEXT = CSV_TEXT !! '"' !! BH950.SORT_CPR !! '";';             
      CSV_TEXT = CSV_TEXT !! '"' !! BH950.SORT_KOMNR !! '";';           
      CSV_TEXT = CSV_TEXT !! '"' !! BH950.SORT_EJDNR !! '";';           
      CSV_TEXT = CSV_TEXT !! '"' !! BH950.COPR_BEVSFREM  !! '";';       
      CSV_TEXT = CSV_TEXT !! '"' !! BN006_LÆS.COPR_BEVSFREM             
                          !! '";';                                      
      CSV_TEXT = CSV_TEXT !! '"' !! BH950.BEVSFREM !! '";';             
      CSV_TEXT = CSV_TEXT !! '"' !! BN006_LÆS.BEVSFREM !! '";';         
                                                                        
      WRITE FILE (BH3722O) FROM (CSV_TEXT);                             
                                                                        
 END SKRIV_KONTROLFIL;                                                  
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC(SF_SEQNR);                                              
                                                                        
 DCL SF_SEQNR            CHAR(3);                                       
                                                                        
       CALL ZS67000(SQLCA);                                             
       ABENDMEDD =  FEJLTEKST;                                          
       CALL ZS30101 (4, ABENDMEDD, 2);                                  
       CALL PLIDUMP ('SNHOB');                                          
                                                                        
 END SQL_FEJL;                                                          
                                                                        
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE: PROC;                                                      
     PUT SKIP LIST('ANTAL LÆSTE PÅ BH950..:',BH950_ANT_LÆST);           
     PUT SKIP LIST('ANTAL OPDATEREDE......:',DB_ANT_OPDATERET);         
     PUT SKIP LIST('ANTAL MED FEJLSAG.....:',ANT_FEJLSAG);              
     PUT SKIP LIST('ANTAL SKRIV TRANSER...:',ANT_SKRIV_TRANS);          
     PUT SKIP LIST('ANTAL EJ IREG.........:',ANT_EJ_IREG);              
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /*      SKAF PARM                                                    */
 /*********************************************************************/
 SKAF_PARM: PROC;                                                       
                                                                        
    OPEN FILE(BH372I) TITLE('BH37200I');                                
    READ FILE(BH372I) INTO(PARMI);                                      
    IF ^PARM_EOF                                                        
    THEN                                                                
      DO;                                                               
        IF PARMI.KØRSW ^= ' '                                           
        THEN                                                            
           OPDAT_SW = JA;                                               
        ELSE                                                            
           OPDAT_SW = NEJ;                                              
      END;                                                              
    ELSE                                                                
      DO;                                                               
        PUT SKIP LIST('DEFAULT OPDATERING OPSAT');                      
        OPDAT_SW = NEJ;                                                 
      END;                                                              
                                                                        
    IF OPDAT_SW                                                         
    THEN                                                                
       PUT SKIP LIST('DER SKER OPDATERING AF BN00600T');                
    ELSE                                                                
       PUT SKIP LIST('INGEN OPDATERING AF BN00600T');                   
 END SKAF_PARM;                                                         
 /*********************************************************************/
 /* KONTROLLER SAGS- OG FEJLSYSTEM                                    */
 /*********************************************************************/
                                                                        
 FINDES_FEJLSAG: PROC() RETURNS(BIT (1));                               
                                                                        
 DCL  HJ_DCPRN            DEC(11);                                      
 DCL  FEJSAG_FUNDET       BIT (1);                                      
                                                                        
    FEJSAG_FUNDET = NEJ;                                                
                                                                        
    IF FEJLSAG_PÅ_CPR(BH950.SORT_CPR) THEN                              
    DO;                                                                 
       FEJSAG_FUNDET = JA;                                              
    END;                                                                
    ELSE DO;                                                            
       /*  UNDERSØG ÆGTEFÆLLE  */                                       
       IF LÆS_IREG(BH950.SORT_CPR) THEN                                 
       DO;                                                              
          IF BR601I.DHENV > 0    /*UNDERSØG ÆGTEFÆLLE*/                 
          & (BR601I.CSBSK = 2 !                                         
             BR601I.CSBSK = 3 !                                         
             BR601I.CSBSK = 6) THEN                                     
          DO;                                                           
             IF FEJLSAG_PÅ_CPR(BR601I.DHENV) THEN                       
             DO;                                                        
                FEJSAG_FUNDET = JA;                                     
             END;                                                       
          END;                                                          
       END;                                                             
    END;                                                                
                                                                        
    RETURN(FEJSAG_FUNDET);                                              
                                                                        
 END FINDES_FEJLSAG;                                                    
                                                                        
 /********************************************************************/ 
 /*   LÆS INDTÆGTS REGISTER                                           */
 /*********************************************************************/
                                                                        
 FEJLSAG_PÅ_CPR: PROC(PERSONNR)   RETURNS(BIT(1));                      
                                                                        
 DCL  PERSONNR      FIXED DEC(11,0);                                    
 DCL  DUMMY_FELT    CHAR(1);                                            
                                                                        
       DCLBN00000T.PERSONNR = PERSONNR;                                 
       EXEC SQL                                                         
          SELECT DISTINCT 'X'                                           
          INTO :DUMMY_FELT                                              
          FROM BN00000T T1                                              
             , BN00200T T2                                              
          WHERE   T1.INDKOMSTÅR = 2016                                  
            AND   T1.PERSONNR   = :DCLBN00000T.PERSONNR                 
            AND   T2.INK_KOMNR  = T1.INK_KOMNR                          
            AND   T2.PERSONNR   = T1.PERSONNR                           
            AND   T2.INDKOMSTÅR = T1.INDKOMSTÅR                         
            AND   T2.LØBE_NR    = T1.LØBE_NR                            
            AND   T1.HISTORIK   IN (' ','P','G')                        
            AND  (T2.FEJLTYPE   = 'B' OR T2.FEJLNR = 9077);             
                                                                        
       SELECT (SQLCA.SQLCODE);                                          
       WHEN (0)                                                         
          DO;                                                           
             PUT SKIP LIST('FEJLSAG PERSONNR =', DCLBN00000T.PERSONNR); 
             RETURN(JA);                                                
          END;                                                          
       WHEN (100)                                                       
          DO;                                                           
             RETURN(NEJ);                                               
          END;                                                          
       OTHERWISE                                                        
          DO;                                                           
             FEJLTEKST = 'FEJL VED FEJLSAG_PÅ_CPR: '!!                  
                          SQLCA.SQLCODE;                                
             CALL SQL_FEJL('008');                                      
          END;                                                          
       END;                                                             
                                                                        
 END FEJLSAG_PÅ_CPR;                                                    
                                                                        
 /********************************************************************/ 
 /*   LÆS INDTÆGTS REGISTER                                           */
 /*********************************************************************/
                                                                        
 DAN_TRANSAKTION: PROC();                                               
                                                                        
       IF ^LÆS_IREG(BH950.SORT_CPR) THEN                                
       DO;                                                              
          RETURN;                                                       
       END;                                                             
                                                                        
       IF (BR601I.DHENV > 0                                             
        & (BR601I.CSBSK = 2 !                                           
           BR601I.CSBSK = 3 !                                           
           BR601I.CSBSK = 6))                                           
         ! BR601I.FELTBIT(769) THEN                                     
       DO;                                                              
          IF BH950.TIL_EVS_FOR.CPENSNED1 = '1' THEN                     
          DO;                                                           
             IF BR601I.BPENSIND < 401300 &                              
                GEM_CPRNR ^= BH950.SORT_CPR THEN                        
             DO;                                                        
                CALL SKRIV_OMBEREGN_TRANS;                              
             END;                                                       
             ELSE DO;                                                   
                 ANT_BPENS = ANT_BPENS + 1;                             
             END;                                                       
          END;                                                          
          ELSE DO;                                                      
             IF BR601I.BPENSIND < 321300 &                              
                GEM_CPRNR ^= BH950.SORT_CPR THEN                        
             DO;                                                        
                CALL SKRIV_OMBEREGN_TRANS;                              
             END;                                                       
             ELSE DO;                                                   
                ANT_BPENS = ANT_BPENS + 1;                              
             END;                                                       
          END;                                                          
       END;                                                             
       ELSE DO;                                                         
          IF BH950.TIL_EVS_FOR.CPENSNED1 = '1' THEN                     
          DO;                                                           
             IF BR601I.BPENSIND < 302900 &                              
                GEM_CPRNR ^= BH950.SORT_CPR THEN                        
             DO;                                                        
                CALL SKRIV_OMBEREGN_TRANS;                              
             END;                                                       
             ELSE DO;                                                   
                ANT_BPENS = ANT_BPENS + 1;                              
             END;                                                       
          END;                                                          
          ELSE DO;                                                      
             IF BR601I.BPENSIND < 222900 &                              
                GEM_CPRNR ^= BH950.SORT_CPR THEN                        
             DO;                                                        
                CALL SKRIV_OMBEREGN_TRANS;                              
             END;                                                       
             ELSE DO;                                                   
                ANT_BPENS = ANT_BPENS + 1;                              
             END;                                                       
          END;                                                          
       END;                                                             
                                                                        
 END DAN_TRANSAKTION;                                                   
                                                                        
 /*********************************************************************/
 /*   LÆS INDTÆGTS REGISTER                                           */
 /*********************************************************************/
 LÆS_IREG: PROC(LI_CPRNR) RETURNS(BIT(1));                              
 DCL LI_CPRNR            FIXED(11,0);                                   
 DCL RTC                 BIT(1)        INIT('0'B);                      
                                                                        
    RTC              = NEJ;                                             
    PARM.NØGLE.DCPRN = LI_CPRNR;                                        
    PARM.BEHANDLING  = '3';  /*LÆS*/                                    
    CALL BR67500(PARM,BR601);                                           
    SELECT (PARM.SVAR);                                                 
       WHEN (00) DO;                                                    
          RTC = JA;                                                     
       END;                                                             
       WHEN (51) DO;                                                    
          ANT_EJ_IREG = ANT_EJ_IREG + 1;                                
          RTC = NEJ;                                                    
       END;                                                             
       OTHERWISE DO;                                                    
          PUT SKIP LIST ('FEJL I PARM SVAR VED LÆS PÅ IREG ' !!         
                         PARM.NØGLE.DCPRN);                             
          RTC = NEJ;                                                    
       END;                                                             
    END; /* END-SELECT */                                               
                                                                        
    RETURN(RTC);                                                        
                                                                        
 END LÆS_IREG;                                                          
 /********************************************************************* 
 *                                                                    * 
 *********************************************************************/ 
                                                                        
 SKAL_MEDTAGES: PROC()  RETURNS(BIT(1));                                
         IF BH950.AKT_VURD.CBENYT = '78'   /* IKKE EVS RELEVANT EJD.*/  
        THEN                               /* SKULLE HAVDE VÆRET    */  
           RETURN(NEJ);                    /* OVERLÆST I BH31800L.  */  
                                                                        
        IF BH950.TIL_EVS.AKT_EJER.COMPLA = '1'                          
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        IF BH950.MT.CSTATUS = '2'           /* OMPLACERET */            
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        IF BH950.MT.CCIVS = 'D'            /* ALTID HVIS DØD */         
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        IF BH950.TIL_EVS.CEJBEVS ^= 'A' &  /* HVIS IKKE FREMSKREVET */  
          (BH950.FEJLNR = 5001 !                                        
           BH950.FEJLNR = 5020)                                         
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        IF BH950.TIL_EVS.CINVPNR ^= '0'                                 
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        IF BH950.TIL_EVS.AKT_EJER.DCPRCIR > 0 &                         
          (BH950.TIL_EVS.AKT_EJER.DAFSTÅ > 0 &                          
           BH950.TIL_EVS.AKT_EJER.DAFSTÅ <=                             
                                BH950.MT.DRGNÅRTIL)                     
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        /* F2005 */                                                     
        IF BH950.TIL_EVS.AKT_EJER.DCPRCIR > 0 &                         
          (BH950.TIL_EVS.CEJBEVS = '0' !                                
           BH950.TIL_EVS.CEJBEVS = '2' !                                
           BH950.TIL_EVS.CEJBEVS = '3' !                                
           BH950.TIL_EVS.CEJBEVS = '7' !                                
           BH950.TIL_EVS.CEJBEVS = '8') &                               
           BH950.EVS_SLUT_EJER.DCPRN > 0 &                              
           BH950.EVS_SLUT_EJER.SIDSTE.CSLET < '1' &                     
          (BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '0' !                   
           BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '4' !                   
           BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '5') &                  
         ((BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA > 0 &                     
           BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA <=                        
                                        BH950.MT.DRGNÅRTIL) !           
          (BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT > 0 &                    
           BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT <=                       
                                        BH950.MT.DRGNÅRTIL))            
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        /* F2005 */                                                     
        IF BH950.TIL_EVS.AKT_EJER.DCPRCIR > 0 &                         
          (BH950.TIL_EVS.CEJBEVS = '0' !                                
           BH950.TIL_EVS.CEJBEVS = '2' !                                
           BH950.TIL_EVS.CEJBEVS = '3' !                                
           BH950.TIL_EVS.CEJBEVS = '7' !                                
           BH950.TIL_EVS.CEJBEVS = '8') &                               
           BH950.EVS_FORSK_EJER.DCPRN > 0 &                             
          (BH950.EVS_FORSK_EJER.CEJBEVS = '0' !                         
           BH950.EVS_FORSK_EJER.CEJBEVS = '4') &                        
         ((BH950.EVS_FORSK_EJER.DAFSTAA > 0 &                           
           BH950.EVS_FORSK_EJER.DAFSTAA <=                              
                                      BH950.MT.DRGNÅRTIL) !             
          (BH950.EVS_FORSK_EJER.DFRAFLYT > 0 &                          
           BH950.EVS_FORSK_EJER.DFRAFLYT <=                             
                                      BH950.MT.DRGNÅRTIL))              
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
        /* F2005 */                                                     
        IF BH950.TIL_EVS.AKT_EJER.DCPRCIR > 0 &                         
           BH950.TIL_EVS.CEJBEVS = '1'                                  
        THEN                                                            
          DO;                                                           
            IF BH950.EVS_SLUT_EJER.DCPRN > 0 &                          
               BH950.EVS_SLUT_EJER.SIDSTE.CSLET < '1'                   
            THEN                                                        
              DO;                                                       
                IF BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '0' !           
                   BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '4' !           
                   BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '5' !           
                   BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '6' !           
                   BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '7' !           
                   BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '8'             
                THEN;                                                   
                ELSE                                                    
                   RETURN(NEJ);                                         
                IF (BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '0' !          
                    BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '4' !          
                    BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '5') &         
                  ((BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA  > 0 &           
                    BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA  <=              
                                           BH950.MT.DRGNÅRTIL) !        
                   (BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT > 0 &           
                    BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT <=              
                                           BH950.MT.DRGNÅRTIL))         
                 THEN                                                   
                    RETURN(NEJ);                                        
              END;                                                      
            IF BH950.EVS_FORSK_EJER.DCPRN > 0                           
            THEN                                                        
              DO;                                                       
                 IF (BH950.EVS_FORSK_EJER.CEJBEVS = '0' !               
                     BH950.EVS_FORSK_EJER.CEJBEVS = '4') &              
                   ((BH950.EVS_FORSK_EJER.DAFSTAA  > 0 &                
                     BH950.EVS_FORSK_EJER.DAFSTAA  <=                   
                                      BH950.MT.DRGNÅRTIL) !             
                    (BH950.EVS_FORSK_EJER.DFRAFLYT > 0 &                
                     BH950.EVS_FORSK_EJER.DFRAFLYT <=                   
                                      BH950.MT.DRGNÅRTIL))              
                 THEN                                                   
                    RETURN(NEJ);                                        
                 IF BH950.EVS_FORSK_EJER.CEJBEVS = '1' !                
                    BH950.EVS_FORSK_EJER.CEJBEVS = '2' !                
                    BH950.EVS_FORSK_EJER.CEJBEVS = '3'                  
                 THEN                                                   
                    RETURN(NEJ);                                        
              END;                                                      
          END;                                                          
                                                                        
        /* F2005 */                                                     
        IF BH950.TIL_EVS.AKT_EJER.DCPRCIR = 0 &                         
           BH950.SORT_KOMNR < 999                                       
        THEN                                                            
          DO;                                                           
            IF (BH950.EVS_SLUT_EJER.DCPRN > 0 &                         
                BH950.EVS_SLUT_EJER.SIDSTE.CSLET < '1' &                
               (BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '0' !              
                BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '4' !              
                BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '5' !              
                BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '6' !              
                BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '7' !              
                BH950.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '8')) !            
               (BH950.EVS_FORSK_EJER.DCPRN > 0 &                        
               (BH950.EVS_FORSK_EJER.CEJBEVS = '0' !                    
                BH950.EVS_FORSK_EJER.CEJBEVS = '4' !                    
                BH950.EVS_FORSK_EJER.CEJBEVS = '5' !                    
                BH950.EVS_FORSK_EJER.CEJBEVS = '6' !                    
                BH950.EVS_FORSK_EJER.CEJBEVS = '7' !                    
                BH950.EVS_FORSK_EJER.CEJBEVS = '8'))                    
            THEN                                                        
              DO;                                                       
                IF BH950.EVS_SLUT_EJER.DCPRN > 0 &                      
                   BH950.EVS_SLUT_EJER.SIDSTE.CSLET < '1' &             
                 ((BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA  > 0 &            
                   BH950.EVS_SLUT_EJER.SIDSTE.DAFSTAA  <=               
                                           BH950.MT.DRGNÅRTIL) !        
                  (BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT > 0 &            
                   BH950.EVS_SLUT_EJER.SIDSTE.DFRAFLYT <=               
                                           BH950.MT.DRGNÅRTIL))         
                THEN                                                    
                   RETURN(NEJ);                                         
                IF BH950.EVS_FORSK_EJER.DCPRN > 0 &                     
                 ((BH950.EVS_FORSK_EJER.DAFSTAA  > 0 &                  
                   BH950.EVS_FORSK_EJER.DAFSTAA  <=                     
                                           BH950.MT.DRGNÅRTIL) !        
                  (BH950.EVS_FORSK_EJER.DFRAFLYT > 0 &                  
                   BH950.EVS_FORSK_EJER.DFRAFLYT <=                     
                                           BH950.MT.DRGNÅRTIL))         
                THEN                                                    
                   RETURN(NEJ);                                         
              END;                                                      
            ELSE                                                        
               RETURN(NEJ);                                             
          END;                                                          
                                                                        
        IF BH950.SORT_KOMNR = 999 &                                     
          (BH950.TIL_EVS.DAFSTAA > 0 !                                  
           BH950.TIL_EVS.DFRAFLYT > 0)                                  
        THEN                                                            
           RETURN(NEJ);                                                 
                                                                        
     RETURN(JA);                                                        
                                                                        
 END SKAL_MEDTAGES;                                                     
                                                                        
 /********************************************************************* 
 *                                                                    * 
 *********************************************************************/ 
                                                                        
 ER_KANDIDAT: PROC()    RETURNS(BIT(1));                                
                                                                        
 DCL  DUMMY_FELT        CHAR(1);                                        
                                                                        
      PERSONEJENDOMSOPL.PERSONNUMMER = BH950.SORT_CPR;                  
      PERSONEJENDOMSOPL.DINDKAAR     = 2016;                            
      PERSONEJENDOMSOPL.EKOMNR       = BH950.SORT_KOMNR;                
      PERSONEJENDOMSOPL.EEJDNR       = BH950.SORT_EJDNR;                
      PERSONEJENDOMSOPL.BEVSFREM     = BH950.BEVSFREM;                  
                                                                        
      EXEC SQL                                                          
         SELECT BEVSFREM                                                
              , COPR_BEVSFREM                                           
         INTO :BN006_LÆS.BEVSFREM                                       
            , :BN006_LÆS.COPR_BEVSFREM                                  
         FROM BN00600T                                                  
         WHERE PERSONNUMMER    =  :PERSONEJENDOMSOPL.PERSONNUMMER       
           AND EKOMNR          =  :PERSONEJENDOMSOPL.EKOMNR             
           AND EEJDNR          =  :PERSONEJENDOMSOPL.EEJDNR             
           AND DINDKAAR        =  :PERSONEJENDOMSOPL.DINDKAAR           
           AND BEVSFREM        <> :PERSONEJENDOMSOPL.BEVSFREM           
           AND CEJBEVS         =  'A'                                   
           AND DTILGLD         =  '9999-12-31-23.59.59.999999';         
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
      WHEN (0)                                                          
         DO;                                                            
            RETURN(JA);                                                 
         END;                                                           
      WHEN (100)                                                        
         DO;                                                            
            RETURN(NEJ);                                                
         END;                                                           
      OTHERWISE                                                         
         DO;                                                            
            FEJLTEKST = 'FEJL VED ER_KANDIDAT: '!!                      
                         SQLCA.SQLCODE;                                 
            CALL SQL_FEJL('004');                                       
         END;                                                           
      END;                                                              
                                                                        
 END ER_KANDIDAT;                                                       
                                                                        
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT: PROC;                                                          
         CALL TOTALLISTE;                                               
         CLOSE FILE (BH950I),                                           
               FILE (BH372O),                                           
               FILE (BH3722O),                                          
               FILE (BH372I);                                           
         PARM.BEHANDLING  = '5';  /*LUK*/                               
         CALL BR67500(PARM,BR601);                                      
                                                                        
 END AFSLUT;                                                            
 /*********************************************************************/
 /*      DIVERSE INCLUDE                                              */
 /*********************************************************************/
    %INCLUDE ZZ20301M;     /* PRINT STYRE TEGN                        */
    %INCLUDE BS36103M;     /* RETURNERE DATO OG TID I FIXED TID       */
 END  BH37200;                                                          
