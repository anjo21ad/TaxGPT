 BH32200: /* SKAF HENVISNINGSNR. FRA NYT SLUTÅR TIL ESR-UDTRÆK */       
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /********************************************************************* 
 *                                                                    * 
 * FORMÅL : AT SKAFFE NUVÆRENDE ELLER TIDLIGERE ÆGTEFÆLLES PERSONNUM- * 
 *          MER FRA MINI MT-FIL FRA NYT SLUTÅR TIL PÅSÆTNING PÅ ESR   * 
 *          UDTRÆK FOR NYT SLUTÅR AF HENSYN TIL BEHANDLING AF ÆGTE-   * 
 *          FÆLLEFORDELINGER.                                         * 
 *                                                                    * 
 *          HVIS ÆGTEFÆLLE PERSONNUMMER SKAFFES SKRIVES TO INDIVIDER  * 
 *          1 MED SORTCPR=DHENVN OG 1 MED SORTCPR=DCPRCIR A.H.T.      * 
 *          EFTERFØLGENDE DANNELSE AF 1 INDIVID PR. PERSON INDEHOLDEN-* 
 *          DE BÅDE EGNE OG EVT. ÆGTEFÆLLES 'PERSONLIGE' ESR-OPLYSNIN-* 
 *          GER SAMT DE 'FÆLLES' ESR-STAMOPLYSNINGER. DER SKAL DOG    * 
 *          IKKE SKRIVES TO INDIVIDER HVIS DEN AKTUELLE EJER ER OM-   * 
 *          PLACERET TIL ET NYT PERSONNUMMER D.V.S. HVIS COMPLA=1.    * 
 *                                                                    * 
 * INPUT  : B.H32100D  UDTRÆK FRA ESR EFTER 2-EJER-BEHANDLING         * 
 *          B.H65501D  MINI MT-FIL FRA EVS NYT SLUTÅR (HOVEDLEV.).    * 
 *                                                                    * 
 * OUTPUT : B.H32200D  ESR UDTRÆK EFTER PÅFØRSEL AF EVT. ÆGTEFÆLLE    * 
 *                     PERSONNUMMER.                                  * 
 *          BH32201Y   KØRSELSKONTROLLISTE                            * 
 *                                                                    * 
 * PROGRAMMØR: AAGE RASMUSSEN   LSU T&S EVS            NOV 2001       * 
 * RETTET:     OLUF MØRK        LSU T&S EVS            JUN 2002       * 
 * RETTET:     BO SCHMIDT       LSU T&S EVS            JUL 2003       * 
 * RETTET:     GGP              LSU T&S EVS            NOV 2003       * 
 * RETTET:     BO SCHMIDT       LSU T&S EVS            JUN 2004       * 
 * RETTET:     AAGE RASMUSSEN   LSU T&S EVS            NOV 2004       * 
 *             (OPSÆTNING AF FORSKUDT REGNSKABSÅR FJERNET             * 
 *              ---> FLYTTET TIL BH65300 FRA OG MED EVS SLUT 2004)    * 
 *  OMCOMPILERET P.G.A.                            LKA, OKTOBER 2012  * 
 *  NYT FELT CEANV I STRUKTUR BH32200N (BH32202N)                     * 
 *  KRAV 18-01 - BENYTTELSESKODE 25                                   * 
 *  FLYTTER INFORMATIONEN I CEANV IMELLEM BH326 OG BH323 BY NAME      * 
 *                                                                    * 
 * FORSKUD2014 Lars Harritshøj                         Maj 2013       * 
 *             Compileret ingen ændringer                             * 
 * Slut 2013   Jørgen Saugmann                         Okt 2013       * 
 *             Compileret ingen ændringer                             * 
 * FORSKUD2015 Jørgen Saugmann                         jun 2014       * 
 *             Compileret Ingen ændringer                             * 
 * SLUT2014    Jørgen Saugmann                         OKT 2014       * 
 *             Compileret Ingen ændringer                             * 
 * FORSKUD2016 Z6JNK                                    08.06.2015    * 
 *             Compileret. Ingen ændringer                            * 
 * FORSKUD2017 Z8QEB                                    12.06.2016    * 
 *             Compileret. Ingen ændringer                            * 
 * SLUT2018    Esben Brodersen                          12.09.2018    * 
 *             Compileret ændering i struktur BH32200N                * 
 *             til 2Familie huse                                      * 
 * Rettelse #02: Edward Laryea, EDL                     22-03-2019    * 
 *               Nyt felt STRAKS_EVS i BH32202N                       * 
 * Forskud2021 Per Brunk                               Marts 2020     * 
 *               Nye felter i BH32202N VURMARK og VUR2001             * 
 * SLUT 2021   Z6SEP                                   Marts 2021     * 
 *             BH65300M rettet til BH55300M                           * 
 *             BH65300N rettet til BH65321N                           * 
 *********************************************************************/ 
                                                                        
   DCL COPYRIGHT   CHAR  (30) STATIC  INIT ('COPYRIGHT KMD A/S 2004');  
                                                                        
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH655I FILE RECORD INPUT;     /* MINI MT-FIL */                
 DCL     BH326I FILE RECORD INPUT;     /* ESR UDTRÆK */                 
 DCL     BH322O FILE RECORD OUTPUT;    /* BEHANDLET ESR UDTRÆK */       
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
 /********************************************************************* 
 *       DECLARE AF MINI MT-FIL FRA NYT SLUTÅR                        * 
 *********************************************************************/ 
 DCL     BH655_AR CHAR (200) VAR;                                       
 DCL     1 BH655 BASED(ADDR(BH655_AR)),                                 
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH65324N;;                                          
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     BH326_AR        CHAR      (2000) VAR;                          
 DCL     1 BH326         BASED     (ADDR(BH326_AR)),                    
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH32200N;;                                          
 /********************************************************************* 
 *       DECLARE AF ESR UDTRÆK OUTPUT                                 * 
 *********************************************************************/ 
 DCL     1 BH322,                                                       
           %INCLUDE BH32200N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                        /* TÆLLEVÆRKER TIL TOTALLISTEN */ 
           2 LÆS_BH655   FIXED DEC (7),                                 
           2 LÆS_BH326   FIXED DEC (7),                                 
           2 SALG_FØR    FIXED DEC (7),                                 
           2 KØB_EFTER   FIXED DEC (7),                                 
           2 FUNDET      FIXED DEC (7),                                 
           2 FDHENVN     FIXED DEC (7),                                 
           2 HANDLET     FIXED DEC (7),                                 
           2 SKRDUB      FIXED DEC (7),                                 
           2 SKREVET     FIXED DEC (7);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     1 BH55300M,                                                    
           %INCLUDE BH55300M; /* OPSÆT AKTUELT INDKOMSTÅR M.V. */       
                                                                        
 DCL     EOF_BH655       BIT       (1);                                 
 DCL     EOF_BH326       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
 DCL     PUTSKIP         BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     FRADATO         FIXED     (9);                                 
 DCL     TILDATO         FIXED     (9);                                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     CHAR            BUILTIN;                                       
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH32200L'),                                        
           2 F1          CHAR      (21)      INIT      (' '),           
           2 TEKST2      CHAR      (52)      INIT      (                
           'SKAF ÆGTEFÆLLE (DHENVN) TIL ESR UDTRÆK'),                   
           2 F2          CHAR      (21)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (63)      INIT      ((63)' ');       
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH655 = NEJ;                                               
         EOF_BH326 = NEJ;                                               
         ON ERROR CALL PLIDUMP ('SNHOB');                               
         OPEN FILE (BH655I) TITLE ('BH65501I');                         
         OPEN FILE (BH326I) TITLE ('BH32601I');                         
         OPEN FILE (BH322O) TITLE ('BH32200O');                         
         ON ENDFILE (BH655I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
         END;                                                           
         ON ENDFILE (BH326I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
            EOF_BH326 = JA;                                             
         END;                                                           
         CALL ZZ20390 ('*OPEN','BH32201Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!  '-' !! DATO2.ÅR;      
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         TV = '';                                                       
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH655;                                                
         CALL LÆS_BH326;                                                
                                                                        
         DO WHILE (EOF_BH655 = NEJ ! EOF_BH326 = NEJ);                  
            SELECT;                                                     
               WHEN (BH326.DCPRCIR > BH655.DCPRN & EOF_BH655 = NEJ)     
                  CALL LÆS_BH655;                                       
               WHEN (BH326.DCPRCIR < BH655.DCPRN & EOF_BH326 = NEJ)     
                  DO;                                                   
                     CALL BEHANDL_EJ_PÅ_MT;                             
                     CALL LÆS_BH326;                                    
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     TV.FUNDET = TV.FUNDET + 1;                         
                     CALL BEHANDL_MT_OK;                                
                     CALL LÆS_BH326;                                    
                  END;                                                  
            END;                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS ESR UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_BH326:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH326I) INTO (BH326_AR);                            
                                                                        
         IF EOF_BH326 = NEJ                                             
         THEN                                                           
           DO;                                                          
             BH322 = '';                                                
             BH322 = BH326, BY NAME;                                    
                                                                        
       /*    IF BH322.SORTCPR = 0103370019                              
              & BH322.EKOMNR  = 104                                     
              & BH322.EEJDNR  = 110065                                  
             THEN PUTSKIP = JA;                                         
             ELSE PUTSKIP = NEJ; */                                     
                                                                        
             IF BH322.EJER.DAFSTÅ > 0                                   
             THEN                                                       
                BH322.SORTDATO = BH322.EJER.DAFSTÅ;                     
             ELSE                                                       
                BH322.SORTDATO = 99991231;                              
                                                                        
             TV.LÆS_BH326 = TV.LÆS_BH326 + 1;                           
           END;                                                         
                                                                        
 END LÆS_BH326;                                                         
 /********************************************************************* 
 *       LÆS MINI MT-FIL                                              * 
 *********************************************************************/ 
 LÆS_BH655:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH655I) INTO (BH655_AR);                            
                                                                        
         IF EOF_BH655 = NEJ & BH655.DCPRN < 9999999999                  
         THEN                                                           
            TV.LÆS_BH655 = TV.LÆS_BH655 + 1;                            
                                                                        
 END LÆS_BH655;                                                         
                                                                        
                                                                        
 /********************************************************************* 
 *       BEHANDL EJER SOM IKKE FINDES PÅ MINI-MT                      * 
 *********************************************************************/ 
 BEHANDL_EJ_PÅ_MT:                                                      
 PROC;                                                                  
                                                                        
         IF PUTSKIP THEN PUT SKIP LIST('BEHANDL_EJ_PÅ_MT:');            
         BH322.EJER.DRGNÅRFRA = BH55300M.ÅR0101;                        
         BH322.EJER.DRGNÅRTIL = BH55300M.ÅR1231;                        
                                                                        
         IF PUTSKIP THEN DO;                                            
            PUT SKIP LIST('BH55300M.ÅR0101 = '!!BH55300M.ÅR0101);       
            PUT SKIP LIST('BH55300M.ÅR1231 = '!!BH55300M.ÅR1231);       
         END;                                                           
                                                                        
         IF BH322.EJER.DAFSTÅ > 0 &                                     
            BH322.EJER.DAFSTÅ < BH322.EJER.DRGNÅRFRA       /* <= ??? */ 
         THEN                                                           
            DO;                                                         
               IF PUTSKIP THEN DO;                                      
                  PUT SKIP LIST('BH322.EJER.DAFSTÅ    = '!!             
                                BH322.EJER.DAFSTÅ);                     
                  PUT SKIP LIST('BH322.EJER.DRGNÅRFRA = '!!             
                                BH322.EJER.DRGNÅRFRA);                  
               END;                                                     
               TV.SALG_FØR = TV.SALG_FØR + 1;                           
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH322.EJER.DOVTG > BH322.EJER.DRGNÅRTIL                     
         THEN                                                           
            DO;                                                         
               IF PUTSKIP THEN DO;                                      
                  PUT SKIP LIST('BH322.EJER.DOVTG     = '!!             
                                 BH322.EJER.DOVTG);                     
                  PUT SKIP LIST('BH322.EJER.DRGNÅRTIL = '!!             
                                 BH322.EJER.DRGNÅRTIL);                 
               END;                                                     
               TV.KØB_EFTER = TV.KØB_EFTER + 1;                         
               RETURN;                                                  
            END;                                                        
                                                                        
         BH322.SORTCPR  = BH326.DCPRCIR;                                
         BH322.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BH322.CCIVS = ' ';                                             
         BH322.CSBSK = ' ';                                             
                                                                        
         WRITE FILE (BH322O) FROM (BH322);                              
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
                                                                        
 END BEHANDL_EJ_PÅ_MT;                                                  
 /********************************************************************* 
 *       BEHANDL EJER SOM FINDES PÅ MINI-MT                           * 
 *********************************************************************/ 
 BEHANDL_MT_OK:                                                         
 PROC;                                                                  
                                                                        
         /* OPSÆT DATA VEDR. REGNSKABSÅR FRA MINI MT */                 
         IF PUTSKIP THEN PUT SKIP LIST('BEHANDL_MT_OK:');               
         BH322.EJER.DRGNÅRFRA = BH655.DRGNÅRFRA;                        
         BH322.EJER.DRGNÅRTIL = BH655.DRGNÅRTIL;                        
                                                                        
         IF PUTSKIP THEN DO;                                            
            PUT SKIP LIST('BH55300M.ÅR0101 = '!!BH55300M.ÅR0101);       
            PUT SKIP LIST('BH55300M.ÅR1231 = '!!BH55300M.ÅR1231);       
         END;                                                           
                                                                        
         IF BH322.EJER.DAFSTÅ > 0 &                                     
            BH322.EJER.DAFSTÅ < BH322.EJER.DRGNÅRFRA       /* <= ??? */ 
         THEN                                                           
            DO;                                                         
               IF PUTSKIP THEN DO;                                      
                  PUT SKIP LIST('BH322.EJER.DAFSTÅ    = '!!             
                                BH322.EJER.DAFSTÅ);                     
                  PUT SKIP LIST('BH322.EJER.DRGNÅRFRA = '!!             
                                BH322.EJER.DRGNÅRFRA);                  
               END;                                                     
               TV.SALG_FØR = TV.SALG_FØR + 1;                           
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH322.EJER.DOVTG > BH322.EJER.DRGNÅRTIL                     
         THEN                                                           
            DO;                                                         
               IF PUTSKIP THEN DO;                                      
                  PUT SKIP LIST('BH322.EJER.DOVTG     = '!!             
                                 BH322.EJER.DOVTG);                     
                  PUT SKIP LIST('BH322.EJER.DRGNÅRTIL = '!!             
                                 BH322.EJER.DRGNÅRTIL);                 
               END;                                                     
               TV.KØB_EFTER = TV.KØB_EFTER + 1;                         
               RETURN;                                                  
            END;                                                        
                                                                        
         BH322.SORTCPR  = BH326.DCPRCIR;                                
         BH322.SORTTYPE = '1'; /* AKTUEL EJER */                        
         BH322.CCIVS = BH655.CCIVS;                                     
         BH322.CSBSK = BH655.HIST_OPL(SLUTÅR).CSBSK;                    
                                                                        
         WRITE FILE (BH322O) FROM (BH322);                              
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
                                                                        
         IF BH655.HIST_OPL(SLUTÅR).DHENVN > 0 !                         
            BH655.HIST_OPL(GLSLUTÅR).DHENVN > 0                         
         THEN                                                           
            DO;                                                         
               TV.FDHENVN = TV.FDHENVN + 1;                             
               CALL OPSÆT_EJERPERIODE;                                  
                                                                        
               IF FRADATO ^= BH322.EJER.DRGNÅRFRA !                     
                  TILDATO ^= BH322.EJER.DRGNÅRTIL                       
               THEN                                                     
                  TV.HANDLET = TV.HANDLET + 1;                          
                                                                        
               IF BH322.EJER.COMPLA ^= '1'                              
               THEN                                                     
                  CALL SKRIV_DUBLET;                                    
            END;                                                        
                                                                        
 /*********************************************************************/
 /*      OPSÆT EJERPERIODE                                            */
 /*********************************************************************/
 OPSÆT_EJERPERIODE:                                                     
 PROC;                                                                  
                                                                        
         FRADATO = BH322.EJER.DOVTG;                                    
         TILDATO = BH322.EJER.DAFSTÅ;                                   
                                                                        
         IF FRADATO < BH322.EJER.DRGNÅRFRA                              
         THEN                                                           
            FRADATO = BH322.EJER.DRGNÅRFRA;                             
                                                                        
         IF TILDATO > BH322.EJER.DRGNÅRTIL !                            
            TILDATO = 0                                                 
         THEN                                                           
            TILDATO = BH322.EJER.DRGNÅRTIL;                             
                                                                        
 END OPSÆT_EJERPERIODE;                                                 
                                                                        
 END BEHANDL_MT_OK;                                                     
 /********************************************************************* 
 *       SKRIV DUBLET                                                 * 
 *********************************************************************/ 
 SKRIV_DUBLET:                                                          
 PROC;                                                                  
                                                                        
         IF BH655.HIST_OPL(SLUTÅR).DHENVN > 0                           
         THEN                                                           
            BH322.SORTCPR = BH655.HIST_OPL(SLUTÅR).DHENVN;              
         ELSE                                                           
            BH322.SORTCPR = BH655.HIST_OPL(GLSLUTÅR).DHENVN;            
                                                                        
         BH322.SORTTYPE = '2'; /* DUBLET (TIL ÆGTEFÆLLE) */             
                                                                        
         WRITE FILE (BH322O) FROM (BH322);                              
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
         TV.SKRDUB = TV.SKRDUB + 1;                                     
                                                                        
 END SKRIV_DUBLET;                                                      
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.LÆS_BH655;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ MINI MT-FIL NYT SLUTÅR    B.H65501D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.LÆS_BH326;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ ESR UDTRÆK                B.H32100D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SALG_FØR;                                   
         DETLIN1.TEKST = '      HERAF OVERLÆST SALG FØR REGNSKABSÅR  '; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.KØB_EFTER;                                  
         DETLIN1.TEKST = '      HERAF OVERLÆST KØB EFTER REGNSKABSÅR '; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.FUNDET;                                     
         DETLIN1.TEKST = '      HERAF EJER FUNDET PÅ MINI MT-FIL  ';    
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.FDHENVN;                                    
         DETLIN1.TEKST = '            HERAF MED HENVISNINGSNUMMER';     
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKREVET;                                    
         DETLIN1.TEKST = 'SKREVNE INDIVIDER INCL. DUBLETTER  B.H32200D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.SKRDUB;                                     
         DETLIN1.TEKST = '        HERAF DUBLETTER';                     
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.HANDLET;                                    
         DETLIN1.TEKST = '              HERAF HANDEL I REGNSKABSÅR';    
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
  END TOTALLISTE;                                                       
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
                                                                        
         CLOSE FILE (BH326I),                                           
               FILE (BH655I),                                           
               FILE (BH322O);                                           
                                                                        
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
                                                                        
                                                                        
 END  BH32200;                                                          
