 /*********************************************************************/
 /*      BH86072M:                                                   */ 
 /*      OPSÆT CEJBEVS (BEREGNINGSKODE) OG FEJLNR M.V. TIL EVS SLUT  */ 
 /*      VEDR. EJERENS PERSONLIGE FORHOLD                            */ 
 /*  SLUT05          SUSANNE KROGH VILLUMSEN            11/11-2005   */ 
 /*          TILRETTET SLUT 2005                                     */ 
 /*  SLUT06          BO SCHMIDT                         04/10-2006   */ 
 /*          TILRETTET SLUT 2006                                     */ 
 /*  SLUT07          BO SCHMIDT                         20/11-2007   */ 
 /*          TILRETTET SLUT 2007                                     */ 
 /*  SLUT08          BO SCHMIDT                         15/11-2008   */ 
 /*          TILRETTET SLUT 2008                                     */ 
 /*  SLUT12  LARS KAAE HARRITSHØJ                       06/11-2012   */ 
 /*          TILRETTET SLUT 2012                                     */ 
 /*  SLUT13  JØRGEN SAUGMANN                            08/10-2013   */ 
 /*          TILRETTET SLUT 2013                                     */ 
 /*                KRAV 18-02: FEJLKODE 5017 UDGåR UNDTAGEN HVIS     */ 
 /*                            EJERPCT. ER 0,00%                     */ 
 /*------------------------------------------------------------------*/ 
 /* EVS SLUT-15.  JNK :  Ingen ændringer                             */ 
 /* EVS SLUT-16   QEB :  Indlagt fejl 5164 fra Forskud 2017 KS30     */ 
 /* EVS SLUT-17   QEB :  Rettet fejl 5059 da der ikke kan spørges    */ 
 /*                      MT.DCPRN = 0 DB2 løsningen                  */ 
 /* EVS SLUT-18   EBD :  Nyt fejlnr 5184 til defect 116              */ 
 /*                      Ændring til fejlnr 5017 defect 209          */ 
 /*                      så ejerprocent ikke hentes fra slut         */ 
 /*                      for 2 familiehuse                           */ 
 /* EVS SLUT-19   EBD : Ændring til fejlnummer 5017 nu må ejerandel  */ 
 /*                     gerne tages fra fra tidligere slut år for    */ 
 /*                     for at opsætte ejerprocent, men det bør      */ 
 /*                     allerede være sket i BH84300L for 2fam       */ 
 /* Slut2020    Ole Nielsen ONE KS02-34                  Nov 2020     */
 /*             Indsat fejlkode 5200                                  */
 /* Slut2021    Per Brunk PBR                            OKT 2021     */
 /*              D528/D530 fejlnummer 5200 nu også 158=7              */
 /*              Og åbnet op for at udenlandsk ejendomme              */
 /*              kan få fejlnummer 5200                               */
 /* Slut2022    Jan Jensen JJJ                           OKT 2022     */
 /*              D90 Ejendom ejet af forening fraflyttes              */
 /* Slut2022    Esben Brodersen EBD                      OKT 2022     */
 /*             D1060 fejl 5017 må ikke opsættes for 2familiehuse     */
 /* Slut2022    Jan Jensen JJJ                           OKT 2022     */
 /*              D1291 Negativt ejerskab                              */
 /* SLUT 2024   May Britt Simonsen (MBS)                 FEB 2024     */
 /*             Fejlkode 5164 og 5200 skal kun sættes op før 2024     */
 /*********************************************************************/
 OPSÆT_PERSONLIGE_FEJLNR: PROC;                                         
                                                                        
         SELECT;                                                        
                                                                        
           WHEN ( BQ78_AREA.TILEVS_CINVPNR ^= '0' )                     
             DO;                                                        
                BQ78_AREA.FEJLNR = 5020;                                
                BQ78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           /* Defect 116 nyt fejlnummer til døde som har */             
           /* udlejnings ejendommme så disse ikke sendes til DXC */     
           WHEN ( BQ76_AREA.CCIVS = 'D' &            /* EJER DØD FØR/ */
                 (BQ76_AREA.HIST_OPD_DETTE_DCIÆN <= /* ELLER I INDKÅR*/ 
                  BQ76_AREA.DRGNÅRTIL)  &                               
                  BQ78_AREA.FEJLNR = 5024 )                             
             DO;                                                        
                BQ78_AREA.FEJLNR = 5184;                                
                BQ78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           WHEN ( BQ76_AREA.CCIVS = 'D' &          /* EJER DØD FØR/ */  
                 (BQ76_AREA.HIST_OPD_DETTE_DCIÆN <= /* ELLER I INDKÅR*/ 
                  BQ76_AREA.DRGNÅRTIL)  &                               
                  BQ78_AREA.FEJLNR ^= 5001 )                            
             DO;                                                        
                BQ78_AREA.FEJLNR = 5004;                                
                IF (BQ76_AREA.HIST_OPD_SIDSTE_CSBSK = '2' !             
                    BQ76_AREA.HIST_OPD_SIDSTE_CSBSK = '4' !             
                    BQ76_AREA.HIST_OPD_SIDSTE_CSBSK = '6')        &     
                   BQ76_AREA.HIST_OPD_DETTE_DCIÆN >=                    
                                              BQ76_AREA.DRGNÅRFRA  &    
                   BQ76_AREA.HIST_OPD_DETTE_DCIÆN <=                    
                                              BQ76_AREA.DRGNÅRTIL  &    
                   BQ77_AREA.EVSSEJ_DCPRN > 0                           
                THEN                                                    
                  DO;                                                   
                    IF BQ78_AREA.TILEVS_DOVTG >= BQ76_AREA.DRGNÅRFRA    
                    THEN                                                
                       BQ78_AREA.TILEVS_CEJBEVS = '0';                  
                     ELSE                                               
                       DO;                                              
                         IF BQ77_AREA.EVSSEJ_SID_CEJBEVS = '4' !        
                            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '5' !        
                            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '6' !        
                            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '9'          
                         THEN                                           
                           BQ78_AREA.TILEVS_CEJBEVS = '0';              
                         ELSE                                           
                           BQ78_AREA.TILEVS_CEJBEVS =                   
                                 BQ77_AREA.EVSSEJ_SID_CEJBEVS;          
                       END;                                             
                  END;                                                  
                ELSE                                                    
                  DO; /* CHECK MED SKAT */                              
                    BQ78_AREA.TILEVS_CEJBEVS = '1';                     
                  END;                                                  
             END;                                                       
                                                                        
           WHEN ( BQ78_AREA.TILEVS_A_EJ_CEJFORH = '50' !                
                  BQ78_AREA.TILEVS_A_EJ_CEJFORH = '60' !                
                  BQ78_AREA.TILEVS_A_EJ_CEJFORH = '70' !                
                  BQ78_AREA.TILEVS_A_EJ_CEJFORH = '80' )                
                     /* Ejeren er offentlig dvs. komm./amt/stat */      
             DO;                                                        
                BQ78_AREA.FEJLNR = 5020;                                
                BQ78_AREA.TILEVS_CEJBEVS = '1';                         
             END;                                                       
                                                                        
           /*QEB ret test på om person findes på MT */                  
           /* BQ76_AREA.DCPRN = 0 */                                    
           /*SLUT05  FLYTTET HERTIL*/                                   
           /*Der skal ikke spørges på X da de er ville indeholde */     
           /* DCPRN i fil systemmet                              */     
                                                                        
           WHEN (BQ76_AREA.DB2FUNKTION = 'F' &  /* fiktiv række */      
                 BQ78_AREA.EKOMNR ^= 881 &    /* EJER EJ PÅ MT + */     
                 BQ78_AREA.TILEVS_A_EJ_DCPRCIR > 0 & /* EJER JVF.ESR */ 
                 BQ78_AREA.FEJLNR ^= 5001 )       /* ELLER OPRETTET  */ 
             DO;                                  /* P.G.A. GL. SLUT */ 
                BQ78_AREA.FEJLNR = 5059;                                
                BQ78_AREA.TILEVS_CEJBEVS = '2';                         
             END;                                                       
                                                                        
           /* KS30 Forskud 2017 afvisning 5164 */                       
           /* 5164 skal fjernes fra og med 2024 MBS*/                   
           WHEN (BQ78_AREA.TILEVS_CENKE = '5'  &                        
                 BQ78_AREA.TILEVS_HJ_67 ^= 'J'  &                       
                 ER_BEGRÆNSET_SKATTEPLIGTIG = NEJ &                     
                 BQ78_AREA.TIL_EVS_AAR < 2024)                          
             DO;                                                        
                BQ78_AREA.FEJLNR = 5164;                                
                BQ78_AREA.TILEVS_CEJBEVS = '2';                         
             END;                                                       
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BQ78_AREA.FEJLNR ^= 0                                       
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BQ78_AREA.TILEVS_C5060 = '5060' &                           
           (BQ77_AREA.EVSSEJ_SID_GPCTEJER = 0 !                         
            BQ77_AREA.EVSSEJ_SID_CNEDSL1 = '  ' !                       
            BQ77_AREA.EVSSEJ_SID_CPTYPSLUT = ' ')                       
         THEN                                                           
           DO;                                                          
             /*    EJER OPRETTET MANGELFULDT P.G.A. GL. SLUT    */      
             BQ78_AREA.FEJLNR = 5059;                                   
             BQ78_AREA.TILEVS_CEJBEVS = '2';                            
             RETURN;                                                    
           END;                                                         
                                                                        
         /* Fejlnummer 5017 ejer procent fra ESR       */               
         /* regler omlagt til Slut 2020 EBD 12-11-2020 */               
         IF BQ78_AREA.TILEVS_A_EJ_GANE > 10000                          
         THEN                                                           
           DO;                                                          
             BQ78_AREA.TILEVS_A_EJ_GANE = 10000;                        
           END;                                                         
                                                                        
         IF BQ78_AREA.TILEVS_A_EJ_GANE = 0  &                           
            BQ78_AREA.EENHEDLBNR = 0 &                                  
            BQ78_AREA.TILEVS_CEJBEVS  ^= 'B'                            
         THEN                                                           
           DO;                                                          
             BQ78_AREA.FEJLNR = 5017;                                   
           END;                                                         
                                                                        
         IF BQ78_AREA.FEJLNR = 5017 &                                   
            BQ77_AREA.EVSSEJ_DCPRN > 0 &                                
          ((BQ77_AREA.EVSSEJ_SID_CEJBEVS = '0' !                        
            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '4' !                        
            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '5' !                        
            BQ77_AREA.EVSSEJ_SID_CEJBEVS = '6') &                       
            BQ77_AREA.EVSSEJ_SID_CSLET < '1') &                         
            BQ78_AREA.EJERSKAB_BBERGRL_SW = 0 &                         
            BQ78_AREA.TILEVS_DOVTG < BQ76_AREA.DRGNÅRFRA &              
            BQ77_AREA.EVSSEJ_SID_GPCTEJER_SW = 0 &                      
           (BQ78_AREA.TILEVS_GPCTEJER_SW = -1 !                         
           (BQ78_AREA.TILEVS_GPCTEJER ^=                                
            BQ77_AREA.EVSSEJ_SID_GPCTEJER))                             
         THEN                                                           
            DO;                                                         
               BQ78_AREA.TILEVS_GPCTEJER =                              
               BQ77_AREA.EVSSEJ_SID_GPCTEJER;                           
               BQ78_AREA.TILEVS_GPCTEJER_SW = 0;                        
               BQ78_AREA.TILFORSK_COPR_GPCTEJER = 'S';                  
               BQ78_AREA.TILEVS_CDATAFRA = '3';                         
               BQ78_AREA.FEJLNR = 0;                                    
            END;                                                        
                                                                        
         /* Defect 203                                              */  
         /* Hvis felt 158(CEJBEVS) er 0/4/C og 735 er 0 /NULL/blank */  
         /* skal der opsættes et fejlnummer 5200         ej n       */  
         /* 5200 skal fjernes fra og med 2024 MBS*/                     
         IF ((BQ78_AREA.TILEVS_CEJBEVS = '0' !                          
              BQ78_AREA.TILEVS_CEJBEVS = '4' !                          
              BQ78_AREA.TILEVS_CEJBEVS = '7' !      // D528/530         
              BQ78_AREA.TILEVS_CEJBEVS = 'A' !      // D960             
              BQ78_AREA.TILEVS_CEJBEVS = 'C') &                         
             (BQ78_AREA.TILEVS_GPCTEJER = 0 !                           
              BQ78_AREA.TILEVS_GPCTEJER_SW = -1) &                      
              BQ78_AREA.TIL_EVS_AAR < 2024)                             
         THEN                                                           
           DO;                                                          
             BQ78_AREA.FEJLNR = 5200;                                   
             BQ78_AREA.TILEVS_CEJBEVS = '2';                            
             RETURN;                                                    
           END;                                                         
                                                                        
         /* Defect 90                                               */  
         IF BQ78_AREA.TILEVS_DAFSTAA = 0 &                              
            BQ78_AREA.DCPRN > 99999999 THEN                             
           DO;                                                          
             CALL HENT_ADRESSE;                                         
             HJ_VEJKOD_CH = DCLBQ70000T.VEJKOD;                         
                                                                        
             SELECT;                                                    
                WHEN (ADRESSE_FUNDET = NEJ);                            
                /* Adresse fundet - enhedløbenr = 0 */                  
                WHEN (BQ78_AREA.EENHEDLBNR = 0 &                        
                      BQ78_AREA.EJENDOM_VEJK_EJD = HJ_VEJKOD_NUM &      
                      UPPERCASE(BQ78_AREA.EJENDOM_HUSNR_EJD) =          
                                (UPPERCASE(DCLBQ70000T.HUSNR) ||        
                                 UPPERCASE(DCLBQ70000T.HUSBOGSTAADR)) & 
                      UPPERCASE(BQ78_AREA.EJENDOM_ETAGE_EJD) =          
                                 UPPERCASE(DCLBQ70000T.ETAGENR) &       
                      UPPERCASE(BQ78_AREA.EJENDOM_SDØR_EJD)  =          
                                 UPPERCASE(DCLBQ70000T.SIDEDOERNR));    
                /* Adresse fundet - enhedløbenr større end 0 */         
                WHEN (BQ78_AREA.EENHEDLBNR > 0 &                        
                      BQ78_AREA.EJENDOM_VEJK_ENHED = HJ_VEJKOD_NUM &    
                      UPPERCASE(BQ78_AREA.EJENDOM_HUSNR_ENHED) =        
                                (UPPERCASE(DCLBQ70000T.HUSNR) ||        
                                 UPPERCASE(DCLBQ70000T.HUSBOGSTAADR)) & 
                      UPPERCASE(BQ78_AREA.EJENDOM_ETAGE_ENHED) =        
                                 UPPERCASE(DCLBQ70000T.ETAGENR) &       
                      UPPERCASE(BQ78_AREA.EJENDOM_SDØR_ENHED)  =        
                                 UPPERCASE(DCLBQ70000T.SIDEDOERNR));    
                /* Adresse fundet men ej match = fraflyttet */          
                OTHERWISE DO;                                           
                      BQ78_AREA.FEJLNR = 5222;                          
                      BQ78_AREA.TILEVS_CEJBEVS = '1';                   
                      RETURN;                                           
                   END;                                                 
                END; /* select */                                       
           END;                                                         
                                                                        
         /* Defect 1291 */                                              
         IF BQ78_AREA.TILEVS_DAFSTAA < BQ78_AREA.TILEVS_DOVTG &         
            BQ78_AREA.TILEVS_DAFSTAA > 0 &                              
            BQ78_AREA.TILEVS_DOVTG > 0                                  
         THEN                                                           
          DO;                                                           
            BQ78_AREA.FEJLNR = 5224;                                    
            BQ78_AREA.TILEVS_CEJBEVS = '1';                             
            RETURN;                                                     
          END;                                                          
                                                                        
 END OPSÆT_PERSONLIGE_FEJLNR;                                           
  /********************************************************************/
 /* UNDERSØG OM EN BORGER ER BEGRÆNSET SKATTEPLIGTIG. FINDES MT DATA */ 
 /* ANVENDES DE ELLERS UNDERSØGES SIDSTE EVS SLUT ÅR                 */ 
 /********************************************************************/ 
 ER_BEGRÆNSET_SKATTEPLIGTIG:PROC RETURNS(BIT(1));                       
                                                                        
   DCL HJBEGRÆNSET_BIT BIT(1);                                          
                                                                        
   HJBEGRÆNSET_BIT = NEJ;                                               
   HJBEGRÆNSET_TP = '0';                                                
                                                                        
   IF BQ76_AREA.DCPRN > 0 &                                             
      BQ76_AREA.HIST_OPD_DETTE_CSKPLOM > '0'                            
   THEN                                                                 
     DO;                                                                
       IF BQ76_AREA.HIST_OPD_DETTE_CSKPLOM = '1' !                      
          BQ76_AREA.HIST_OPD_DETTE_CSKPLOM = '3' !                      
          BQ76_AREA.HIST_OPD_DETTE_CSKPLOM = '8'                        
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP = BQ76_AREA.HIST_OPD_DETTE_CSKPLOM;           
         END;                                                           
     END;                                                               
   ELSE                                                                 
     DO;                                                                
       IF BQ77_AREA.EVSSEJ_SID_CSKPLOMF = '1' !                         
          BQ77_AREA.EVSSEJ_SID_CSKPLOMF = '3' !                         
          BQ77_AREA.EVSSEJ_SID_CSKPLOMF = '8'                           
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP =                                             
                BQ77_AREA.EVSSEJ_SID_CSKPLOMF;                          
         END;                                                           
     END;                                                               
                                                                        
   RETURN(HJBEGRÆNSET_BIT);                                             
                                                                        
 END ER_BEGRÆNSET_SKATTEPLIGTIG;                                        
