 BH65000: PROCEDURE OPTIONS (MAIN);                                     
 /*********************************************************************/
 /* OBS : ÅRSRULNING ER IKKE NØDVENDIG MED MINDRE DER ER ÆNDRINGER    */
 /*       TIL SQL'LET (F.EKS. TABELNAVN) DER LAVER UDTRÆKKET FRA DW.  */
 /*       M E N  PROGRAMMET SKAL KOMPILERES NÅR MAKRO BH65300M ER     */
 /*       ÅRSRULLET (D.V.S. ÅRSTAL OG DATOER ER RETTET TIL NYT ÅR)    */
 /*       HVILKET SKER I FORBINDELSE MED ÅRSRULNING TIL EVS FORSKUD.  */
 /*********************************************************************/
 /**********************************************************************
 * PROJEKT:     EVS FORSKUD / EVS SLUT                                 *
 * PROGRAMNAVN: BH65000.                                               *
 * PROGRAMTYPE: BATCH.                                                 *
 * FUNKTION:    PROGRAMMET UDTRÆKKER FORSKUDT REGNSKABSÅR FRA          *
 *              SLUT VEDR. 'GAMMELT' SLUTÅR.                           *
 *              PROGRAMMET AFVIKLES KUN I BALLERUP.                    *
 * PGMFORLØB:   1. ÅBEN SLUT-UDTRÆK                                    *
 *              2. DECLARE PERSONCURSOR.                               *
 *              3. OPEN PERSONCURSOR.                                  *
 *              4. LÆS/FETCH RÆKKE PÅ SKT-TABEL.                       *
 *              5. BEHANDL DATA TIL FILSTRUKTUR.                       *
 *              6. SKRIV RÆKKE PÅ UDTRÆKSFIL.                          *
 *              7. LUK PERSONCURSOR.                                   *
 *              8. LUK SLUT-UDTRÆK                                     *
 *              9. SKRIV KONTROLLISTE.                                 *
 * INDDATA:     BG41000T - SLUT INDBERET                               *
 *                                                                     *
 * UDDATA:      BH65000O - BH65000N UDTRÆK FRA SLUT                    *
 *              BH65001Y - KONTROLLISTE.                               *
 * KONSTRUKTØR: BO SCHMDIT            BOB, PKF    BALLERUP 15.09.2006. *
 * TILRETTET    BO SCHMDIT            BOB, PKF    BALLERUP 22.05.2007. *
 * TILRETTET    PER BRUNK             PBR,        BALLERUP 23.03.2020. *
 * SLUT 2021    Elena Flygenring      EFL                  Marts 2021  *
 *              BH65300M erstattes med BH55300M                        *
 ***********************************************************************
                                                                        
 DCL     COPYRIGHT      CHAR (30) INIT ('(C) KOMMUNEDATA A/S 2006');    
                                                                        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT. KALD AF EKSTERNE MODULER */
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE BG41100T;        /* SLUT INDBERET           */
         EXEC SQL INCLUDE SQLCA;           /* SQL KOMMUNIKATIONSAREAL */
                                                                        
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
                                                                        
 DCL     1 BH55300M,                                                    
           %INCLUDE BH55300M;                                           
                                                                        
 DCL     FEJLTEKST      CHAR (20)        INIT(' ');                     
 DCL     ABENDMEDD      CHAR (90)        INIT(' ');                     
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH65000L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'UDTRÆK FORSKUDT REGNSKABSÅR FRA SLUT '),                    
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
                                                                        
 /*********************************************************************/
 /* DECLARE DATASÆT                                                   */
 /*********************************************************************/
                                                                        
 DCL     BH650O FILE OUTPUT RECORD;                    /* SLUT UDTRÆK */
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
                                                                        
 /*********************************************************************/
 /* DECLARE STRUKTURER                                                */
 /*********************************************************************/
                                                                        
 DCL     1 UDTRÆK_SLUT,                                                 
         %INCLUDE BH65000N;                                             
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
                                                                        
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /* DECLARE AF TÆLLERE                                                */
 /*********************************************************************/
                                                                        
 DCL     ANT_SKR BIN FIXED (31) INIT(0);   /* ANTAL SKREVET PÅ UDTRÆK */
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH65000L');             
            END;                                                        
                                                                        
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
         OPEN FILE (BH650O) TITLE ('BH65000O');                         
                                                                        
         CALL ZZ20390('*OPEN','BH65001Y');       /* ÅBEN KONTROLLISTE */
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.ÅR   = GLSLUTÅR;                                      
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
                                                                        
         CALL DECLARE_PERSONCURSOR;                                     
         CALL OPEN_PERSONCURSOR;                                        
         CALL FETCH_PERSONCURSOR;                                       
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               DO WHILE (SQLCA.SQLCODE = 0);                            
                  CALL BEHANDL_OG_SKRIV;                                
                  CALL FETCH_PERSONCURSOR;                              
               END;                                                     
             END;                                                       
                                                                        
           WHEN (100)                                                   
               PUT SKIP LIST ('INGEN RÆKKER LÆST');                     
                                                                        
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST  = 'FETCH PERSONCURSOR  ';                     
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* SELECT*/                                               
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /* DECLARE CURSOR TIL LÆS AF SLUT                             RUTINE */
 /*********************************************************************/
 DECLARE_PERSONCURSOR: PROC;                                            
                                                                        
         EXEC SQL DECLARE PERSONCURSOR CURSOR FOR                       
            SELECT T1.PNR,                                              
                   T1.FSKUDRESKAMDKOD                                   
            FROM BG41100T T1,                                           
                 BG41600T T2                                            
            WHERE T1.DINDKÅR = :GLSLUTÅR                                
            AND   T2.DINDKÅR = T1.DINDKÅR                               
            AND   T2.REGISTMP =                                         
                    (SELECT MAX(T3.REGISTMP)                            
                     FROM BG41600T T3                                   
                     WHERE T2.PNR     = T3.PNR                          
                     AND   T2.DINDKÅR = T3.DINDKÅR)                     
            AND   T2. AENDRNR  =                                        
                    (SELECT MAX(T4.AENDRNR)                             
                     FROM BG41600T T4                                   
                     WHERE T2.PNR     = T4.PNR                          
                     AND   T2.DINDKÅR = T4.DINDKÅR)                     
            AND   T1.DUMMYID = T2.DUMMYID                               
            AND   T1.FSKUDRESKAMDKOD > ' '                              
            ORDER BY T1.PNR                                             
         ;                                                              
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
           DO;                                                          
             FEJLTEKST = 'DECLARE PERSONCURSOR';                        
             CALL SQL_FEJL;                                             
           END;                                                         
                                                                        
 END DECLARE_PERSONCURSOR;                                              
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF SLUT                                RUTINE */
 /*********************************************************************/
 OPEN_PERSONCURSOR: PROC;                                               
                                                                        
         EXEC SQL OPEN PERSONCURSOR;                                    
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'OPEN PERSONCURSOR';                          
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_PERSONCURSOR;                                                 
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF SLUT                               RUTINE */
 /*********************************************************************/
 FETCH_PERSONCURSOR: PROC;                                              
                                                                        
         EXEC SQL FETCH PERSONCURSOR                                    
                                                                        
         INTO :DCLBG41100T.PNR                                          
             ,:DCLBG41100T.FSKUDRESKAMDKOD                              
         ;                                                              
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0);                                                    
           WHEN (100);                                                  
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST  = 'FETCH PERSONCURSOR  ';                     
               CALL SQL_FEJL;                                           
             END;                                                       
         END; /* SELECT*/                                               
                                                                        
 END FETCH_PERSONCURSOR;                                                
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF SLUT                               RUTINE */
 /*********************************************************************/
 CLOSE_PERSONCURSOR: PROC;                                              
                                                                        
         EXEC SQL CLOSE PERSONCURSOR;                                   
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'CLOSE PERSONCURSOR';                         
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END CLOSE_PERSONCURSOR;                                                
 /*********************************************************************/
 /* BEHANDL OG SKRIV SLUT OPLYSNINGER                          RUTINE */
 /*********************************************************************/
 BEHANDL_OG_SKRIV: PROC;                                                
                                                                        
         UDTRÆK_SLUT.DCPRN = DCLBG41100T.PNR;                           
         UDTRÆK_SLUT.CFRÅR = DCLBG41100T.FSKUDRESKAMDKOD;               
                                                                        
         WRITE FILE (BH650O) FROM (UDTRÆK_SLUT);                        
         ANT_SKR = ANT_SKR + 1;                                         
                                                                        
 END BEHANDL_OG_SKRIV;                                                  
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         CLOSE FILE (BH650O);                                           
         CALL CLOSE_PERSONCURSOR;                                       
         CALL ZS67000(SQLCA);                                           
                                                                        
         ABENDMEDD = FEJLTEKST !! SQLCA.SQLCODE;                        
                                                                        
         CALL ZS30101 (5, ABENDMEDD, 2);                                
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL UDTRUKNE MED FORSKUDT REGNSKABSÅR PÅ B.H65000D';        
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL KONTROLLISTE;                                             
         CLOSE FILE (BH650O);                                           
         CALL CLOSE_PERSONCURSOR;                                       
         CALL ZZ20300(' ','99');                                        
                                                                        
 END AFSLUT;                                                            
                                                                        
 END BH65000;                                                           
