 /*********************************************************************/
 /* BH94103M                                                          */
 /* Slut09       Gerd Holm-Pedersen                     05-12-2009    */
 /* Krav #18-03: Nedtagning af 158=7                                  */
 /*              Evt. beboede perioder på ejendommen hentes i P-data  */
 /*                                                                   */
 /* Hvis sidste fraflyt  < Regnskabsårstart: ingen beboede perioder   */
 /* Hvis sidste fraflyt => Regnskabsårslut:  beboede perioder         */
 /*                                                                   */
 /*********************************************************************/
 /*SLUT09        GERD HOLM-PEDERSEN        21-01-2010                 */
 /*              RETTELSE TIL KRAV #18-03-1                           */
 /*              HVIS REGNSKABSÅRET IKKE FINDES, BRUGES KALENDERÅRET  */
 /*********************************************************************/
 /*SLUT10        GERD HOLM-PEDERSEN        11-10-2010                 */
 /*              BH946.EJERSKAB ERSTATTET MED BH946.MT.PER.           */
 /*              Perioderne skal læses ind i felter, der ikke ændres! */
 /*********************************************************************/
 /* FORSKUD 2013 OMLAGT TIL AT ANVENDE CSRPDATA FRA BQ70000T OG       */
 /* BQ71000T                                                          */
 /*********************************************************************/
 /* CURSOR TIL CSRP                                                   */
 /*********************************************************************/
 DECLARE_ERHVERV_CSRP_CUR: PROC;                                        
                                                                        
                                                                        
      EXEC SQL DECLARE EJD_ERHV_FLYT_CSRP_CUR CURSOR FOR                
           SELECT T1.ADRFRADTO,  /*INDFLYT*/                            
                  T1.ADRTILDTO   /*FRAFLYT*/                            
           FROM   BQ71000T T1                                           
           WHERE  T1.PNR        = :SØG_DCPR                             
            AND   T1.BOPAEKMNR  = :SØG_EKOMNR                           
            AND   T1.VEJKOD     = :SØG_VEJK_EJD                         
            AND (T1.HUSNR       = :SØG_HUSNR_EJD1                       
             OR  T1.HUSNR       = :SØG_HUSNR_EJD2)                      
            AND (T1.HUSBOGSTAADR  =:SØG_HUSBOGSTAV1                     
             OR  T1.HUSBOGSTAADR  =:SØG_HUSBOGSTAV2)                    
            AND   T1.ADRFRADTO <= :TMP_SLUT     /*TILFLYT*/             
            AND   T1.ADRTILDTO >= :TMP_START    /*FRAFLYT*/             
           ORDER BY ADRFRADTO                                           
           FOR READ ONLY;                                               
                                                                        
      EXEC SQL DECLARE ERHV_FLYT_CSRP_CUR CURSOR FOR                    
           SELECT T1.ADRFRADTO,  /*INDFLYT*/                            
                  T1.ADRTILDTO   /*FRAFLYT*/                            
           FROM   BQ71000T T1                                           
           WHERE  T1.PNR        = :SØG_DCPR                             
            AND   T1.BOPAEKMNR  = :SØG_EKOMNR                           
            AND   T1.VEJKOD     = :SØG_VEJK_EJD                         
            AND (T1.HUSNR          = :SØG_HUSNR_EJD1                    
             OR  T1.HUSNR          = :SØG_HUSNR_EJD2)                   
            AND (T1.HUSBOGSTAADR =:SØG_HUSBOGSTAV1                      
             OR  T1.HUSBOGSTAADR =:SØG_HUSBOGSTAV2)                     
            AND  (T1.ETAGENR    = :SØG_ETAGE1                           
              OR  T1.ETAGENR    = :SØG_ETAGE2                           
              OR  T1.ETAGENR    = :SØG_ETAGE3)                          
            AND  (T1.SIDEDOERNR    = :SØG_SIDE_DØRNR1                   
              OR  T1.SIDEDOERNR    = :SØG_SIDE_DØRNR2                   
              OR  T1.SIDEDOERNR    = :SØG_SIDE_DØRNR3)                  
            AND  T1.ADRFRADTO <= :TMP_SLUT   /*TILFLYT*/                
            AND  T1.ADRTILDTO >= :TMP_START  /*FRAFLYT*/                
           ORDER BY ADRFRADTO                                           
           FOR READ ONLY;                                               
                                                                        
      /*FIND PÅ BQ70000T KUN AKTUEL*/                                   
      EXEC SQL DECLARE AKT_EJD_ERHV_FLYT_CSRP_CUR CURSOR FOR            
           SELECT T1.ADRFRADTO   /*INDFLYT*/                            
           FROM   BQ70000T T1                                           
           WHERE  T1.PNR        = :SØG_DCPR                             
            AND   T1.BOPAEKMNR  = :SØG_EKOMNR                           
            AND   T1.VEJKOD     = :SØG_VEJK_EJD                         
            AND (T1.HUSNR       = :SØG_HUSNR_EJD1                       
             OR  T1.HUSNR       = :SØG_HUSNR_EJD2)                      
            AND (T1.HUSBOGSTAADR  =:SØG_HUSBOGSTAV1                     
             OR  T1.HUSBOGSTAADR  =:SØG_HUSBOGSTAV2)                    
            AND   T1.ADRFRADTO <= :TMP_SLUT     /*TILFLYT*/             
           ORDER BY ADRFRADTO                                           
           FOR READ ONLY;                                               
                                                                        
      EXEC SQL DECLARE AKT_ERHV_FLYT_CSRP_CUR CURSOR FOR                
           SELECT T1.ADRFRADTO   /*INDFLYT*/                            
           FROM   BQ70000T T1                                           
           WHERE  T1.PNR        = :SØG_DCPR                             
            AND   T1.BOPAEKMNR  = :SØG_EKOMNR                           
            AND   T1.VEJKOD     = :SØG_VEJK_EJD                         
            AND (T1.HUSNR          = :SØG_HUSNR_EJD1                    
             OR  T1.HUSNR          = :SØG_HUSNR_EJD2)                   
            AND (T1.HUSBOGSTAADR =:SØG_HUSBOGSTAV1                      
             OR  T1.HUSBOGSTAADR =:SØG_HUSBOGSTAV2)                     
            AND  (T1.ETAGENR    = :SØG_ETAGE1                           
              OR  T1.ETAGENR    = :SØG_ETAGE2                           
              OR  T1.ETAGENR    = :SØG_ETAGE3)                          
            AND  (T1.SIDEDOERNR    = :SØG_SIDE_DØRNR1                   
              OR  T1.SIDEDOERNR    = :SØG_SIDE_DØRNR2                   
              OR  T1.SIDEDOERNR    = :SØG_SIDE_DØRNR3)                  
            AND  T1.ADRFRADTO <= :TMP_SLUT   /*TILFLYT*/                
           ORDER BY ADRFRADTO                                           
           FOR READ ONLY;                                               
                                                                        
 END DECLARE_ERHVERV_CSRP_CUR;                                          
 /*********************************************************************/
 /*                                                                   */
 /*      OPSÆTNING AF IND- OG UDFLYTNINGSDATOER UDFRA CSRP DATA       */
 /*                                                                   */
 /*********************************************************************/
 OPSÆT_EJD_ERHVERV_PER_CSRP: PROC;                                      
                                                                        
      DCLBQ71000T.ADRFRADTO = '';                                       
      DCLBQ71000T.ADRTILDTO = '';                                       
                                                                        
      CALL UDFYLD_SØG;                                                  
                                                                        
      IF BH946.MT.DRGNÅRFRA > 0                           /*#18-03-1*/  
      &  BH946.MT.DRGNÅRTIL > 0                                         
      THEN                                                              
         DO;                                                            
            PIC_DATO   = BH946.MT.DRGNÅRFRA;                            
            TMP_START  = CHAR_DATO;                                     
            PIC_DATO   = BH946.MT.DRGNÅRTIL;                            
            TMP_SLUT   = CHAR_DATO;                                     
         END;                                                           
      ELSE                                                              
         DO;                                                            
            PIC_ÅR        = BH65300M.GLSLUTÅR;                          
            TMP_START     = CHAR_ÅR !! '0101';                          
            TMP_SLUT      = CHAR_ÅR !! '1231';                          
         END;                                                           
                                                                        
      EXEC SQL OPEN EJD_ERHV_FLYT_CSRP_CUR;                             
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      PER_IX = 0;                                                       
      CALL FETCH_EJD_ERHV_FLYT_CSRP;                                    
      IF SQLCODE = 0                                                    
      THEN                                                              
         TV.CPR_BEBOET = TV.CPR_BEBOET + 1;                             
                                                                        
      DO WHILE (SQLCODE = 0 & PER_IX <= 6);                             
         IF DCLBQ71000T.ADRFRADTO <= TMP_SLUT     /*INDFLYT*/           
         &  DCLBQ71000T.ADRTILDTO >= TMP_START    /*UDFLYT*/            
         THEN                                                           
            DO;                                                         
               PER_IX = PER_IX + 1;                                     
               CALL FLYT_PER_TIL_BH946(PER_IX,DCLBQ71000T.ADRFRADTO,    
                                              DCLBQ71000T.ADRTILDTO);   
               BH946.EJERSKAB.FERHVPER_ANT = PER_IX;                    
            END;                                                        
         CALL FETCH_EJD_ERHV_FLYT_CSRP;                                 
      END;                                                              
                                                                        
      EXEC SQL CLOSE EJD_ERHV_FLYT_CSRP_CUR;                            
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      EXEC SQL OPEN AKT_EJD_ERHV_FLYT_CSRP_CUR;                         
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      CALL FETCH_AKT_EJD_ERHV_FLYT_CSRP;                                
                                                                        
      IF SQLCODE = 0 & PER_IX <= 6                                      
      THEN                                                              
        DO;                                                             
          IF DCLBQ70000T.ADRFRADTO <= TMP_SLUT     /*INDFLYT*/          
          THEN                                                          
            DO;                                                         
              IF PER_IX = 0                                             
              THEN                                                      
                TV.CPR_BEBOET = TV.CPR_BEBOET + 1;                      
              PER_IX = PER_IX + 1;                                      
              CALL FLYT_PER_TIL_BH946(PER_IX,DCLBQ70000T.ADRFRADTO,     
                                             '99991231');               
              BH946.EJERSKAB.FERHVPER_ANT = PER_IX;                     
            END;                                                        
        END;                                                            
                                                                        
      EXEC SQL CLOSE AKT_EJD_ERHV_FLYT_CSRP_CUR;                        
                                                                        
 END OPSÆT_EJD_ERHVERV_PER_CSRP;                                        
 /*********************************************************************/
 /* OPSÆTNING AF IND- OG UDFLYTNINGSDATOER UDFRA CSRP-DATA            */
 /*********************************************************************/
 OPSÆT_ERHVERV_PER_CSRP: PROC;                                          
                                                                        
      DCLBQ71000T.ADRFRADTO = '';                                       
      DCLBQ71000T.ADRTILDTO = '';                                       
                                                                        
      CALL UDFYLD_SØG;                                                  
                                                                        
      IF BH946.MT.DRGNÅRFRA > 0                           /*#18-03-1*/  
      &  BH946.MT.DRGNÅRTIL > 0                                         
      THEN                                                              
         DO;                                                            
            PIC_DATO   = BH946.MT.DRGNÅRFRA;                            
            TMP_START  = CHAR_DATO;                                     
            PIC_DATO   = BH946.MT.DRGNÅRTIL;                            
            TMP_SLUT   = CHAR_DATO;                                     
         END;                                                           
      ELSE                                                              
         DO;                                                            
            PIC_ÅR        = BH65300M.GLSLUTÅR;                          
            TMP_START     = CHAR_ÅR !! '0101';                          
            TMP_SLUT      = CHAR_ÅR !! '1231';                          
         END;                                                           
                                                                        
      EXEC SQL OPEN ERHV_FLYT_CSRP_CUR;                                 
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      PER_IX = 0;                                                       
      CALL FETCH_ERHV_FLYT_CSRP;                                        
      IF SQLCODE = 0                                                    
      THEN                                                              
         TV.CPR_BEBOET = TV.CPR_BEBOET + 1;                             
                                                                        
      DO WHILE (SQLCODE = 0 & PER_IX <= 6);                             
   /*  PUT SKIP LIST('DCLBQ71000T.ADRFRADTO =', DCLBQ71000T.ADRFRADTO); 
       PUT SKIP LIST('DCLBQ71000T.ADRTILDTO =', DCLBQ71000T.ADRTILDTO); 
       PUT SKIP LIST('TMP_SLUT  =', TMP_SLUT);                          
       PUT SKIP LIST('TMP_START =', TMP_START); */                      
         IF DCLBQ71000T.ADRFRADTO <= TMP_SLUT                           
         &  DCLBQ71000T.ADRTILDTO >= TMP_START                          
         THEN                                                           
            DO;                                                         
               PER_IX = PER_IX + 1;                                     
               CALL FLYT_PER_TIL_BH946(PER_IX,DCLBQ71000T.ADRFRADTO,    
                                              DCLBQ71000T.ADRTILDTO);   
               BH946.EJERSKAB.FERHVPER_ANT = PER_IX;                    
            END;                                                        
         CALL FETCH_ERHV_FLYT_CSRP;                                     
      END;                                                              
                                                                        
      EXEC SQL CLOSE ERHV_FLYT_CSRP_CUR;                                
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      EXEC SQL OPEN AKT_ERHV_FLYT_CSRP_CUR;                             
      IF SQLCODE ^= 0                                                   
      THEN                                                              
         CALL SQL_FEJL;                                                 
                                                                        
      CALL FETCH_AKT_ERHV_FLYT_CSRP;                                    
                                                                        
      IF SQLCODE = 0 & PER_IX <= 6                                      
      THEN                                                              
        DO;                                                             
          IF DCLBQ70000T.ADRFRADTO <= TMP_SLUT     /*INDFLYT*/          
          THEN                                                          
            DO;                                                         
              IF PER_IX = 0                                             
              THEN                                                      
                TV.CPR_BEBOET = TV.CPR_BEBOET + 1;                      
              PER_IX = PER_IX + 1;                                      
              CALL FLYT_PER_TIL_BH946(PER_IX,DCLBQ70000T.ADRFRADTO,     
                                             '99991231');               
              BH946.EJERSKAB.FERHVPER_ANT = PER_IX;                     
            END;                                                        
        END;                                                            
                                                                        
      EXEC SQL CLOSE AKT_ERHV_FLYT_CSRP_CUR;                            
                                                                        
 END OPSÆT_ERHVERV_PER_CSRP;                                            
 /*********************************************************************/
 /*  Fetch EJENDOM CSRP.                                              */
 /*********************************************************************/
 FETCH_ERHV_FLYT_CSRP: PROC;                                            
                                                                        
      EXEC SQL FETCH ERHV_FLYT_CSRP_CUR                                 
         INTO :DCLBQ71000T.ADRFRADTO,     /*INDFLYT*/                   
              :DCLBQ71000T.ADRTILDTO;     /*FRAFLYT*/                   
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
         WHEN (0) /*;*/                                                 
           DO;                                                          
   /* PUT SKIP LIST('DCLBQ71000T.ADRFRADTO =', DCLBQ71000T.ADRFRADTO);  
      PUT SKIP LIST('DCLBQ71000T.ADRTILDTO =', DCLBQ71000T.ADRTILDTO);*/
           END;                                                         
         WHEN (100);                                                    
        OTHERWISE                                                       
         PUT SKIP LIST                                                  
          ('A INDFLYT FEJL CSRP-DATA SQLKODE = ',SQLCA.SQLCODE);        
      END;                                                              
                                                                        
 END FETCH_ERHV_FLYT_CSRP;                                              
 /*********************************************************************/
 /*  Fetch EJENDOM CSRP.                                              */
 /*********************************************************************/
 FETCH_AKT_ERHV_FLYT_CSRP: PROC;                                        
                                                                        
      EXEC SQL FETCH AKT_ERHV_FLYT_CSRP_CUR                             
         INTO :DCLBQ70000T.ADRFRADTO;     /*INDFLYT*/                   
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
         WHEN (0) /*;*/                                                 
           DO;                                                          
   /*PUT SKIP LIST('ZDCLBQ70000T.ADRFRADTO =', DCLBQ70000T.ADRFRADTO);*/
           END;                                                         
         WHEN (100);                                                    
        OTHERWISE                                                       
         PUT SKIP LIST                                                  
          ('A INDFLYT FEJL CSRP-DATA SQLKODE = ',SQLCA.SQLCODE);        
      END;                                                              
                                                                        
 END FETCH_AKT_ERHV_FLYT_CSRP;                                          
 /*********************************************************************/
 /*  Fetch EJENDOM CSRP.                                              */
 /*********************************************************************/
 FETCH_AKT_EJD_ERHV_FLYT_CSRP: PROC;                                    
                                                                        
      EXEC SQL FETCH AKT_EJD_ERHV_FLYT_CSRP_CUR                         
         INTO :DCLBQ70000T.ADRFRADTO;     /*INDFLYT*/                   
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
         WHEN (0);                                                      
         WHEN (100);                                                    
        OTHERWISE                                                       
         PUT SKIP LIST                                                  
          ('B INDFLYT FEJL CSRP-DATA SQLKODE = ',SQLCA.SQLCODE);        
      END;                                                              
                                                                        
 END FETCH_AKT_EJD_ERHV_FLYT_CSRP;                                      
 /*********************************************************************/
 FETCH_EJD_ERHV_FLYT_CSRP: PROC;                                        
                                                                        
      EXEC SQL FETCH EJD_ERHV_FLYT_CSRP_CUR                             
         INTO :DCLBQ71000T.ADRFRADTO,     /*INDFLYT*/                   
              :DCLBQ71000T.ADRTILDTO;     /*FRAFLYT*/                   
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
         WHEN (0);                                                      
         WHEN (100);                                                    
        OTHERWISE                                                       
         PUT SKIP LIST                                                  
          ('B INDFLYT FEJL CSRP-DATA SQLKODE = ',SQLCA.SQLCODE);        
      END;                                                              
                                                                        
 END FETCH_EJD_ERHV_FLYT_CSRP;                                          
 /*********************************************************************/
 /*                                                                   */
 /*      Flyt beboede perioder til beregningbåndet                    */
 /*                                                                   */
 /*********************************************************************/
 FLYT_PER_TIL_BH946: PROC(I,DATO_INDFLYT,DATO_FRAFLYT);                 
 DCL  I  FIXED BIN (15);                                                
 DCL  DATO_INDFLYT CHAR(8);                                             
 DCL  DATO_FRAFLYT CHAR(8);                                             
                                                                        
      HJDATO_FRA = '';                                                  
      HJDATO_TIL = '';                                                  
      HJDATO_FRA = SUBSTR(DATO_INDFLYT,1,4) !! '-' !!                   
                   SUBSTR(DATO_INDFLYT,5,2) !! '-' !!                   
                   SUBSTR(DATO_INDFLYT,7,2);                            
      HJDATO_TIL = SUBSTR(DATO_FRAFLYT,1,4) !! '-' !!                   
                   SUBSTR(DATO_FRAFLYT,5,2) !! '-' !!                   
                   SUBSTR(DATO_FRAFLYT,7,2);                            
      SELECT (I);                                                       
        WHEN (1)                                                        
          DO;                                                           
            BH946.MT.PER.DERHVPER_FRA1 = HJDATO_FRA;                    
            BH946.MT.PER.DERHVPER_TIL1 = HJDATO_TIL;                    
          END;                                                          
        WHEN (2)                                                        
          DO;                                                           
            BH946.MT.PER.DERHVPER_FRA2 = HJDATO_FRA;                    
            BH946.MT.PER.DERHVPER_TIL2 = HJDATO_TIL;                    
          END;                                                          
        WHEN (3)                                                        
          DO;                                                           
            BH946.MT.PER.DERHVPER_FRA3 = HJDATO_FRA;                    
            BH946.MT.PER.DERHVPER_TIL3 = HJDATO_TIL;                    
          END;                                                          
        WHEN (4)                                                        
          DO;                                                           
            BH946.MT.PER.DERHVPER_FRA4 = HJDATO_FRA;                    
            BH946.MT.PER.DERHVPER_TIL4 = HJDATO_TIL;                    
          END;                                                          
        WHEN (5)                                                        
          DO;                                                           
            BH946.MT.PER.DERHVPER_FRA5 = HJDATO_FRA;                    
            BH946.MT.PER.DERHVPER_TIL5 = HJDATO_TIL;                    
          END;                                                          
       OTHERWISE;                                                       
     END;  /*select*/                                                   
                                                                        
 END FLYT_PER_TIL_BH946;                                                
