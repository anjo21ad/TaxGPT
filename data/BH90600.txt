  PROCESS OPT(TIME);                                                    
 BH90600:PROCEDURE OPTIONS (MAIN) REORDER;                              
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KMD A/S');                    
 /********************************************************************* 
 *                                                                    * 
 * FORMÅL: FORBRÆNDER TIL DATASÆT FRA ESR FOR AT DET OMSAT TIL DET    * 
 *         GAMMLE FORMAT DER ER KOMPILERET IND I BH90700L             * 
 *                                                                    * 
 *         SKAL KUN BRUGES TIL SLUT 2011 SUPPLEMENTSKØRSEL            * 
 *         OPRETTET AF ESB JULI 2013                                  * 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************* 
 *       DECLARE AF REGISTRE                                          * 
 *********************************************************************/ 
 DCL     BH906I FILE RECORD INPUT;      /* ESR UDTRÆK NY STRUKTUR    */ 
 DCL     BH906O FILE RECORD OUTPUT;     /* ESR UDTRÆK GL STRUKTUR    */ 
 DCL     SYSPRINT FILE EXTERNAL PRINT;                                  
 /********************************************************************* 
 *       DECLARE AF UDTRÆK FRA ESR                                    * 
 *********************************************************************/ 
 DCL     AREAL_NY CHAR (1000) VAR;                                      
 DCL     1 CN57220_NY    BASED     (ADDR(AREAL_NY)),                    
           4 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH31800N;;                                          
                                                                        
 DCL     AREAL_GL CHAR (1000) VAR;                                      
 DCL     1 CN57220_GL    BASED     (ADDR(AREAL_GL)),                    
           4 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH31801N;;                                          
                                                                        
 DCL     EOF_BH906       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
 DCL     TV_LÆS    BIN FIXED(31);                                       
 DCL     TV_SKRIV  BIN FIXED(31);                                       
 /********************************************************************* 
 *       DECLARE AF HJÆLPEFELTER TIL MASKINEL AFSTEMNING              * 
 *********************************************************************/ 
 DCL     SWFEJL          CHAR      (1)       INIT      (' ');           
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
 /*      CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH906 = NEJ;                                               
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
         OPEN FILE (BH906I)  TITLE ('BH90600I');                        
         OPEN FILE (BH906O)  TITLE ('BH90600O');                        
                                                                        
         ON ENDFILE (BH906I)                                            
            EOF_BH906 = JA;                                             
                                                                        
         TV_LÆS = 0;                                                    
         TV_SKRIV = 0;                                                  
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_FIL;                                                  
         DO WHILE(EOF_BH906 = NEJ);                                     
            CALL BEHANDL;                                               
            CALL LÆS_FIL;                                               
         END;                                                           
         CALL AFSLUT;                                                   
 /********************************************************************* 
 *       LÆS UDTRÆK FRA ESR                                           * 
 *********************************************************************/ 
 LÆS_FIL:PROC;                                                          
                                                                        
         CN57220_NY = '';                                               
         READ FILE (BH906I) INTO (AREAL_NY);                            
         TV_LÆS = TV_LÆS +  1;                                          
                                                                        
 END LÆS_FIL;                                                           
 /********************************************************************/ 
 BEHANDL: PROC;                                                         
                                                                        
    CN57220_GL = '';                                                    
    CN57220_GL = CN57220_NY, BY NAME;                                   
    CN57220_GL.LÆNGDE = CN57220_NY.LÆNGDE - 3;                          
    WRITE FILE (BH906O) FROM (AREAL_GL);                                
    TV_SKRIV = TV_SKRIV +  1;                                           
                                                                        
 END BEHANDL;                                                           
 /*********************************************************************/
 AFSLUT:PROC;                                                           
   PUT SKIP LIST('ANTAL LÆST    :',TV_LÆS);                             
   PUT SKIP LIST('ANTAL SKREVET :',TV_SKRIV);                           
   CLOSE FILE (BH906I);                                                 
   CLOSE FILE (BH906O);                                                 
 END AFSLUT;                                                            
 END  BH90600;                                                          
