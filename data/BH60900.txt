  PROCESS GOSTMT,AG,OPTIONS,OPT(TIME);                                  
 /* BH60900 -  UDTRÆK/SORTERING TIL INCASSO- OG/ELLER SOMMERHUSLISTE */ 
 BH60900 : PROC OPTIONS (MAIN) REORDER;                                 
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');       
 /********************************************************************* 
 *                                                                    * 
 *   FORMÅL  :      INTERN SORTERING OG UDSKRIVNING AF LISTER         * 
 *                                                                    * 
 *   INDDATA :      B.H62020D      UDTRÆK FRA BH62000L                * 
 *                                                                    * 
 *   UDDATA  :      BH60901Y       KONTROLLISTE                       * 
 *                  BH60903Y       INKASSO- OG/ELLER SOMMERHUSLISTE   * 
 *                                                                    * 
 *   PROGRAMMØR :   KJELD MØLLER POULSEN , AC D. 771115               * 
 *                                                                    * 
 *   ÆNDRET SEPTEMBER 1984 AF MARGIT PEDERSBÆK                        * 
 *          OKTOBER   1985 AF MARGIT PEDERSBÆK                        * 
 *          SEPTEMBER 1987 AF MARGIT PEDERSBÆK                        * 
 *          JANUAR    1988 AF JESPER OLSEN   -  KALD TIL P-DATA       * 
 *          SEPTEMBER 1989 AF PALLE CHRISTIANSEN,  UC-SKAT, BRØNDBY   * 
 *          OKTOBER   1990 AF MARGIT PEDERSBÆK  , DIV-SKAT, BRØNDBY   * 
 *          OKTOBER   1991 AF AAGE RASMUSSEN    , DIV-SKAT, BRØNDBY   * 
 *          NOVEMBER  1992 AF AAGE RASMUSSEN    , DIV-SKAT, BRØNDBY   * 
 *          JUNI      1993 AF AAGE RASMUSSEN    , DIV-SKAT, BRØNDBY   * 
 *          JAN       1994 AF AAGE RASMUSSEN    , PUS     , BRØNDBY   * 
 *          DEC       1994 AF AAGE RASMUSSEN    , PUS     , BRØNDBY   * 
 *          FEBRUAR   1995 AF AAGE RASMUSSEN    , PUS     , BRØNDBY   * 
 *          DECEMBER  1995 AF AAGE RASMUSSEN    , PUS     , BRØNDBY   * 
 *          JANUAR    1998 AF AAGE RASMUSSEN    , PFB     , BRØNDBY   * 
 *          JANUAR    1999 AF AAGE RASMUSSEN    , PFB     , BRØNDBY   * 
 *          JANUAR    2000 AF AAGE RASMUSSEN    , PUS     , BRØNDBY   * 
 *                                                                    * 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED;                            
 /*********************************************************************/
 /*                                                                   */
 /*          ÅRSBESTEMTE VARIABLE......                               */
 /*                                                                   */
 /*   S K A L  R E T T E S  Å R L I G T   (INDSAT AF PSC I 1989)      */
 /*   *********************************                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ÅR_TXT1           CHAR (56);                                   
         ÅR_TXT1 =                                                      
          'Liste over ejerforhold vedr. fast ejendom pr. 31/12-1999';   
 DCL     ÅR_TXT2           CHAR (69);                                   
         ÅR_TXT2 =                                                      
          'Liste vedr. sommerhuse/ejerlejligheder i anden kommune' !!   
          ' pr. 31/12-1999';                                            
 /********************************************************************* 
 *                                                                    * 
 *      DCL AF BH62020D                                               * 
 *                                                                    * 
 *********************************************************************/ 
 DCL    BH6202I FILE RECORD INPUT;                                      
 DCL    BH62020_AR CHAR (133);                                          
 DCL    1 FS             BASED (ADDR(BH62020_AR)),                      
        %INCLUDE BH62020N;                                              
 DCL    1 KOMOPL         BASED (ADDR(BH62020_AR)),                      
        %INCLUDE BH62022N;                                              
 /*********************************************************************/
 /*                                                                   */
 /*      DCL AF TABEL MED KOMMUNENAVNE                                */
 /*                                                                   */
 /*********************************************************************/
 DCL     N               BIN FIXED (15);   /* INDEX TIL KOMMUNETABEL  */
 DCL     1 KOM_ADR (101:910),                                           
           2 MYNVNADR    CHAR      (43);      /*SLUTLIGN.KOMM.NAVN    */
 DCL     GEMKOM          PIC       '999'     INIT (0);                  
 DCL     GEMTYP          PIC       '9'       INIT (0);                  
 DCL     SP_KNR          CHAR      (08);                                
 DCL     JA              BIT       (1)        INIT ('1'B);              
 DCL     NEJ             BIT       (1)        INIT ('0'B);              
 DCL     FØRSTE_GANG     BIT       (1);                                 
 DCL     EOF_BH620       BIT       (1);                                 
 /*********************************************************************/
 /*                                                                   */
 /*       DCL AF  PRINTAREALER.                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     L               CHAR      (133);                               
 DCL     P               CHAR      (2);                                 
 DCL     1 L1            BASED(ADDR(L)),                                
           2 CCA         CHAR      (1),                                 
           2 LIN         CHAR      (132);                               
 DCL     1 L2            BASED(ADDR(L)),                                
           2 CCB         CHAR      (1),                                 
           2 PRKOM       PIC       '999',                               
           2 F1          CHAR      (7),                                 
           2 TIINK       PIC       'Z.ZZZ.ZZZBB',                       
           2 T_LÆS       PIC       'Z.ZZZ.ZZZBB',                       
           2 TSINK       PIC       'Z.ZZZ.ZZZBB',                       
           2 TLINK       PIC       'ZZ.ZZZ.ZZZ',                        
           2 F2          CHAR      (2),                                 
           2 TEKST       CHAR      (54);                                
 DCL     1 L2_DEF        BASED(ADDR(L)),                                
           2 CCB         CHAR      (1),                                 
           2 TOT         CHAR      (06),                                
           2 F1          CHAR      (126);                               
 DCL     1 L4_INK        BASED(ADDR(L)),                                
           2 CCD         CHAR      (1),                                 
           2 IDSORP      CHAR      (12),                                
           2 IDCPRC      CHAR      (12),                                
           2 IANAVN      CHAR      (32),                                
           2 FILLER      CHAR      (1),                                 
           2 IEKOM       PIC       '999BB',                             
           2 IEEJD       PIC       '99999B9B',                          
           2 IABELI      CHAR      (50),                                
           2 SLUT2       CHAR      (1),                                 
           2 IBEJV       PIC       'ZZZ.ZZZ.ZZZ';                       
 DCL     1 L4_SOM        BASED(ADDR(L)),                                
           2 CCD         CHAR      (1),                                 
           2 IDSORP      CHAR      (12),                                
           2 FILLER1     CHAR      (3),                                 
           2 IDCPRC      CHAR      (12),                                
           2 FILLER2     CHAR      (4),                                 
           2 IANAVN      CHAR      (35),                                
           2 FILLER3     CHAR      (4),                                 
           2 IEKOM       PIC       '999',                               
           2 FILLER4     CHAR      (2),                                 
           2 IEEJD       PIC       'ZZZZZ9B9',                          
           2 FILLER5     CHAR      (3),                                 
           2 BK_BBRANV   CHAR      (8),                                 
           2 FILLER6     CHAR      (4),                                 
           2 GANE        PIC       'ZZ9,99B',                           
           2 PROCENT     CHAR      (1);                                 
 DCL     1 L5            BASED(ADDR(L)),                                
          2 LL(133)      CHAR      (1);                                 
 DCL     1 LTOT          BASED(ADDR(L)),                                
           2 CCE         CHAR      (1),                                 
           2 TEKST       CHAR      (35),                                
           2 TEJD        PIC       'Z.ZZZ.ZZZ',                         
           2 FILLER      CHAR      (88);                                
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DCL AF BUILTIN FUNCTIONS, ENTRIES OG WORKFELTER              */
 /*                                                                   */
 /*********************************************************************/
 DCL     (PLISRTD,                                                      
          PLIRETC,                                                      
          SUBSTR,                                                       
          PLIDUMP,                                                      
          STG,                                                          
          ADDR)     BUILTIN;                                            
 %INCLUDE AS27100N;                                                     
 DCL     ZS30101    ENTRY;                    /* ABEND-RUTINE         */
 DCL     ZS31101    ENTRY;                                              
 DCL     ZS34900    ENTRY OPTIONS (ASM INTER);/* HALVAUTOM. FAKTURER. */
 DCL     ZS61900    ENTRY;                    /* SKRIV SSI-DATO I LOG */
 DCL     ZS38100    ENTRY OPTIONS (INTER ASM);        /* TIMESTAMP    */
 /********************************************************************* 
 *                                                                    * 
 *      DCL AF DIVERSE                                                * 
 *                                                                    * 
 *********************************************************************/ 
 DCL     (I,Q,Z)         BIN FIXED (15,0);                              
 DCL     RETURN_CODE     BIN FIXED (31,0);                              
 DCL     SORTFLT         CHAR      (16);                                
 DCL     1 SORT2         BASED(ADDR(SORTFLT)),                          
           2 EÆKNR       FIXED     (3),                                 
           2 CRAPTYP     PIC       '9',                                 
           2 DSORP       FIXED     (11),                                
           2 CPART       CHAR      (1),                                 
           2 EKOMNR      FIXED     (3),                                 
           2 EEJDNR      FIXED     (7);                                 
 DCL     INDV            CHAR      (149);                               
 DCL     1 DFINDV        BASED(ADDR(INDV)),                             
          2 SFELT        CHAR      (16),                                
          2 INDHOLD      CHAR      (133);                               
 DCL     1 TÆLTAB,                                                      
           2 TLINK       FIXED     (7),                                 
           2 TSINK       FIXED     (7),                                 
           2 TIINK       FIXED     (7),                                 
           2 T_LÆS       FIXED     (7);                                 
 DCL     1 TOTAL,                                                       
           2 TLINK       FIXED     (7),                                 
           2 TSINK       FIXED     (7),                                 
           2 TIINK       FIXED     (7),                                 
           2 T_LÆS       FIXED     (7);                                 
 DCL     TOTEJD          FIXED     (7)       INIT      (0);             
 DCL     S_INKA          FIXED     (5)       INIT      (0);             
 DCL     L_INKA          FIXED     (2)       INIT      (60);            
 DCL     MASKE1          PIC       'ZZ.ZZ9';                            
 DCL     HBIP            PIC       '(11)9';                             
 DCL     1 HB1           BASED(ADDR(HBIP)),                             
           2 NI          CHAR      (10),                                
           2 KØN         CHAR      (1);                                 
 DCL     GDSORP          PIC       '(10)9'   INIT      (0);             
 DCL     TSKON           PIC       'ZZ9'     INIT      (0);             
 DCL     TLKON           PIC       '99'      INIT      (60);            
 DCL     SW_LISTE        CHAR      (1)       INIT      ('0');           
 DCL     1 HJ,                                                          
           2 HJABELMAT   CHAR      (61),                                
           2 SLUT        CHAR      (1)       INIT      (' ');           
 DCL     HJ_MASKE        CHAR      (12);                                
 DCL     CPRMASKE        PIC       '(6)9B9999B' BASED(ADDR(HJ_MASKE));  
 DCL     CIRMASKE        PIC       '(11)ZB'     BASED(ADDR(HJ_MASKE));  
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      FELTER TIL BRUG FOR SUBRUTINEN ZS34900                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 DEBSTRUC,                                                    
           2 DEBIT_OK    BIT       (8),                                 
           2 EBRUG       PIC       '9999',                              
           2 EINST       PIC       '(3)9',                              
           2 FOPGANT     PIC       '(7)9',                              
           2 CENHED      CHAR      (3),                                 
           2 AENHED      CHAR      (20);                                
 /*********************************************************************/
 /*                                                                   */
 /*      HOUSEKEEPING                                                 */
 /*                                                                   */
 /*********************************************************************/
         CALL ZS61900;                                                  
         ON ENDFILE (BH6202I)                                           
            EOF_BH620 = JA;                                             
         EOF_BH620 = NEJ;                                               
         OPEN FILE (BH6202I) TITLE ('BH62020I');                        
         CALL ZZ20390('*OPEN','BH60901Y');                              
         CALL ZZ20390('*OPEN','BH60903Y');                              
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         EINST = 0;                                                     
         GEMKOM = 0;                                                    
         GEMTYP = 0;                                                    
         ON ERROR CALL PLIDUMP('SNHOB');                                
 /********************************************************************* 
 *       OPSAML AF KOMMUNEADRESSER FRA B.H62011D'S KOMMUNEINDIVIDER   * 
 *********************************************************************/ 
         KOM_ADR(*) = '';                                               
         READ FILE(BH6202I) INTO (BH62020_AR);                          
         DO WHILE (KOMOPL.FILLER04 = 0 & EOF_BH620 = NEJ);              
            N = KOMOPL.EÆKNR;                                           
            KOM_ADR(N).MYNVNADR  = KOMOPL.MYNVNADR;                     
            READ FILE(BH6202I) INTO (BH62020_AR);                       
         END;                                                           
         FØRSTE_GANG = JA;                                              
 /*********************************************************************/
 /*                                                                   */
 /*      HOVEDRUTINE                                                  */
 /*                                                                   */
 /*********************************************************************/
         CALL PLISRTD ('SORT FIELDS=(1,16,BI,A) ',                      
                      ' RECORD TYPE=F,LENGTH=(149) ',                   
                       500000,                                          
                       RETURN_CODE,                                     
                       E15X,                                            
                       E35X);                                           
         IF RETURN_CODE ^= 0                                            
         THEN                                                           
            CALL ZS30101 (4,'BH60900 ** 250 SORT FAILED, RETURN-CODE'   
                    !!'='  !!RETURN_CODE,2);                            
 /*********************************************************************/
 /*                                                                   */
 /*      AFSLUT PROGRAM                                               */
 /*                                                                   */
 /*********************************************************************/
         CALL SKRIV_KOMTOTAL;     /* VED EOF */                         
         CALL KOMBRUD;            /* VED EOF */                         
                                                                        
         LIN = ' ';                                                     
         L2_DEF.TOT = 'TOTAL ';                                         
         L2 = TOTAL, BY NAME;                                           
         CCA = ZZWS2;                                                   
         CALL ZZ20300(L,'01');                                          
                                                                        
         LIN = '';                                                      
         CALL ZZ20300(L,'99');                                          
 /*********************************************************************/
 /*                                                                   */
 /*      E15  PROCEDURE                                               */
 /*                                                                   */
 /*********************************************************************/
 E15X:   PROC RETURNS(CHAR(149));                                       
         ON ENDFILE (BH6202I)                                           
            GOTO END15X;                                                
         IF FØRSTE_GANG = JA                                            
         THEN                                                           
            FØRSTE_GANG = NEJ;                                          
         ELSE                                                           
            READ FILE(BH6202I) INTO (BH62020_AR);                       
         SORTFLT = '';                                                  
         SORT2 = FS, BY NAME;                                           
         DFINDV.INDHOLD = BH62020_AR;                                   
         DFINDV.SFELT = SORTFLT;                                        
         CALL PLIRETC(12);                   /* INPUT TIL SORT       */ 
         RETURN(INDV);                                                  
 END15X:                                                                
         CALL PLIRETC(8);                    /* END OF INPUT         */ 
         END E15X;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      E35  PROCEDURE                                               */
 /*                                                                   */
 /*********************************************************************/
 E35X:   PROC(FRASORT);                                                 
 DCL     FRASORT         CHAR      (149);                               
 DCL     1 BH_INK        BASED     (ADDR(FRASORT)),                     
           2 KNR         FIXED     (3),                                 
           2 FILLER0     CHAR      (14),                                
           %INCLUDE BH62020N;                                           
 DCL     1 BH_SOM        BASED     (ADDR(FRASORT)),                     
           2 KNR         FIXED     (3),                                 
           2 FILLER0     CHAR      (14),                                
           %INCLUDE BH62021N;                                           
                                                                        
         IF GEMKOM = 0                                                  
         THEN                                                           
            DO;                                                         
               TOTAL.TLINK = 0;                                         
               TOTAL.TSINK = 0;                                         
               TOTAL.TIINK = 0;                                         
               TOTAL.T_LÆS = 0;                                         
               TÆLTAB.TLINK = 0;                                        
               TÆLTAB.TSINK = 0;                                        
               TÆLTAB.TIINK = 0;                                        
               TÆLTAB.T_LÆS = 0;                                        
               GEMKOM = BH_INK.EÆKNR;                                   
               GEMTYP = BH_INK.CRAPTYP;                                 
               SW_LISTE = '0';                                          
            END;                                                        
         IF (GEMKOM ^= BH_INK.EÆKNR !                                   
             GEMTYP ^= BH_INK.CRAPTYP)                                  
         THEN                                                           
            DO;                                                         
               CALL SKRIV_KOMTOTAL;                                     
               CALL KOMBRUD;                                            
               TÆLTAB.TLINK = 0;                                        
               TÆLTAB.TSINK = 0;                                        
               TÆLTAB.TIINK = 0;                                        
               TÆLTAB.T_LÆS = 0;                                        
               GEMKOM = BH_INK.EÆKNR;                                   
               GEMTYP = BH_INK.CRAPTYP;                                 
               SW_LISTE = '0';                                          
            END;                                                        
         CALL SKRIV_LISTE;                                              
         TÆLTAB.T_LÆS = TÆLTAB.T_LÆS + 1;                               
         CALL PLIRETC(4);                    /* REQUEST RECORD       */ 
 /*********************************************************************/
 /*                                                                   */
 /*       DENNE PROCEDURE UDSKRIVER INCASSO- OG/ELLER SOMMERHUSLISTE  */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_LISTE:                                                           
         PROC;                         /* EVT EN FORSIDE */             
         IF SW_LISTE = '0'                                              
         THEN                                                           
            CALL SKRIV_FORSIDE;                                         
         TÆLTAB.TIINK = TÆLTAB.TIINK + 1;                               
         IF L_INKA > 52                                                 
         THEN                                                           
            CALL SKRIV_OVERSKRIFT;                                      
         IF GDSORP ^= BH_INK.DSORP                                      
         THEN                                                           
            DO;                                                         
               IF (BH_INK.CRAPTYP = 1 &      /* INKASSOLISTE */         
                   L_INKA = 4) !                                        
                  (BH_INK.CRAPTYP ^= 1 &     /* SOMMERHUSLISTE */       
                   L_INKA = 5)                                          
               THEN;                                                    
               ELSE                                                     
                  DO;                                                   
                     L_INKA = L_INKA + 1;                               
                     L = '';                                            
                     CCA = ZZWS1;                                       
                     CALL ZZ20300 (L,'03');                             
                  END;                                                  
            END;                                                        
         L_INKA = L_INKA + 1;                                           
         L = '';                                                        
         TÆLTAB.TLINK = TÆLTAB.TLINK + 1;                               
         IF BH_INK.DSORP < 100000000                                    
         THEN                                                           
            DO;                                                         
               CIRMASKE = BH_INK.DSORP;                                 
               IF BH_INK.CRAPTYP = 1  /* INKASSOLISTE */                
               THEN                                                     
                  L4_INK.IDSORP = HJ_MASKE;                             
               ELSE                                                     
                  L4_SOM.IDSORP = HJ_MASKE;                             
            END;                                                        
         ELSE                                                           
            DO;                                                         
               CPRMASKE = BH_INK.DSORP;                                 
               SUBSTR(HJ_MASKE,7,1) = '-';                              
               L4_INK.IDSORP = HJ_MASKE;                                
            END;                                                        
         IF BH_INK.DCPRCIR < 100000000                                  
         THEN                                                           
            CIRMASKE = BH_SOM.DCPRCIR;                                  
         ELSE                                                           
            DO;                                                         
               CPRMASKE = BH_INK.DCPRCIR;                               
               SUBSTR(HJ_MASKE,7,1) = '-';                              
            END;                                                        
         HBIP = BH_INK.DCPRCIR * 5;                                     
         IF KØN          = '0' &                                        
            BH_INK.CPART = '2' &                                        
            BH_INK.CRAPTYP = 1        /* INKASSOLISTE */                
         THEN                                                           
            DO;                                                         
               L4_INK.IDCPRC = HJ_MASKE;                                
           END;                                                         
         ELSE                                                           
            IF BH_INK.CRAPTYP = 2        /* SOMMERHUSLISTE */           
            THEN                                                        
               L4_SOM.IDCPRC = HJ_MASKE;                                
                                                                        
         IF BH_INK.CRAPTYP = 1  /* INKASSOLISTE */                      
         THEN                                                           
            DO;                                                         
               L4_INK.IANAVN = BH_INK.ANAVN;                            
               L4_INK.IEKOM  = BH_INK.EKOMNR;                           
               L4_INK.IEEJD  = BH_INK.EEJDNR;                           
               L4_INK.SLUT2  = ' ';                                     
               HJABELMAT     = BH_INK.ABELMAT;                          
               AS271_PTR = ADDR(HJABELMAT);                             
               AS271_LGD = STG (HJABELMAT);                             
               CALL AS27100(RUT_KOHREGEL,AS271_PTR,AS271_LGD);          
               IF HJABELMAT ^= (61)' '                                  
               THEN                                                     
                  CALL ZS31101 (HJABELMAT,SLUT);                        
               L4_INK.IABELI = SUBSTR (HJABELMAT,1,50);                 
               L4_INK.IBEJV = BH_INK.BEJDVÆR;                           
               LL(69) = '-';                                            
            END;                                                        
         ELSE                                                           
            DO;                                                         
               L4_SOM.IANAVN = BH_INK.ANAVN;                            
               L4_SOM.IEKOM  = BH_SOM.EKOMNR;                           
               L4_SOM.IEEJD  = BH_SOM.EEJDNR;                           
               LL(83)        = '-';                                     
               IF BH_SOM.BBRANV = '510'                                 
               THEN                                                     
                  L4_SOM.BK_BBRANV = BH_SOM.CBENYT !! ' / 510';         
               ELSE                                                     
                  L4_SOM.BK_BBRANV = '   ' !! BH_SOM.CBENYT;            
               IF BH_SOM.GANE ^= 0                                      
               THEN                                                     
                  DO;                                                   
                     L4_SOM.GANE    = BH_SOM.GANE;                      
                     L4_SOM.PROCENT = '%';                              
                  END;                                                  
            END;                                                        
         CCA = ZZWS1;                                                   
         CALL ZZ20300 (L,'03');                                         
         GDSORP = BH_INK.DSORP;                                         
         RETURN;                                                        
         END SKRIV_LISTE;                                               
         END E35X;                                                      
 /********************************************************************* 
 *                                                                    * 
 *   DENNE PROC UDFØRER FLG.VED KOMMUNEBRUD :                         * 
 *                                                                    * 
 *       - UDSKRIVER EN FORSIDE PÅ INCASSO- ELLER SOMMERHUSLISTEN     * 
 *       - UDSKRIVER EN DETAILLINIE PÅ KONTROLLISTEN + EVT. HOVED     * 
 *       - FORETAGER HALVAUTOMATISK FAKTURERING VIA ZS34900           * 
 *       - NULSTILLER TÆLLERE M.M.                                    * 
 *                                                                    * 
 *********************************************************************/ 
 KOMBRUD : PROC;                                                        
                                                                        
         IF TLKON > 55                                                  
         THEN                                                           
            DO;                                                         
               L = ZZWS1 !! (132)' ';                                   
               TSKON = TSKON + 1;                                       
               CALL ZZ20300 (L,'01');                                   
               l = zzws2!!'Kørselskontrolliste fra udskrivning'!!       
                   ' af liste(r)  BH60900' !! (49)' ' !!                
                   'Dato ' !! UDDATO !! '   Side  '!!TSKON;             
               CALL ZZ20300(L,'01');                                    
               LIN = ' ';                                               
               lin = 'Kom    Ant.udtrukne  Ant.læste  Ant.sider'        
               !!'  Ant.linier  Listetype';                             
               CCA = ZZWS1;                                             
               CALL ZZ20300 (L,'01');                                   
               L = ' ';                                                 
               l = zzws2!!'nr.    ejendomme     individer  '            
               !!'skrevet    skrevet     ';                             
               CALL ZZ20300 (L,'01');                                   
               TLKON = 5;                                               
            END;                                                        
         /* UDSKR. KONTROLLISTE-DETAILLIN. */                           
         TLKON = TLKON + 2;                                             
         CCA = ZZWS2;                                                   
         LIN = ' ';                                                     
         PRKOM = GEMKOM;                                                
         L2 = TÆLTAB,  BY NAME;                                         
         IF GEMTYP = 1  /* INKASSOLISTE */                              
         THEN                                                           
            L2.TEKST = SUBSTR(ÅR_TXT1,1,41);                            
         ELSE                                                           
            L2.TEKST = SUBSTR(ÅR_TXT2,1,54);                            
         CALL ZZ20300 (L,'01');                                         
         TOTAL.TLINK = TOTAL.TLINK + TÆLTAB.TLINK;                      
         TOTAL.TSINK = TOTAL.TSINK + TÆLTAB.TSINK;                      
         TOTAL.TIINK = TOTAL.TIINK + TÆLTAB.TIINK;                      
         TOTAL.T_LÆS = TOTAL.T_LÆS + TÆLTAB.T_LÆS;                      
         /* HALVAUTOMATISK FAKTURERING     */                           
         IF GEMTYP = 1                                                  
         THEN                                                           
            DO;                                                         
               /* INKASSOLISTE */                                       
               EBRUG = GEMKOM;                                          
               EINST = 0;                                               
               AENHED  = 'INKASSOLISTE/ANT LIN';                        
               CENHED = '034';                                          
               FOPGANT = TÆLTAB.TLINK;                                  
               DEBIT_OK = '00000000'B;                                  
               CALL ZS34900(DEBSTRUC);                                  
               IF DEBIT_OK ^= '00000000'B                               
               THEN                                                     
                  CALL ZS30101 (4,                                      
                      'BH60900 ** 251 FEJL PÅ HALVAUTO.FAKT. R'!!       
                      'ETURKODE='!!DEBIT_OK,2);                         
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* SOMMERHUSLISTE */                                     
               EBRUG = GEMKOM;                                          
               EINST = 0;                                               
               AENHED  = 'SOMMERHUSE/EJ.LEJL. ';                        
               CENHED = '901';                                          
               FOPGANT = 1;                                             
               DEBIT_OK = '00000000'B;                                  
               CALL ZS34900(DEBSTRUC);                                  
               IF DEBIT_OK ^= '00000000'B                               
               THEN                                                     
                  CALL ZS30101 (4,                                      
                      'BH60900 ** 251 FEJL PÅ HALVAUTO.FAKT. ' !!       
                      'RETURKODE='!!DEBIT_OK,2);                        
            END;                                                        
         L_INKA = 60;                                                   
         S_INKA = 0;                                                    
 END KOMBRUD;                                                           
 SKRIV_KOMTOTAL:                                                        
         PROC;                                                          
         IF L_INKA > 50                                                 
         THEN                                                           
            CALL SKRIV_OVERSKRIFT;                                      
         TÆLTAB.TLINK = TÆLTAB.TLINK + 1;                               
         LIN = ' ';                                                     
         CCA = ZZWS1;                                                   
         CALL ZZ20300 (L,'03');                                         
         LIN = ' ';                                                     
         CCA = ZZWS0;                                                   
         ltot.tekst = 'Antal skrevne ejendomme på listen: ';            
         LTOT.TEJD  = TÆLTAB.T_LÆS;                                     
         CALL ZZ20300 (L,'03');                                         
 END SKRIV_KOMTOTAL;                                                    
 SKRIV_FORSIDE:                                                         
         PROC;                                                          
         TÆLTAB.TSINK = 1;                                              
         TÆLTAB.TLINK = 2;                                              
         SW_LISTE = '1';                                                
         SP_KNR = '0' !! GEMKOM;                                        
         CALL ZZ20390('*SUB','BH60903Y',SP_KNR);                        
         IF GEMKOM ^= 0                                                 
         THEN                                                           
            DO;                                                         
               CCA = ZZWK1;                                             
               LIN = ' ';                                               
               CALL ZZ20300 (L,'03');                                   
            END;                                                        
         LIN = ' ';                                                     
         CCA = ZZWS3;                                                   
         DO Z = 1 TO 4;                                                 
            CALL ZZ20300 (L,'03');                                      
         END;                                                           
         L = ZZWS2 !! (35)' ' !! GEMKOM !! ' ' !!                       
             KOM_ADR(GEMKOM).MYNVNADR;                                  
         CALL ZZ20300 (L,'03');                                         
         IF GEMTYP = 1  /* INKASSOLISTE */                              
         THEN                                                           
            DO;                                                         
               L = ZZWS1 !! (35)' ' !! ÅR_TXT1;                         
               CALL ZZ20300 (L,'03');                                   
               L = ZZWS1 !! (35)' ' !! 'til brug for opkrævningsaf'     
               !! 'delingen';                                           
            END;                                                        
         ELSE                   /* SOMMERHUSLISTE */                    
            DO;                                                         
               L = ZZWS1 !! (35)' ' !! ÅR_TXT2;                         
               CALL ZZ20300 (L,'03');                                   
               L = ZZWS1 !! (35)' ' !! 'til brug for ligningsafdel'     
               !! 'ingen';                                              
            END;                                                        
         CALL ZZ20300 (L,'03');                                         
         S_INKA = 0;                                                    
 END SKRIV_FORSIDE;                                                     
 SKRIV_OVERSKRIFT:                                                      
        PROC;                                                           
        S_INKA = S_INKA + 1;                                            
        TÆLTAB.TLINK = TÆLTAB.TLINK + 2;                                
        TÆLTAB.TSINK = TÆLTAB.TSINK + 1;                                
        L = ZZWK1 !! (132)' ';                                          
        CALL ZZ20300(L,'03');                                           
        MASKE1 = S_INKA;                                                
        IF GEMTYP = 1  /* INKASSOLISTE */                               
        THEN                                                            
           DO;                                                          
              L_INKA = 4;                                               
              L = ZZWS2 !! GEMKOM !! ' ' !!                             
              KOM_ADR(GEMKOM).MYNVNADR !!                               
              (3)' ' !! 'E' !!                                          
              SUBSTR(ÅR_TXT1,13,44) !! (9)' ' !!                        
              ' Dato ' !! UDDATO !!                                     
              ' Side ' !! MASKE1;                                       
              CALL ZZ20300 (L,'03');                                    
              L = ZZWS2!!'Cpr-/SE-nr'!!(14)' '!!                        
              'Ejernavn'!!(24)' '!!                                     
              'Kom.nr Ejd.nr Beliggenhed og '!!                         
              'matrikelbetegnelse'!!(18)' '!!'Sidste vurd';             
              CALL ZZ20300 (L,'03');                                    
           END;                                                         
        ELSE                                                            
           DO;                                                          
              L_INKA = 5;                                               
              L = ZZWS2 !! GEMKOM !! ' ' !!                             
              KOM_ADR(GEMKOM).MYNVNADR !!                               
              (2)' ' !! 'E' !!                                          
              SUBSTR(ÅR_TXT2,14,56) !! (3)' ' !!                        
              ' Side ' !! MASKE1 !!                                     
              ' ' !! UDDATO;                                            
              CALL ZZ20300 (L,'03');                                    
              L = ZZWS1!!'Hovedpersonnr  '!!'Ejers personnr  '!!        
              'Ejernavn'!!(29)' '!!                                     
              'Kom.nr   Ejd.nr  Benyttelse/  Ejerandel';                
              CALL ZZ20300 (L,'03');                                    
              L = '';                                                   
              L = ZZWS2!!'               '!!'                '!!        
              '        '!!(29)' '!!                                     
              '                 BBR-anvend.           ';                
              CALL ZZ20300 (L,'03');                                    
           END;                                                         
 END SKRIV_OVERSKRIFT;                                                  
                                                                        
 END BH60900;                                                           
