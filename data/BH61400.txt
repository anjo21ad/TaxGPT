  PROCESS OPT(TIME);                                                    
 BH61400: /* DAN UDTRÆK TIL LUP FRA EVS SLUT */                         
         PROC OPTIONS (MAIN) REORDER;                                   
         DCL COPYRIGHT   CHAR      (30) STATIC                          
      /*SLUT05*/         INIT ('COPYRIGHT KOMMUNEDATA I/S 2005');       
 /*********************************************************************/
 /*      NAVN         UDTRÆK TIL LUP                                  */
 /*                                                                   */
 /*      INDDATA      B.H96000D BEREGNINGSBÅND                        */
 /*                                                                   */
 /*      UDDATA       B.C51300D EJD.OPLYSNINGER TIL LUP               */
 /*                   BH61401Y KØRSELSKONTROLLISTE                    */
 /*                                                                   */
 /*      PROGRAMMØR   BO SCHMIDT                  LSU      MAR 2005   */
 /*                                                                   */
 /*      SLUT05       SUSANNE KROGH VILLUMSEN             11/11-2005  */
 /*                   TILRETTET SLUT 2005                             */
 /*                   VURDOPL0 -> VURDOPLNY                           */
 /*                   VURDOPL1 -> VURDOPLGL                           */
 /*                   VURDOPL2 -> VURDOPL02                           */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      FILE-DECLARE                                                 */
 /*********************************************************************/
 DCL     BH960I          FILE RECORD INPUT;                             
 DCL     BH614O          FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      ENTRY-DECLARE                                                */
 /*********************************************************************/
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM);   /* SSI-DATO     */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS34900         ENTRY OPTIONS (INTER ASM);   /* FAKTURERING  */
 /*********************************************************************/
 /*      STRUCTUR-DECLARE  INPUT                                      */
 /*********************************************************************/
 DCL     BH960_AR CHAR (6000) VAR;                                      
 DCL     1 BH960 BASED(ADDR(BH960_AR)),                                 
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH90000N;;                                          
 /*********************************************************************/
 /*      STRUCTURE-DECLARE  OUTPUT                                    */
 /*********************************************************************/
 DCL     1 BH614,                                                       
           2 DCPRN       DEC FIXED (11),                                
           2 EKOM        DEC FIXED (3),                                 
           2 EJDNR       DEC FIXED (7),                                 
           2 CBENYT      CHAR      (02),                                
           2 DOVTG       DEC FIXED (9),                                 
           2 DAFSTÅ      DEC FIXED (9);                                 
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL     1 LIN1,                                                        
          2 CTL          CHAR      (1),                                 
          2 TX1          CHAR      (47)      INIT                       
                         ('PROGRAM: BH61400L'),                         
          2 TX2          CHAR      (59)      INIT                       
                         ('UDTRÆK FRA EVS SLUT TIL LUP'),               
          2 TX3          CHAR      (14)      INIT                       
                         ('UDSKREVET DEN '),                            
          2 DATO         CHAR      (10),                                
          2 REST         CHAR      (02)      INIT      (' ');           
 DCL     1 LIN2,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (54)      INIT      ('  '),          
          2 TX1          CHAR      (20)      INIT                       
                         ('KØRSELSKONTROLLISTE'),                       
          2 REST         CHAR      (58)      INIT      (' ');           
 DCL     1 LIN3,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (2)       INIT      (' '),           
          2 TEKST        CHAR      (52),                                
          2 ANT          PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZZ',               
          2 REST         CHAR      (59)      INIT      (' ');           
 DCL     1 LIN_BLANK,                                                   
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (132)     INIT      (' ');           
 DCL     LIN_DEB         CHAR      (133);                               
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     TV_LÆS              FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_CPR          FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_SENR         FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_ØVR          FIXED     (9)       INIT      (0);         
 DCL     TV_KOM              FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_CPR         FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_SENR        FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_ØVR         FIXED     (9)       INIT      (0);         
 DCL     TV_SKRIV            FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_ØVR        FIXED     (15)  INIT (0);    /*SLUT05*/    
 DCL     TV_SKRIV_SENR       FIXED     (15)  INIT (0);    /*SLUT05*/    
 DCL     TV_SKRIV_CPR        FIXED     (15)  INIT (0);    /*SLUT05*/    
 /*********************************************************************/
 /*      DECLARE AF BUILTIN                                           */
 /*********************************************************************/
 DCL     LOW             BUILTIN;                                       
 DCL     HIGH            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2                              BASED(ADDR(DATO1)),       
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     HJ_CBENYT       CHAR      (2);                                 
                                                                        
 DCL     EOF             PIC       '9'       INIT (0);                  
 /*********************************************************************/
 /*      HOUSE-KEEPING                                                */
 /*********************************************************************/
         ON ERROR CALL PLIDUMP('SNHOB');                                
         CALL ZS61900;                 /*   UDSKRIVNING AF SSI-DATO   */
         LIN_BLANK.CTL = ZZIK1;                                         
         LIN1.CTL = ZZWS2;                                              
         LIN2.CTL = ZZWS3;                                              
         LIN3.CTL = ZZWS2;                                              
         OPEN FILE (BH960I) TITLE ('BH96000I');                         
         OPEN FILE (BH614O) TITLE ('BH61400O');                         
         CALL ZZ20390 ('*OPEN','BH61401Y');                             
         ON ENDFILE (BH960I)                                            
         BEGIN;                                                         
            EOF = 1;                                                    
         END;                                                           
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         LIN1.DATO = UDDATO;                                            
         BH614  = '';                                                   
 /*********************************************************************/
 /*      PROCES                                                       */
 /*********************************************************************/
         READ FILE (BH960I) INTO (BH960_AR);                            
         DO WHILE (EOF = 0);                                            
            TV_LÆS = TV_LÆS + 1;                                        
            IF BH960.SORT_CPR >= 9999999999         /* OPTÆLL.INDV. */  
            THEN                                                        
               TV_KOM = TV_KOM + 1;                                     
            ELSE                                                        
               CALL DAN_LUP_UDTRÆK;                                     
            READ FILE (BH960I) INTO (BH960_AR);                         
         END;                                                           
                                                                        
         CALL SLUT;                                                     
         CALL ZZ20300(LIN_BLANK,'99');                                  
         CLOSE FILE (BH960I),                                           
               FILE (BH614O);                                           
 /*********************************************************************/
 /*    UNDERSØG OM DER SKAL DANNES UDTRÆK                             */
 /*********************************************************************/
 DAN_LUP_UDTRÆK:PROC;                                                   
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
         THEN                                                           
            TV_LÆS_CPR = TV_LÆS_CPR + 1;                                
         ELSE                                                           
         IF BH960.SORT_CPR > 999999                                     
         THEN                                                           
            TV_LÆS_SENR = TV_LÆS_SENR + 1;                              
         ELSE                                                           
            TV_LÆS_ØVR = TV_LÆS_ØVR + 1;                                
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
          & BH960.TIL_EVS.DOVTG >            /* KØB EFTER 31.12.XXXX */ 
            BH960.MT.DRGNÅRTIL                                          
         THEN                                                           
            DO;                                                         
               TV_FRAS_CPR = TV_FRAS_CPR + 1;                           
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.SORT_CPR < 100000000                                  
          & BH960.AKT_EJER.DOVTG >          /* KØB EFTER 31.12.XXXX */  
            BH960.MT.DRGNÅRTIL                                          
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF (BH960.AKT_EJER.CEJFORH = '50'  !  /* HVIS  EJEREN ER   */  
             BH960.AKT_EJER.CEJFORH = '60'  !  /* OFFENTLIG D.V.S.  */  
             BH960.AKT_EJER.CEJFORH = '70'  !  /* KOMM./AMT/STAT    */  
             BH960.AKT_EJER.CEJFORH = '80') &                           
             BH960.CINVPNR > '0'               /* IKKE VALIDT CPRNR */  
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.CINVPNR = '0' !              /* HVIS VALIDT CPRNR */  
            BH960.CINVPNR = '2'                /* HVIS SE-NR        */  
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.FEJLNR = 5060                                         
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.SORT_KOMNR = 999                                      
          ! BH960.AKT_EJER.DCPRCIR = 0                                  
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         CALL SKRIV_LUP_UDTRÆK;                                         
                                                                        
 END DAN_LUP_UDTRÆK;                                                    
 /*********************************************************************/
 /*    SKRIV LUP-UDTRÆK                                               */
 /*********************************************************************/
 SKRIV_LUP_UDTRÆK:                                                      
         PROC;                                                          
                                                                        
         BH614 = '';                                                    
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
         THEN                                                           
            TV_SKRIV_CPR = TV_SKRIV_CPR + 1;                            
         ELSE                                                           
         IF BH960.SORT_CPR > 999999                                     
         THEN                                                           
            TV_SKRIV_SENR = TV_SKRIV_SENR + 1;                          
         ELSE                                                           
            TV_SKRIV_ØVR = TV_SKRIV_ØVR + 1;                            
                                                                        
         BH614.DCPRN  = BH960.SORT_CPR;                                 
         BH614.EKOM   = BH960.SORT_KOMNR;                               
         BH614.EJDNR  = BH960.SORT_EJDNR;                               
         BH614.DOVTG  = BH960.AKT_EJER.DOVTG;                           
         BH614.DAFSTÅ = BH960.AKT_EJER.DAFSTÅ;                          
                                                                        
         HJ_CBENYT = BH960.ESR.VURDOPLNY.ÅRSVURD.CBENYT;    /*SLUT05*/  
         IF HJ_CBENYT < '01'                                            
         THEN                                                           
            DO;                                                         
  /*SLUT05*/  HJ_CBENYT = BH960.ESR.VURDOPLGL.ÅRSVURD.CBENYT;           
              IF HJ_CBENYT < '01'                                       
              THEN                                                      
    /*SLUT05*/   HJ_CBENYT = BH960.ESR.VURDOPL02.ÅRSVURD.CBENYT;        
            END;                                                        
                                                                        
         BH614.CBENYT = HJ_CBENYT;                                      
                                                                        
         WRITE FILE (BH614O) FROM (BH614);                              
         TV_SKRIV = TV_SKRIV + 1;                                       
                                                                        
 END SKRIV_LUP_UDTRÆK;                                                  
 /*********************************************************************/
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*********************************************************************/
 SLUT:                                                                  
         PROC;                                                          
         CALL ZZ20300(LIN_BLANK,'01');                                  
         CALL ZZ20300(LIN1,'01');                                       
         CALL ZZ20300(LIN2,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST ='ANTAL LÆSTE INDIVIDER PÅ BH96000D BEREGNINGSBÅND';
         LIN3.ANT = TV_LÆS;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = '     HERAF FRASORTEREDE OPTÆLL.INDV.  ';         
         LIN3.ANT = TV_KOM;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE PERSONER PÅ BH9600D ';              
         LIN3.ANT = TV_LÆS_CPR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE SELSKABER PÅ BH9600D ';             
         LIN3.ANT = TV_LÆS_SENR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE ØVRIGE PÅ BH9600D ';                
         LIN3.ANT = TV_LÆS_ØVR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE PERSONER';                   
         LIN3.ANT = TV_FRAS_CPR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE SELSKABER';                  
         LIN3.ANT = TV_FRAS_SENR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE ØVRIGE';                     
         LIN3.ANT = TV_FRAS_ØVR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ BH61400D';            
         LIN3.ANT = TV_SKRIV;                                           
         CALL ZZ20300(LIN3,'01');                                       
 END SLUT;                                                              
 END BH61400;                                                           
