 BH90800: /* SUPPL EJERSKABER JUSTERES */                               
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2020');       
 /**********************************************************************
 *                                                                     *
 * FORMÅL :  AT FJERNE EKSTRA PERIODER FRA EJERSKABER I SUPLEMENT      *
 *           MED REGLER SOM I BH90000L og BH90800L                     *
 *                                                                     *
 *           1) ER EJERSKABET MED I  REGNSKABSÅRETS PERIODE             
 *           2) SKAL DER SLÅS PERIODER SAMMEN                           
 *                                                                     *
 * SLUT20      EBD/PBR                           NOV 2020              *
 **********************************************************************/
         DFT RANGE (*) STATIC;                                          
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH9000I FILE RECORD INPUT;     /* EJERFORHOLD               */ 
 DCL     BH9080O FILE RECORD OUTPUT;    /* EJERFORHOLD               */ 
 /********************************************************************* 
 *       DECLARE AF BEREGNINGSBÅND                                    * 
 *********************************************************************/ 
 DCL     BH900        CHAR      (6000) VAR;                             
 DCL     BH908        CHAR      (6000) VAR;                             
                                                                        
 DCL     1 BH900I        BASED     (ADDR(BH900)),                       
           2 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90000N;;                                          
                                                                        
 DCL     1 BH908O        BASED     (ADDR(BH908)),                       
           2 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90000N;;                                          
                                                                        
                                                                        
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    LIN_DEB          CHAR      (133);                               
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH90800L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (                
           'SUPPL EJERSKABER JUSTERES                 '),               
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     INDEX           BIN FIXED (31);                                
 DCL     EOF_BH900       BIT (1) INIT ('0'B);                           
 DCL     BH908_SKREVET   BIT (1) INIT ('0'B);                           
 DCL     FEJLTEKST       CHAR (20)        INIT(' ');                    
 DCL     ABENDMEDD       CHAR (56)        INIT(' ');                    
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     ANTAL_BH900     BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_SKRIV     BIN FIXED (31) INIT(0);                        
 DCL     SALG_FØR        BIN FIXED (31) INIT(0);                        
 DCL     KØB_EFTER       BIN FIXED (31) INIT(0);                        
 DCL     ANTAL_SAMME     BIN FIXED (31) INIT(0);                        
 DCL     GEM_DOVTG_GL    FIXED     (9)       INIT (0);                  
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
                                                                        
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH87100L');             
            END;                                                        
                                                                        
         ON ENDFILE (BH9000I) EOF_BH900 = '1'B;                         
                                                                        
         OPEN FILE (BH9000I) TITLE ('BH90701I');                        
         OPEN FILE (BH9080O) TITLE ('BH90800O');                        
                                                                        
         CALL ZZ20390 ('*OPEN','BH90801Y');                             
         DATO1 = '';                                                    
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         DETLIN1 = '';                                                  
         BH908O = '';                                                   
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         READ FILE(BH9000I) INTO (BH900);                               
                                                                        
         DO WHILE (^EOF_BH900);                                         
            ANTAL_BH900 = ANTAL_BH900 +1;                               
            CALL BEHANDL;                                               
            READ FILE(BH9000I) INTO (BH900);                            
         END;  /*DO WHILE*/                                             
                                                                        
         IF ^BH908_SKREVET                                              
         then                                                           
            call SKRIV;                                                 
                                                                        
         CALL LUK_PROG;                                                 
 /********************************************************************/ 
 /*********************************************************************/
 /*      BEHANDLING AF INPUT                                          */
 /*********************************************************************/
 BEHANDL: PROC;                                                         
                                                                        
         /* Regel 1 fra BH32200 */                                      
         IF BH900I.AKT_EJER.DAFSTÅ > 0 &                                
            BH900I.AKT_EJER.DAFSTÅ < BH900I.AKT_EJER.DRGNÅRFRA          
         THEN                                                           
            DO;                                                         
               SALG_FØR = SALG_FØR + 1;                                 
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH900I.AKT_EJER.DOVTG > BH900I.AKT_EJER.DRGNÅRTIL           
         THEN                                                           
            DO;                                                         
               KØB_EFTER = KØB_EFTER + 1;                               
               RETURN;                                                  
            END;                                                        
                                                                        
         /* Regel 2 fra BH32400 */                                      
         SELECT;                                                        
                                                                        
            WHEN (BH908O.SORT_CPR ^= BH900I.SORT_CPR)                   
               DO;                                                      
                  IF BH908O.SORT_CPR ^= 0   /* IKKE FØRSTE GANG */      
                  THEN                                                  
                     CALL SKRIV;                                        
                                                                        
               /*   GEM_DOVTG_GL = 0; */                                
                                                                        
               /*   IF BH900I.SORT_CITYP = '1' */ /* AKTUEL EJER */     
               /*   THEN                                                
                     GEM_DOVTG_GL = BH900I.EJER.DOVTG; */               
                                                                        
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
            WHEN (BH908O.SORT_CPR        = BH900I.SORT_CPR &            
                 (BH908O.SORT_KOMNR     ^= BH900I.SORT_KOMNR !          
                  BH908O.SORT_EJDNR     ^= BH900I.SORT_EJDNR !          
                  BH908O.SORT_ENHEDLBNR ^= BH900I.SORT_ENHEDLBNR))      
               DO;                                                      
                  /* SAMME PERSON MEN NY EJENDOM/ENHED 2FAMILIE BOLIG */
                  CALL SKRIV;                                           
                                                                        
               /*   GEM_DOVTG_GL = 0; */                                
                                                                        
               /*   IF BH900I.SORT_CITYP = '1' */ /* AKTUEL EJER */     
               /*   THEN                                                
                     GEM_DOVTG_GL = BH900I.ESR.EJER.DOVTG; */           
                                                                        
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
            WHEN (BH908O.SORT_CPR       = BH900I.SORT_CPR &             
                  BH908O.SORT_KOMNR     = BH900I.SORT_KOMNR  &          
                  BH908O.SORT_EJDNR     = BH900I.SORT_EJDNR  &          
                  BH908O.SORT_ENHEDLBNR = BH900I.SORT_ENHEDLBNR &       
                  BH908O.SORT_DATO     ^= BH900I.SORT_DATO)             
              DO;                                                       
                 /* SAMME PERSON OG SAMME EJENDOM */                    
                 /* MEN NY EJERPERIODE            */                    
              /*  IF BH900I.ESR.CCIVS  ^= 'D' &                         
                   BH908O.ESR.CCIVS  ^= 'D' &                           
                   BH908O.ESR.EJER.DCPRCIR > 0                          
                THEN                                                    
                  DO;                                                   
                 IF BH908O.ESR.EJER.DCPRCIR  =  BH900I.ESR.EJER.DCPRCIR 
                    THEN                                                
                      DO;                                               
                        BH900I.ESR.EJER.CTILKØB = 'J';                  
                                                                        
                        IF BH908O.ESR.EJER.DOVTG  =  GEM_DOVTG_GL       
                        THEN                                            
                           BH908O.ESR.EJER.CTILKØB = 'J';               
                        IF GEM_DOVTG_GL  >=  BH908O.ESR.EJER.DRGNÅRFRA  
                        THEN                                            
                           BH908O.ESR.EJER.CTILKØB = ' ';               
                      END;                                              
                                                                        
                    CALL SKRIV;                                         
                 END; */                                                
                                                                        
                CALL SKRIV;                                             
                                                                        
                CALL FLYT_DATA;                                         
              END;                                                      
                                                                        
            OTHERWISE                                                   
               DO;                                                      
                  /* SAMME PERSON OG SAMME EJENDOM */                   
                  /* OG SAMME EJERPERIODE          */                   
                  ANTAL_SAMME = ANTAL_SAMME + 1;                        
                  CALL FLYT_DATA;                                       
               END;                                                     
                                                                        
         END; /* SELECT */                                              
                                                                        
 END BEHANDL;                                                           
 FLYT_DATA: PROC;                                                       
                                                                        
    /*FLYT DATA TIL BH908O */                                           
                                                                        
    BH908O = BH900I, BY NAME;                                           
    BH908_SKREVET = '0'B;                                               
                                                                        
 END FLYT_DATA;                                                         
                                                                        
 SKRIV: PROC;                                                           
                                                                        
    /*SKRIV DATA FRA BH908O */                                          
                                                                        
    WRITE FILE (BH9080O) FROM (BH908);                                  
    BH908_SKREVET = '1'B;                                               
    ANTAL_SKRIV = ANTAL_SKRIV + 1;                                      
                                                                        
 END SKRIV;                                                             
                                                                        
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:PROC;                                                       
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_BH900;                                   
         DETLIN1.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H90701D........:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = SALG_FØR;                                      
         DETLIN1.TEKST = ' HERAF OVERLÆST SALG FØR REGNSKABSÅR......:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = KØB_EFTER;                                     
         DETLIN1.TEKST = ' HERAF OVERLÆST KØB EFTER REGNSKABSÅR.....:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_SAMME;                                   
         DETLIN1.TEKST = ' SAMME PERSON, EJENDOM OG EJERPERIODE.....:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = ANTAL_SKRIV;                                   
         DETLIN1.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H90800D......:'; 
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
                                                                        
  END TOTALLISTE;                                                       
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 LUK_PROG: PROC;                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH9000I),                                          
               FILE (BH9080O);                                          
 END LUK_PROG;                                                          
 END  BH90800;                                                          
