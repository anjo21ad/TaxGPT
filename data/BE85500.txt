 PROCESS OPT(TIME);                                                     
 BE85500: PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                  
 /*********************************************************************/
 DCL COPYRIGHT   CHAR      (30) STATIC  INIT ('COPYRIGHT KMD A/S 2022');
 /**********************************************************************
 *                                                                     *
 * FORMÅL    : BEREGNING AF EVS TIL FORSKUD                            *
 *               BEREGN FAKTISK RABAT                                  *
 *                                                                     *
 * INPUT     : BE75000T - ESR (FORSKUD)                                *
 *             BE76000T - MANDTAL (FORSKUD)                            *
 *             BE77000T - EVS_UDTRÆK (FORSKUD)                         *
 *             BE78000T - EVS_RESULTAT (FORSKUD NY)                    *
 *                                                                     *
 * OUTPUT    : BEREGNET RABAT OG EVS TIL BE78000T                      *
 *             BH85501Y - KØRSELSKONTROLLISTE                          *
 *                                                                     *
 *             Danner grundlag (på BE78000T) for load af:              *
 *             BS01500T - EVS EJENDOM                                  *
 *             BS01600T - EVS EJERRELATION                             *
 *             BS01700T - EVS EJERFORHOLD                              *
 *             BS01900T - RABATBERETTIGET EJERANDEL                    *
 *                                                                     *
 *             BS01800T - BASISRABAT (2024)  (Ikke load men opdatering)*
 *                                                                     *
 * PROGRAMMØR: Tina Carstens (TIC)   Marts 2022  BF17 Forskud 2024     *
 *                                                                     *
 * Forskud 2025: Tina Carstens (TIC) Maj   2023  BF17 Forskud 2025     *
 * Forskud 2025: Jan Jensen JJJ BE55600M erstatter BE55300M 20.06.24   *
 * Forskud 2025: Per Jensen PEN Defect #2502                24.07.24   *
 *               BN58050 output BFAKTISKRABAT bliver nu ikke benyttet  *
 *               ved CEJBEVS = 'A' (EVS til FORSKUD)                   *
 *                                                                     *
 ***********************************************************************
 *                                                                     *
 *                                                                     *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     SYSPRINT   EXTERNAL FILE PRINT;                                
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE BE75000T;  /* ESR (FORSKUD)                 */
         EXEC SQL INCLUDE BE76000T;  /* MANDTAL (FORSKUD)             */
         EXEC SQL INCLUDE BE77000T;  /* EVS_UDTRÆK (FORSKUD)          */
         EXEC SQL INCLUDE BE78000T;  /* EVS RESULTAT (FORSKUD) NY     */
                                                                        
         /* Indikatorvariable BE780_SW til BE78000T                   */
         %INCLUDE BE85501N;                                             
                                                                        
 DCL     ANT_GEM_BE78000T    BIN FIXED (31);                            
 DCL     1 GEM_BE78000T(15),                                            
         %INCLUDE BE7800TN;                                             
 DCL     1 GEM_BE780_SW(15),                                            
         %INCLUDE BE7800IT;                                             
                                                                        
 DCL     1 GEM_NY_BE78000T,                                             
         %INCLUDE BE7800TN;                                             
 DCL     1 GEM_NY_BE780_SW,                                             
         %INCLUDE BE7800IT;                                             
                                                                        
 DCL     RESULT_FUNDET       BIT       (1)     INIT ('0'B);             
 DCL     ESR_FUNDET          BIT       (1)     INIT ('0'B);             
 DCL     MT_FUNDET           BIT       (1)     INIT ('0'B);             
 DCL     SLUT_FUNDET         BIT       (1)     INIT ('0'B);             
 DCL     AFVIST_FEJL_I_INP   BIT       (1)     INIT ('0'B);             
 DCL     NY_EJERRELATION     BIT       (1)     INIT ('0'B);             
                                                                        
                                                                        
 DCL     AFVIST_FEJL_INP     BIN FIXED (31);                            
 DCL     AFVIST_PGA_EJ_EJET  BIN FIXED (31);                            
 DCL     AFVIST_PGA_FEJL     BIN FIXED (31);                            
 DCL     AFVIST_PGA_RES0     BIN FIXED (31);                            
 DCL     AFVIST_PGA_BERKODE  BIN FIXED (31);                            
 DCL     ANT_BS01900T_0      BIN FIXED (31);                            
                                                                        
      /* EXEC SQL INCLUDE BE7800HT;  /* EVS_RESULTAT (FORSKUD) NY HIST*/
      /* EXEC SQL INCLUDE BQ7800HT;  /* EVS_RESULTAT (FORSKUD) GL HIST*/
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ØVRIGE FELTER                                     */
 /*********************************************************************/
                                                                        
 DCL     WS_FORSKUD_SLUT_TXT            CHAR(16) INIT (' ');            
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
 DCL     WS_PROGRAMNAVN                 CHAR(8)  INIT ('BE85500');      
 /********************************************************************* 
 *       DECLARE AF DIV. FÆLLESMACROER                                * 
 *********************************************************************/ 
 DCL     1 BE55600M,                                                    
           %INCLUDE BE55600M;                                           
                                                                        
 DCL     1 BE60101M      ALIGNED,                                       
           %INCLUDE BE60101M;                                           
                                                                        
 /********************************************************************* 
 *       DECLARE AF POINTER TIL BN58050                               * 
 *********************************************************************/ 
                                                                        
         /* Beregning af rabat                                       */ 
 DCL     BN58050_PTR          PTR;                                      
 DCL     1 BN58050P,                                                    
         %INCLUDE BN58050N;                                             
                                                                        
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     MULTIPLY        BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     DIVIDE          BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL    (STG,CSTG)       BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     BN58050         ENTRY;               /* Beregn Rabat        */ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*     DECLARE AF DB2-variabler                                      */
 /*********************************************************************/
  EXEC SQL INCLUDE SQLCA;                                               
 DCL    SQLKODE         FIXED     (5);                                  
 DCL    SQLCODE_CH      CHAR      (5);                                  
 DCL    SQLCODE_PIC     PIC       'SSSS9' DEF SQLCODE_CH;               
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BE85500'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BE85500L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (' '),           
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN0,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (41),                                
           2 CPUÅR       PIC       '99',                                
           2 TEKST2      CHAR      (11),                                
           2 START       PIC       '999999',                            
           2 TEKST3      CHAR      (9),                                 
           2 SLUT        PIC       '999999',                            
           2 TEKST4      CHAR      (57);                                
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
 DCL     1 DETLIN2,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (33),                                
           2 RCKOD       CHAR      (01)      INIT      (' '),           
           2 F1          CHAR      (19)      INIT      (' '),           
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (68)      INIT      (' ');           
 DCL     1 DETLIN3,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F2          CHAR      (10)      INIT      (' '),           
           2 TEKST2      CHAR      (07)      INIT      ('BELØB: '),     
           2 BELØB       PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZ9',               
           2 F3          CHAR      (25)      INIT      (' ');           
         %INCLUDE ZZ20301M;                                             
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
 EXTERNAL INIT ('NOTEST(ALL,,;)');                                      
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     TEKST45         CHAR      (45);                                
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
 DCL     HJ_CHAR_4_PIC   PIC       '9999';                              
 DCL     HJ_CHAR_4       CHAR      (4) DEF HJ_CHAR_4_PIC;               
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     HJ_DATO         PIC       '(8)9';                              
 DCL     1 HJ_DATOP      DEF       HJ_DATO,                             
          2 ÅR           PIC       '(4)9',                              
          2 MDDG         PIC       '(4)9';                              
                                                                        
 DCL     AKTUEL_EVS_AAR         BIN FIXED (15);                         
 DCL     AKTUEL_FSK_SLUT_TXT    CHAR      (16);                         
 DCL     AKTUEL_DATA_ID         BIN FIXED (15);                         
 DCL     AKTUEL_PERSONNUMMER    DEC FIXED (11);                         
 DCL     AKTUEL_EKOMNR          BIN FIXED (15);                         
 DCL     AKTUEL_EEJDNR          BIN FIXED (31);                         
 DCL     AKTUEL_EENHEDLBNR      BIN FIXED (15);                         
                                                                        
 DCL     GEM_PERSONNUMMER       DEC FIXED (11);                         
 DCL     GEM_EEJDNR             BIN FIXED (31);                         
 /********************************************************************* 
 *       DECLARE AF HJÆLPEFELTER TIL MASKINEL AFSTEMNING              * 
 *********************************************************************/ 
 DCL     (I,J,K,N)        BIN FIXED (31)      INIT      (0);            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                PUT SKIP LIST ('FEJL PERSON: ',DCLBE78000T.DCPRN);      
                CALL PLIDUMP  ('SNHOB','PROGRAM BE85500L');             
            END;                                                        
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         FETCH BN58050;                         /* Rabatberegning     */
                                                                        
         CALL TERMINER_LISTE(' ');                                      
         CALL TERMINER_LISTE                                            
                      ('***           SKAT EVS FORSKUD           ***'); 
         CALL TERMINER_LISTE                                            
                      ('***        BE85500 - Beregn Rabat        ***'); 
         CALL TERMINER_LISTE('***         '!!                           
                              BE55600M.AFVIKLINGS_ID!!' '!!             
                              BE55600M.INDKÅR2!!'         ***');        
         CALL TERMINER_LISTE(' ');                                      
                                                                        
         CALL ZZ20390 ('*OPEN','BE85501Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         OVERLIN1.TEKST2 =                                              
                         'BEREGNING AF FAKTISK RABAT TIL ' !!           
                         'FORSKUD ' !! BE55600M.INDKÅR2; /*#01*/        
                                                                        
         /* Erklæring og initiering af tællere                        */
         %INCLUDE BE85102M;                                             
                                                                        
         AFVIST_FEJL_INP      = 0;                                      
         AFVIST_PGA_EJ_EJET   = 0;                                      
         AFVIST_PGA_FEJL      = 0;                                      
         AFVIST_PGA_RES0      = 0;                                      
         AFVIST_PGA_BERKODE   = 0;                                      
         ANT_BS01900T_0       = 0;                                      
                                                                        
         AKTUEL_EVS_AAR       = 0;                                      
         AKTUEL_FSK_SLUT_TXT  = '';                                     
         AKTUEL_DATA_ID       = 0;                                      
         AKTUEL_PERSONNUMMER  = 0;                                      
         AKTUEL_EKOMNR        = 0;                                      
         AKTUEL_EEJDNR        = 0;                                      
         AKTUEL_EENHEDLBNR    = 0;                                      
                                                                        
         GEM_NY_BE78000T      = '';                                     
         GEM_NY_BE780_SW      = '';                                     
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         SELECT (BE55600M.AFVIKLINGS_ID);                               
            WHEN ('OPRET_EVS_FORSK')                                    
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT  = BE55600M.AFVIKLINGS_ID;        
                  WS_TIL_EVS_AAR       = BE55600M.FORSKÅR;              
                                                                        
                  PUT SKIP EDIT('Dette er en EVS FORSKUD kørsel ',      
                                 WS_TIL_EVS_AAR) (A,P'9999');           
               END;                                                     
            OTHERWISE                                                   
               DO;                                                      
                  PUT STRING(ABENDTXT)                                  
                        EDIT('101 fejl ved AFVIKLINGS_ID ',             
                             BE55600M.AFVIKLINGS_ID) (A,A);             
                  CALL PROGRAM_FEJL;                                    
               END;                                                     
         END; /* select */                                              
                                                                        
         /*************************************************************/
         /*  NU STARTER NORMALT FLOW                                  */
         /*************************************************************/
                                                                        
         /* Fra BE78000T opsamles alle Ejerforhold for en Ejerrelation*/
         /* før der beregnes. Dvs. ved skift af Ejerrelation beregnes */
         /* det tidligere, og opsamling af oplysninger for ny         */
         /* Ejerrelation startes.                                     */
                                                                        
         CALL CLOSE_RESULTCURS; /* BE78 */                              
         CALL OPEN_RESULTCURS;                                          
         CALL FETCH_RESULTCURS;                                         
                                                                        
         DO WHILE (RESULT_FUNDET);                                      
                                                                        
            CALL SELECT_ESR;      /* BE75 */                            
            CALL SELECT_MT;       /* BE76 */                            
            CALL SELECT_SLUT;     /* BE77 */                            
                                                                        
            CALL BEHANDL;                                               
                                                                        
            IF AFVIST_FEJL_I_INP = JA                                   
            THEN DO;                                                    
               AFVIST_FEJL_INP = AFVIST_FEJL_INP + 1;                   
            /* CALL TEST_UDSKRIV('BEREGNEJ'); */                        
               END;                                                     
                                                                        
            CALL FETCH_RESULTCURS;                                      
         END;                                                           
                                                                        
         /* Beregn og gem oplysninger for den sidste person           */
         IF AKTUEL_PERSONNUMMER ^= 0                                    
         THEN                                                           
            CALL BEREGN_RABAT;                                          
                                                                        
         IF TV.FETCH_RESULT > 0                                         
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
            Put skip list ('Commit efter ' !! TV.FETCH_RESULT !!        
                          ' læsninger.  Ialt ' !! TV.ANT_UPD !!         
                          ' opdateringer på RESULTAT BE78000T');        
            END;                                                        
                                                                        
         CALL CLOSE_RESULTCURS;                                         
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       PERSONER SOM EVT. SKAL BEREGNES                              * 
 *********************************************************************/ 
 BEHANDL: PROC;                                                         
                                                                        
         AFVIST_FEJL_I_INP = JA;                                        
                                                                        
         /* Resultat frasorteres ift. forskellige kriterier.          */
         /* Der foretages visse optællinger.                          */
         %INCLUDE BE85103M;                                             
                                                                        
         AFVIST_FEJL_I_INP = NEJ;                                       
                                                                        
         IF AKTUEL_PERSONNUMMER ^= DCLBE78000T.DCPRN                    
          ! AKTUEL_EKOMNR       ^= DCLBE78000T.EKOMNR                   
          ! AKTUEL_EEJDNR       ^= DCLBE78000T.EEJDNR                   
          ! AKTUEL_EENHEDLBNR   ^= DCLBE78000T.EENHEDLBNR               
         THEN DO;                                                       
            /* Start på ny Ejerrelation                               */
            GEM_PERSONNUMMER = AKTUEL_PERSONNUMMER;                     
            GEM_EEJDNR       = AKTUEL_EEJDNR;                           
                                                                        
            /* Man bruger DCLBE78000T ifb. med Beregn/Gem af forrige  */
            /* person, så den indlæste gemmes                         */
            GEM_NY_BE78000T  = DCLBE78000T, BY NAME;                    
            GEM_NY_BE780_SW  = BE780_SW, BY NAME;                       
                                                                        
            /* Ved Skift af ejerrelation kan man afslutte den forrige,*/
            /* dvs. beregne EVS RABAT og gemme EVS Resultat           */
            IF AKTUEL_PERSONNUMMER ^= 0                                 
            THEN                                                        
               CALL BEREGN_RABAT;                                       
                                                                        
            /* Check om der skal committes                            */
            IF TV.ANT_COMM > 9999                                       
            THEN DO;                                                    
               EXEC SQL COMMIT;                                         
                                                                        
               Put skip list ('Commit efter ' !! TV.FETCH_RESULT !!     
                           ' læsninger på Beregningsbånd  Ialt ' !!     
                           TV.ANT_UPD !!                                
                           ' opdateringer');                            
               TV.ANT_COMM = 0;                                         
               END;                                                     
                                                                        
         /* CALL TEST_UDSKRIV('PERSONST'); */                           
                                                                        
            DCLBE78000T          = '';                                  
            BE780_SW             = '';                                  
                                                                        
            DCLBE78000T          = GEM_NY_BE78000T, BY NAME;            
            BE780_SW             = GEM_NY_BE780_SW, BY NAME;            
                                                                        
            NY_EJERRELATION      = JA;                                  
                                                                        
            AKTUEL_EVS_AAR       = DCLBE78000T.TIL_EVS_AAR;             
            AKTUEL_FSK_SLUT_TXT  = DCLBE78000T.AFVIKLINGS_ID;           
            AKTUEL_DATA_ID       = DCLBE78000T.DATA_ID;                 
            AKTUEL_PERSONNUMMER  = DCLBE78000T.DCPRN;                   
            AKTUEL_EKOMNR        = DCLBE78000T.EKOMNR;                  
            AKTUEL_EEJDNR        = DCLBE78000T.EEJDNR;                  
            AKTUEL_EENHEDLBNR    = DCLBE78000T.EENHEDLBNR;              
                                                                        
            IF DCLBE78000T.OVERFØRT_ÆGTEF_RABAT = '1'                   
            THEN                                                        
               TV.RABAT_OVERFØRT = TV.RABAT_OVERFØRT + 1;               
            END;                                                        
         ELSE                                                           
            NY_EJERRELATION  = NEJ;                                     
                                                                        
         /* Udfyld beregningsoplysninger                              */
         CALL UDFYLD_BEREGNINGSOPLYSNINGER;                             
                                                                        
 END BEHANDL;                                                           
 /********************************************************************* 
 *       UDFYLD BEREGNINGSOPLYSNINGER                                 * 
 *********************************************************************/ 
 UDFYLD_BEREGNINGSOPLYSNINGER:     PROC;                                
                                                                        
         /* Ejerrelationen: Indkomstår, Personnr., kommunennr.,       */
         /*                 Enhedsløbenummer, Ejendomsnr.             */
         /* Ejerforhold   : Indkomstår, Personnr., kommunennr.,       */
         /*                 Enhedsløbenummer, Ejendomsnr. og          */
         /*                 Periodeløbenummer                         */
                                                                        
         /* For at beregne EVS Rabat, skal man bruge alle             */
         /* Ejerforhold for en Ejerrelation                           */
                                                                        
         IF NY_EJERRELATION = JA                                        
         THEN DO;                                                       
            BN58050P                      = '';                         
            BN58050P.Tech.ParmLængde      =  CSTG(BN58050P);            
            BN58050P.TIL_EVS_AAR          = WS_TIL_EVS_AAR;             
            BN58050P.ANT_EJERFH           = 0;                          
                                                                        
            ANT_GEM_BE78000T              = 0;                          
            GEM_BE78000T(*)               = '';                         
            GEM_BE780_SW(*)               = '';                         
            END;                                                        
                                                                        
         IF BN58050P.ANT_EJERFH < 15                                    
         THEN DO;                                                       
            /* Gem nyt Ejerforhold                                    */
            BN58050P.ANT_EJERFH    = BN58050P.ANT_EJERFH + 1;           
            J                      = BN58050P.ANT_EJERFH;               
                                                                        
            ANT_GEM_BE78000T       = ANT_GEM_BE78000T + 1;              
            GEM_BE78000T(J)        = DCLBE78000T, BY NAME;              
            GEM_BE780_SW(J)        = BE780_SW, BY NAME;                 
                                                                        
            IF  BE780_SW.RABATEJERANDEL_SW = -1                         
             ! (BE780_SW.RABATEJERANDEL_SW = 0                          
             &  DCLBE78000T.RABATEJERANDEL = 0)                         
            THEN DO;                                                    
               BN58050P.GRABATEJERANDEL_ST = 0;                         
                                                                        
               /* Burde have overført Rabatberettiget Ejerandel       */
               IF DCLBE78000T.EJET20240101 = '1'                        
               THEN                                                     
                  ANT_BS01900T_0 = ANT_BS01900T_0 + 1;                  
               END;                                                     
            ELSE                                                        
               BN58050P.GRABATEJERANDEL_ST =                            
                                    DCLBE78000T.RABATEJERANDEL/10000;   
                                                                        
            BN58050P.EVS_EJERFH(J).EVS.RABATBERETTIGET =                
                                    DCLBE78000T.EJET20240101;           
            BN58050P.EVS_EJERFH(J).EVS.FEJLNR          =                
                                    DCLBE78000T.FEJLNR;                 
            BN58050P.EVS_EJERFH(J).EVS.DOVTG           =                
                                    DCLBE78000T.TILEVS_DOVTG;           
            BN58050P.EVS_EJERFH(J).EVS.DAFSTAA         =                
                                    DCLBE78000T.TILEVS_DAFSTAA;         
            BN58050P.EVS_EJERFH(J).EVS.CEJBEVS         =                
                                    DCLBE78000T.TILEVS_CEJBEVS;         
                                                                        
            /*              DEC (5,4)              DEC(5,2)           */
            BN58050P.EVS_EJERFH(J).EVS.GPCTEJER        =                
                                    DCLBE78000T.TILEVS_GPCTEJER / 100;  
            BN58050P.EVS_EJERFH(J).EVS.GPCTEJER_SW     =                
                                    DCLBE78000T.TILEVS_GPCTEJER_SW;     
                                                                        
            IF DCLBE78000T.OVERFØRT_ÆGTEF_RABAT = '1'                   
            THEN DO;                                                    
               /*              DEC (5,4)              DEC(5,0)        */
               BN58050P.EVS_EJERFH(J).EVS.GPCTRABAT       =             
                                  DCLBE78000T.RES_RABATEJERANDEL/10000; 
               BN58050P.EVS_EJERFH(J).EVS.GPCTRABAT_SW    =             
                                  BE780_SW.RES_RABATEJERANDEL_SW;       
               BN58050P.EVS_EJERFH(J).EVS.COPR_GPCTRABAT  =             
                                  DCLBE78000T.COPR_RES_RABATEJERANDEL;  
               END;                                                     
                                                                        
            BN58050P.EVS_EJERFH(J).EVS.BEVSFREM        =                
                                  DCLBE78000T.TILEVS_FOR_BEVSFREM;      
            BN58050P.EVS_EJERFH(J).EVS.BEVSFREM_SW     =                
                                  DCLBE78000T.TILEVS_FOR_BEVSFREM_SW;   
            BN58050P.EVS_EJERFH(J).EVS.BNYMAN          =                
                                  DCLBE78000T.TILEVS_BNYMAN;            
            BN58050P.EVS_EJERFH(J).EVS.BNYMAN_SW       =                
                                  DCLBE78000T.TILEVS_BNYMAN_SW;         
            BN58050P.EVS_EJERFH(J).EVS.BBRUTTO         =                
                                  DCLBE78000T.FRAEVS_FOR_BBRUTTO;       
            BN58050P.EVS_EJERFH(J).EVS.B100            =                
                                  DCLBE78000T.FRAEVS_FOR_B100;          
            BN58050P.EVS_EJERFH(J).EVS.BEJERAN         =                
                                  DCLBE78000T.FRAEVS_FOR_BEJERAN;       
                                                                        
            /* Basisrabat (fra BS01800T (2024))                       */
            BN58050P.RABAT.BSMPBASRABAT   =                             
                                 DCLBE78000T.R24_SIMPELBASISRABAT;      
            BN58050P.RABAT.BBASISRABAT    =                             
                                 DCLBE78000T.R24_BASISRABAT;            
            BN58050P.RABAT.BBASISRABATREG = 0;                          
         /* BN58050P.RABAT.BBASISRABATREG =                             
                                 DCLBE78000T.GRDL_BASISRABATREGULERING; 
         */                                                             
            /* Initier resultat                                       */
            BN58050P.EVS_EJERFH(J).RESULTAT.BFAKTISKRABAT    = 0;       
            BN58050P.EVS_EJERFH(J).RESULTAT.BFAKTISKRABAT_SW = -1;      
            BN58050P.EVS_EJERFH(J).RESULTAT.BEVSEFTERRABAT   = 0;       
            BN58050P.EVS_EJERFH(J).RESULTAT.GRABATEJERANDEL  = 0;       
                                                                        
            BN58050P.EVS_EJERFH(J).KONTROL.FEJLNR            = 0;       
            BN58050P.EVS_EJERFH(J).KONTROL.AARSAG            = '';      
                                                                        
            BN58050P.SUM.BEVSEJERRELATION = 0;                          
            END;                                                        
         ELSE DO;                                                       
            ABENDMEDD.ABENDTXT =                                        
                               'Mere end 15 Perioder for Ejerrelation'; 
            CALL PROGRAM_FEJL;                                          
            END;                                                        
                                                                        
 END UDFYLD_BEREGNINGSOPLYSNINGER;                                      
 /********************************************************************* 
 *       HER BEREGNES EVS RABAT                                       * 
 *********************************************************************/ 
 BEREGN_RABAT: PROC;                                                    
                                                                        
      /* CALL TEST_UDSKRIV('BEREGNRAB');                                
         CALL TEST_UDSKRIV('BASIS'); */                                 
                                                                        
         /* Initier Rabatberegningsfelter                             */
         CALL INITIER_RABATBEREGNINGSRES;                               
                                                                        
      /* CALL TEST_UDSKRIV('BEREGNIND'); */                             
                                                                        
         /* Beregn Rabat                                              */
         BN58050_PTR = ADDR(BN58050P);                                  
                                                                        
         CALL BN58050(BN58050_PTR);                                     
                                                                        
         If BN58050P.Cretur = 0                                         
         THEN DO;                                                       
         /* CALL TEST_UDSKRIV('BEREGNUD'); */                           
                                                                        
            CALL DAN_OPTÆLLINGER_FRA_BEREGNING;                         
                                                                        
            DO J = 1 TO BN58050P.ANT_EJERFH;                            
                                                                        
               IF  BN58050P.EVS_EJERFH(J).Kontrol.FEJLNR     > 0        
                & (BN58050P.EVS_EJERFH(J).Evs.CEJBEVS      ^= 'A'       
                &  BN58050P.EVS_EJERFH(J).Evs.CEJBEVS      ^= '6')      
               THEN DO;                                                 
                  INDEX = BN58050P.EVS_EJERFH(J).Kontrol.FEJLNR;        
                  SKR_FEJLNR(INDEX).ANTAL = SKR_FEJLNR(INDEX).ANTAL + 1;
                                                                        
                  GEM_BE78000T(J).FEJLNR =                              
                                 BN58050P.EVS_EJERFH(J).Kontrol.FEJLNR; 
                  END;                                                  
               ELSE                                                     
                  IF BN58050P.EVS_EJERFH(J).Evs.FEJLNR > 0              
                  THEN DO;                                              
                     INDEX                   =                          
                                     BN58050P.EVS_EJERFH(J).Evs.FEJLNR; 
                     SKR_FEJLNR(INDEX).ANTAL =                          
                                     SKR_FEJLNR(INDEX).ANTAL + 1;       
                     END;                                               
                  /* Rabat er beregnet - Flyt resultat til BE78000T   */
               IF BN58050P.EVS_EJERFH(J).Resultat.BFAKTISKRABAT_SW = 0  
                     THEN                                               
                     CALL OPDATER_MED_RESULTAT;                         
               ELSE DO;                                                 
                  /* Opdater Resultat efter rabat                  */   
                  GEM_BE78000T(J).RES_EVSEFTERRABAT      =              
                     BN58050P.EVS_EJERFH(J).Resultat.BEVSEFTERRABAT;    
                  GEM_BE780_SW(J).RES_EVSEFTERRABAT_SW   = 0;           
                  GEM_BE78000T(J).RES_BNETTO_EVS_EJERREL =              
                     BN58050P.Sum.BEVSEJERRELATION;                     
                  END;                                                  
                                                                        
                /* Opdater BE78000T                                   */
                DCLBE78000T = '';                                       
                DCLBE78000T = GEM_BE78000T(J), BY NAME;                 
                BE780_SW    = GEM_BE780_SW(J), BY NAME;                 
                                                                        
             /* CALL TEST_UDSKRIV('TABOPD'); */                         
                                                                        
                CALL OPDATER_RESULTAT;                                  
            END;                                                        
                                                                        
         /* CALL TEST_UDSKRIV('PERSONSL'); */                           
            END;                                                        
         ELSE DO;                                                       
            PUT SKIP LIST((51)'*');                                     
            PUT SKIP EDIT('* Fejl fra BN58050 Beregn Basisrabat: ',     
                            BN58050P.Cretur,' ',BN58050P.Årsag,' *')    
                           (A,P'----9',A,P'----9',A);                   
            PUT SKIP EDIT('* Person: ',AKTUEL_PERSONNUMMER,(30)' ','*') 
                         (A,P'9999999999',A,A);                         
            PUT SKIP LIST((51)'*');                                     
                                                                        
            PUT STRING(ABENDTXT)                                        
                  EDIT('101 Fejl fra Rabatberegning BN58050 ') (A);     
            CALL PROGRAM_FEJL;                                          
            END;                                                        
                                                                        
 END BEREGN_RABAT;                                                      
 /********************************************************************* 
 *       DAN OPTÆLLINGER                                              * 
 *********************************************************************/ 
 DAN_OPTÆLLINGER_FRA_BEREGNING:         PROC;                           
                                                                        
         DO J = 1 TO BN58050P.ANT_EJERFH;                               
                                                                        
            /* Dan til optællinger                                    */
            SELECT(BN58050P.EVS_EJERFH(J).Kontrol.AARSAG);              
               WHEN('EJET') AFVIST_PGA_EJ_EJET =                        
                                               AFVIST_PGA_EJ_EJET  + 1; 
               WHEN('FEJL') AFVIST_PGA_FEJL    = AFVIST_PGA_FEJL + 1;   
               WHEN('BERK') AFVIST_PGA_BERKODE = AFVIST_PGA_BERKODE + 1;
               OTHERWISE DO;                                            
                  /* Hvis EVS før forholdsmæssige reduktioner er 0    */
                  /* skal beregningen ikke gennemføres, og resultat   */
                  /* sættes til 0 (sker i BN58050, her optælles)      */
                  IF BN58050P.EVS_EJERFH(J).Evs.BBRUTTO <= 0            
                  THEN                                                  
                     AFVIST_PGA_RES0 = AFVIST_PGA_RES0 + 1;             
                  ELSE                                                  
                  IF BN58050P.EVS_EJERFH(J).Resultat.BFAKTISKRABAT > 0  
                  THEN                                                  
                     TV.RABAT_BEREGNET  = TV.RABAT_BEREGNET + 1;        
                  ELSE                                                  
                     TV.RABAT_BEREGNET0 = TV.RABAT_BEREGNET0 + 1;       
                 END;                                                   
            END;                                                        
         END;                                                           
                                                                        
 END DAN_OPTÆLLINGER_FRA_BEREGNING;                                     
 /********************************************************************* 
 *       OPDATER RESULTAT MED DET BEREGNEDE                           * 
 *********************************************************************/ 
 OPDATER_MED_RESULTAT: PROC;                                            
                                                                        
         /* Opdater Ejerrelation  (Felter til BS01600T)               */
         /* Basisrabat regulering skal ikke overføres fra tidligere år*/
      /* DCLBE78000T.GRDL_BASISRABATREGULERING                          
         DCLBE78000T.COPR_GRDL_BASISRABATREGUL */                       
                                                                        
         /* Ejerrelationen skal opdateres med summen af Ejerforhold-  */
         /* enes EVS.                                                 */
         GEM_BE78000T(J).RES_BNETTO_EVS_EJERREL    =                    
                                        BN58050P.Sum.BEVSEJERRELATION;  
                                                                        
         /* Opdater Ejerforhold   (Felter til BS01700T)               */
         GEM_BE78000T(J).RES_RABATGRDL_NYLOV =                          
                                       GEM_BE78000T(J).FRAEVS_FOR_BSUM2;
         GEM_BE780_SW(J).RES_RABATGRDL_NYLOV_SW = 0;                    
                                                                        
         GEM_BE78000T(J).RES_EVSFØRREDUKTION =                          
                                   BN58050P.EVS_EJERFH(J).Evs.BBRUTTO;  
         GEM_BE780_SW(J).RES_EVSFØRREDUKTION_SW = 0;                    
                                                                        
         GEM_BE78000T(J).RES_EVSEFTERREDUKTION =                        
                                   BN58050P.EVS_EJERFH(J).Evs.B100;     
         GEM_BE780_SW(J).RES_EVSEFTERREDUKTION_SW = 0;                  
                                                                        
         /*              DEC (5,0)               DEC(5,4)             */
         GEM_BE78000T(J).RES_EJERANDEL  =                               
                           BN58050P.EVS_EJERFH(J).Evs.GPCTEJER * 10000; 
         GEM_BE780_SW(J).RES_EJERANDEL_SW  = 0;                         
                                                                        
         /*               DEC (5,0)                 DEC (5,4)         */
         GEM_BE78000T(J).RES_RABATEJERANDEL    =                        
                BN58050P.EVS_EJERFH(J).Resultat.GRABATEJERANDEL * 10000;
         GEM_BE780_SW(J).RES_RABATEJERANDEL_SW = 0;                     
                                                                        
         IF BN58050P.EVS_EJERFH(J).EVS.COPR_GPCTRABAT = 'L'             
         THEN                                       /* Overført Rabat */
            GEM_BE78000T(J).COPR_RES_RABATEJERANDEL =                   
                BN58050P.EVS_EJERFH(J).EVS.COPR_GPCTRABAT;              
         ELSE                                                           
            GEM_BE78000T(J).COPR_RES_RABATEJERANDEL = 'E';              
         GEM_BE780_SW(J).COPR_RES_RABATEJERANDEL_SW = 0;                
                                                                        
         IF BN58050P.EVS_EJERFH(J).Evs.CEJBEVS      ^= 'A'              
         THEN DO;                                                       
            GEM_BE78000T(J).RES_FAKTISKRABAT =                          
                       BN58050P.EVS_EJERFH(J).Resultat.BFAKTISKRABAT;   
            GEM_BE780_SW(J).RES_FAKTISKRABAT_SW =                       
                       BN58050P.EVS_EJERFH(J).Resultat.BFAKTISKRABAT_SW;
         END;                                                           
         /* Opdater Resultat efter rabat                              */
         GEM_BE78000T(J).RES_EVSEFTERRABAT =                            
                        BN58050P.EVS_EJERFH(J).Resultat.BEVSEFTERRABAT; 
         GEM_BE780_SW(J).RES_EVSEFTERRABAT_SW = 0;                      
                                                                        
 END OPDATER_MED_RESULTAT;                                              
 /**********************************************************************
 *       INITIER RESULTAT FOR RABATBEREGNING                           *
 **********************************************************************/
 INITIER_RABATBEREGNINGSRES:       PROC;                                
                                                                        
         DO J = 1 TO ANT_GEM_BE78000T;                                  
                                                                        
            GEM_BE78000T(J).RES_RABATGRDL_NYLOV            = 0;         
            GEM_BE780_SW(J).RES_RABATGRDL_NYLOV_SW         = -1;        
            GEM_BE78000T(J).RES_EVSFØRREDUKTION            = 0;         
            GEM_BE780_SW(J).RES_EVSFØRREDUKTION_SW         = -1;        
            GEM_BE78000T(J).RES_EVSEFTERREDUKTION          = 0;         
            GEM_BE780_SW(J).RES_EVSEFTERREDUKTION_SW       = -1;        
            GEM_BE78000T(J).RES_EJERANDEL                  = 0;         
            GEM_BE780_SW(J).RES_EJERANDEL_SW               = -1;        
            GEM_BE78000T(J).RES_RABATEJERANDEL             = 0;         
            GEM_BE780_SW(J).RES_RABATEJERANDEL_SW          = -1;        
            GEM_BE78000T(J).COPR_RES_RABATEJERANDEL        = 'E';       
            GEM_BE780_SW(J).COPR_RES_RABATEJERANDEL_SW     = 0;         
            IF GEM_BE78000T(J).TILEVS_CEJBEVS  ^= 'A'                   
            THEN DO;                                                    
               GEM_BE78000T(J).RES_FAKTISKRABAT            = 0;         
               GEM_BE780_SW(J).RES_FAKTISKRABAT_SW         = -1;        
            END;                                                        
            GEM_BE78000T(J).RES_EVSEFTERRABAT              = 0;         
            GEM_BE780_SW(J).RES_EVSEFTERRABAT_SW           = -1;        
                                                                        
            GEM_BE78000T(J).RES_BNETTO_EVS_EJERREL         = 0;         
         END;                                                           
                                                                        
 END INITIER_RABATBEREGNINGSRES;                                        
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE: PROC;                                                      
 DCL     ANT_FEJL_IALT        BIN FIXED (31);                           
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = TV.FETCH_RESULT;                               
         DETLIN1.TEKST = 'ANTAL LÆSTE PÅ UDTRÆK             BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         ANT_FEJL_IALT = 0;                                             
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            IF LÆST_FEJLNR(INDEX).ANTAL > 0                             
            THEN DO;                                                    
               DETLIN1.CCA = ZZWS1;                                     
               HJ_PIC4 = INDEX;                                         
               DETLIN1.TEKST = '      HERAF MED FEJLNR. ' !! HJ_CHAR4;  
               DETLIN1.ANTAL = LÆST_FEJLNR(INDEX).ANTAL;                
               CALL ZZ20300 (DETLIN1,'01');                             
               ANT_FEJL_IALT = ANT_FEJL_IALT +                          
                               LÆST_FEJLNR(INDEX).ANTAL;                
               END;                                                     
         END;                                                           
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = ANT_FEJL_IALT;                                 
         DETLIN1.TEKST = '      HERAF MED FEJLNR I ALT';                
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         IF ANT_BS01900T_0 > 0                                          
         THEN DO;                                                       
            DETLIN1.CCA   = ZZWS1;                                      
            DETLIN1.ANTAL = ANT_BS01900T_0;                             
            DETLIN1.TEKST = 'ANTAL RABAT.EJERANDEL 0 (fra BS01900T) ';  
            CALL ZZ20300 (DETLIN1,'01');                                
            END;                                                        
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = AFVIST_FEJL_INP;                               
         DETLIN1.TEKST = 'AFVIST PGA. FEJL I INPUT          BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = TV.ANT_BEHANDLES;                              
         DETLIN1.TEKST = 'ANTAL BEHANDLES                   BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = AFVIST_PGA_EJ_EJET;                            
         DETLIN1.TEKST = 'AFVIST IKKE EJET FØR 2024         BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = AFVIST_PGA_FEJL;                               
         DETLIN1.TEKST = 'AFVIST FEJL I EVS                 BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = AFVIST_PGA_BERKODE;                            
         DETLIN1.TEKST = 'BEREGNINGSKODER - INGEN BEREGNING BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = AFVIST_PGA_RES0;                               
         DETLIN1.TEKST = 'EVS FØR FORHOLDSM. REDUKTION = 0  BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = TV.RABAT_OVERFØRT;                             
         DETLIN1.TEKST = 'ANTAL RABAT OVERFØRT              BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = TV.RABAT_BEREGNET;                             
         DETLIN1.TEKST = 'ANTAL RABAT BEREGNET > 0                  ';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.ANTAL = TV.RABAT_BEREGNET0;                            
         DETLIN1.TEKST = 'ANTAL RABAT BEREGNET = 0                  ';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS1;                                           
         DETLIN1.ANTAL = TV.ANT_UPD;                                    
         DETLIN1.TEKST = 'ANTAL OPDATERINGER                BE78000T';  
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            IF SKR_FEJLNR(INDEX).ANTAL > 0                              
            THEN DO;                                                    
               DETLIN1.CCA = ZZWS1;                                     
               HJ_PIC4 = INDEX;                                         
               DETLIN1.TEKST = '      HERAF MED FEJLNR. ' !! HJ_CHAR4;  
               DETLIN1.ANTAL = SKR_FEJLNR(INDEX).ANTAL;                 
               CALL ZZ20300 (DETLIN1,'01');                             
               END;                                                     
         END;                                                           
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /* TEST - PUT SKIP                                                   */
 /*********************************************************************/
 TEST_UDSKRIV: PROC(FUNKTION);                                          
 DCL     FUNKTION        CHAR      (10);                                
                                                                        
       /* IF (GEM_PERSONNUMMER          = xxxxxxxxxx                    
           &  GEM_EEJDNR                = xxxxxx)                       
           ! (DCLBE78000T.PERSONNUMMER  = xxxxxxxxxx                    
           &  DCLBE78000T.EEJDNR        = xxxxxx)                       
          THEN;                                                         
          ELSE */                                                       
            RETURN;                                                     
                                                                        
         SELECT(FUNKTION);                                              
           WHEN('PERSONST') DO;                                         
              PUT SKIP EDIT('********** Start ',' **********') (A,A);   
              PUT SKIP EDIT('Person: ',                                 
                             DCLBE78000T.TIL_EVS_AAR,' ',               
                             DCLBE78000T.DATA_ID,' ',                   
                             DCLBE78000T.DCPRN,' ',                     
                             DCLBE78000T.EKOMNR,' ',                    
                             DCLBE78000T.EEJDNR,' ',                    
                             DCLBE78000T.EENHEDLBNR,' ',                
                             DCLBE78000T.EGENNR,' Fejlnr: ',            
                             DCLBE78000T.FEJLNR)                        
                           (A,P'9999',A,A,A,P'9999999999',A,            
                            P'Z999',A,P'ZZ9999',A,P'ZZZ9',A,P'ZZZ9',A,  
                            P'9999');                                   
              END;                                                      
           WHEN('PERSONSL') DO;                                         
              PUT SKIP EDIT('********** Slut  ',' **********') (A,A);   
              PUT SKIP LIST(' ');                                       
              END;                                                      
           WHEN('BEREGNRAB') DO;                                        
              PUT SKIP EDIT('********** Rabat beregnes **********') (A);
              END;                                                      
           WHEN('BEREGNEJ') DO;                                         
               PUT SKIP EDIT('Person: ',                                
                             DCLBE78000T.TIL_EVS_AAR,' ',               
                             DCLBE78000T.DATA_ID,' ',                   
                             DCLBE78000T.DCPRN,' ',                     
                             DCLBE78000T.EKOMNR,' ',                    
                             DCLBE78000T.EEJDNR,' ',                    
                             DCLBE78000T.EENHEDLBNR,' ',                
                             DCLBE78000T.EGENNR,' Rabat beregnes ikke ',
                             'Afvist ved input')                        
                           (A,P'9999',A,A,A,P'9999999999',A,            
                            P'Z999',A,P'ZZ9999',A,P'ZZZ9',A,P'ZZZ9',A,  
                            A);                                         
              END;                                                      
           WHEN('BASIS') DO;                                            
              PUT SKIP EDIT('Fra BS01800T Basisrabat  : ') (A);         
         /*      PUT SKIP EDIT(' Gældende fra        : ',               
                                   BS018_AREA.DFRAGLD) (A,A);           
                 PUT SKIP EDIT(' Simpel Rabat        : ',               
                                   BS018_AREA.R24_SIMPELBASISRABAT)     
                              (A,P'----.---.--9');                      
                 PUT SKIP EDIT(' Simpel Rabat Neg    : ',               
                                   BS018_AREA.R24_SIMPELBASISRABAT_NEG) 
                               (A,P'---.---.---9');                     
                 PUT SKIP EDIT(' Basisrabat          : ',               
                                   BS018_AREA.R24_BASISRABAT)           
                              (A,P'----.---.--9');                      
                 PUT SKIP EDIT(' Basisrabat Reg.     : ',               
                                   BS018_AREA.R24_BASISRABAT_REGUL)     
                              (A,P'----.---.--9');                      
                 PUT SKIP EDIT(' Basisrabat Reg. Opr.: ',               
                                  BS018_AREA.COPR_R24_BASISRABAT_REGUL) 
                              (A,A);                                    
                 IF BS018_AREA.R24_FAST_STIGNINGSBEGR_NED_SW = 0        
                 THEN                                                   
                    PUT SKIP EDIT(' Fast sti. nedsl.    : ',            
                             BS018_AREA.R24_FAST_STIGNINGSBEGR_NEDSLAG) 
                              (A,P'----.---.--9');                      
                 ELSE                                                   
                    PUT SKIP EDIT(' Fast sti. nedsl.    : NULL') (A);   
                 IF BS018_AREA.R24_STIGNINGSBEGR_NEDSLAG_SW = 0         
                 THEN                                                   
                    PUT SKIP EDIT(' Sti. bgr. nedsl.    : ',            
                             BS018_AREA.R24_STIGNINGSBEGR_NEDSLAG)      
                              (A,P'----.---.--9');                      
                 ELSE                                                   
                    PUT SKIP EDIT(' Sti. bgr. nedsl.    : NULL') (A);   
                 IF BS018_AREA.R24_EJERANDEL_SW = 0                     
                 THEN                                                   
                    PUT SKIP EDIT(' Ejerandel           : ',            
                             BS018_AREA.R24_EJERANDEL)                  
                              (A,P'99999');                             
                 ELSE                                                   
                    PUT SKIP EDIT(' Ejerandel           : NULL') (A);   
                    */                                                  
              END;                                                      
           WHEN('BEREGNIND') DO;                                        
              PUT SKIP EDIT('Input til Beregning: ') (A);               
              PUT SKIP EDIT(' EVS År            : ',                    
                             BN58050P.TIL_EVS_AAR) (A,P'9999');         
              PUT SKIP EDIT(' Rabatber. Ejerand: ',                     
                             BN58050P.GRABATEJERANDEL_ST)               
                           (A,P'---------9V,9999');                     
                                                                        
              DO J = 1 TO BN58050P.Ant_Ejerfh;                          
                                                                        
                 PUT SKIP EDIT('Ejerforhold: ',J) (A,P'99');            
                 PUT SKIP EDIT(' Rabatberettiget    : ',                
                 BN58050P.EVS_Ejerfh(J).Evs.Rabatberettiget) (A,A);     
                 PUT SKIP EDIT(' FEJLNR             :            ',     
                             BN58050P.EVS_Ejerfh(J).Evs.FEJLNR)         
                            (A,P'9999');                                
                 PUT SKIP EDIT(' DOVTG              :               ',  
                             BN58050P.EVS_Ejerfh(J).Evs.DOVTG)          
                            (A,P'99999999');                            
                 PUT SKIP EDIT(' DAFSTAA            :               ',  
                             BN58050P.EVS_Ejerfh(J).Evs.DAFSTAA)        
                            (A,P'99999999');                            
                 PUT SKIP EDIT(' CEJBEVS            :               ',  
                             BN58050P.EVS_Ejerfh(J).Evs.CEJBEVS) (A,A); 
                 PUT SKIP EDIT(' GPCTEJER/SW        : ',                
                             BN58050P.EVS_Ejerfh(J).Evs.GPCTEJER,' ',   
                             BN58050P.EVS_Ejerfh(J).Evs.GPCTEJER_SW)    
                             (A,P'---------9V,9999',A,P'--9');          
                  PUT SKIP EDIT(' GPCTRABAT/SW       : ',               
                             BN58050P.EVS_Ejerfh(J).Evs.GPCTRABAT,' ',  
                             BN58050P.EVS_Ejerfh(J).Evs.GPCTRABAT_SW)   
                             (A,P'---------9V,9999',A,P'--9');          
                  PUT SKIP EDIT(' COPR_GPCTRABAT     : ',               
                             BN58050P.EVS_Ejerfh(J).Evs.COPR_GPCTRABAT) 
                             (A,A);                                     
                  PUT SKIP EDIT(' BEVSFREM          : ',                
                             BN58050P.EVS_Ejerfh(J).Evs.BEVSFREM)       
                           (A,P'----.---.--9V,99');                     
                  PUT SKIP EDIT(' BNYMAN            : ',                
                             BN58050P.EVS_Ejerfh(J).Evs.BNYMAN)         
                           (A,P'----.---.--9V,99');                     
                 PUT SKIP EDIT(' BBRUTTO           : ',                 
                             BN58050P.EVS_Ejerfh(J).Evs.BBRUTTO)        
                           (A,P'----.---.--9V,99');                     
                 PUT SKIP EDIT(' B100              : ',                 
                             BN58050P.EVS_Ejerfh(J).Evs.B100)           
                           (A,P'----.---.--9V,99');                     
                 PUT SKIP EDIT(' BEJERAN           : ',                 
                             BN58050P.EVS_Ejerfh(J).Evs.BEJERAN)        
                           (A,P'----.---.--9V,99');                     
                                                                        
                 PUT SKIP EDIT(' BSMPBASRABAT      : ',                 
                             BN58050P.Rabat.BSMPBASRABAT)               
                           (A,P'----.---.--9V,99');                     
                 PUT SKIP EDIT(' BBASISRABATREG    : ',                 
                           BN58050P.Rabat.BBASISRABATREG)               
                           (A,P'----.---.--9V,99');                     
                 PUT SKIP EDIT(' BBASISRABAT       : ',                 
                             BN58050P.Rabat.BBASISRABAT)                
                           (A,P'----.---.--9V,99');                     
              END;                                                      
              END;                                                      
           WHEN('BEREGNUD') DO;                                         
              PUT SKIP EDIT('Output fra Beregning: ') (A);              
              PUT SKIP EDIT(' Retur                : ',                 
                             BN58050P.Tech.Cretur,'/',                  
                             BN58050P.Tech.Årsag)                       
                           (A,P'----9',A,P'----9');                     
                                                                        
              DO J = 1 TO BN58050P.Ant_Ejerfh;                          
                                                                        
                 PUT SKIP EDIT(' Kontrol               : ',             
                             BN58050P.EVS_Ejerfh(J).Kontrol.FEJLNR,'/', 
                             BN58050P.EVS_Ejerfh(J).Kontrol.Aarsag)     
                           (A,P'9999',A,A);                             
                 PUT SKIP EDIT(' Faktisk rabat         : ',             
                   BN58050P.EVS_Ejerfh(J).Resultat.BFAKTISKRABAT,' ',   
                   BN58050P.EVS_Ejerfh(J).Resultat.BFAKTISKRABAT_SW)    
                        (A,P'----.---.--9V,99',A,P'--9');               
                 PUT SKIP EDIT(' Rabatberettiget ejer. : ',             
                   BN58050P.EVS_Ejerfh(J).Resultat.GRABATEJERANDEL)     
                        (A,P'---------9V,9999');                        
                 PUT SKIP EDIT(' EVS efter Rabat       : ',             
                   BN58050P.EVS_Ejerfh(J).Resultat.BEVSEFTERRABAT)      
                        (A,P'----.---.--9V,99');                        
              END;                                                      
                                                                        
              PUT SKIP EDIT(' Sum Ejelation         : ',                
                   BN58050P.Sum.BEVSEJERRELATION)                       
                        (A,P'----.---.--9V,99');                        
              END;                                                      
           WHEN('TABOPD') DO;                                           
              PUT SKIP LIST(' ');                                       
              PUT SKIP EDIT('Tabelopdatering     : ') (A);              
              PUT SKIP EDIT('RES_RABATGRDL_NYLOV          : ',          
                            BE780_SW.RES_RABATGRDL_NYLOV_SW,' ',        
                            DCLBE78000T.RES_RABATGRDL_NYLOV)            
                           (A,P'--9',A,P'---.---.--9V,99');             
              PUT SKIP EDIT('RES_EVSFØRREDUKTION          : ',          
                            BE780_SW.RES_EVSFØRREDUKTION_SW,' ',        
                            DCLBE78000T.RES_EVSFØRREDUKTION)            
                           (A,P'--9',A,P'---.---.--9V,99');             
              PUT SKIP EDIT('RES_EVSEFTERREDUKTION        : ',          
                            BE780_SW.RES_EVSEFTERREDUKTION_SW,' ',      
                            DCLBE78000T.RES_EVSEFTERREDUKTION)          
                           (A,P'--9',A,P'---.---.--9V,99');             
              PUT SKIP EDIT('RES_EJERANDEL                : ',          
                            BE780_SW.RES_EJERANDEL_SW,' ',              
                            DCLBE78000T.RES_EJERANDEL)                  
                           (A,P'--9',A,P'--.---.--99999');              
              PUT SKIP EDIT('RES_RABATEJERANDEL           : ',          
                            BE780_SW.RES_RABATEJERANDEL_SW,' ',         
                            DCLBE78000T.RES_RABATEJERANDEL)             
                           (A,P'--9',A,P'--.---.--99999');              
              PUT SKIP EDIT('COPR_RES_RABATEJERANDEL      : ',          
                            BE780_SW.COPR_RES_RABATEJERANDEL_SW,' ',    
                            DCLBE78000T.COPR_RES_RABATEJERANDEL)        
                           (A,P'--9',A,A);                              
              PUT SKIP EDIT('RES_FAKTISKRABAT             : ',          
                            BE780_SW.RES_FAKTISKRABAT_SW,' ',           
                            DCLBE78000T.RES_FAKTISKRABAT)               
                           (A,P'--9',A,P'---.---.--9V,99');             
              PUT SKIP EDIT('RES_EVSEFTERRABAT            : ',          
                            BE780_SW.RES_EVSEFTERRABAT_SW,' ',          
                            DCLBE78000T.RES_EVSEFTERRABAT)              
                           (A,P'--9',A,P'---.---.--9V,99');             
              END;                                                      
           OTHERWISE;                                                   
         END;                                                           
                                                                        
 END TEST_UDSKRIV;                                                      
 /*********************************************************************/
 /*      TERMINER LISTE                                               */
 /*********************************************************************/
 TERMINER_LISTE:  PROCEDURE (TEKST);                                    
 DCL     TEKST            CHAR      (45);                               
                                               /* Medd. til termine-  */
 DCL     1 MEDDELELSE     STATIC,              /* ringslisten         */
           3 MODULNAVN    CHAR      (07)    INIT ('BE85500'),           
           3 FILLER1      CHAR      (01)    INIT (' '),                 
           3 MEDDNR       PIC       '999'   INIT (6),                   
           3 DATA         CHAR      (45)    INIT (' ');                 
 DCL     MEDDELELSEN      CHAR      (56)    DEF MEDDELELSE;             
                                                                        
         MEDDELELSE.MEDDNR = MEDDELELSE.MEDDNR + 1;                     
         MEDDELELSE.DATA   = TEKST;                                     
                                                                        
         CALL ZS30101 (2,                                               
                       MEDDELELSEN,                                     
                       2);                                              
                                                                        
 END TERMINER_LISTE;                                                    
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:               ' !! ABENDTXT);        
         PUT SKIP LIST ('PERSON  :               ' !!                   
                                            AKTUEL_PERSONNUMMER);       
         PUT SKIP LIST ('KOMMUNE :               ' !!                   
                                            AKTUEL_EKOMNR);             
         PUT SKIP LIST ('EJENDOM :               ' !!                   
                                            AKTUEL_EEJDNR);             
         PUT SKIP LIST ('*****************************************');   
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:               ' !! ABENDTXT);        
         PUT SKIP LIST ('PERSON  :               ' !!                   
                                            AKTUEL_PERSONNUMMER);       
         PUT SKIP LIST ('KOMMUNE :               ' !!                   
                                            AKTUEL_EKOMNR);             
         PUT SKIP LIST ('EJENDOM :               ' !!                   
                                            AKTUEL_EEJDNR);             
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
          %INCLUDE BE85501M;         /* Indlæsning/Opdatering af data */
                                     /* OPEN_/FETCH_/CLOSE_RESULTCURS */
                                     /*                   (BE78000T)  */
                                     /* SELECT_ESR        (BE75000T)  */
                                     /* SELECT_MT         (BE76000T)  */
                                     /* SELECT_SLUT       (BE77000T)  */
                                     /* OPDATER_RESULTAT  (BE78000T)  */
 END BE85500;                                                           
