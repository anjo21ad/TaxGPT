  PROCESS OPT(TIME);                                                    
 BH61330:/***   OPBYG LINIER MED EJENDOMSOPLYSNINGER TIL SLS        ***/
         PROC (BH601_AR,BH61330N,TV_TEKSTREC) REORDER;                  
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');       
 /*********************************************************************/
 /*                                                                   */
 /*      NAVN           OPBYG LINIER MED EJENDOMSOPLYSNINGER TIL SLS  */
 /*                                                                   */
 /*      FUNKTION       MODULET OPBYGGER LINIER MED EJENDOMSOPLYSN.   */
 /*                     TIL UDSKRIVNING PÅ PRINTSLEVANGIVELSE (PSA).  */
 /*                                                                   */
 /*      INDDATA        B.H61000D          BEREGN. EJD. UDTRÆK        */
 /*                                                                   */
 /*      UDDATA         D0314314.A206      TEKSTRECORDS TIL SLS (PSA) */
 /*                                                                   */
 /*      PROGRAMMØR :   AAGE RASMUSSEN    DIV-SKAT , NOV.  1992       */
 /*      RETTET     :   AAGE RASMUSSEN    PUS      , DEC.  1993       */
 /*      RETTET     :   AAGE RASMUSSEN    PUS      , DEC.  1994       */
 /*      RETTET     :   AAGE RASMUSSEN    PUS      , DEC.  1995       */
 /*      RETTET     :   AAGE RASMUSSEN    PFB      , JAN.  1997       */
 /*      RETTET     :   AAGE RASMUSSEN    PFB      , JAN.  1998       */
 /*      RETTET     :   AAGE RASMUSSEN    PFB      , JAN.  1999       */
 /*      RETTET     :   AAGE RASMUSSEN    PUS      , JAN.  2000       */
 /*                                                                   */
 /*                                                                   */
 /* NB.: DETTE MODUL ER LAVET PÅ GRUNDLAG AF BH60830, OG VEDLIGEHOL-  */
 /*      DES DERFOR JVF. SAMME.                                       */
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONNECTED;                  
 DCL SYSPRINT EXTERNAL FILE PRINT;                                      
 DCL     1 BH60100M,                                                    
           %INCLUDE BH60100M;                                           
 /*********************************************************************/
 /*                                                                   */
 /*      FILE-DECLARE                                                 */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH6130O         FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*                                                                   */
 /*      STRUCTUR-DECLARE  INPUT                                      */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH601_AR CHAR (4000) VAR;                                      
 DCL     1 BH601 BASED(ADDR(BH601_AR)),                                 
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60100N;                                           
 DCL     HJ_CBENYT       PIC       '99' BASED (ADDR(BH601.CBENYT));     
 DCL     HJ_CLIN(15)     PIC       '9'  BASED (ADDR(BH601.CLIN01));     
 /*********************************************************************/
 /*                                                                   */
 /*      STRUCTUR-DECLARE  OUTPUT                                     */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 BH61330N ALIGNED,                                            
         %INCLUDE BH61330N;                                             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     TV_TEKSTREC     FIXED     (15);                                
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF DIVERSE HJÆLPEFELTER                              */
 /*                                                                   */
 /*********************************************************************/
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     STG             BUILTIN;                                       
 DCL     VERIFY          BUILTIN;                                       
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS31101         ENTRY;      /* FJERN OVERFLØDIGE BLANKE POS. */
 %INCLUDE AS27100N;                                                     
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF LINIER TIL REDIGERING                             */
 /*                                                                   */
 /*********************************************************************/
 DCL     LINIE           CHAR      (132);                               
 DCL     1 LIN01         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (7),                                 
           2 EKOMNR      PIC       '999B',                              
           2 EEJDNR      CHAR      (8),                                 
           2 FYLD1       CHAR      (1),                                 
           2 ABELIG      CHAR      (30),                                
           2 FYLD2       CHAR      (1),                                 
           2 ABENYT      CHAR      (31),                                
           2 FYLD3       CHAR      (1),                                 
           2 FLEJL       PIC       'ZZZZZB',                            
           2 TEKST2      CHAR      (11);                                
 DCL     1 LIN01A        BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (7),                                 
           2 EKOMNR      PIC       '999B',                              
           2 EEJDNR      CHAR      (8),                                 
           2 PUNKTUM1    CHAR      (2),                                 
           2 ABELIG      CHAR      (28),                                
           2 PUNKTUM2    CHAR      (2),                                 
           2 ABENYT      CHAR      (31),                                
           2 FYLD1       CHAR      (1),                                 
           2 FLEJL       PIC       'ZZZZZB',                            
           2 TEKST2      CHAR      (11);                                
 DCL     1 LIN02         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (18),                                
           2 BEJDVÆR     PIC       '---.---.---.--9',                   
           2 TEKST2      CHAR      (2),                                 
           2 TEKST3      CHAR      (17),                                
           2 BEJEBOL1    PIC       '---.---.---.--9',                   
           2 TEKST4      CHAR      (2),                                 
           2 TEKST5      CHAR      (17),                                
           2 BEJEBOL2    PIC       '---.---.---.--9';                   
 DCL     1 LIN03         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (22),                                
           2 BLEJE       PIC       'ZZ.ZZZ.ZZZ.ZZ9',                    
           2 TEKST2      CHAR      (23),                                
           2 BSTDF       PIC       'Z.ZZZB',                            
           2 TEKST3      CHAR      (17),                                
           2 BNETTO      PIC       'ZZ.ZZZ.ZZZ.ZZ9';                    
 DCL     1 LIN04         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (12),                                
           2 DATO1,                                                     
             3 DD          PIC   '99.',                                 
             3 MD          PIC   '99',                                  
           2 STREG       CHAR      (1),                                 
           2 DATO2,                                                     
             3 DD          PIC   '99.',                                 
             3 MD          PIC   '99',                                  
           2 TEKST2      CHAR      (12),         /* indgår med */       
           2 FDAGE       PIC       'ZZ9',                               
           2 TEKST3      CHAR      (5),          /* dage */             
           2 TEKST4      CHAR      (15);         /* i beregningen */    
 DCL     1 LIN05         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (10),                                
           2 GANE        PIC       'Z9,99',                             
           2 PCT_TEGN    CHAR      (1),                                 
           2 TEKST2      CHAR      (18),                                
           2 BNETTO      PIC       'ZZ.ZZZ.ZZZ.ZZ9';                    
 DCL     1 LIN10         BASED (ADDR(LINIE)),                           
           2 TEKST1      CHAR      (27),                                
           2 BELØB1      PIC       'ZZ.ZZZ.ZZZ.ZZ9',                    
           2 TEKST2      CHAR      (39),                                
           2 BELØB2      PIC       'ZZ.ZZZ.ZZZ.ZZ9';                    
 DCL     1 LIN10A        BASED (ADDR(LINIE)),                           
           2 FYLD        CHAR      (41),                                
           2 TEKST2      CHAR      (59);                                
 /*********************************************************************/
 /*                                                                   */
 /*     HJÆLPEFELTER                                                  */
 /*                                                                   */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
 DCL     TABTXT_IDX      BIN FIXED (15);                                
 DCL     1 HJ,                                                          
           2 LIN_INDH    CHAR      (132),                               
           2 SLUT        CHAR      (1)       INIT (' ');                
 DCL     HJEJD           CHAR      (8);                                 
 DCL     PICEJD          PIC '(5)Z9B9'       BASED(ADDR(HJEJD));        
 DCL     EJD_STREG  (8)  CHAR      (1)       BASED(ADDR(HJEJD));        
 DCL     CHPCT4          CHAR      (4);                                 
 DCL     PICPCT4         PIC     '9,99'      BASED(ADDR(CHPCT4));       
 DCL     CHPCT5          CHAR      (5);                                 
 DCL     PICPCT5         PIC    '99,99'      BASED(ADDR(CHPCT5));       
 DCL     HJDATO          PIC      '9999'     INIT (0);                  
 DCL     1 HJDATO1                           BASED(ADDR(HJDATO)),       
           2 MD          PIC      '99',                                 
           2 DD          PIC      '99';                                 
 DCL     HJ_BEJDVÆR      FIXED    (11);                                 
 DCL     CBETXT (0:99)   CHAR     (31)       ALIGNED INIT               
         ('                               ', /* 00 ANVENDES IKKE */     
          'Beboelse                       ', /* CBENYT = 01 */          
          'Beboelse og forretning         ', /* 02 */                   
          'Forretning                     ', /* 03 */                   
          'Fabrik og lager                ', /* 04 */                   
          'Landbrug, bebygget             ', /* 05 */                   
          'Særskilt vurderet skov/plantage', /* 06 */                   
          'Frugtplant/gartneri/planteskole', /* 07 */                   
          'Sommerhus                      ', /* 08 */                   
          'Ubebygget areal                ', /* 09 */                   
          '                               ', /* 10 */                   
          '                               ', /* 11 */                   
          '                               ', /* 12 */                   
          'Andre vurderinger              ', /* 13 */                   
          '                               ', /* 14 */                   
          'Ejendom under opførelse        ', /* 15 */                   
          '                               ', /* 16 */                   
          'Ubebygget landbrugslod         ', /* 17 */                   
          '                               ', /* 18 */                   
          '                               ', /* 19 */                   
          '                               ', /* 20 */                   
          'Ejerlejlighed beboelse         ', /* 21 */                   
          'Ejerlejl. beboelse/forretning  ', /* 22 */                   
          'Ejerlejlighed forretning       ', /* 23 */                   
          'Ejerlejlighed fab./lager       ', /* 24 */                   
          'Anden ejerlejlighed            ', /* 25 */                   
          'Ejerlejlighed villa            ', /* 26 */                   
          'Ejerlejlighed rækkehus         ', /* 27 */                   
          'Ejerlejlighed sommerhus        ', /* 28 */                   
          'Ejerlejlighed på fremmed grund ', /* 29 */                   
          '                               ', /* 30 */                   
          'Andelsbolig, støttet           ', /* 31 */                   
          '                               ', /* 32 */                   
          'Institution og service         ', /* 33 */                   
          'Speciel erhvervsejendom        ', /* 34 */                   
          '                               ', /* 35 */                   
          '                               ', /* 36 */                   
          '                               ', /* 37 */                   
          '                               ', /* 38 */                   
          '                               ', /* 39 */                   
          '                               ', /* 40 */                   
          'Beboelse på fremmed grund      ', /* 41 */                   
          'Beboelse/forretn. på frem.grund', /* 42 */                   
          'Forretning på fremmed grund    ', /* 43 */                   
          'Fabrik/lager på fremmed grund  ', /* 44 */                   
          'Andre bygninger på frem.grund  ', /* 45 */                   
          '                               ', /* 46 */                   
          '                               ', /* 47 */                   
          'Sommerhus på fremmed grund     ', /* 48 */                   
          'Udlejet areal                  ', /* 49 */                   
          '                               ', /* 50 */                   
          '                               ', /* 51 */                   
          '                               ', /* 52 */                   
          '                               ', /* 53 */                   
          '                               ', /* 54 */                   
          '                               ', /* 55 */                   
          '                               ', /* 56 */                   
          '                               ', /* 57 */                   
          '                               ', /* 58 */                   
          '                               ', /* 59 */                   
          '                               ', /* 60 */                   
          '                               ', /* 61 */                   
          '                               ', /* 62 */                   
          '                               ', /* 63 */                   
          '                               ', /* 64 */                   
          '                               ', /* 65 */                   
          '                               ', /* 66 */                   
          '                               ', /* 67 */                   
          '                               ', /* 68 */                   
          '                               ', /* 69 */                   
          'Nyopstået ejendom              ', /* 70 */                   
          'Nyopstået ejerlejlighed        ', /* 71 */                   
          '                               ', /* 72 */                   
          '                               ', /* 73 */                   
          '                               ', /* 74 */                   
          '                               ', /* 75 */                   
          '                               ', /* 76 */                   
          '                               ', /* 77 */                   
          '                               ', /* 78 */                   
          '                               ', /* 79 */                   
          '                               ', /* 80 */                   
          '                               ', /* 81 */                   
          '                               ', /* 82 */                   
          '                               ', /* 83 */                   
          '                               ', /* 84 */                   
          '                               ', /* 85 */                   
          '                               ', /* 86 */                   
          '                               ', /* 87 */                   
          '                               ', /* 88 */                   
          '                               ', /* 89 */                   
          '                               ', /* 90 */                   
          '                               ', /* 91 */                   
          '                               ', /* 92 */                   
          '                               ', /* 93 */                   
          '                               ', /* 94 */                   
          '                               ', /* 95 */                   
          '                               ', /* 96 */                   
          '                               ', /* 97 */                   
          'Opkrævning i anden kommune     ', /* 98 */                   
          '                               ');/* 99 ANVENDES IKKE */     
   %INCLUDE BH60800M; /* Teksttabel */                                  
 /*********************************************************************/
 /*                                                                   */
 /*      PROCESS                                                      */
 /*                                                                   */
 /*********************************************************************/
     /*  CALL OPBYG_LIN01;     uden punktum'er */                       
         CALL OPBYG_LIN01A; /*  med punktum'er */                       
         CALL OPBYG_LIN02;                                              
         IF BH601.CLIN03 = 1                                            
         THEN                                                           
            CALL OPBYG_LIN03;                                           
         DO TABTXT_IDX = 4 TO 15;                                       
            IF HJ_CLIN(TABTXT_IDX) = 1                                  
            THEN                                                        
               DO;                                                      
                  CALL OPBYG_LINIE(TABTXT_IDX);                         
                  HJ.LIN_INDH = LINIE;                                  
                  CALL ZS31101 (HJ.LIN_INDH,SLUT);                      
                  LINIE = HJ.LIN_INDH;                                  
                  BH61330N.ELINIENR = TABTXT_IDX;                       
                  CALL SKRIV;                                           
               END;                                                     
         END;                                                           
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG LINIE 01 - uden '.' mellem oplysningerne og uden at    */
 /*                       St, Tv, Th og Mf laves om til 'småt'        */
 /*********************************************************************/
 OPBYG_LIN01:                                                           
         PROC;                                                          
 DCL     HJ_ABELIG       CHAR     (31) ALIGNED;                         
 DCL     HJ_ABELIG1 (31) CHAR     (01) ALIGNED BASED(ADDR(HJ_ABELIG));  
 DCL     HJ_ABEL30       CHAR     (30) ALIGNED BASED(ADDR(HJ_ABELIG));  
         LINIE = ' ';                                                   
         LIN01.TEKST1 = 'Ejdnr.';                                       
         LIN01.EKOMNR = BH601.EKOMNR;                                   
         PICEJD       = BH601.EEJDNR;                                   
         EJD_STREG(7) = '-';                                            
         LIN01.EEJDNR = HJEJD;                                          
         HJ_ABELIG = SUBSTR(BH601.ABELMAT,1,31);                        
         IF VERIFY(HJ_ABELIG1(31),'1234567890') = 0                     
         THEN                                                           
            DO I = 31 TO 25 BY -1 WHILE                                 
               (VERIFY(HJ_ABELIG1(I),'1234567890') = 0);                
               HJ_ABELIG1(I) = ' ';                                     
            END;                                                        
         AS271_PTR = ADDR(HJ_ABELIG);                                   
         AS271_LGD = STG(HJ_ABELIG);                                    
         CALL AS27100(RUT_KOHREGEL,AS271_PTR,AS271_LGD);                
         LIN01.ABELIG = HJ_ABEL30;                                      
         IF BH601.CEJEV = '5'                                           
         THEN                                                           
            BH601.CBENYT =  '15';                                       
         LIN01.ABENYT = CBETXT(HJ_CBENYT);                              
         IF BH601.FLEJL > 0                                             
         THEN                                                           
            DO;                                                         
               LIN01.FLEJL = BH601.FLEJL;                               
               IF BH601.FLEJL = 1                                       
               THEN                                                     
                  LIN01.TEKST2 = 'lejlighed';                           
               ELSE                                                     
                  LIN01.TEKST2 = 'lejligheder';                         
            END;                                                        
         HJ.LIN_INDH = LINIE;                                           
         CALL ZS31101 (HJ.LIN_INDH,SLUT);                               
         LINIE = HJ.LIN_INDH;                                           
         BH61330N.ELINIENR = 01;                                        
         CALL SKRIV;                                                    
 END OPBYG_LIN01;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG LINIE 01 - med '.' mellem oplysningerne og hvor        */
 /*      ST, TV, TH, MF, St, Tv, Th og Mf laves om til 'småt'         */
 /*********************************************************************/
 OPBYG_LIN01A:                                                          
         PROC;                                                          
 DCL     HJ_BGST1        CHAR     (1);                                  
 DCL     HJ_BGST2        CHAR     (1);                                  
 DCL     HJ_ABELIG       CHAR     (29) ALIGNED;                         
 DCL     HJ_ABELIG1 (29) CHAR     (01) ALIGNED BASED(ADDR(HJ_ABELIG));  
 DCL     HJ_ABEL28       CHAR     (28) ALIGNED BASED(ADDR(HJ_ABELIG));  
         LINIE = ' ';                                                   
         LIN01A.TEKST1 = 'Ejdnr.';                                      
         LIN01A.EKOMNR = BH601.EKOMNR;                                  
         PICEJD        = BH601.EEJDNR;                                  
         EJD_STREG(7)  = '-';                                           
         LIN01A.EEJDNR = HJEJD;                                         
         HJ_ABELIG = SUBSTR(BH601.ABELMAT,1,29);                        
         IF VERIFY(HJ_ABELIG1(29),'1234567890') = 0                     
         THEN                                                           
            DO I = 28 TO 24 BY -1 WHILE                                 
               (VERIFY(HJ_ABELIG1(I),'1234567890') = 0);                
               HJ_ABELIG1(I) = ' ';                                     
            END;                                                        
         AS271_PTR = ADDR(HJ_ABELIG);                                   
         AS271_LGD = STG(HJ_ABELIG);                                    
         CALL AS27100(RUT_KOHREGEL,AS271_PTR,AS271_LGD);                
         if HJ_ABEL28 ^= ' '                                            
         THEN                                                           
            DO;                                                         
               DO I = 27 TO 3 BY -1;                                    
                  HJ_BGST1 = HJ_ABELIG1(I);                             
                  IF (HJ_ABELIG1(I+1) = 'T' !                           
                      HJ_ABELIG1(I+1) = 't') &                          
                     HJ_ABELIG1(I)   = 'S'                              
                  THEN                                                  
                     DO;                                                
                        HJ_BGST1 = 's';                                 
                        HJ_BGST2 = 't';                                 
                     END;                                               
                  IF (HJ_ABELIG1(I+1) = 'H' !                           
                      HJ_ABELIG1(I+1) = 'h') &                          
                      HJ_ABELIG1(I)   = 'T'                             
                  THEN                                                  
                     DO;                                                
                        HJ_BGST1 = 't';                                 
                        HJ_BGST2 = 'h';                                 
                     END;                                               
                  IF (HJ_ABELIG1(I+1) = 'V' !                           
                      HJ_ABELIG1(I+1) = 'v') &                          
                      HJ_ABELIG1(I)   = 'T'                             
                  THEN                                                  
                     DO;                                                
                        HJ_BGST1 = 't';                                 
                        HJ_BGST2 = 'v';                                 
                     END;                                               
                  IF (HJ_ABELIG1(I+1) = 'F' !                           
                      HJ_ABELIG1(I+1) = 'f') &                          
                      HJ_ABELIG1(I)   = 'M'                             
                  THEN                                                  
                     DO;                                                
                        HJ_BGST1 = 'm';                                 
                        HJ_BGST2 = 'f';                                 
                     END;                                               
                  IF I < 28 &                                           
                    ((HJ_ABELIG1(I+2) = 'D' !                           
                      HJ_ABELIG1(I+2) = 'd') &                          
                     (HJ_ABELIG1(I+1) = 'L' !                           
                      HJ_ABELIG1(I+1) = 'l') &                          
                      HJ_ABELIG1(I)   = 'K')                            
                  THEN                                                  
                     DO;                                                
                        HJ_ABELIG1(I)   = 'k';                          
                        HJ_ABELIG1(I+1) = 'l';                          
                        HJ_ABELIG1(I+2) = 'd';                          
                     END;                                               
                  ELSE                                                  
                   DO;                                                  
                     IF HJ_BGST1 = HJ_ABELIG1(I)                        
                     THEN;                                              
                     ELSE                                               
                        DO;                                             
                           /* Hvis st,tv,th og/eller mf er en del af  */
                           /* et vejnavn skal 'stort' bogstav bevares */
                           IF VERIFY(HJ_ABELIG1(I+2),                   
                              'abcdefghijklmnopqrstuvxyzæøåäüö') = 0    
                           THEN;                                        
                           ELSE                                         
                              DO;                                       
                                 HJ_ABELIG1(I)   = HJ_BGST1;            
                                 HJ_ABELIG1(I+1) = HJ_BGST2;            
                              END;                                      
                        END;                                            
                   END;                                                 
               END;                                                     
            END;                                                        
         LIN01A.ABELIG = HJ_ABEL28;                                     
         IF BH601.CEJEV = '5'                                           
         THEN                                                           
            BH601.CBENYT =  '15';                                       
         LIN01A.ABENYT = CBETXT(HJ_CBENYT);                             
         IF BH601.FLEJL > 0                                             
         THEN                                                           
            DO;                                                         
               LIN01A.FLEJL = BH601.FLEJL;                              
               IF BH601.FLEJL = 1                                       
               THEN                                                     
                  LIN01A.TEKST2 = 'lejlighed';                          
               ELSE                                                     
                  LIN01A.TEKST2 = 'lejligheder';                        
            END;                                                        
         IF LIN01A.ABELIG ^= ' ' !                                      
            LIN01A.ABENYT ^= ' ' !                                      
            BH601.FLEJL > 0                                             
         THEN                                                           
            LIN01A.PUNKTUM1 = '.';                                      
         IF HJ_ABELIG1(28) = '.'                                        
         THEN;                                                          
         ELSE                                                           
            IF LIN01A.ABELIG ^= ' ' &                                   
              (LIN01A.ABENYT ^= ' ' !                                   
               BH601.FLEJL > 0)                                         
            THEN                                                        
               DO;                                                      
                  IF HJ_ABELIG1(28) ^= ' '                              
                  THEN                                                  
                     LIN01A.PUNKTUM2 = '.';                             
                  ELSE                                                  
                     DO;                                                
                        DO I = 28 TO 1 BY -1                            
                            WHILE(HJ_ABELIG1(I) = ' ');                 
                        END;                                            
                        IF HJ_ABELIG1(I) ^= '.'                         
                        THEN                                            
                           HJ_ABELIG1(I+1) = '.';                       
                        LIN01A.ABELIG = HJ_ABEL28;                      
                     END;                                               
               END;                                                     
         HJ.LIN_INDH = LINIE;                                           
         CALL ZS31101 (HJ.LIN_INDH,SLUT);                               
         LINIE = HJ.LIN_INDH;                                           
         BH61330N.ELINIENR = 01;                                        
         CALL SKRIV;                                                    
 END OPBYG_LIN01A;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG LINIE 02                                               */
 /*                                                                   */
 /*********************************************************************/
 OPBYG_LIN02:                                                           
         PROC;                                                          
         LINIE = ' ';                                                   
         SELECT (BH601.CREDIG2);                                        
            WHEN ('1')                                                  
               DO;                                                      
                  LINIE =                                               
                     'Ejendommen er ikke vurderet';                     
               END;                                                     
            OTHERWISE                      /* EJENDOMSVÆRDI */          
               DO;                                                      
                  LIN02.TEKST1 = 'Ejendomsværdi kr. ';                  
                  LIN02.BEJDVÆR = BH601.BEJDVÆR;                        
               END;                                                     
         END;                                                           
         SELECT (BH601.CREDIG2);                                        
            WHEN ('3','4')                  /* EJERBOLIGVÆRDI  */       
               DO;                                                      
                                                                        
                  LIN02.TEKST2 = '. ';                                  
                  LIN02.BEJEBOL1 =                                      
                  BH601.EJEBOL(1).BEJEBOL;                              
                  IF BH601.CREDIG2 = '3'                                
                  THEN                                                  
                     LIN02.TEKST3 = 'Ejerbolig kr. ';                   
                  ELSE                                                  
                     DO;                                                
                        LIN02.TEKST3 = 'Ejerbolig(1) kr. ';             
                        LIN02.TEKST4 = '. ';                            
                        LIN02.TEKST5 = 'Ejerbolig(2) kr. ';             
                        LIN02.BEJEBOL2 =                                
                        BH601.EJEBOL(2).BEJEBOL;                        
                     END;                                               
                                                                        
               END;                                                     
                                                                        
            WHEN ('5','6')                      /* STUEHUSVÆRDI    */   
               DO;                                                      
                                                                        
                  LIN02.TEKST2 = '. ';                                  
                  LIN02.BEJEBOL1 =                                      
                  BH601.EJEBOL(1).BEJEBOL;                              
                  IF BH601.CREDIG2 = '5'                                
                  THEN                                                  
                     LIN02.TEKST3 = 'Stuehus kr. ';                     
                  ELSE                                                  
                     DO;                                                
                        LIN02.TEKST3 = 'Stuehus(1) kr. ';               
                        LIN02.TEKST4 = '. ';                            
                        LIN02.TEKST5 = 'Stuehus(2) kr. ';               
                        LIN02.BEJEBOL2 =                                
                        BH601.EJEBOL(2).BEJEBOL;                        
                     END;                                               
                                                                        
               END;                                                     
                                                                        
            WHEN ('7')          /* STUEHUS- OG EJERBOLIGVÆRDI */        
               DO;                                                      
                                                                        
                  LIN02.TEKST3 = 'Stuehus(1) kr. ';                     
                  LIN02.BEJEBOL1 =                                      
                  BH601.EJEBOL(1).BEJEBOL;                              
                  LIN02.TEKST4 = '. ';                                  
                  LIN02.TEKST5 = 'Ejerbolig(1) kr. ';                   
                  LIN02.BEJEBOL2 =                                      
                  BH601.EJEBOL(2).BEJEBOL;                              
                                                                        
               END;                                                     
                                                                        
            WHEN ('8')          /* EJERBOLIG- OG STUEHUSVÆRDI */        
               DO;                                                      
                                                                        
                  LIN02.TEKST3 = 'Stuehus(1) kr. ';                     
                  LIN02.BEJEBOL2 =                                      
                  BH601.EJEBOL(2).BEJEBOL;                              
                  LIN02.TEKST4 = '. ';                                  
                  LIN02.TEKST5 = 'Ejerbolig(1) kr. ';                   
                  LIN02.BEJEBOL1 =                                      
                  BH601.EJEBOL(1).BEJEBOL;                              
                                                                        
               END;                                                     
                                                                        
            OTHERWISE;                                                  
                                                                        
         END;   /* SELECT                */                             
         HJ.LIN_INDH = LINIE;                                           
         CALL ZS31101 (HJ.LIN_INDH,SLUT);                               
         LINIE = HJ.LIN_INDH;                                           
         BH61330N.ELINIENR = 02;                                        
         CALL SKRIV;                                                    
 END OPBYG_LIN02;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG LINIE 03 - LEJEVÆRDI, STANDARDFRADRAG, OVER-/UNDERSKUD */
 /*                                                                   */
 /*********************************************************************/
 OPBYG_LIN03:                                                           
         PROC;                                                          
         LINIE = ' ';                                                   
         IF BH601.BLEJE2 > 0                                            
         THEN                                                           
            LIN03.BLEJE = BH601.BLEJE2;                                 
         ELSE                                                           
            LIN03.BLEJE = BH601.BLEJE1;                                 
         LIN03.TEKST1 = 'Lejeværdi kr. ';/* 2% LEJEVÆRDI EJ SOMMERHUS */
         SELECT (BH601.CREDIG3);                                        
            WHEN ('6','7','8','9','A')                                  
               DO;                                                      
                  /* 1% LEJEVÆRDI PENSIONIST */                         
                  LIN03.TEKST1 = 'Lejeværdi 67-årig kr. ';              
               END;                                                     
            OTHERWISE                                                   
               DO;                                                      
                  IF (BH601.CBENYT = '08' !                             
                      BH601.CBENYT = '28' !                             
                      BH601.CBENYT = '48')                              
                  THEN                                                  
                     DO;                                                
                        IF (BH601.C_2PCT = '1' !                        
                            BH601.C_2PCT = '2' !                        
                            BH601.C_2PCT = '3')                         
                        THEN                                            
                           DO;                                          
                              /* 2% LEJEVÆRDI SOMMERHUS */              
                              LIN03.TEKST1 = 'Lejeværdi 2% kr. ';       
                           END;                                         
                        ELSE                                            
                           DO;                                          
                              /* 2,5% LEJEVÆRDI SOMMERHUS */            
                              LIN03.TEKST1 = 'Lejeværdi 2,5% kr. ';     
                           END;                                         
                     END;                                               
               END;                                                     
         END; /*select */                                               
         SELECT (BH601.CREDIG3);                                        
            WHEN ('1','6')                                              
               DO;                                                      
                  /* LEJEVÆRDI ??? %    */                              
                  /* U D E N  STD.FRAD. */                              
                  /* SAMT               */                              
                  /* U D E N  NETTORES. */                              
               END;                                                     
            WHEN ('2','7')                                              
               DO;                                                      
                  /* LEJEVÆRDI ??? %    */                              
                  /* M E D  STD.FRAD.   */                              
                  /* MEN                */                              
                  /* U D E N  NETTORES. */                              
                  LIN03.TEKST2 = '. Standardfradrag kr. ';              
                  LIN03.BSTDF = BH601.BSTDF;                            
               END;                                                     
            WHEN ('5','A')                                              
               DO;                                                      
                  /* LEJEVÆRDI ??? %    */                              
                  /* U D E N  STD.FRAD. */                              
                  /* MEN                */                              
                  /* M E D  NETTORES.   */                              
                  LIN03.TEKST2 = '. Overskud kr. ';                     
                  LIN03.BNETTO = BH601.BNETTO;                          
               END;                                                     
            OTHERWISE                                                   
               DO;                                                      
                  /* LEJEVÆRDI ??? %    */                              
                  /* M E D  STD.FRAD.   */                              
                  /* OG                 */                              
                  /* M E D  NETTORES.   */                              
                  LIN03.TEKST2 = ' - standardfradrag kr. ';             
                  LIN03.BSTDF  = BH601.BSTDF;                           
                  IF BH601.BNETTO >= 0                                  
                  THEN                                                  
                     LIN03.TEKST3 = ' = overskud kr. ';                 
                  ELSE                                                  
                     LIN03.TEKST3 = ' = underskud kr. ';                
                  LIN03.BNETTO = BH601.BNETTO;                          
               END;                                                     
         END; /* SELECT    */                                           
         HJ.LIN_INDH = LINIE;                                           
         CALL ZS31101 (HJ.LIN_INDH,SLUT);                               
         LINIE = HJ.LIN_INDH;                                           
         BH61330N.ELINIENR = 03;                                        
         CALL SKRIV;                                                    
 END OPBYG_LIN03;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG TEKSTLINIERNE                                          */
 /*                                                                   */
 /*********************************************************************/
 OPBYG_LINIE:                                                           
         PROC(TABTXT_IDX);                                              
 DCL     TABTXT_IDX      BIN FIXED (15);                                
         LINIE = TABTXT(TABTXT_IDX);                                    
         SELECT (TABTXT_IDX);                                           
            WHEN (04)                                                   
               DO;                                                      
                  HJDATO = BH601.FRADAG;                                
                  LIN04.DATO1 = HJDATO1, BY NAME;                       
                  LIN04.STREG = '-';                                    
                  HJDATO = BH601.TILDAG;                                
                  LIN04.DATO2 = HJDATO1, BY NAME;                       
                  LIN04.FDAGE = BH601.FDAGE;                            
                  if BH601.FDAGE = 1                                    
                  THEN                                                  
                     LIN04.TEKST3 = ' dag ';                            
                  ELSE                                                  
                     LIN04.TEKST3 = ' dage';                            
                  IF BH601.BLEJE1 = 0 &                                 
                     BH601.BLEJE2 = 0                                   
                  THEN                                                  
                     LIN04.TEKST4 = ' ';                                
                  ELSE                                                  
                     LIN04.TEKST4 = ' i beregningen ';                  
               END;                                                     
            WHEN (05)                                                   
               DO;                                                      
                  LIN05.GANE  = BH601.GANE;                             
                  LIN05.BNETTO =                                        
                            (BH601.BNETTO  * BH601.GANE) / 10000;       
                  IF BH601.BNETTO < 0                                   
                  THEN                                                  
                     LIN05.TEKST2 = ' af underskud kr. ';               
                  ELSE                                                  
                     LIN05.TEKST2 = ' af overskud kr. ';                
                  IF BH601.BNETTO = 0                                   
                  THEN                                                  
                     SUBSTR(LINIE,17,84) = ' ';                         
               END;                                                     
            WHEN (06)                                                   
               DO;                                                      
                  SELECT (BH601.CP20_3500);                             
                     WHEN (1)                                           
                        SUBSTR(LINIE,38,63) = 'kr. 1.000';              
                     WHEN (2)                                           
                        SUBSTR(LINIE,38,63) = '20%';                    
                     WHEN (3)                                           
                        SUBSTR(LINIE,38,63) = 'kr. 3.500';              
                     WHEN (4)                                           
                        SUBSTR(LINIE,38,63) = '(20% - kr. 2.500)';      
                     OTHERWISE;                                         
                  END;                                                  
               END;                                                     
            WHEN (07)                                                   
               DO;                                                      
                  IF BH601.CREDIG7 = '1'                                
                  THEN                                                  
                     LINIE = 'Ejendomsværdien indgår i beregningen af ov
 erskud af den tilstødende beboelsesejendom';                           
                  ELSE                                                  
                     LINIE = 'Ejendomsværdien indgår i beregningen af ov
 erskud af det tilstødende sommerhus';                                  
               END;                                                     
            WHEN (08)                                                   
               DO;                                                      
                  IF BH601.CBENYT = '01'                                
                  THEN                                                  
                     LINIE = 'Beregningen er foretaget for beboelsesejen
 dommen og den tilstødende grund under ét';                             
                  ELSE                                                  
                     LINIE = 'Beregningen er foretaget for sommerhuset o
 g den tilstødende grund under ét';                                     
               END;                                                     
            WHEN (09)                                                   
               DO;                                                      
                  IF BH601.CBENYT = '08'                                
                  THEN                                                  
                     LINIE = 'Lejeværdi af sommerhuset og den tilstødend
 e ubebyggede grund skal beregnes under ét';                            
                  ELSE                                                  
                     LINIE = 'Lejeværdi af beboelsesejendommen og den ti
 lstødende ubebyggede grund skal beregnes under ét';                    
               END;                                                     
            WHEN (10)                                                   
               DO;                                                      
                  LIN10.BELØB1 = BH601.BFSKAT;                          
                  IF BH601.BERHVAN ^= 0                                 
                  THEN                                                  
                     DO;                                                
                        IF BH601.CREDIG10 = '2'                         
                        THEN                                            
                           DO;                                          
                              LIN10.TEKST2 = '. Heraf skønnet erhvervsmæ
 ssig del kr. ';                                                        
                              LIN10.BELØB2 = BH601.BERHVAN;             
                           END;                                         
                        ELSE                                            
                           LIN10A.TEKST2 = '. Evt. erhvervsmæssig del an
 føres i regnskab/selvangivelse';                                       
                     END;                                               
               END;                                                     
            OTHERWISE;                                                  
         END;                                                           
 END OPBYG_LINIE;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      SKRIV TEKSTRECORD'S                                          */
 /*                                                                   */
 /*********************************************************************/
 SKRIV:                                                                 
         PROC;                                                          
         BH61330N.HPRTTKST = LINIE;                                     
         WRITE FILE (BH6130O) FROM (BH61330N);                          
         TV_TEKSTREC = TV_TEKSTREC + 1;                                 
 END SKRIV;                                                             
 END BH61330;                                                           
