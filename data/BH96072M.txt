 /*********************************************************************/
 /*      BH96072M:                                                   */ 
 /*      OPSÆT CEJBEVS (BEREGNINGSKODE) OG FEJLNR M.V. TIL EVS SLUT  */ 
 /*      VEDR. EJERENS PERSONLIGE FORHOLD                            */ 
 /*  SLUT05          SUSANNE KROGH VILLUMSEN            11/11-2005   */ 
 /*          TILRETTET SLUT 2005                                     */ 
 /*  SLUT06          BO SCHMIDT                         04/10-2006   */ 
 /*          TILRETTET SLUT 2006                                     */ 
 /*  SLUT07          BO SCHMIDT                         20/11-2007   */ 
 /*          TILRETTET SLUT 2007                                     */ 
 /*  SLUT08          BO SCHMIDT                         15/11-2008   */ 
 /*          TILRETTET SLUT 2008                                     */ 
 /*  SLUT12  LARS KAAE HARRITSHØJ                       06/11-2012   */ 
 /*          TILRETTET SLUT 2012                                     */ 
 /*  SLUT13  JØRGEN SAUGMANN                            08/10-2013   */ 
 /*          TILRETTET SLUT 2013                                     */ 
 /*                KRAV 18-02: FEJLKODE 5017 UDGåR UNDTAGEN HVIS     */ 
 /*                            EJERPCT. ER 0,00%                     */ 
 /*------------------------------------------------------------------*/ 
 /* EVS SLUT-15.  JNK :  Ingen ændringer                             */ 
 /* EVS SLUT-16   QEB :  Indlagt fejl 5164 fra Forskud 2017 KS30     */ 
 /********************************************************************/ 
 OPSÆT_PERSONLIGE_FEJLNR: PROC;                                         
                                                                        
         SELECT;                                                        
                                                                        
           WHEN ( BH945.CINVPNR ^= '0' )                                
             DO;                                                        
                BH945.FEJLNR = 5020;                                    
                BH945.TIL_EVS.CEJBEVS = '1';                            
             END;                                                       
                                                                        
           WHEN ( BH945.MT.CCIVS = 'D' &             /* EJER DØD FØR/ */
                 (BH945.MT.HIST_OPL(SLUTÅR).DCIÆN <= /* ELLER I INDKÅR*/
                  BH945.MT.DRGNÅRTIL)  &                                
                  BH945.FEJLNR ^= 5001 )                                
             DO;                                                        
                BH945.FEJLNR = 5004;                                    
                BH945.TIL_EVS.CEJBEVS = '1';                            
             END;                                                       
                                                                        
           WHEN ( BH945.AKT_EJER.CEJFORH = '50' ! /* EJEREN ER   */     
                  BH945.AKT_EJER.CEJFORH = '60' ! /* OFFENTLIG D.V.S. */
                  BH945.AKT_EJER.CEJFORH = '70' ! /* KOMM./AMT/STAT */  
                  BH945.AKT_EJER.CEJFORH = '80' )                       
             DO;                                                        
                BH945.FEJLNR = 5020;                                    
                BH945.TIL_EVS.CEJBEVS = '1';                            
             END;                                                       
                                                                        
           WHEN ( BH945.MT.DCPRN = 0 &       /*SLUT05  FLYTTET HERTIL*/ 
                  BH945.SORT_KOMNR ^= 881 &       /* EJER EJ PÅ MT + */ 
                  BH945.AKT_EJER.DCPRCIR > 0 &    /* EJER JVF. ESR   */ 
                  BH945.FEJLNR ^= 5001 )          /* ELLER OPRETTET  */ 
             DO;                                  /* P.G.A. GL. SLUT */ 
                BH945.FEJLNR = 5059;                                    
                BH945.TIL_EVS.CEJBEVS = '2';                            
             END;                                                       
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BH945.FEJLNR ^= 0                                           
         THEN                                                           
            RETURN;                                                     
                                                                        
         SELECT;                                                        
           WHEN ( BH945.AKT_EJER.FEJER = 1 &                            
                 (BH945.AKT_EJER.GANE > 10000 !                         
                 (BH945.AKT_EJER.GANE > 0 &                             
                  BH945.AKT_EJER.GANE < 10000)) )                       
             DO;                                                        
             END;                                                       
                                                                        
           WHEN ( BH945.AKT_EJER.GANE > 10000 )                         
             DO;                                                        
                  BH945.AKT_EJER.GANE = 10000;                          
             END;                                                       
                                                                        
           WHEN ( BH945.AKT_EJER.GANE = 0 )                             
             DO;                                                        
                  BH945.FEJLNR = 5017;                                  
             END;                                                       
                                                                        
           WHEN ( BH945.AKT_EJER.FEJER = 2 &                            
                 (BH945.AKT_EJER.CEJ_100 ^= '1' &                       
                  BH945.AKT_EJER.CEJ_100 ^= '2') )                      
             DO;                                                        
             END;                                                       
                                                                        
           WHEN ( BH945.AKT_EJER.FEJER > 2 &                            
                 (BH945.AKT_EJER.GANE = 0 !                             
                  BH945.AKT_EJER.CEJ_100 ^= '1') )                      
             DO;                                                        
             END;                                                       
                                                                        
           WHEN ( BH945.TIL_EVS.C5060 = '5060' &                        
                 (BH945.EVS_SLUT_EJER.SIDSTE.GPCTEJER = 0 !             
                  BH945.EVS_SLUT_EJER.SIDSTE.CNEDSL1 = '  ' !           
                  BH945.EVS_SLUT_EJER.SIDSTE.CPTYPSLUT = ' ') )         
             DO;                                                        
             /*    EJER OPRETTET MANGELFULDT P.G.A. GL. SLUT    */      
                BH945.FEJLNR = 5059;                                    
                BH945.TIL_EVS.CEJBEVS = '2';                            
             END;                                                       
                                                                        
           /* KS30 Forskud 2017 afvisning 5164 */                       
           WHEN (BH945.TIL_EVS.CENKE = '5'  &                           
                 BH945.HJ_67 ^= 'J'  &                                  
                 ER_BEGRÆNSET_SKATTEPLIGTIG = NEJ)                      
             DO;                                                        
                BH945.FEJLNR = 5164;                                    
                BH945.TIL_EVS.CEJBEVS = '2';                            
             END;                                                       
                                                                        
                                                                        
           OTHERWISE;                                                   
                                                                        
         END; /* SELECT */                                              
                                                                        
         IF BH945.FEJLNR = 5017 &                                       
            BH945.EVS_SLUT_EJER.DCPRN > 0 &                             
          ((BH945.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '0' !                  
            BH945.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '4' !                  
            BH945.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '5' !                  
            BH945.EVS_SLUT_EJER.SIDSTE.CEJBEVS = '6') &                 
            BH945.EVS_SLUT_EJER.SIDSTE.CSLET < '1') &                   
            BH945.EJERSKAB.BBERGRL_SW = 0 &                             
            BH945.TIL_EVS.DOVTG < BH945.MT.DRGNÅRFRA &                  
            BH945.EVS_SLUT_EJER.SIDSTE.GPCTEJER_SW = 0 &                
           (BH945.TIL_EVS.GPCTEJER_SW = -1 !                            
           (BH945.TIL_EVS.GPCTEJER ^=                                   
            BH945.EVS_SLUT_EJER.SIDSTE.GPCTEJER))                       
         THEN                                                           
            DO;                                                         
               BH945.TIL_EVS.GPCTEJER =                                 
               BH945.EVS_SLUT_EJER.SIDSTE.GPCTEJER;                     
               BH945.TIL_EVS.GPCTEJER_SW = 0;                           
               BH945.TIL_FORSKUD.COPR_GPCTEJER = 'S';                   
               BH945.TIL_EVS.CDATAFRA = '3';                            
               BH945.FEJLNR = 0;                                        
            END;                                                        
                                                                        
 END OPSÆT_PERSONLIGE_FEJLNR;                                           
  /********************************************************************/
 /* UNDERSØG OM EN BORGER ER BEGRÆNSET SKATTEPLIGTIG. FINDES MT DATA */ 
 /* ANVENDES DE ELLERS UNDERSØGES SIDSTE EVS SLUT ÅR                 */ 
 /********************************************************************/ 
 ER_BEGRÆNSET_SKATTEPLIGTIG:PROC RETURNS(BIT(1));                       
                                                                        
   DCL HJBEGRÆNSET_BIT BIT(1);                                          
                                                                        
   HJBEGRÆNSET_BIT = NEJ;                                               
   HJBEGRÆNSET_TP = '0';                                                
                                                                        
   IF BH945.MT.DCPRN > 0 &                                              
      BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM > '0'                           
   THEN                                                                 
     DO;                                                                
       IF BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM = '1' !                     
          BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM = '3' !                     
          BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM = '8'                       
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP = BH945.MT.HIST_OPL(SLUTÅR).CSKPLOM;          
         END;                                                           
     END;                                                               
   ELSE                                                                 
     DO;                                                                
       IF BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF = '1' !        
          BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF = '3' !        
          BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF = '8'          
       THEN                                                             
         DO;                                                            
           HJBEGRÆNSET_BIT = JA;                                        
           HJBEGRÆNSET_TP =                                             
                BH945.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF;         
         END;                                                           
     END;                                                               
                                                                        
   RETURN(HJBEGRÆNSET_BIT);                                             
                                                                        
 END ER_BEGRÆNSET_SKATTEPLIGTIG;                                        
                                                                        
                                                                        
