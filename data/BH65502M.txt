 /**********************************************************************
 * BH65502M NY UDGAVE AF BH65500M MED STIGENE PENSIONS ALDER           *
 *                                                                     *
 * DENNE MACRO BRUGES VED EJENDOMSVÆRDISKATTE-BEREGNING TIL HENHOLDVIS *
 * SLUT OG FORSKUD TIL BEREGNING AF EN PERSONS ALDER PR. EN DATO       *
 *                                                                     *
 * OBS.: 'PR-DAGEN' ER PR. 31/12 I DET AKTUELLE ÅR F.EKS. 20181231.    *
 *                                                                     *
 * FOR 'DØDE' PERSONER SKAL DOG  A L T I D  BENYTTES 'DØDSDAGEN'       *
 * D.V.S. CIVILSTANDSÆNDRINGSDATOEN  UNDTAGEN HVIS DENNE               *
 * ER EFTER SLUTDATOEN FOR DET AKTUELLE REGNSKABSÅR.                   *
 *                                                                     *
 * 'PR-DAGEN' OPSÆTTES I DET KALDENDE PROGRAM.                         *
 *                                                                     *
 * MACRO INDEHOLDER TO FUNKTIONER:                                     *
 * 1) UNDERSØGE OM PERSON ER ALDERSPENSIONIST PÅ EN DATO               *
 * 2) UNDERSØG  OM PERSON ER HAR EVS FØRTIDSPENSIONIST ALDER           *
 *                                                                     *
 * SLUT 2021    Z6EFL                                   Marts 2021     *
 *              - BH65300M erstattes med BH55300M                      *
 * SLUT 2024    Z6JJJ                                   Juni  2024     *
 *              - BH55600M erstatter BH55300M                          *
 **********************************************************************/
 ER_ALDERS_PENSIONIST: PROC (PRDATO,DCPRN) RETURNS (BIT(1));            
                                                                        
 DCL     RETURBIT        BIT       (1);                                 
 DCL     PRDATO          FIXED     (9);                                 
 DCL     DCPRN           PIC       '(10)9';                             
 DCL     ÅR_TIL_PENSION  BIN FIXED(31); /* ÅR TIL PENSION */            
 DCL     HJALDER         FIXED(3);                                      
 DCL     HJALDER_GRÆNSE  FIXED(3);                                      
 DCL     GL_REGLER       BIT(1);                                        
 DCL     DATO_GL_REGL    FIXED(9);                                      
                                                                        
    GL_REGLER      = '0'B;                                              
    HJALDER_GRÆNSE = 0;                                                 
    HJALDER        = 0;                                                 
    DATO_GL_REGL   = 20040630;                                          
                                                                        
    /*Fejlrettelse EVS slut 2018 */                                     
    IF PRDATO = 0                                                       
    THEN                                                                
      DO;                                                               
        PUT SKIP LIST('ALDERS PRDATO sat lig ', BH55600M.ÅR1231);       
        PUT SKIP LIST('for DCPRN',DCPRN);                               
        PRDATO = BH55600M.ÅR1231;                                       
      END;                                                              
                                                                        
    HJALDER = FIND_ALDER(PRDATO,DCPRN);                                 
                                                                        
    /* Undersøg om personene skal være omfattede af de gl */            
    /* regler altså være fyldt 65 før den 20040630        */            
                                                                        
    HJALDER_GRÆNSE = FIND_ALDER(DATO_GL_REGL,DCPRN);                    
                                                                        
    IF HJALDER_GRÆNSE >= 65                                             
    THEN                                                                
      GL_REGLER = '1'B;                                                 
                                                                        
    /* Dato checkes af hensyn til gl. døde der skal undersøges */       
    /* Fra deres døds dato som kan lige langt tilbage          */       
                                                                        
    IF GL_REGLER = '0'B                                                 
    THEN                                                                
      DO;                                                               
        ÅR_TIL_PENSION = TID_TIL_PENS(DCPRN,HJALDER);                   
      END;                                                              
    ELSE   /* gl regl af hensyn til døde */                             
      DO;                                                               
        IF HJALDER >= 67                                                
        THEN                                                            
          ÅR_TIL_PENSION = 0;                                           
        ELSE                                                            
          ÅR_TIL_PENSION = 67 - HJALDER;                                
      END;                                                              
                                                                        
    IF ÅR_TIL_PENSION > 0                                               
    THEN                                                                
      RETURBIT = '0'B;                                                  
    ELSE                                                                
      RETURBIT = '1'B;                                                  
                                                                        
    RETURN (RETURBIT);                                                  
                                                                        
 END ER_ALDERS_PENSIONIST;                                              
 /*********************************************************************/
 /* FOR AT BLIVE BEHANDLET EVS FØRTIDS PENSIONIST SKAL EN PERSON      */
 /* VÆRE FYLDT MINDST 60 ÅR OG IKKE VÆRE BLEVET ALDERS PENSIONIST     */
 /*********************************************************************/
 HAR_EVS_FØRTIDS_PENSIONIST_ALDER:PROC(PRDATO,DCPRN) RETURNS (BIT(1));  
                                                                        
 DCL     RETURBIT        BIT       (1);                                 
 DCL     PRDATO          FIXED     (9);                                 
 DCL     DCPRN           PIC       '(10)9';                             
 DCL     ÅR_TIL_PENSION  BIN FIXED(31); /* ÅR TIL PENSION */            
                                                                        
                                                                        
 DCL     HJALDER         FIXED(3);                                      
 DCL     HJALDER_GRÆNSE  FIXED(3);                                      
 DCL     GL_REGLER       BIT(1);                                        
 DCL     DATO_GL_REGL    FIXED(9);                                      
                                                                        
    /* Undersøg om personene skal være omfattede af de gl */            
    /* regler altså være fyldt 67 den 20040630            */            
    GL_REGLER      = '0'B;                                              
    HJALDER_GRÆNSE = 0;                                                 
    HJALDER        = 0;                                                 
    DATO_GL_REGL   = 20040630;                                          
                                                                        
    HJALDER_GRÆNSE = FIND_ALDER(DATO_GL_REGL,DCPRN);                    
                                                                        
    IF HJALDER_GRÆNSE >= 65                                             
    THEN                                                                
      GL_REGLER = '1'B;                                                 
                                                                        
    /*Fejlrettelse EVS slut 2018 */                                     
    IF PRDATO = 0                                                       
    THEN                                                                
      DO;                                                               
        PUT SKIP LIST('F PENSION PRDATO sat lig ', BH55600M.ÅR1231);    
        PUT SKIP LIST('FOR DCPRN',DCPRN);                               
        PRDATO = BH55600M.ÅR1231;                                       
      END;                                                              
                                                                        
    HJALDER = FIND_ALDER(PRDATO,DCPRN);                                 
                                                                        
    /* Dato checkes af hensyn til gl. døde der skal undersøges */       
    /* Fra deres døds dato som kan lige langt tilbage          */       
    IF GL_REGLER = '0'B                                                 
    THEN                                                                
      DO;                                                               
        IF HJALDER >= 60                                                
        THEN                                                            
          DO;                                                           
            ÅR_TIL_PENSION = TID_TIL_PENS(DCPRN,HJALDER);               
                                                                        
            IF ÅR_TIL_PENSION > 0                                       
            THEN                                                        
              RETURBIT = '1'B;                                          
            ELSE                                                        
              RETURBIT = '0'B;                                          
          END;                                                          
        ELSE                                                            
          DO;                                                           
            RETURBIT = '0'B;                                            
          END;                                                          
      END;                                                              
    ELSE   /* gl regl af hensyn til døde */                             
      DO;                                                               
        IF HJALDER >= 60 & HJALDER < 67                                 
        THEN                                                            
          RETURBIT = '1'B;                                              
        ELSE                                                            
          RETURBIT = '0'B;                                              
      END;                                                              
                                                                        
    RETURN (RETURBIT);                                                  
                                                                        
 END HAR_EVS_FØRTIDS_PENSIONIST_ALDER;                                  
 /*********************************************************************/
 %INCLUDE BS36009M; /* Find antal år til alderspension                */
 %INCLUDE BH36012M; /* Find alder ved dato udfra personnummer         */
