  PROCESS OPT(TIME);                                                    
 BH95002: /* OPSÆT DIVERSE EVS-KOMPONENTER FOR GL. OG NYT SLUTÅR */     
 PROCEDURE (BH950_AR);                                                  
 /*********************************************************************/
                                                                        
 DCL COPYRIGHT CHAR (30) STATIC INIT ('COPYRIGHT KOMMUNEDATA I/S 2003');
                                                                        
 /**********************************************************************
 * FORMÅL: BRUGES I EVS-SYSTEMET TIL OPSÆT-/BEREGNING AF:              *
 *                                                                     *
 *         DIVERSE KOMPONENTER VEDR. NYT SLUT INDKOMSTÅR TIL BRUG VED  *
 *         BEREGNING AF EVS FOR AKTUELT FORSKUDSÅR.                    *
 *                                                                     *
 *         DE OPSATTE/BEREGNEDE DATA SKAL BENYTTES TIL BEREGNING       *
 *         AF SAMMENLIGNINGSGRUNDLAG FOR EVT. STIGNINGSBEGRÆNSNING     *
 *         FRA AKTUELT SLUTÅR TIL AKTUELT FORSKUDSÅR.                  *
 *         (DETTE BELØB SKAFFES VIA MAKRO BR48680M).                   *
 *                                                                     *
 *  PROGRAMMERET AF AAGE RASMUSSEN   PUS T&S    AUGUST 2003            *
 *                                                                     *
 *  PROGRAMMET ERSTATTER PROGRAM BH95005 I.F.M. FORSKUD 2004 PÅ GRUND  *
 *  AF ÆNDREDE REGLER SOM FØLGE AF SKATTESTOP-LOVEN (PROGRAM BH95005   *
 *  SKAL BRUGES IGEN TIL FORSKUD 2005 DA DER HER IGEN KAN VÆRE STIG-   *
 *  NINGSBEGRÆNSNING FRA GL. TIL NYT SLUTÅR).                          *
 *                                                                     *
 *  RETTET AF                                                          *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
                                                                        
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF DIVERSE PARAMETRE TIL DETTE MODUL                 */
 /*********************************************************************/
                                                                        
 DCL     1 BH950_AR,                                                    
           %INCLUDE BH95002N;                                           
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
                                                                        
 DCL     MULTIPLY        BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF DIV. HJÆLPEFELTER                                 * 
 *********************************************************************/ 
                                                                        
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     HJVÆRDI1        FIXED (13,3);                                  
                                                                        
 DCL     1 HJ,                                                          
           3 BGRPROM1    FIXED (11),   /* BERGRUNDLAG FOR 10 PROM EVS */
           3 BGRPROM2    FIXED (11),   /* BERGRUNDLAG FOR 30 PROM EVS */
           3 BPROM1      FIXED (11,2), /* BELØB FOR 10 PROMILLE EVS   */
           3 BPROM2      FIXED (11,2); /* BELØB FOR 30 PROMILLE EVS   */
                                                                        
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
                                                                        
         /* INITIER DIVERSE FELTER INDEN BEREGNING */                   
                                                                        
         BH950_AR.FRA = '';                                             
                                                                        
         DO I = 1 TO 3;                                                 
            BH950_AR.FEJLTAB(I) = 0;                                    
         END;                                                           
                                                                        
         HJ.BGRPROM1 = 0;                                               
         HJ.BGRPROM2 = 0;                                               
         HJ.BPROM1 = 0;                                                 
         HJ.BPROM2 = 0;                                                 
                                                                        
         /* KONTROLLER DIVERSE FELTER INDEN BEREGNING */                
                                                                        
         CALL UDFØR_KONTROLLER;                                         
                                                                        
 /*********************************************************************/
 /* RETURKODE SÆTTES LIG 0 HVIS INGEN FEJL FOREKOMMER (BEREGNING OK)  */
 /* RETURKODE SÆTTES LIG FEJLNR FOR FØRSTE FEJLTAB(I) > 0             */
 /*********************************************************************/
                                                                        
         BH950_AR.RETURKODE = 0; /* BEREGNING OK */                     
                                                                        
         DO I = 1 TO 3;                                                 
            IF BH950_AR.FEJLTAB(I) > 0                                  
            THEN                                                        
               DO;                                                      
                  /* DATA TIL BEREGNING ER IKKE OK   */                 
                  /* SÆT RETURKODE LIG FØRSTE FEJL */                   
                  BH950_AR.RETURKODE = BH950_AR.FEJLTAB(I);             
                  I = 3;                                                
               END;                                                     
         END;                                                           
                                                                        
         IF BH950_AR.RETURKODE = 0                                      
         THEN                                                           
            CALL OPSÆT_NY_SLUTÅR_KOMPO;                                 
                                                                        
 /*********************************************************************/
 /* FØLGENDE KONTROLLER UDFØRES VED HVERT KALD AF PROGRAMMET          */
 /* FOR AT SIKRE KORREKT BEREGNING AF EJENDOMSVÆRDISKATTEN            */
 /*                                                                   */
 /*TAB FEJL FEJL                                                      */
 /* NR   NR TEKST                                                     */
 /* ----------------------------------------------------------------- */
 /*  1 5032 KODE FOR EJENDOMSTYPE FORKERT                             */
 /*  2 5033 BEREGNINGSGRUNDLAG MANGLER                                */
 /*  3 5055 2-FAMILIE-BEREGNINGSGRUNDLAG MANGLER                      */
 /*                                                                   */
 /*  FEJLTAB(NR) OPSÆTTES LIG SAMTLIGE FUNDNE FEJLNUMRE               */
 /*  RETURKODE SÆTTES LIG FØRST FUNDNE FEJLNR                         */
 /*********************************************************************/
 UDFØR_KONTROLLER:                                                      
 PROC;                                                                  
                                                                        
         IF BH950_AR.CEJDTYPE = '1' !                                   
            BH950_AR.CEJDTYPE = '3' !                                   
            BH950_AR.CEJDTYPE = '4' !                                   
            BH950_AR.CEJDTYPE = '5' !                                   
            BH950_AR.CEJDTYPE = '7' !                                   
            BH950_AR.CEJDTYPE = '8' !                                   
            BH950_AR.CEJDTYPE = '9'                                     
         THEN;                                                          
         ELSE                                                           
            BH950_AR.FEJLTAB(1) = 5032;                                 
                                                                        
         IF BH950_AR.BBERGRLGL_SW = -1                                  
         THEN                                                           
            BH950_AR.FEJLTAB(2) = 5033;                                 
                                                                        
         IF (BH950_AR.CEJDTYPE = '4' !                                  
             BH950_AR.CEJDTYPE = '5') &                                 
            BH950_AR.BBER2FAMGL_SW = -1                                 
         THEN                                                           
            BH950_AR.FEJLTAB(3) = 5055;                                 
                                                                        
 END UDFØR_KONTROLLER;                                                  
                                                                        
 /*********************************************************************/
 /*      BEREGNING AF EJENDOMSVÆRDISKATTE-KOMPONENTER FOR NYT SLUTÅR  */
 /*********************************************************************/
 OPSÆT_NY_SLUTÅR_KOMPO:                                                 
 PROC;                                                                  
                                                                        
         IF BH950_AR.BBERGRLGL > 3040000      /* SKAL EVT. RETTES NÅR */
         THEN                                 /* DEN RIGTIGE GRÆNSE-  */
            DO;                               /* VÆRDI KENDES         */
               HJ.BGRPROM1 = 3040000;                                   
               HJ.BGRPROM2 = BH950_AR.BBERGRLGL - 3040000;              
            END;                                                        
         ELSE                                                           
            HJ.BGRPROM1 = BH950_AR.BBERGRLGL;                           
                                                                        
         /* BEREGN EJENDOMSVÆRDISKAT 10 PROMILLE */                     
         HJ.BPROM1 = MULTIPLY(HJ.BGRPROM1,0.010,11,2);                  
                                                                        
         /* BEREGN EJENDOMSVÆRDISKAT 30 PROMILLE */                     
         IF HJ.BGRPROM2 > 0                                             
         THEN                                                           
            HJ.BPROM2 = MULTIPLY(HJ.BGRPROM2,0.030,11,2);               
                                                                        
         BH950_AR.BGLEVS10 = HJ.BPROM1 + HJ.BPROM2;                     
                                                                        
         /* BEREGN 2 PROMILLE NEDSLAG - EJENDOM EJET 1/7-98 */          
         HJVÆRDI1 = (MULTIPLY(BH950_AR.BBERGRLGL,0.002,11,3)) + 0.005;  
         BH950_AR.BGLEVS2 = HJVÆRDI1;                                   
                                                                        
         /* BEREGN 4 PROMILLE NEDSLAG - EJENDOM EJET 1/7-98 */          
         SELECT (BH950_AR.CNEDSL2);                                     
           WHEN ('1')                                                   
             DO;                                                        
                /* BEREGN 4 PROMILLE NEDSLAG - 1-FAM-HUS */             
                HJVÆRDI1 =                                              
                (MULTIPLY(BH950_AR.BBERGRLGL,0.004,11,3)) + 0.005;      
                /* NEDSLAGET KAN MAX. VÆRE 1200,00 KR. */               
                IF HJVÆRDI1 < 1200.000                                  
                THEN                                                    
                   BH950_AR.BGLEVS4 = HJVÆRDI1;                         
                ELSE                                                    
                   BH950_AR.BGLEVS4 = 1200.00;                          
             END;                                                       
           WHEN ('4')                                                   
             DO;                                                        
                /* BEREGN 4 PROMILLE NEDSLAG - 2-FAM-HUS */             
                /* MED KUN EEN EJERBOLIGVÆRDI            */             
                HJVÆRDI1 =                                              
                (MULTIPLY(BH950_AR.BBER2FAMGL,0.004,11,3)) + 0.005;     
                /* NEDSLAGET KAN MAX. VÆRE 2400,00 KR. */               
                IF HJVÆRDI1 < 2400.000                                  
                THEN                                                    
                   BH950_AR.BGLEVS4 = HJVÆRDI1;                         
                ELSE                                                    
                   BH950_AR.BGLEVS4 = 2400.00;                          
             END;                                                       
           WHEN ('5')                                                   
             DO;                                                        
                /* BEREGN 4 PROMILLE NEDSLAG             */             
                /* 2-FAM-HUS MED 2 EJERBOLIGVÆRDIER      */             
                HJVÆRDI1 =                                              
                (MULTIPLY(BH950_AR.BBER2FAMGL,0.004,11,3)) + 0.005;     
                /* NEDSLAGET KAN MAX. VÆRE 1200,00 KR. */               
                IF HJVÆRDI1 < 1200.000                                  
                THEN                                                    
                   BH950_AR.BGLEVS4 = HJVÆRDI1;                         
                ELSE                                                    
                   BH950_AR.BGLEVS4 = 1200.00;                          
             END;                                                       
           OTHERWISE; /* INTET NEDSLAG */                               
         END;                                                           
                                                                        
         /* BEREGN 4 PROMILLE NEDSLAG FOR PERSONER MED */               
         /* STATUS SOM 67-ÅRIGE M.V.                   */               
                                                                        
         IF BH950_AR.CEJDTYPE = '1' !                                   
            BH950_AR.CEJDTYPE = '4' !                                   
            BH950_AR.CEJDTYPE = '5' !                                   
            BH950_AR.CEJDTYPE = '7' !                                   
          ((BH950_AR.CEJDTYPE = '3' ! BH950_AR.CEJDTYPE = '8') &        
          ((BH950_AR.CHELÅR >= '1' & BH950_AR.CHELÅR <= '3') !          
           (BH950_AR.CHELÅR >= '7' & BH950_AR.CHELÅR <= '9')))          
         THEN                                                           
            DO;                                                         
               /* HELÅRSBOLIG */                                        
               HJVÆRDI1 =                                               
               (MULTIPLY(BH950_AR.BBERGRLGL,0.004,11,3)) + 0.005;       
               /* NEDSLAGET KAN MAX. VÆRE 6000,00 KR. */                
               IF HJVÆRDI1 < 6000.000                                   
               THEN                                                     
                  BH950_AR.BGLEVSP = HJVÆRDI1;                          
               ELSE                                                     
                  BH950_AR.BGLEVSP = 6000.00;                           
            END;                                                        
         ELSE                                                           
            DO;                                                         
               /* FRITIDSBOLIG */                                       
               HJVÆRDI1 =                                               
               (MULTIPLY(BH950_AR.BBERGRLGL,0.004,11,3)) + 0.005;       
               /* NEDSLAGET KAN MAX. VÆRE 2000,00 KR. */                
               IF HJVÆRDI1 < 2000.000                                   
               THEN                                                     
                  BH950_AR.BGLEVSP = HJVÆRDI1;                          
               ELSE                                                     
                  BH950_AR.BGLEVSP = 2000.00;                           
            END;                                                        
                                                                        
 END OPSÆT_NY_SLUTÅR_KOMPO;                                             
                                                                        
                                                                        
 END BH95002;                                                           
