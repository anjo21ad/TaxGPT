  PROCESS OPT(TIME);                                                    
 BE85300: /* DAN DATA TIL EVS EJENDOMSTABEL TIL FORSKUD               */
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2002');       
 /**********************************************************************
 *                                                                     *
 * FORMÅL :                                                            *
 * LEVERING AF OPLYSNINGER TIL FORSKUDS EVS EJENDOMSTABEL (BS01500T).  *
 *                                                                     *
 * INPUT     : BE75000T - ESR (FORSKUD)                                *
 *             BE76000T - MANDTAL (FORSKUD)                            *
 *             BE77000T - EVS_UDTRÆK (FORSKUD)                         *
 *             BE78000T - EVS_RESULTAT (FORSKUD)                       *
 *                                                                     *
 * OUTPUT    : B.E85300D UDTRÆK - ALLE PERSONER                        *
 *             BE85301Y  KØRSELSKONTROLLISTE                           *
 *                                                                     *
 * PROGRAMMØR: AAGE RASMUSSEN            Aug. 1999                     *
 * RETTET    : Tina Carstens             Feb. 2022                     *
 *             Kopieret fra BH85300                                    *
 *             Forskud 2024                                            *
 *                                                                     *
 * Forskud2023 Defect 30                 EBD           Juli 2022       *
 * FORSKUD2023 Udgåede ejendomme         PBR           08.09.2022      *
 * FORSKUD2025 Tina Carstens, TIC                      16.05.2023      *
 * Forskud2025 Per Jensen, PEN Marts 2024         BF17 25.03.2024      *
 *             Ukendt VURMARK kode - 299 (tidligere 99) bliver nu      *
 *             opdateret direkte.                                      *
 *             Interval for VURMARK udvidet fra 0-99 til 0-299         *
 * Forskud2025 Jan Jensen, JJJ Juni 2024          BF17 20.06.2024      *
 *             BE55600M erstatter BE55300M                             *
                                                                        
 * Forskud2025 Per Jensen, PEN     BF17 ÆA65           02.07.2024      *
 *             Justering så 'Ukendt VURMARK kode' - 299 bliver sat op  *
 *             uden hensyntagen til tidligere værdi - blev tidligere   *
 *             kun sat hvis VURMARK var = 0                            *
 * Forskud2025 Per Brunk     Defect 2669               13.08.2024      *
 *             Fejlnummer 5310, 5322, 5324, 5326 skal ikke sættes på   *
 *             Ejendommen, BS01500T                                    *
 *             Fejlnummer 5242 må ikke komme på BS01500T               *
 ***********************************************************************
 *                                                                     *
 *                                                                     *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
 /**********************************************************************
 *                       DECLARE AF REGISTRE                           *
 **********************************************************************/
 DCL     BE853O FILE RECORD OUTPUT;     /* LOAD-GRUNDLAG TIL EVS      */
 /**********************************************************************
 *       DECLARE AF UDTRÆK                                             *
 **********************************************************************/
 DCL     1 BE853,                                                       
           %INCLUDE BE85300N;                                           
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
         EXEC SQL INCLUDE SQLCA;     /* SQL KOMMUNIKATIONSAREAL       */
                                                                        
         EXEC SQL INCLUDE BE75000T;  /* ESR (FORSKUD)                 */
         EXEC SQL INCLUDE BE76000T;  /* MANDTAL (FORSKUD)             */
         EXEC SQL INCLUDE BE77000T;  /* EVS_UDTRÆK (FORSKUD)          */
         EXEC SQL INCLUDE BE78000T;  /* EVS_RESULTAT (FORSKUD)        */
                                                                        
         /* BE780_SW Indikatorvariable til BE78000T                   */
         %INCLUDE BE85501N;                                             
                                                                        
         /* BE770_SW Indikatorvariable til BE77000T                   */
 DCL     1 BE770_SW,                                                    
          2 EVSFEJ_EVSEFTERRABAT_SW          BIN FIXED (15),            
          2 EVSFEJ_RABATBER_EJERANDEL_SW     BIN FIXED (15),            
          2 EVSFEJ_EJERANDEL_SW              BIN FIXED (15),            
          2 EVSFÆG_EVSEFTERRABAT_SW          BIN FIXED (15),            
          2 EVSFÆG_RABATBER_EJERANDEL_SW     BIN FIXED (15),            
          2 EVSFÆG_EJERANDEL_SW              BIN FIXED (15);            
                                                                        
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BE85300L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (' '),           
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN,                                                      
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
 EXTERNAL INIT ('NOTEST(ALL,,;)');                                      
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
 DCL     I               BIN FIXED (31);                                
 DCL     HJ_FEJLNR       CHAR      (4);                                 
 DCL     HJ_FEJLNR_PIC   PIC       '(4)9'  BASED(ADDR(HJ_FEJLNR));      
 DCL     TEKST45         CHAR     (45);                                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     GEM_EKOMNR      BIN FIXED(15)       INIT      (0);             
 DCL     GEM_EEJDNR      BIN FIXED(31)       INIT      (0);             
 DCL     GEM_EENHEDLBNR  BIN FIXED(15)       INIT      (0);             
                                                                        
 DCL     1 BE55600M,                                                    
           %INCLUDE BE55600M;        /*#01*/                            
                                                                        
 DCL     WS_FORSKUD_SLUT_TXT            CHAR(16) INIT (' ');            
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
 DCL     WS_GLSLUTÅR                    BIN FIXED (15);                 
 DCL     WS_PROGRAMNAVN                 CHAR(8)  INIT ('BE85300');      
                                                                        
 DCL     WS_VURMARK_UKENDT              FIXED (3) INIT (299);           
 DCL     WS_MAX_VURMARK                 FIXED (3) INIT (299);           
                                                                        
 DCL     HJ_VURD_A_VURMARK_AKT          DEC (3,0);                      
 DCL     HJ_EJERSKAB_VURMARK            DEC (3,0);                      
                                                                        
 /*********************************************************************/
 /* TIL BE85111M                                                      */
 /*********************************************************************/
 DCL     RESULT_FUNDET         BIT       (1);                           
 DCL     MT_FUNDET             BIT       (1);                           
 DCL     SLUT_FUNDET           BIT       (1);                           
 DCL     ESR_FUNDET            BIT       (1);                           
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     FEJLTEKST      CHAR (20)        INIT(' ');                     
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BE85300'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         CALL TERMINER_LISTE(' ');                                      
         CALL TERMINER_LISTE                                            
                      ('***           SKAT EVS FORSKUD           ***'); 
         CALL TERMINER_LISTE                                            
                      ('***       BE85300 - Dan EVS Ejendom      ***'); 
         CALL TERMINER_LISTE                                            
                      ('***              (BS01500T)              ***'); 
         CALL TERMINER_LISTE('***         '!!                           
                              BE55600M.AFVIKLINGS_ID!!' '!!             
                              BE55600M.INDKÅR2!!'         ***');        
         CALL TERMINER_LISTE(' ');                                      
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BE85300L');             
            END;                                                        
                                                                        
         SELECT (BE55600M.AFVIKLINGS_ID);                               
            WHEN ('OPRET_EVS_FORSK')                                    
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BE55600M.FORSKÅR;                    
               END;                                                     
            WHEN ('OPRET_EVS_SLUT')                                     
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BE55600M.SLUTÅR;                     
               END;                                                     
            WHEN ('SUPPL_EVS_SLUT')                                     
               DO;                                                      
                  WS_FORSKUD_SLUT_TXT = BE55600M.AFVIKLINGS_ID;         
                  WS_TIL_EVS_AAR = BE55600M.SLUTÅR;                     
               END;                                                     
            OTHERWISE                                                   
               DO;                                                      
                  ABENDTXT = '101 PARAMETERFEJL '!!                     
                              BE55600M.AFVIKLINGS_ID;                   
                  CALL PROGRAM_FEJL;                                    
               END;                                                     
         END; /* select */                                              
                                                                        
         PUT SKIP EDIT ('Start ' !! WS_PROGRAMNAVN !!                   
                        ' År '   !! WS_TIL_EVS_AAR)                     
                       (A,A,A,P'9999');                                 
                                                                        
         /********************************************************/     
         /*  NU STARTER NORMALT FLOW                             */     
         /********************************************************/     
                                                                        
         OPEN FILE (BE853O)  TITLE ('BE85300O');                        
                                                                        
         CALL ZZ20390 ('*OPEN','BE85301Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         OVERLIN1.TEKST2 = 'DAN LOADGRUNDLAG TIL EVS EJENDOMSTABEL ' !! 
                           'FORSKUD ' !! BE55600M.INDKÅR2; /*#01*/      
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
         /* Erklæring og initiering af tællere                        */
         %INCLUDE BE85102M;                                             
                                                                        
         /*HOVED STYRING FRA BH95100L HER SQL KODE ? og orderby      */ 
         CALL CLOSE_RESULTCURS;                                         
         CALL OPEN_RESULTCURS;                                          
         CALL FETCH_RESULT;       /* BE78 */                            
                                                                        
         DO WHILE (RESULT_FUNDET);                                      
                                                                        
            CALL SELECT_ESR;      /* BE75 */                            
            CALL SELECT_MT;       /* BE76 */                            
            CALL SELECT_SLUT;     /* BE77 */                            
                                                                        
            CALL BEHANDL;                                               
                                                                        
            CALL FETCH_RESULT;    /* BE78 */                            
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       EJENDOMME TIL EVS - KUN 1 INDIVID PR. EKOMNR/EEJDNR          * 
 *********************************************************************/ 
 BEHANDL: PROC;                                                         
                                                                        
        /* IKKE EVS RELEVANT EJD. SKULLE HAVDE VÆRET */                 
        /* OVERLÆST I BE31800L                       */                 
        IF DCLBE78000T.TILEVS_A_V_CBENYT = '78'                         
        THEN                                                            
           RETURN;                                                      
                                                                        
        IF DCLBE78000T.FEJLNR = 5060                                    
        THEN                                                            
          RETURN;                                                       
                                                                        
        /* Disse skal ikke oprettes fejlnummer 5500 findes */           
        /* kun internt i EVS oprettelsen se BE85000 KS25   */           
        IF DCLBE78000T.FEJLNR = 5500                                    
         & DCLBE78000T.EKOMNR = 999                                     
        THEN                                                            
           RETURN;                                                      
                                                                        
                                                                        
        // PBR 08.09.2022                                               
        // Fejlnummer 5502 findes kun internt i EVS oprettelsen         
        // se BE85000  D864                                             
        IF DCLBE78000T.FEJLNR = 5502                                    
        THEN                                                            
          DCLBE78000T.FEJLNR = 5001;                                    
                                                                        
        /* Disse skal ikke oprettes fejlnummer 5503 findes */           
        /* kun internt i EVS oprettelsen se BE85000  D782  */           
        IF DCLBE78000T.FEJLNR = 5503                                    
        THEN                                                            
           RETURN;                                                      
                                                                        
        IF DCLBE78000T.FEJLNR > 0                                       
        THEN                                                            
           DO;                                                          
              INDEX                    = DCLBE78000T.FEJLNR;            
              LÆST_FEJLNR(INDEX).ANTAL = LÆST_FEJLNR(INDEX).ANTAL + 1;  
           END;                                                         
                                                                        
        IF GEM_EKOMNR     = DCLBE78000T.EKOMNR                          
         & GEM_EEJDNR     = DCLBE78000T.EEJDNR                          
         & GEM_EENHEDLBNR = DCLBE78000T.EENHEDLBNR                      
        THEN                                                            
           RETURN;                                                      
                                                                        
        GEM_EKOMNR     = DCLBE78000T.EKOMNR;                            
        GEM_EEJDNR     = DCLBE78000T.EEJDNR;                            
        GEM_EENHEDLBNR = DCLBE78000T.EENHEDLBNR;                        
                                                                        
        TV.LÆST_EJD = TV.LÆST_EJD + 1;                                  
                                                                        
        IF DCLBE78000T.EKOMNR = 999                                     
         & DCLBE78000T.EJENDOM_CLANDEKODE > ' '                         
        THEN                                                            
           TV.LÆST_CLANDEKODE = TV.LÆST_CLANDEKODE + 1;                 
                                                                        
        BE853             = '';                                         
        BE853.DINDKAAR    = BE55600M.FORSKÅR;           /*#01*/         
        BE853.EKOMNR      = DCLBE78000T.EKOMNR;                         
        BE853.EEJDNR      = DCLBE78000T.EEJDNR;                         
        BE853.EENHEDLBNR  = DCLBE78000T.EENHEDLBNR;                     
                                                                        
        BE853.CBERGRL     = DCLBE78000T.EJENDOM_CBERGRL;                
        BE853.CEJDTYPE    = DCLBE78000T.EJENDOM_CEJDTYPE;               
                                                                        
        IF DCLBE78000T.FEJLNR = 5017 !                                  
           DCLBE78000T.FEJLNR = 5018 !                                  
           DCLBE78000T.FEJLNR = 5020 !                                  
           DCLBE78000T.FEJLNR = 5021 !                                  
           DCLBE78000T.FEJLNR = 5022 !                                  
           DCLBE78000T.FEJLNR = 5028 !                                  
           DCLBE78000T.FEJLNR = 5029 !                                  
           DCLBE78000T.FEJLNR = 5030 !                                  
           DCLBE78000T.FEJLNR = 5045 !                                  
        // Disse skal ikke sættes på Ejendommen. Defect 2669            
           DCLBE78000T.FEJLNR = 5310 !                                  
           DCLBE78000T.FEJLNR = 5322 !                                  
           DCLBE78000T.FEJLNR = 5324 !                                  
           DCLBE78000T.FEJLNR = 5326 !                                  
           DCLBE78000T.FEJLNR = 5242       //EBD efter aftale med JAM   
        THEN                                                            
           BE853.FEJLNR = 0;                                            
        ELSE                                                            
           BE853.FEJLNR = DCLBE78000T.FEJLNR;                           
                                                                        
        BE853.CICSID  = ' ';                                            
        BE853.CBENYT  = DCLBE78000T.EJENDOM_CBENYT;                     
        BE853.CFRED   = DCLBE78000T.TILEVS_CFRED;                       
                                                                        
        BE853.DFRAGLD = DATO2.ÅR !! '-' !! DATO2.MD !! '-' !!           
                        DATO2.DG !! '-01.01.01.000001';                 
                                                                        
        IF DCLBE78000T.EJENDOM_FLEJL > 999                              
        THEN                                                            
           BE853.FLEJL = 999;                                           
        ELSE                                                            
           BE853.FLEJL = DCLBE78000T.EJENDOM_FLEJL;                     
                                                                        
        IF DCLBE75000T.OPL_VURDGL_CUOP > '0'                            
        THEN                                                            
           BE853.CUOP_AKT = DCLBE75000T.OPL_VURDGL_CUOP;                
                                                                        
        /* EBD 14/12-2022 felterne flyttes altid ny EVS */              
        /* Check på SW er mest for det skal se pænt ud  */              
        IF EJERSKAB_BBERGRL_SW = 0                                      
        THEN                                                            
          BE853.BBERGRL  = DCLBE78000T.EJERSKAB_BBERGRL;                
        ELSE                                                            
          BE853.BBERGRL  = 0;                                           
                                                                        
        /* Defect 30 Forskud 2023 EBD */                                
        /* felt 611 må kun opsættes 2 familie ejendomme */              
        IF EJERSKAB_BBER2FAM_SW = 0 &                                   
           (BE853.CEJDTYPE = '4' ! BE853.CEJDTYPE = '5')                
        THEN                                                            
          BE853.BBER2FAM = DCLBE78000T.EJERSKAB_BBER2FAM;               
        ELSE                                                            
          BE853.BBER2FAM  = 0;                                          
                                                                        
        BE853.BEJDVÆR_AKT = DCLBE78000T.VURD_A_BEJDVÆR_AKT;             
        IF DCLBE78000T.VURD_A_BEJDVÆR_A_SW = -1                         
        THEN                                                            
           BE853.BEJDVÆR_AKT_NULL = '?';                                
                                                                        
        BE853.BEJBOL1_AKT       = DCLBE78000T.VURD_A_BEJBOL1_AKT;       
        BE853.CEJBOLTYP1_AKT    = DCLBE78000T.VURD_A_CEJBOLTYP1_AKT;    
        BE853.BEJBOL2_AKT       = DCLBE78000T.VURD_A_BEJBOL2_AKT;       
        BE853.CEJBOLTYP2_AKT    = DCLBE78000T.VURD_A_CEJBOLTYP2_AKT;    
        BE853.EJDBELIG          = DCLBE78000T.EJENDOM_EJDBELIG;         
        BE853.EJD_VEJK          = DCLBE78000T.EJENDOM_VEJK_EJD;         
        BE853.EJD_HUSNR         = DCLBE78000T.EJENDOM_HUSNR_EJD;        
        BE853.EJD_ETAGE         = DCLBE78000T.EJENDOM_ETAGE_EJD;        
        BE853.EJD_SDØR          = DCLBE78000T.EJENDOM_SDØR_EJD;         
        BE853.ENHEDBELIG        = DCLBE78000T.EJENDOM_ENHEDBELIG;       
        BE853.ENHED_VEJK        = DCLBE78000T.EJENDOM_VEJK_ENHED;       
        BE853.ENHED_HUSNR       = DCLBE78000T.EJENDOM_HUSNR_ENHED;      
        BE853.ENHED_ETAGE       = DCLBE78000T.EJENDOM_ETAGE_ENHED;      
        BE853.ENHED_SDØR        = DCLBE78000T.EJENDOM_SDØR_ENHED;       
        BE853.LANDEKODE         = DCLBE78000T.EJENDOM_CLANDEKODE;       
                                                                        
        // Eventuel justering af vurmark                                
        HJ_VURD_A_VURMARK_AKT = DCLBE78000T.VURD_A_VURMARK_AKT;         
        HJ_EJERSKAB_VURMARK   = DCLBE78000T.EJERSKAB_VURMARK;           
                                                                        
        IF DCLBE78000T.EKOMNR = 999 THEN                                
           DO;                                                          
           HJ_VURD_A_VURMARK_AKT  = WS_VURMARK_UKENDT;                  
           HJ_EJERSKAB_VURMARK    = WS_VURMARK_UKENDT;                  
        END;                                                            
        ELSE DO;                                                        
           IF (DCLBE78000T.TILEVS_CDATAFRA = '1' !                      
               DCLBE78000T.TILEVS_CDATAFRA = '2' !                      
               DCLBE78000T.TILEVS_CDATAFRA = '4' !                      
               DCLBE78000T.TILEVS_CDATAFRA = '5' !                      
               DCLBE78000T.TILEVS_CDATAFRA = '6' !                      
               DCLBE78000T.TILEVS_CDATAFRA = '7')  THEN                 
              DO;                                                       
              HJ_VURD_A_VURMARK_AKT  = WS_VURMARK_UKENDT;               
              HJ_EJERSKAB_VURMARK    = WS_VURMARK_UKENDT;               
           END;                                                         
                                                                        
           IF DCLBE78000T.VURD_A_BBERGRL_A_SW   = -1 THEN               
              DO;                                                       
              HJ_VURD_A_VURMARK_AKT = WS_VURMARK_UKENDT;                
           END;                                                         
                                                                        
           IF DCLBE78000T.EJERSKAB_BBERGRL_SW = -1 THEN                 
              DO;                                                       
              HJ_EJERSKAB_VURMARK = WS_VURMARK_UKENDT;                  
           END;                                                         
        END;                                                            
                                                                        
        /* Nye felter Forskud 2021 */                                   
        BE853.VURMARK_AKT     = HJ_VURD_A_VURMARK_AKT;                  
        BE853.VURMARK         = HJ_EJERSKAB_VURMARK;                    
                                                                        
        /* Nye felter Forskud 2022 */                                   
        BE853.JUR_KATEGO_AKT     = DCLBE78000T.VURD_A_JUR_KATEGO;       
 /*BF17 BE853.JUR_KATEGO_OM      = DCLBE78000T.VURD_02OM_JUR_KATEGO; */ 
        BE853.JUR_KATEGO         = DCLBE78000T.EJERSKAB_JUR_KATEGO;     
        BE853.JUR_UKATEGO_AKT    = DCLBE78000T.VURD_A_JUR_UKATEGO;      
 /*BF17 BE853.JUR_UKATEGO_OM     = DCLBE78000T.VURD_02OM_JUR_UKATEGO;*/ 
        BE853.JUR_UKATEGO        = DCLBE78000T.EJERSKAB_JUR_UKATEGO;    
        BE853.VURDATO_AKT        = DCLBE78000T.VURD_A_VURDATO;          
 /*BF17 BE853.VURDATO_OM         = DCLBE78000T.VURD_02OM_VURDATO; */    
        BE853.VURDATO            = DCLBE78000T.EJERSKAB_JUR_VURDATO;    
        BE853.FORSIGT_EVS_AKT    = DCLBE78000T.VURD_A_FORSIGT_EVS;      
 /*BF17 BE853.FORSIGT_EVS_OM     = DCLBE78000T.VURD_02OM_FORSIGT_EVS; */
        BE853.FORSIGT_EVS        = DCLBE78000T.EJERSKAB_FORSIGT_EVS;    
        BE853.BESKAT_EVS_AKT     = DCLBE78000T.VURD_A_BESKAT_EVS;       
 /*BF17 BE853.BESKAT_EVS_OM      = DCLBE78000T.VURD_02OM_BESKAT_EVS; */ 
        BE853.BESKAT_EVS         = DCLBE78000T.EJERSKAB_BESKAT;         
                                                                        
        /* Tre glemte felter tilføjet 12-08-2021 EBD  */                
        BE853.BFENR         = DCLBE75000T.BFENR;                        
        BE853.VUREJENDOMID  = DCLBE75000T.VUREJENDOMID;                 
        IF VERIFY(DCLBE75000T.OPL_VURDGL_DVURDÅR,'0123456789') = 0      
        THEN                                                            
          DO;                                                           
            HJ_PIC4             = DCLBE75000T.OPL_VURDGL_DVURDÅR;       
            BE853.DVURDÅR       = HJ_PIC4;                              
          END;                                                          
        ELSE                                                            
          BE853.DVURDÅR       = 0;                                      
                                                                        
        BE853.BBERGRL_AKT      = DCLBE78000T.VURD_A_BBERGRL_AKT;        
        IF DCLBE78000T.VURD_A_BBERGRL_A_SW = -1                         
        THEN                                                            
          BE853.BBERGRL_AKT_NULL = '?';                                 
                                                                        
        IF BE853.FEJLNR > 0                                             
        THEN                                                            
           DO;                                                          
              INDEX = BE853.FEJLNR;                                     
              SKR_FEJLNR(INDEX).ANTAL = SKR_FEJLNR(INDEX).ANTAL + 1;    
           END;                                                         
                                                                        
                                                                        
        TV.SKREVET = TV.SKREVET + 1;                                    
                                                                        
        WRITE FILE (BE853O) FROM (BE853);                               
                                                                        
 END BEHANDL;                                                           
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE: PROC;                                                      
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.FETCH_RESULT;                                
         DETLIN.TEKST = 'ANTAL LÆSTE PÅ TABEL BE78000T';                
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.LÆST_EJD;                                    
         DETLIN.TEKST = 'ANTAL LÆSTE EJENDOMME PÅ TABEL BE78000T';      
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.LÆST_CLANDEKODE;                             
         DETLIN.TEKST = '      HERAF UDL. EJENDOMME MED LANDEKODE';     
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            IF LÆST_FEJLNR(INDEX).ANTAL > 0                             
            THEN                                                        
               DO;                                                      
                  DETLIN.CCA = ZZWS1;                                   
                  HJ_FEJLNR_PIC = INDEX;                                
                  DETLIN.TEKST =                                        
                  '      HERAF MED FEJLNR. ' !! HJ_FEJLNR;              
                  DETLIN.ANTAL = LÆST_FEJLNR(INDEX).ANTAL;              
                  CALL ZZ20300 (DETLIN,'01');                           
               END;                                                     
         END;                                                           
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.SKREVET;                                     
         DETLIN.TEKST = 'ANTAL SKREVNE INDIVIDER           B.H85300D';  
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            IF SKR_FEJLNR(INDEX).ANTAL > 0                              
            THEN                                                        
               DO;                                                      
                  DETLIN.CCA = ZZWS1;                                   
                  HJ_FEJLNR_PIC = INDEX;                                
                  DETLIN.TEKST =                                        
                  '      HERAF MED FEJLNR. ' !! HJ_FEJLNR;              
                  DETLIN.ANTAL = SKR_FEJLNR(INDEX).ANTAL;               
                  CALL ZZ20300 (DETLIN,'01');                           
               END;                                                     
         END;                                                           
                                                                        
 END TOTALLISTE;                                                        
 /*********************************************************************/
 /*      TERMINER LISTE                                               */
 /*********************************************************************/
 TERMINER_LISTE:  PROCEDURE (TEKST);                                    
 DCL     TEKST            CHAR      (45);                               
                                               /* Medd. til termine-  */
 DCL     1 MEDDELELSE     STATIC,              /* ringslisten         */
           3 MODULNAVN    CHAR      (07)    INIT ('BE85300'),           
           3 FILLER1      CHAR      (01)    INIT (' '),                 
           3 MEDDNR       PIC       '999'   INIT (6),                   
           3 DATA         CHAR      (45)    INIT (' ');                 
 DCL     MEDDELELSEN      CHAR      (56)    DEF MEDDELELSE;             
                                                                        
         MEDDELELSE.MEDDNR = MEDDELELSE.MEDDNR + 1;                     
         MEDDELELSE.DATA   = TEKST;                                     
                                                                        
         CALL ZS30101 (2,                                               
                       MEDDELELSEN,                                     
                       2);                                              
                                                                        
 END TERMINER_LISTE;                                                    
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT: PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BE853O);                                           
                                                                        
 END AFSLUT;                                                            
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
         %INCLUDE BE85111M;                /* SQL TIL LÆS AF TABELLER */
 END  BE85300;                                                          
