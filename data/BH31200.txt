  PROCESS OPT(TIME);                                                    
 BH31200: /* EVS - AJOURFØR EJERE AF UDENLANDSKE EJD. MED LANDEKODE */  
         PROC OPTIONS (MAIN) REORDER;                                   
 /*********************************************************************/
                                                                        
         DCL COPYRIGHT CHAR (30) STATIC                                 
         INIT ('COPYRIGHT KOMMUNEDATA I/S 2007');                       
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      PROGRAMMET PÅFØRER LANDEKODEN FOR UDENLANDSKE EJENDOMME.     */
 /*      LANDEKODEN LEVERES AF CSC PÅ EN UDTRÆK AF SKAT'S SLUTSYSTEM  */
 /*                                                                   */
 /*      INDDATA : B.H31100D SAMSORTEREDE UDTRÆK FRA SLUT OG FORSKUD  */
 /*              : B.H31200D UDTRÆK FRA CSC AF LANDEKODER             */
 /*                                                                   */
 /*      UDDATA  : B.H31100D MED LANDEKODE PÅFØRT                     */
 /*                                                                   */
 /*      EXT. REF: ZS61900   (SSI-DATO)                               */
 /*                                                                   */
 /* PROGRAMMØR:    INGRID FISCHER, PKF ÅRHUS   NOV.2007               */
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      DECLARE AF ENTRIES OG BUILTINS                               */
 /*********************************************************************/
 DCL     ZS61900   ENTRY OPTIONS (INTER ASM);          /* UDSKRIV SSI */
 DCL     ZS38100   ENTRY OPTIONS (INTER ASM);          /* TIMESTAMP   */
 DCL     ADDR      BUILTIN;                                             
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
 DCL     BH311I          FILE RECORD INPUT;                             
 DCL     BH312I          FILE RECORD INPUT;                             
 DCL     BH312O          FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      DECLARE AF INPUT/OUTPUT                                      */
 /*********************************************************************/
 /* UDENLANDSKE EJENDOMME */                                            
 DCL     BH311_AR   CHAR      (1000) VAR;                               
                                                                        
 DCL     1 BH300              BASED(ADDR(BH311_AR)),                    
   4 LÆNGDE       BIN FIXED (15),                                       
   %INCLUDE BH30000N;;                                                  
                                                                        
 DCL     1 BH301              BASED(ADDR(BH311_AR)),                    
   4 LÆNGDE       BIN FIXED (15),                                       
   %INCLUDE BH30100N;;                                                  
                                                                        
                                                                        
 /* UDTRÆK FRA SKAT'S SLUTSYSTEM */                                     
 DCL     BH312_AR   CHAR      (1000) VAR;                               
                                                                        
 DCL     1 BH312              BASED(ADDR(BH312_AR)),                    
   4 LÆNGDE       BIN FIXED (15),                                       
           %INCLUDE BH31200N;                                           
 DCL   BH312_PNR            FIXED(11);                                  
 DCL   BH312_KOMNR          BIN FIXED (15);                             
 DCL   BH312_ENR            BIN FIXED (31);                             
 DCL   HJ_C_03              CHAR(03);                                   
 DCL   HJ_P_03              PIC'999' DEF HJ_C_03;                       
 DCL   HJ_C_07              CHAR(07);                                   
 DCL   HJ_P_07              PIC'(07)9' DEF HJ_C_07;                     
 DCL   HJ_C_10              CHAR(10);                                   
 DCL   HJ_P_10              PIC'(10)9' DEF HJ_C_10;                     
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     1 TV,                                                          
           2 LÆS_BH311      FIXED DEC (7),                              
           2 LÆS_BH312      FIXED DEC (7),                              
           2 OVF_LANDEKODE  FIXED DEC (7),                              
           2 SKRIV_BH312    FIXED DEC (7);                              
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     EOF_BH311       BIT       (1);                                 
 DCL     EOF_BH312       BIT       (1);                                 
 DCL     JA              BIT       (1)       INIT ('1'B);               
 DCL     NEJ             BIT       (1)       INIT ('0'B);               
 DCL     OK_BH312        BIT       (1);                                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL   VERIFY            BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTE                      */
 /*********************************************************************/
 DCL     1 OVLIN1,                                                      
           2 CTL         CHAR      (01),                                
           2 TXT1        CHAR      (50) INIT ('PROGRAM: BH31200L'),     
           2 TXT2        CHAR      (58) INIT                            
             ('AJOURFØR EJERE AF UDENLANDSKE EJD. MED LANDEKODE'),      
           2 TXT3        CHAR      (14) INIT ('UDSKREVET DEN'),         
           2 DATO        CHAR      (10);                                
 DCL     1 OVLIN2,                                                      
           2 CTL         CHAR      (01),                                
           2 F1          CHAR      (51) INIT (' '),                     
           2 TXT1        CHAR      (91) INIT                            
           ('          KØRSELSKONTROLLISTE');                           
 DCL     LINIE           CHAR      (133);                               
 DCL     1  DETAIL                           DEF LINIE,                 
          2  STYR        CHAR      (1),                                 
          2  TEKST       CHAR      (70),                                
          2  ANTAL       PIC       'Z.ZZZ.ZZ9';                         
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      H O U S E - K E E P I N G                                    */
 /*********************************************************************/
                                                                        
         CALL ZS61900;                                                  
                                                                        
         OPEN FILE (BH311I)  TITLE ('BH31100I');                        
         OPEN FILE (BH312I)  TITLE ('BH31200I');                        
         OPEN FILE (BH312O)  TITLE ('BH31200O');                        
                                                                        
         CALL ZZ20390 ('*OPEN','BH31201Y');                             
                                                                        
         ON ENDFILE (BH311I)                                            
         BEGIN;                                                         
            EOF_BH311 = JA;                                             
         END;                                                           
                                                                        
         ON ENDFILE (BH312I)                                            
         BEGIN;                                                         
            EOF_BH312 = JA;                                             
         END;                                                           
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
                                                                        
         TV = '';                                                       
 /*********************************************************************/
 /*      P R O C E S S                                                */
 /*********************************************************************/
                                                                        
         READ FILE (BH311I) INTO (BH311_AR);  /* UDENLANDSK EJENDOM */  
         TV.LÆS_BH311 = TV.LÆS_BH311 + 1;                               
         CALL LÆS_BH312;                                                
                                                                        
         DO WHILE (EOF_BH311 = NEJ);                                    
                                                                        
            DO WHILE (EOF_BH311 = NEJ);                                 
              SELECT;                                                   
               WHEN (EOF_BH312 = NEJ &                                  
                     EOF_BH311 = NEJ &                                  
                     BH300.DCPRN  = BH312_PNR &                         
                  /* BH300.EKOMNR = BH312_KOMNR &   */                  
                     BH300.EEJDNR = BH312_ENR)                          
                  DO;                                                   
                     IF BH300.CFRATYPE = '2'                            
                     THEN                                               
                        DO;                                             
                          BH300.CLANDEKODE = BH312.LANDKOD;             
                          TV.OVF_LANDEKODE = TV.OVF_LANDEKODE + 1;      
                        END;                                            
                     IF BH300.CFRATYPE = '4'                            
                     THEN                                               
                        DO;                                             
                          BH301.CLANDEKODE = BH312.LANDKOD;             
                        END;                                            
                     WRITE FILE (BH312O) FROM (BH311_AR); /* LANDEKODE*/
                     TV.SKRIV_BH312 = TV.SKRIV_BH312 + 1;               
                     READ FILE (BH311I) INTO (BH311_AR); /* EJENDOM   */
                     TV.LÆS_BH311 = TV.LÆS_BH311 + 1;                   
                /*   CALL LÆS_BH312 */                                  
                  END;                                                  
               WHEN (EOF_BH312 = JA !         /* IKKE FLERE LANDEKODER*/
                     (BH300.DCPRN < BH312_PNR !                         
                     (BH300.DCPRN = BH312_PNR &                         
                      BH300.EEJDNR < BH312_ENR)))                       
                  DO;    /* INGEN LANDEKODE TIL DENNE EJENDOM */        
                     WRITE FILE (BH312O) FROM (BH311_AR);               
                     TV.SKRIV_BH312 = TV.SKRIV_BH312 + 1;               
                     READ FILE (BH311I) INTO (BH311_AR);                
                     TV.LÆS_BH311 = TV.LÆS_BH311 + 1;                   
                  END;                                                  
               WHEN (BH300.DCPRN > BH312_PNR !                          
                     (BH300.DCPRN = BH312_PNR &                         
                      BH300.EEJDNR > BH312_ENR))                        
                  DO;    /* LANDEKODE TIL IKKE-EKSISTERENDE EJENDOM */  
                     CALL LÆS_BH312;                                    
                  END;                                                  
               OTHERWISE;                                               
              END;                                                      
            END;                                                        
         END;                                                           
                                                                        
         CALL KONTROLLISTE;                                             
                                                                        
         CALL ZZ20300(' ','99');                                        
                                                                        
         CLOSE FILE (BH311I),                                           
               FILE (BH312I),                                           
               FILE (BH312O);                                           
                                                                        
 /*********************************************************************/
 /*      LÆS UDTRÆK FRA CSC MED LANDEKODER                            */
 /*********************************************************************/
 LÆS_BH312:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH312I) INTO (BH312_AR);  /* LANDEKODE          */  
         TV.LÆS_BH312 = TV.LÆS_BH312 + 1;                               
                                                                        
         CALL CHECK_BH312;                                              
                                                                        
         DO WHILE (OK_BH312 = NEJ & EOF_BH312 = NEJ);                   
           READ FILE (BH312I) INTO (BH312_AR);  /* LANDEKODE          */
           TV.LÆS_BH312 = TV.LÆS_BH312 + 1;                             
           CALL CHECK_BH312;                                            
         END;                                                           
                                                                        
                                                                        
 CHECK_BH312:                                                           
 PROC;                                                                  
            OK_BH312 = JA;                                              
            IF VERIFY(BH312.PNR,'0123456789') = 0 &                     
               VERIFY(BH312.ENR,'0123456789') = 0 THEN                  
               DO;                                                      
                  HJ_C_10 = BH312.PNR;                                  
                  BH312_PNR = HJ_P_10;                                  
                  HJ_C_07 = BH312.ENR;                                  
                  BH312_ENR = HJ_P_07;                                  
               END;                                                     
            ELSE                                                        
               OK_BH312 = NEJ;                                          
 END CHECK_BH312;                                                       
 END LÆS_BH312;                                                         
                                                                        
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         LINIE = ZZIK1;                                                 
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         OVLIN1.CTL= ZZWS1;                                             
         OVLIN1.DATO = UDDATO;                                          
         CALL ZZ20300 (OVLIN1,'01');                                    
                                                                        
         OVLIN2.CTL= ZZWS2;                                             
         CALL ZZ20300 (OVLIN2,'01');                                    
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'LÆSTE UDENLANDSKE EJENDOMME         B.H31100D';
         DETAIL.ANTAL = TV.LÆS_BH311 - 1;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS1;                                           
         DETAIL.TEKST = 'LÆSTE PÅ LANDEKODE UDTRÆK       D0311607.A240';
         DETAIL.ANTAL = TV.LÆS_BH312 - 1;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'UDENLANDSKE EJENDOMME AJOURFØRT M. LANDEKODE'; 
         DETAIL.ANTAL = TV.OVF_LANDEKODE;                               
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
         LINIE = '';                                                    
         DETAIL.STYR = ZZWS2;                                           
         DETAIL.TEKST = 'SKREVET UDENLANDSKE EJENDOMME       B.H31200D';
         DETAIL.ANTAL = TV.SKRIV_BH312;                                 
         CALL ZZ20300 (LINIE,'01');                                     
                                                                        
 END KONTROLLISTE;                                                      
                                                                        
 END BH31200;                                                           
