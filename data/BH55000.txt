 BH55000: PROCEDURE OPTIONS (MAIN);                                     
 /*********************************************************************/
 /* OBS : ÅRSRULNING ER IKKE NØDVENDIG MED MINDRE DER ER ÆNDRINGER    */
 /*       TIL SQL'LET (F.EKS. TABELNAVN) DER LAVER UDTRÆKKET FRA DW.  */
 /*       M E N  PROGRAMMET SKAL KOMPILERES NÅR MAKRO BH65300M ER     */
 /*       ÅRSRULLET (D.V.S. ÅRSTAL OG DATOER ER RETTET TIL NYT ÅR)    */
 /*       HVILKET SKER I FORBINDELSE MED ÅRSRULNING TIL EVS FORSKUD.  */
 /*********************************************************************/
 /**********************************************************************
 * PROJEKT:     EVS FORSKUD / EVS SLUT                                 *
 * PROGRAMNAVN: BH55000.                                               *
 * PROGRAMTYPE: BATCH.                                                 *
 * FUNKTION:    PROGRAMMET UDTRÆKKER FORSKUDT REGNSKABSÅR FRA          *
 *              SLUT VEDR. 'GAMMELT' SLUTÅR.                           *
 *              PROGRAMMET AFVIKLES KUN I BALLERUP.                    *
 * PGMFORLØB:   1. ÅBEN SLUT-UDTRÆK                                    *
 *              2. DECLARE PERSONCURSOR.                               *
 *              3. OPEN PERSONCURSOR.                                  *
 *              4. LÆS/FETCH RÆKKE PÅ SKT-TABEL.                       *
 *              5. LÆS RÆKKE PÅ DB2-TABEL EVS UDTRÆK BQ62000T          *
 *              6. Hvis række findes, Update BQ62000T ellers Insert    *
 *              7. LUK PERSONCURSOR.                                   *
 *              8. SKRIV KONTROLLISTE.                                 *
 * INDDATA:     BG41000T - SLUT INDBERET                               *
 *                                                                     *
 * UDDATA:      BH55001Y - KONTROLLISTE.                               *
 *              BQ62000T - EVS UDTRÆK SLUT                             *
 * KONSTRUKTØR: BO SCHMDIT            BOB, PKF    BALLERUP 15.09.2006. *
 * TILRETTET    BO SCHMDIT            BOB, PKF    BALLERUP 22.05.2007. *
 * TILRETTET    Lene Maj Jensen       QLE         BALLERUP 11.01.2017. *
 *              Omlægning af uddatafil til DB2                         *
 ***********************************************************************
                                                                        
 DCL     COPYRIGHT      CHAR (30) INIT ('(C) KOMMUNEDATA A/S 2006');    
                                                                        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT. KALD AF EKSTERNE MODULER */
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE BG41100T;        /* SLUT INDBERET           */
         EXEC SQL INCLUDE SQLCA;           /* SQL KOMMUNIKATIONSAREAL */
                                                                        
         EXEC SQL INCLUDE BQ62000T;  /* SLUT UDTRÆK                   */
         EXEC SQL INCLUDE BQ6200HT;  /* SLUT UDTRÆK historik          */
                                                                        
         EXEC SQL INCLUDE BQ69000T;  /* Kørselstabel                  */
         EXEC SQL INCLUDE BQ6900HT;  /* Kørselstabel hist             */
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
 DCL     WS_FORSKUD_SLUT_TXT    CHAR(16) INIT (' ');                    
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     WS_TIL_EVS_AAR_GL              BIN FIXED (15);                 
 DCL     WS_SLUT_AFVIKLINGS_ID          CHAR(16) INIT (' ');            
                                                                        
 DCL     WS_PROGRAMNAVN         CHAR(8)  INIT ('BH55000');              
                                                                        
 DCL     1 BH550_PARM,                                                  
           %INCLUDE BH54900N;                                           
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;                                           
                                                                        
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     EOF_BH550       BIT       (1);                                 
 DCL     JA                 BIT       (1)     INIT ('1'B);              
 DCL     NEJ                BIT       (1)     INIT ('0'B);              
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH55000'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH55000L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'UDTRÆK FORSKUDT REGNSKABSÅR FRA SLUT '),                    
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
                                                                        
 /*********************************************************************/
 /* DECLARE DATASÆT                                                   */
 /*********************************************************************/
                                                                        
 DCL     BH550I FILE RECORD INPUT;     /* Parameter Forskud-Slut */     
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
                                                                        
 /*********************************************************************/
 /* DECLARE STRUKTURER                                                */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
                                                                        
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /* DECLARE AF TÆLLERE                                                */
 /*********************************************************************/
                                                                        
 DCL     ANT_SKR BIN FIXED (31) INIT(0);   /* ANTAL SKREVET PÅ UDTRÆK */
 DCL     ANT_INS BIN FIXED (31) INIT(0);   /* HERAF INSERTED */         
 DCL     ANT_UPD BIN FIXED (31) INIT(0);   /* HERAF UPDATED  */         
 DCL     ANT_COMM BIN FIXED (31) INIT(0);  /* COMMIT TÆLLER */          
                                                                        
 /*********************************************************************/
 /* DECLARE AF CURSORS                                                */
 /*********************************************************************/
          EXEC SQL DECLARE PERSONCURSOR CURSOR WITH HOLD FOR            
            SELECT T1.PNR,                                              
                   T1.FSKUDRESKAMDKOD,                                  
                   T1.DINDKÅR                                           
            FROM BG41100T T1,                                           
                 BG41600T T2                                            
            WHERE T1.DINDKÅR = :GLSLUTÅR                                
            AND   T2.DINDKÅR = T1.DINDKÅR                               
            AND   T2.REGISTMP =                                         
                    (SELECT MAX(T3.REGISTMP)                            
                     FROM BG41600T T3                                   
                     WHERE T2.PNR     = T3.PNR                          
                     AND   T2.DINDKÅR = T3.DINDKÅR)                     
            AND   T2. AENDRNR  =                                        
                    (SELECT MAX(T4.AENDRNR)                             
                     FROM BG41600T T4                                   
                     WHERE T2.PNR     = T4.PNR                          
                     AND   T2.DINDKÅR = T4.DINDKÅR)                     
            AND   T1.DUMMYID = T2.DUMMYID                               
            AND   T1.FSKUDRESKAMDKOD > ' '                              
            ORDER BY T1.PNR;                                            
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         EOF_BH550 = NEJ;                                               
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH55000L');             
            END;                                                        
                                                                        
         OPEN FILE (BH550I) TITLE ('BH55000P');                         
                                                                        
         ON ENDFILE (BH550I)                                            
         BEGIN;                                                         
            EOF_BH550 = JA;                                             
         END;                                                           
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
         READ FILE (BH550I) INTO (BH550_PARM);                          
                                                                        
         IF EOF_BH550 = NEJ                                             
         THEN DO;                                                       
            SELECT (BH550_PARM.FS_MRK);                                 
               WHEN ('F')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH550_PARM.FORSKUD_TXT;      
                     WS_TIL_EVS_AAR = BH65300M.FORSKÅR;                 
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH550_PARM.SLUT_TXT;       
                  END;                                                  
               WHEN ('S')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH550_PARM.SLUT_TXT;         
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH550_PARM.SLUT_TXT;       
                  END;                                                  
               WHEN ('U')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH550_PARM.SUPPL_TXT;        
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH550_PARM.SLUT_TXT;       
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     ABENDTXT = '101 PARAMETERFEJL';                    
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
         END;                                                           
                                                                        
         /********************************************************/     
         /*  NU STARTER NORMALT FLOW                             */     
         /********************************************************/     
         CALL OPDATER_KØRSELSTABEL('START',BH550_PARM.FS_MRK);          
                                                                        
         CALL ZZ20390('*OPEN','BH55001Y');       /* ÅBEN KONTROLLISTE */
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.ÅR   = GLSLUTÅR;                                      
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
                                                                        
         CALL CLOSE_PERSONCURSOR;                                       
         CALL OPEN_PERSONCURSOR;                                        
         CALL FETCH_PERSONCURSOR;                                       
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
                                                                        
           WHEN (0)                                                     
             DO;                                                        
               DO WHILE (SQLCA.SQLCODE = 0);                            
                  /* check om der findes en række, og  */               
                  /* insert eller update efterfølgende */               
                  CALL INSERT_UPDATE_EVS_UDTRÆK;                        
                  CALL FETCH_PERSONCURSOR;                              
               END;                                                     
             END;                                                       
                                                                        
           WHEN (100)                                                   
             DO;                                                        
             END;                                                       
           OTHERWISE                                                    
             DO;                                                        
               ABENDTXT = '101 FETCH PERSONCURSOR';                     
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* SELECT*/                                               
                                                                        
         IF ANT_COMM > 0                                                
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
             Put skip list ('Commit efter ' !! ANT_COMM !!              
                            ' opdateringer.  Ialt ' !! ANT_SKR !!       
                            ' opdateringer');                           
             ANT_COMM = 0;                                              
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF SLUT                                RUTINE */
 /*********************************************************************/
 OPEN_PERSONCURSOR: PROC;                                               
                                                                        
         EXEC SQL OPEN PERSONCURSOR;                                    
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              ABENDTXT = '101 OPEN PERSONCURSOR';                       
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_PERSONCURSOR;                                                 
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF SLUT                               RUTINE */
 /*********************************************************************/
 FETCH_PERSONCURSOR: PROC;                                              
                                                                        
         EXEC SQL FETCH PERSONCURSOR                                    
                                                                        
         INTO :DCLBG41100T.PNR                                          
             ,:DCLBG41100T.FSKUDRESKAMDKOD                              
             ,:DCLBG41100T.DINDKÅR                                      
         ;                                                              
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0);                                                    
           WHEN (100);                                                  
           OTHERWISE                                                    
             DO;                                                        
               ABENDTXT = '101 FETCH PERSONCURSOR';                     
               CALL SQL_FEJL;                                           
             END;                                                       
         END; /* SELECT*/                                               
                                                                        
 END FETCH_PERSONCURSOR;                                                
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF SLUT                               RUTINE */
 /*********************************************************************/
 CLOSE_PERSONCURSOR: PROC;                                              
                                                                        
         EXEC SQL CLOSE PERSONCURSOR;                                   
                                                                        
 END CLOSE_PERSONCURSOR;                                                
                                                                        
 /*********************************************************************/
 /* CHECK OM RÆKKE FINDES OG INSERT/UPDATE herefter            RUTINE */
 /*********************************************************************/
 INSERT_UPDATE_EVS_UDTRÆK: PROC;                                        
                                                                        
         DCLBQ62000T = '';                                              
         DCLBQ62000T.TIL_EVS_AAR    = WS_TIL_EVS_AAR;                   
         DCLBQ62000T.INDKOMSTAAR    = DCLBG41100T.DINDKÅR;              
         DCLBQ62000T.DCPRN          = DCLBG41100T.PNR;                  
         DCLBQ62000T.PROGRAMNAVN    = WS_PROGRAMNAVN;                   
         DCLBQ62000T.DB2FUNKTION    = 'I';                              
         DCLBQ62000T.AFVIKLINGS_ID  = WS_FORSKUD_SLUT_TXT;              
         DCLBQ62000T.CFRÅR          = DCLBG41100T.FSKUDRESKAMDKOD;      
                                                                        
         EXEC SQL                                                       
            SELECT TIL_EVS_AAR,INDKOMSTAAR,DCPRN,PROGRAMNAVN,DB2FUNKTION
                  ,AFVIKLINGS_ID,CFRÅR                                  
            INTO   :DCLBQ62000T.TIL_EVS_AAR,                            
                   :DCLBQ62000T.INDKOMSTAAR,                            
                   :DCLBQ62000T.DCPRN,                                  
                   :DCLBQ62000T.PROGRAMNAVN,                            
                   :DCLBQ62000T.DB2FUNKTION,                            
                   :DCLBQ62000T.AFVIKLINGS_ID,                          
                   :DCLBQ62000T.CFRÅR                                   
            FROM BQ62000T                                               
            WHERE TIL_EVS_AAR   = :DCLBQ62000T.TIL_EVS_AAR              
            AND   DCPRN         = :DCLBQ62000T.DCPRN                    
            AND   AFVIKLINGS_ID = :DCLBQ62000T.AFVIKLINGS_ID;           
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
               CALL UPDATE_EVS_UDTRÆK;                                  
            END;                                                        
            WHEN(100) DO;                                               
               CALL INSERT_EVS_UDTRÆK;                                  
            END;                                                        
            OTHERWISE DO;                                               
               ABENDTXT = '101 SELECT BQ62000T';                        
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
         IF ANT_COMM > 9999                                             
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
            Put skip list ('Commit efter ' !! ANT_COMM !!               
                           ' opdateringer.  Ialt ' !! ANT_SKR !!        
                           ' opdateringer');                            
            ANT_COMM = 0;                                               
         END;                                                           
                                                                        
 END INSERT_UPDATE_EVS_UDTRÆK;                                          
 /*********************************************************************/
 /* INSERT række på EVS UDTRÆK                                 RUTINE */
 /*********************************************************************/
 INSERT_EVS_UDTRÆK: PROC;                                               
                                                                        
         DCLBQ62000T = '';                                              
         DCLBQ62000T.TIL_EVS_AAR    = WS_TIL_EVS_AAR;                   
         DCLBQ62000T.INDKOMSTAAR    = DCLBG41100T.DINDKÅR;              
         DCLBQ62000T.DCPRN          = DCLBG41100T.PNR;                  
         DCLBQ62000T.PROGRAMNAVN    = WS_PROGRAMNAVN;                   
         DCLBQ62000T.DB2FUNKTION    = 'I';                              
         DCLBQ62000T.AFVIKLINGS_ID  = WS_FORSKUD_SLUT_TXT;              
         DCLBQ62000T.CFRÅR          = DCLBG41100T.FSKUDRESKAMDKOD;      
                                                                        
         EXEC SQL                                                       
            INSERT INTO BQ62000T                                        
            (                                                           
            TIL_EVS_AAR,INDKOMSTAAR,DCPRN,PROGRAMNAVN,DB2FUNKTION       
            ,AFVIKLINGS_ID,CFRÅR                                        
            )                                                           
            values                                                      
            (                                                           
             :DCLBQ62000T.TIL_EVS_AAR,                                  
             :DCLBQ62000T.INDKOMSTAAR,                                  
             :DCLBQ62000T.DCPRN,                                        
             :DCLBQ62000T.PROGRAMNAVN,                                  
             :DCLBQ62000T.DB2FUNKTION,                                  
             :DCLBQ62000T.AFVIKLINGS_ID,                                
             :DCLBQ62000T.CFRÅR                                         
            );                                                          
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
               ANT_SKR = ANT_SKR + 1;                                   
               ANT_INS = ANT_INS + 1;                                   
               ANT_COMM = ANT_COMM + 1;                                 
            END;                                                        
            OTHERWISE DO;                                               
               ABENDTXT = '101 INSERT BQ62000T';                        
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
 END INSERT_EVS_UDTRÆK;                                                 
 /*********************************************************************/
 /* UPDATE række på EVS UDTRÆK                                 RUTINE */
 /*********************************************************************/
 UPDATE_EVS_UDTRÆK: PROC;                                               
                                                                        
         DCLBQ62000T.PROGRAMNAVN    = WS_PROGRAMNAVN;                   
         DCLBQ62000T.DB2FUNKTION    = 'U';                              
         DCLBQ62000T.CFRÅR          = DCLBG41100T.FSKUDRESKAMDKOD;      
         DCLBQ62000T.INDKOMSTAAR    = DCLBG41100T.DINDKÅR;              
                                                                        
         EXEC SQL                                                       
            UPDATE BQ62000T                                             
            SET PROGRAMNAVN     = :DCLBQ62000T.PROGRAMNAVN,             
                DB2FUNKTION     = :DCLBQ62000T.DB2FUNKTION,             
                CFRÅR           = :DCLBQ62000T.CFRÅR,                   
                INDKOMSTAAR     = :DCLBQ62000T.INDKOMSTAAR              
            WHERE TIL_EVS_AAR   = :DCLBQ62000T.TIL_EVS_AAR              
            AND   DCPRN         = :DCLBQ62000T.DCPRN                    
            AND   AFVIKLINGS_ID = :DCLBQ62000T.AFVIKLINGS_ID;           
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
               ANT_SKR = ANT_SKR + 1;                                   
               ANT_UPD = ANT_UPD + 1;                                   
               ANT_COMM = ANT_COMM + 1;                                 
            END;                                                        
            OTHERWISE DO;                                               
               ABENDTXT = '101 UPDATE BQ62000T';                        
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
 END UPDATE_EVS_UDTRÆK;                                                 
                                                                        
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL UDTRUKNE MED FORSKUDT REGNSKABSÅR PÅ B.H65000D';        
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.TEKST =                                                
         '   HERAF NYE RÆKKER (INSERT)       ';                         
         DETLIN1.ANTAL = ANT_INS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.TEKST =                                                
         '   HERAF OPDATEREDE RÆKKER (UPDATE)';                         
         DETLIN1.ANTAL = ANT_UPD;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL KONTROLLISTE;                                             
         CLOSE FILE (BH550I);                                           
         CALL CLOSE_PERSONCURSOR;                                       
         CALL ZZ20300(' ','99');                                        
                                                                        
         CALL OPDATER_KØRSELSTABEL('SLUT',BH550_PARM.FS_MRK);           
                                                                        
 END AFSLUT;                                                            
 %INCLUDE BH54998M; /* OPDATER KØRSELSTABEL */                          
 END BH55000;                                                           
