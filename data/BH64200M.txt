                                                                        
 /**********************************************************************
 *   BH64200M                                                          *
 *   DENNE MACRO ER LAVET TIL LEJEVÆRDI-BEREGNING TIL HENHOLDVIS       *
 *   ORDINÆR ESKEMA OG FORSKUD.                                        *
 *   DEN KAN UDFØRE 3 FUNKTIONER:                                      *
 *       1. DET UNDERSØGES OM EN PERSON BLIVER 67 ÅR ELLER BLIVER      *
 *          ÆLDRE END 67 ÅR I LØBET AF ET GIVENT INDKOMSTÅR.           *
 *       2. DET UNDERSØGES OM EN PERSON'S ALDER ER IMELLEM             *
 *          60-66 ÅR INCL. I LØBET AF ET GIVENT INDKOMSTÅR.            *
 *       3. DET UNDERSØGES OM EN PERSON ENDNU IKKE ER FYLDT/FYLDER     *
 *          60 ÅR I LØBET AF ET GIVENT INDKOMSTÅR.                     *
 **********************************************************************/
                                                                        
 PERSON_ALDER_OK: PROC (AKTÅR,CPRNR,FUNKTION) RETURNS (BIT(1));         
                                                                        
 DCL     RETURBIT        BIT       (1);                                 
                                                                        
 DCL     FUNKTION        CHAR      (1);                                 
                                                                        
 DCL     AKTÅR           FIXED     (5);                                 
                                                                        
 DCL     CPRNR           PIC       '(10)9';                             
 DCL     1 CPRNUM        BASED     (ADDR(CPRNR)),                       
           2 F1          CHAR      (4),                                 
           2 ÅR          PIC       '99',                                
           2 LBNR        PIC       '9',                                 
           2 NR          CHAR      (2),                                 
           2 CHCIF       CHAR      (1);                                 
                                                                        
 DCL     FØDÅR           FIXED     (5);                                 
                                                                        
         RETURBIT = '0'B;                                               
                                                                        
         IF CPRNR < 0101000000 !       /* AKT. PERSON EJ MED CPR.NR. */ 
            CPRNR > 3112999999                                          
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
                                                                        
                /* OPSÆT PERSONENS FØDSELSÅR JVF. VÆRDI AF */           
                /* 1. CIFFER AF LØBENUMMER + ÅR I CPRNR    */           
                                                                        
                FØDÅR = 1900 + CPRNUM.ÅR; /* DEFAULT FØDT ÅR 19XX */    
                                                                        
                SELECT (CPRNUM.LBNR);                                   
                                                                        
                  WHEN (5,6,7,8)                                        
                    DO;                                                 
                      IF CPRNUM.ÅR >= 58 & CPRNUM.ÅR <= 99              
                      THEN                                              
                         FØDÅR = 1800 + CPRNUM.ÅR;                      
                      ELSE                                              
                         IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36           
                         THEN                                           
                            FØDÅR = 2000 + CPRNUM.ÅR;                   
                    END;                                                
                                                                        
                  WHEN (4)                                              
                    DO;                                                 
                      IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36              
                      THEN                                              
                         FØDÅR = 2000 + CPRNUM.ÅR;                      
                    END;                                                
                                                                        
                  WHEN (9)                                              
                    DO;                                                 
                      IF CPRNUM.ÅR >= 00 & CPRNUM.ÅR <= 36              
                      THEN                                              
                         FØDÅR = 2000 + CPRNUM.ÅR;                      
                    END;                                                
                                                                        
                  OTHERWISE;                                            
                                                                        
                END;                                                    
                                                                        
                /* BEREGN PERSONENS ALDER */                            
                                                                        
                IF FUNKTION = '1' &                                     
                  (AKTÅR - FØDÅR) >= 67                                 
                THEN                                                    
                   RETURBIT = '1'B;                                     
                                                                        
                IF FUNKTION = '2' &                                     
                   (((AKTÅR - FØDÅR) >= 60) &                           
                    ((AKTÅR - FØDÅR) <= 66))                            
                THEN                                                    
                   RETURBIT = '1'B;                                     
                                                                        
                IF FUNKTION = '3' &                                     
                  (AKTÅR - FØDÅR) < 60                                  
                THEN                                                    
                   RETURBIT = '1'B;                                     
                                                                        
            END;                                                        
                                                                        
         RETURN (RETURBIT);                                             
                                                                        
 END PERSON_ALDER_OK;                                                   
