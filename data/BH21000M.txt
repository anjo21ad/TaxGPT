 /**********************************************************************
 * PROJEKT:     EVS SLUT ELLER FORSKUD                                 *
 * PROGRAMNAVN: BH21000M.                                              *
 * PROGRAMTYPE: MAKRO.                                                 *
 * FUNKTION:    SQL UDTRÆK ALLE EJENDOMME FRA EJENDOMSTABEL            *
 *              EVS SLUT 2003                                          *
 * PROGRAMMØR:  BO BECK SCHMIDT     BOB,  EVS              MAJ 2004    *
 * ÆNDRINGSLOG:                                                        *
 **********************************************************************/
 /*********************************************************************/
 /* DECLARE CURSOR TIL LÆS AF EJENDOMSTABEL BQ21000T                  */
 /*********************************************************************/
 DECLARE_CURSOR: PROC;                                                  
                                                                        
         EXEC SQL                                                       
         DECLARE EJENDOMS_CUR CURSOR FOR                                
                                                                        
         SELECT T1.DINDKAAR,                                            
                T1.EKOMNR,                                              
                T1.EEJDNR                                               
                                                                        
         FROM   BQ20000T T1                                             
                                                                        
         WHERE  T1.DINDKAAR = 2003                                      
                                                                        
         ORDER BY T1.DINDKAAR, T1.EKOMNR, T1.EEJDNR                     
         FOR READ ONLY;                                                 
                                                                        
         EXEC SQL                                                       
         DECLARE EJD_FEL_CUR CURSOR FOR                                 
                                                                        
         SELECT T1.INDHOLD_C,                                           
                T1.INDHOLD_D,                                           
                T1.FELTKODE                                             
                                                                        
         FROM   BQ21000T T1                                             
                                                                        
         WHERE  T1.DINDKAAR = 2003                                      
          AND   T1.EKOMNR   = :EVS_EJD.EKOMNR                           
          AND   T1.EEJDNR   = :EVS_EJD.EEJDNR                           
                                                                        
         FOR READ ONLY;                                                 
                                                                        
 END DECLARE_CURSOR;                                                    
 /*********************************************************************/
 /* OPEN CURSOR TIL LÆS AF TABEL BQ20000T                      RUTINE */
 /*********************************************************************/
 OPEN_EJENDOMSOPL: PROC;                                                
                                                                        
         EXEC SQL OPEN EJENDOMS_CUR;                                    
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'OPEN EJENDOMS_CURSOR';                       
              FEJLTEKST  = 'BH30000 ** 253 ' !! FEJLTEKST;              
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END OPEN_EJENDOMSOPL;                                                  
 /*********************************************************************/
 /* FETCH CURSOR TIL LÆS AF EJENDOMSTABEL BQ20000T                    */
 /*********************************************************************/
 FETCH_EJENDOMSOPL: PROC;                                               
                                                                        
         EXEC SQL FETCH EJENDOMS_CUR                                    
                                                                        
         INTO :EVS_EJD.DINDKAAR,                                        
              :EVS_EJD.EKOMNR,                                          
              :EVS_EJD.EEJDNR;                                          
                                                                        
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0)                                                     
               ANTLÆS_EJD = ANTLÆS_EJD + 1;                             
           WHEN (100);                                                  
           OTHERWISE                                                    
             DO;                                                        
               FEJLTEKST  =  'BH21000 ** 262  FETCH EJENDOMS_CURSOR';   
               CALL SQL_FEJL;                                           
             END;                                                       
                                                                        
         END; /* SELECT*/                                               
                                                                        
         FETCH_SQLKODE = SQLCA.SQLCODE;  /* GEMMES TIL SENERE BRUG */   
                                                                        
 END FETCH_EJENDOMSOPL;                                                 
 /*********************************************************************/
 /* SELECT EJENDOMSOPLYSN.                                            */
 /*********************************************************************/
 EJENDOMSOPL: PROC;                                                     
                                                                        
         EXEC SQL OPEN EJD_FEL_CUR;                                     
         CALL KONTROL('268',' OPEN EJD_FEL_CUR');                       
                                                                        
         EXEC SQL                                                       
                                                                        
           FETCH EJD_FEL_CUR                                            
                                                                        
           INTO :EVS_EJD_FELT.INDHOLD_C,                                
                :EVS_EJD_FELT.INDHOLD_D,                                
                :EVS_EJD_FELT.FELTKODE;                                 
                                                                        
         IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)                 
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'EJD_FEL_CUR';                                
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
         DO WHILE (SQLCA.SQLCODE = 0);                                  
                                                                        
           CALL FLYT_EJD_FEL;                                           
                                                                        
           EXEC SQL                                                     
                                                                        
             FETCH EJD_FEL_CUR                                          
                                                                        
             INTO :EVS_EJD_FELT.INDHOLD_C,                              
                  :EVS_EJD_FELT.INDHOLD_D,                              
                  :EVS_EJD_FELT.FELTKODE;                               
                                                                        
           IF (SQLCA.SQLCODE ^= 0 & SQLCA.SQLCODE ^= 100)               
           THEN                                                         
             DO;                                                        
               FEJLTEKST = 'EJD_FEL_CUR';                               
               CALL SQL_FEJL;                                           
             END;                                                       
         END;                                                           
                                                                        
         EXEC SQL CLOSE EJD_FEL_CUR;                                    
         CALL KONTROL('270',' CLOSE EJD_FEL_CUR');                      
                                                                        
 END EJENDOMSOPL;                                                       
 /*********************************************************************/
 /* CLOSE CURSOR TIL LÆS AF TABEL BQ20000T                     RUTINE */
 /*********************************************************************/
 CLOSE_EJENDOMSOPL: PROC;                                               
                                                                        
         EXEC SQL CLOSE EJENDOMS_CUR;                                   
                                                                        
         IF SQLCA.SQLCODE ^= 0                                          
         THEN                                                           
            DO;                                                         
              FEJLTEKST = 'CLOSE EJENDOMS_CURSOR';                      
              FEJLTEKST  = 'BH21000 ** 256 ' !! FEJLTEKST;              
              CALL SQL_FEJL;                                            
            END;                                                        
                                                                        
 END CLOSE_EJENDOMSOPL;                                                 
 /***********************************************************/          
 KONTROL:PROC(TAL,TEKST);                                               
 DCL TAL   CHAR (3);                                                    
 DCL TEKST CHAR (80) VARYING;                                           
         SELECT (SQLCA.SQLCODE);                                        
           WHEN (0);                                                    
           OTHER                                                        
             DO;                                                        
               FEJLTEKST  = 'BH21000 ** '!!TAL!! TEKST;                 
               CALL SQL_FEJL;                                           
             END;                                                       
         END; /* SELECT*/                                               
 END KONTROL;                                                           
