 BG25500: /* TP-INDBERETNING TIL KONTROLSYSTEM                        */
         PROC OPTIONS(MAIN,REENTRANT) REORDER;                          
 /*********************************************************************/
 /*                                                                   */
 /*      FUNKTION:    PROGRAMMET VALIDERER INDBERETNINGER TIL         */
 /*                   KONTROLSYSTEMET. VALIDE INDBERETNINGER          */
 /*                   OVERFØRES TIL DATAFANGST                        */
 /*                                                                   */
 /*      INDDATA:     TRANSAKTIONSKODE : B255                         */
 /*                   SKÆRMBILLEDE     : BG25591 INDBERETNING         */
 /*                   TEMPORARY STORAGE: VALIDE INDBERETNINGER        */
 /*                                                                   */
 /*      EXT. REF.  : ZP36300 - DATAFANGST                            */
 /*                   ZS62202 - SKAN TIOA                             */
 /*                   ZS61202 - CPR VALIDERING                        */
 /*                   ZS32102 - CIR VALIDERING                        */
 /*                   ZS22402 - DATO VALIDERING                       */
 /*                                                                   */
 /*      PROGRAMMØR:  MARGIT PEDERSBÆK / JUNI 1986                    */
 /*                                                                   */
 /*      ÅRSTILRETN:  PALLE CHRISTIANSEN, DIV. SKAT,      31.10.91    */
 /*                                                                   */
 /*      ÅRSTILRETN:  BO SCHMIDT, PUS (SIS-32622)         23.11.95    */
 /*                                                                   */
 /*      ÅRSTILRETN:  AAGE RASMUSSEN PFB SKAT             28.11.96    */
 /*                   (RETTELSER=SEKV.NR. 03 I POS. 79-80-->LEVEL 03) */
 /*         TILRETN:  AAGE RASMUSSEN PFB SKAT             16.12.96    */
 /*                   (RETTET VEDR. SIDSTE DATO FOR 1996-INDBERETNING */
 /*                    RETTELSER=SEKV.NR. 04 I POS. 79-80-->LEVEL 04) */
 /*      ÅRSTILRETN:  OLUF MØRK                           22.03.02    */
 /*                   TILRETTET DAGS_DATO.MD_DG FRA 0415 TIL 0408.    */
 /*                                                                   */
 /*      ÅRSTILRETN:  Bo Schmidt                          07.04.03    */
 /*                   TILRETTET DAGS_DATO.MD_DG FRA 0408 TIL 0409.    */
 /*                                                                   */
 /*      ÅRSTILRETN:  Bo Schmidt                          02.04.04    */
 /*                   TILRETTET DAGS_DATO.MD_DG FRA 0409 TIL 0407.    */
 /*                                                                   */
 /*         TILRETN:  Bo Schmidt                          02.04.04    */
 /*                   (RETTET AF HENSYN TIL FÆLLESSKABER O.L.)        */
 /*                                                                   */
 /*      ÅRSTILRETN:  Bo Schmidt                          04.04.05    */
 /*                   TILRETTET DAGS_DATO.MD_DG FRA 0407 TIL 0406.    */
 /*                                                                   */
 /*      ÅRSTILRETN:  Bo Schmidt                          09.01.06    */
 /*                   TILRETTET DAGS_DATO.MD_DG FRA 0406 TIL 0404.    */
 /*                                                                   */
 /*********************************************************************/
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TWA                                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     TWA_PTR         POINTER;                                       
 DCL     1 TWA           BASED(TWA_PTR),                                
                                                    % INCLUDE BG25500N;;
 DCL     1 DEF_PARM      BASED(ADDR(TWA.PARM)),                         
           2 ART         CHAR      (02),                                
           2 F1          CHAR      (01),                                
           2 ÅR          CHAR      (04),                                
           2 F2          CHAR      (01),                                
           2 KOM         CHAR      (03),                                
           2 F3          CHAR      (29);                                
 DCL     1 DEF_PARM1     CHAR      (40)    BASED(ADDR(TWA.PARM));       
                                                     % INCLUDE ZY67030N;
                                                     % INCLUDE ZB67000N;
 DCL     CWACBAR         POINTER;                                       
 DCL     TCTUA_PTR       POINTER;                                       
 DCL     TCTEKOM         CHAR      (03)   BASED (TCTUA_PTR);            
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF MAPPE     - BG25590.                              */
 /*                                                                   */
 /*********************************************************************/
 % INCLUDE BG25590N;                                                    
 DCL     MAP_STRING      CHAR      (2000)  BASED;                       
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TABEL, SOM LÆGGES HEN OVER MAPPEN                 */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 LINTAB(16)                   BASED(ADDR(BG25591I.EPNR01L)),  
           2 PNRL        BIN FIXED (15),                                
           2 PNRA        CHAR      (01),                                
           2 PNR         CHAR      (10),                                
           2 BELØBL      BIN FIXED (15),                                
           2 BELØBA      CHAR      (01),                                
           2 BELØB       PIC       '(10)9',                             
           2 DATOL       BIN FIXED (15),                                
           2 DATOA       CHAR      (01),                                
           2 DATO        CHAR      (06),                                
           2 MPNRL       BIN FIXED (15),                                
           2 MPNRA       CHAR      (01),                                
           2 MPNR        CHAR      (10);                                
 DCL     1 EDIT_LIN(16)                   BASED(ADDR(BG25591I.EPNR01L)),
           2 F1          CHAR      (17),                                
           2 BELØB       PIC       '----------',                        
           2 F2          CHAR      (25);                                
 DCL     1 LIN_CH(16)                     BASED(ADDR(BG25591I.EPNR01L)),
           2 F1          CHAR      (17),                                
           2 BELØB       CHAR      (10),                                
           2 F2          CHAR      (25);                                
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TEMPORARY STORAGE                                 */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 TS_AREAL,                                                    
           2 ART         CHAR      (02),                                
           2 NYART       CHAR      (02),                                
           2 INDB(16),                                                  
             3 PNR       CHAR      (10),                                
             3 BELØB     FIXED DEC (11),                                
             3 DATO      CHAR      (06),                                
             3 MPNR      CHAR      (10);                                
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF FEJLTABEL                                         */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 FEJLTAB(14)   CHAR  (29) INIT                                
           ('Indkomstår mangler           ',                            
            'Forkert indkomstår           ',                            
            'Art mangler                  ',                            
            'Art ukendt                   ',                            
            'Fejl i person/se-nummer      ',                            
            'Fejl i beløb                 ',                            
            'Beløb mangler                ',                            
            'Fejl i dato                  ',                            
            'Dato mangler                 ',                            
            'Personnummer mangler         ',                            
            'Skilletegn forkert           ',                            
            'Art og indkomstår mangler    ',                            
            'Fejl i modtagers personnummer',                            
            'Kommunenummer mangler        ');                           
 /*********************************************************************/
 /*                                                                   */
 /*      HER ER ARTERNE I KONTROLSYSTEMET, JFR. EFTERFØLGENDE         */
 /*      GÆLDENDE FRA INDKOMSTÅRET  1991.                             */
 /*                                                                   */
 /*      ART/TEKST                               MASKINEL/MANUEL      */
 /*      10  PENSION MED  RET TIL FORH. FRADR.       10               */
 /*      11  PENSION UDEN RET TIL FORH. FRADR.       11               */
 /*      12  LOMMEPENGE TIL BØRN OG UNGE             12     12        */
 /*      13  SKATTEPLIGTIG BARSELSFÆRDBIDRAG         13     13        */
 /*      14  EJ SKATTEPLIGTIG BARSELSFÆRDBIDRAG      14     14        */
 /*      15  MODTAGET UDDANNELSESBIDRAG              15     15        */
 /*      16  YDET          DO                        16     16        */
 /*      17  MODTAGET ÆGTEFÆLLEBIDRAG                17     17        */
 /*      18  YDET          DO                        18     18        */
 /*      20  BOLIGSIKRING                            20     20        */
 /*      30  BØRNETILSKUD                            30               */
 /*      40  SÆRLIG HJÆLP                            40     40        */
 /*      41  UDENLANDSK PENSION                      41               */
 /*      50  STATSTILSKUD TIL SKOLEOPHOLD                   50        */
 /*      58  MEDARBEJDERAKTIER                              58        */
 /*      61  DAGPENGEREFUSION                        61               */
 /*      65  IBRUGTAGNINGSTILLADELSE                        65        */
 /*      69  MODTAGET BØRNEBIDRAG, SK.PL.            69     69        */
 /*      70  MODTAGET BØRNEBIDRAG, NORMALBIDR.       70     70        */
 /*      71  KOMM.TILSKUD TIL SKOLEOPHOLD                   71        */
 /*      72  YDET     BØRNEBIDRAG                    72     72        */
 /*      73  ARVEBELØB                                      73        */
 /*      86  YDET GAVEBELØB                                 86        */
 /*      87  MODTAGET GAVEBELØB                             87        */
 /*      94  -  UTEKSTET  YDELSE  -                         94        */
 /*      95  -  UTEKSTET  YDELSE  -                         95        */
 /*      96  -  UTEKSTET  YDELSE  -                         96        */
 /*      97  -  UTEKSTET  YDELSE  -                         97        */
 /*      98  -  UTEKSTET  YDELSE  -                         98        */
 /*      99  -  UTEKSTET  YDELSE  -                         99        */
 /*                                                                   */
 /*********************************************************************/
 /*********************************************************************/
 /*                                                                   */
 /*      MANUELLE ARTER I KONTROLSYSTEMET, JFR. OGSÅ FORANSTÅENDE     */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 ART_KOD(26)   CHAR (2) INIT                                  
           ('12','13','14','15','16','17','18',                         
            '20',                                                       
            '40',                                                       
            '50','58',                                                  
            '65','69',                                                  
            '70','71','72','73','78',                                   
            '86','87',                                                  
            '94','95','96','97','98','99');                             
 DCL      1 ART_TEKST(26)  CHAR (55) INIT                               
   ('LOMMEPENGE TIL BØRN OG UNGE                            ', /* 12 */ 
    'BARSELSFÆRDBIDRAG M.M. (skattepligtig del)             ', /* 13 */ 
    'BARSELSFÆRDBIDRAG M.M. (ikke skattepligtig del)        ', /* 14 */ 
    'MODTAGET UDDANNELSESBIDRAG                             ', /* 15 */ 
    'YDET UDDANNELSESBIDRAG                                 ', /* 16 */ 
    'MODTAGET ÆGTEFÆLLEBIDRAG                               ', /* 17 */ 
    'YDET ÆGTEFÆLLEBIDRAG                                   ', /* 18 */ 
    'BOLIGSIKRING                                           ', /* 20 */ 
    'SÆRLIG HJÆLP                                           ', /* 40 */ 
    'STATSTILSKUD TIL SKOLEOPHOLD                           ', /* 50 */ 
    'MEDARBEJDERAKTIER                                      ', /* 58 */ 
    'IBRUGTAGNINGSTILLADELSE                                ', /* 65 */ 
    'MODTAGET BØRNEBIDRAG (skattepligtigt)                  ', /* 69 */ 
    'MODTAGET BØRNEBIDRAG (ikke skattepligtigt NORMALBIDRAG)', /* 70 */ 
    'KOMM. TILSKUD TIL SKOLEOPHOLD                          ', /* 71 */ 
    'YDET BØRNEBIDRAG (fradragsberettiget)                  ', /* 72 */ 
    'ARVEBELØB                                              ', /* 73 */ 
    'YDET BØRNEBIDRAG (ikke fradragsberettiget)             ', /* 78 */ 
    'YDET GAVEBELØB                                         ', /* 86 */ 
    'MODTAGET GAVEBELØB                                     ', /* 87 */ 
    '                                                       ', /* 94 */ 
    '                                                       ', /* 95 */ 
    '                                                       ', /* 96 */ 
    '                                                       ', /* 97 */ 
    '                                                       ', /* 98 */ 
    '                                                       ');/* 99 */ 
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF DIVERSE                                           */
 /*                                                                   */
 /*********************************************************************/
 DCL     FEJLNR         BIN FIXED  (15);                                
 DCL     LININDX        BIN FIXED  (15);                                
 DCL     CURSOR_POS     PIC        '99';                                
 DCL     I              BIN FIXED  (15);                                
 DCL     ANT_DF         DEC FIXED  (02);                                
 DCL     LÆST_TS        CHAR       (03);                                
 DCL     KLOKKEN        PIC        '(7)9';                              
 DCL     1 KL           BASED(ADDR(KLOKKEN)),                           
           2 FLYD       PIC        '9',                                 
           2 TIM        PIC        '99',                                
           2 MIN        PIC        '99',                                
           2 SEK        PIC        '99';                                
 DCL     SLUT_ÅR        CHAR       (04);                                
 DCL     SLUT_ÅR1       PIC        '9999' BASED(ADDR(SLUT_ÅR));         
 DCL     HEX80_INIT     BIT        (08)   INIT('10000000');             
 DCL     HEX80          CHAR        (1)   BASED(ADDR(HEX80_INIT));      
 DCL     DAGS_DATO       PIC       '(8)9';                              
 DCL     1 DAGS_DATO1                     BASED(ADDR(DAGS_DATO)),       
           2 ÅR          PIC       '9999',                              
           2 MD          PIC       '99',                                
           2 DAG         PIC       '99';                                
 DCL     1 DAGS_DATO2                     BASED(ADDR(DAGS_DATO)),       
           2 ÅR          PIC       '9999',                              
           2 MD_DG       PIC       '9999';                              
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF BUILTIN                                           */
 /*                                                                   */
 /*********************************************************************/
 DCL     PLIXOPT         CHAR      (04) VAR STATIC EXT INIT ('STAE');   
 DCL     (ADDR,                                                         
          CSTG,                                                         
          INDEX,                                                        
          LOW,                                                          
          MOD,                                                          
          STG,                                                          
          SUBSTR,                                                       
          VERIFY)         BUILTIN;                                      
 /*********************************************************************/
 /*                                                                   */
 /*      HOUSE KEEPING                                                */
 /*                                                                   */
 /*********************************************************************/
         EXEC CICS ADDRESS CWA   (CWACBAR)                              
                           TWA   (TWA_PTR)                              
                           TCTUA (TCTUA_PTR)                            
     ;                                                                  
         TWA.TERMKOM = TCTEKOM;                                         
         PARMPTR  = ADDR (PARMPTR1);                                    
         PARMPTR1 = ADDR (TRANS);                                       
         PARMPTR2 = ADDR (PARM);                                        
         PARMPTR3 = ADDR (MAPID);                                       
         PARMPTR4 = ADDR (SCAN_TIOA_DATA.CRETUR);                       
         EXEC CICS LINK PROGRAM ('ZS62202')                             
     ;                                                                  
         FEJLNR = 0;                                                    
         ANT_DF = 0;                                                    
         CURSOR_POS = 0;                                                
         DAGS_DATO = ZB67000N.B00STD7;                                  
         SLUT_ÅR   = DEF_PARM.ÅR;                                       
         DEF_PARM.F3 = ' ';                                             
                                                                        
         IF ZM_HENT_KOMMUNE_KODE > 999                                  
         THEN                                                           
            TWA.EKOM = DEF_PARM.KOM;                                    
         ELSE                                                           
            TWA.EKOM = TCTEKOM;                                         
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      PROCES                                                       */
 /*                                                                   */
 /*********************************************************************/
         IF TWA.MAPID = 'BG25591'                                       
         THEN                                                           
            DO;                                                         
               EXEC CICS HANDLE CONDITION ERROR (ABEND01)               
                                          MAPFAIL (MAPFEJL)             
          ;                                                             
               EXEC CICS RECEIVE MAP('BG25591')                         
                                 MAPSET ('BG25590')                     
          ;                                                             
 MAPFEJL:                                                               
            END;                                                        
         IF TWA.MAPID ^= 'BG25591'                                      
         THEN                                                           
            DO;                                                         
               CALL SLET_TS;                                            
               CALL VALIDER_ART (DEF_PARM.ART,'PARM ');                 
               IF DEF_PARM.F1 = ' ' !                                   
                  DEF_PARM.F1 = ',' !                                   
                  DEF_PARM.F1 = '.'                                     
               THEN;                                                    
               ELSE                                                     
                  DO;                                                   
                     IF FEJLNR = 0                                      
                     THEN                                               
                        DO;                                             
                           FEJLNR = 11;                                 
                           CURSOR_POS = 10;                             
                           BG25592O.HPARMA = 'I';                       
                        END;                                            
                  END;                                                  
               CALL VALIDER_ÅR (DEF_PARM.ÅR );                          
                                                                        
               IF FEJLNR = 0                                            
                & ZM_HENT_KOMMUNE_KODE > 999                            
               THEN                                                     
                  DO;                                                   
                     IF VERIFY (DEF_PARM.KOM,'0123456789') = 0          
                     THEN                                               
                        DO;                                             
                          BG25591O.NYARTO = DEF_PARM.ART;               
                          TS_AREAL.NYART  = DEF_PARM.ART;               
                          CALL OPBYG_INDBSKÆRM;                         
                        END;                                            
                     ELSE                                               
                        DO;                                             
                           FEJLNR = 14;                                 
                           CURSOR_POS = 16;                             
                           BG25592O.HPARMA = 'I';                       
                           CALL OPBYG_FEJLSKÆRM;                        
                        END;                                            
                  END;                                                  
               ELSE                                                     
               IF FEJLNR = 0                                            
               THEN                                                     
                  DO;                                                   
                     BG25591O.NYARTO = DEF_PARM.ART;                    
                     TS_AREAL.NYART  = DEF_PARM.ART;                    
                     CALL OPBYG_INDBSKÆRM;                              
                  END;                                                  
               ELSE                                                     
                  CALL OPBYG_FEJLSKÆRM;                                 
               GO TO RETUR;                                             
            END;                                                        
         ELSE                                                           
            CALL LÆS_TS;                                                
 /*********************************************************************/
 /*                                                                   */
 /*      BEHANDLING AF INDBERETNING                                   */
 /*                                                                   */
 /*********************************************************************/
         TWA.CART = BG25591I.CARTI;                                     
         DO LININDX = 1 TO 16;                                          
            IF SUBSTR(LINTAB(LININDX).PNR,1,1) = 'P'                    
            THEN                                                        
               DO;                                                      
                  LINTAB(LININDX) = '';                                 
                  LIN_CH(LININDX).BELØB = ' ';                          
                  TS_AREAL.INDB(LININDX) = '';                          
               END;                                                     
            IF ( LINTAB(LININDX).PNRL     > 0   !                       
                 LINTAB(LININDX).BELØBL   > 0   !                       
                 LINTAB(LININDX).DATOL    > 0   !                       
                 LINTAB(LININDX).MPNRL    > 0   !                       
                 TS_AREAL(LININDX).PNR ^= ' ' !                         
                 TS_AREAL(LININDX).BELØB ^= 0   !                       
                 TS_AREAL(LININDX).DATO  ^= ' ' !                       
                 TS_AREAL(LININDX).MPNR  ^= ' ' )                       
            THEN                                                        
               DO;                                                      
                  CALL DAN_TS;                                          
                  CALL VALIDER_INDB;                                    
               END;                                                     
         END;                                                           
         IF BG25591I.NYARTL > 0                                         
         THEN                                                           
            DO;                                                         
               CALL VALIDER_ART(BG25591I.NYARTI,'NYART');               
               TS_AREAL.NYART = BG25591I.NYARTI;                        
            END;                                                        
         IF FEJLNR > 0                                                  
         THEN                                                           
            DO;                                                         
               CALL SKRIV_TS;                                           
               CALL OPBYG_FEJLSKÆRM;                                    
            END;                                                        
         ELSE                                                           
            DO;                                                         
               CALL DATAFANGST;                                         
               CALL SLET_TS;                                            
               CALL OPBYG_INDBSKÆRM;                                    
            END;                                                        
 RETUR:  EXEC CICS RETURN;                                              
 /*********************************************************************/
 /*                                                                   */
 /*      SLETNING AF TEMPORÆR STORAGE                                 */
 /*                                                                   */
 /*********************************************************************/
 SLET_TS: PROC;                                                         
               EXEC CICS HANDLE CONDITION QIDERR (INGEN_B255)           
           ;                                                            
               TS_NØGLE = 'B255' !! EIBTRMID;                           
               EXEC CICS DELETEQ TS QUEUE (TS_NØGLE)                    
           ;                                                            
 INGEN_B255:                                                            
 END SLET_TS;                                                           
 /*********************************************************************/
 /*                                                                   */
 /*      LÆS AF TEMPORÆR STORAGE                                      */
 /*                                                                   */
 /*********************************************************************/
 LÆS_TS: PROC;                                                          
         TWA.TS_NØGLE = 'B255' !! EIBTRMID;                             
         TWA.TS_LGD = STG(TS_AREAL);                                    
         TWA.TS_ITEM = 1;                                               
         EXEC CICS HANDLE CONDITION ERROR(ABEND03) QIDERR (INGEN_TS)    
         ;                                                              
         EXEC CICS READQ TS QUEUE (TWA.TS_NØGLE)                        
                            INTO (TS_AREAL)                             
                            LENGTH(TWA.TS_LGD)                          
                            ITEM(TWA.TS_ITEM)                           
           ;                                                            
         LÆST_TS = 'JA';                                                
         IF BG25591I.CARTI ^= TS_AREAL.ART                              
         THEN                                                           
            TS_AREAL = '';                                              
         RETURN;                                                        
 INGEN_TS:                                                              
         LÆST_TS = 'NEJ';                                               
         TS_AREAL = '';                                                 
 END LÆS_TS;                                                            
 /*********************************************************************/
 /*                                                                   */
 /*      SKRIV AF TEMPORÆR STORAGE                                    */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_TS: PROC;                                                        
           TWA.TS_NØGLE = 'B255' !! EIBTRMID;                           
           TWA.TS_LGD = STG(TS_AREAL);                                  
           TWA.TS_ITEM = 1;                                             
           EXEC CICS HANDLE CONDITION ERROR(ABEND02)                    
           ;                                                            
           IF LÆST_TS = 'JA'                                            
           THEN                                                         
              EXEC CICS WRITEQ TS QUEUE (TWA.TS_NØGLE)                  
                                  FROM(TS_AREAL)                        
                                  LENGTH(TWA.TS_LGD)                    
                                  ITEM(TWA.TS_ITEM)                     
                                  REWRITE                               
           ;                                                            
           ELSE                                                         
              EXEC CICS WRITEQ TS QUEUE(TWA.TS_NØGLE)                   
                                  FROM(TS_AREAL)                        
                                  LENGTH(TWA.TS_LGD)                    
                                  ITEM(TWA.TS_ITEM)                     
        ;                                                               
 END SKRIV_TS;                                                          
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING                                                   */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_INDB: PROC;                                                    
         IF LINTAB(LININDX).PNRL > 0                                    
         THEN                                                           
            CALL VALIDER_PNR;                                           
         IF (TS_AREAL.PNR(LININDX) = ' ' ) &                            
            (TS_AREAL.BELØB(LININDX) ^= 0    !                          
             TS_AREAL.DATO(LININDX)  ^= ' '  !                          
             TS_AREAL.MPNR(LININDX)  ^= ' ')                            
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 10;                                          
               LINTAB(LININDX).PNRL = -1;                               
            END;                                                        
         IF LINTAB(LININDX).BELØBL > 0                                  
         THEN                                                           
            CALL VALIDER_BELØB;                                         
         IF ( TS_AREAL.BELØB(LININDX) = 0 &         /* BELØB MANGLER */ 
              TS_AREAL.PNR(LININDX) ^= ' ')                             
         THEN                                                           
            DO;                                                         
               IF ( TS_AREAL.ART  = '58'  !                             
                    TS_AREAL.ART  = '65'  !                             
                    TS_AREAL.ART >= '94' )                              
               THEN;                                                    
               ELSE                                                     
                  DO;                                                   
                     IF FEJLNR = 0                                      
                     THEN                                               
                        FEJLNR = 7;                                     
                     LINTAB(LININDX).BELØBL = -1;                       
                  END;                                                  
            END;                                                        
         IF LINTAB(LININDX).DATOL > 0                                   
         THEN                                                           
            CALL VALIDER_DATO;                                          
         IF ( TS_AREAL.DATO(LININDX)  = ' ' &      /* DATO MANGLER */   
              TS_AREAL.PNR(LININDX) ^= ' ')                             
         THEN                                                           
            DO;                                                         
               IF ( TS_AREAL.ART = '58'  !                              
                    TS_AREAL.ART = '65'  !                              
                    TS_AREAL.ART = '73' )                               
               THEN                                                     
                  DO;                                                   
                     IF FEJLNR = 0                                      
                     THEN                                               
                        FEJLNR = 9;                                     
                     LINTAB(LININDX).DATOL = -1;                        
                  END;                                                  
            END;                                                        
         IF LINTAB(LININDX).MPNRL > 0                                   
         THEN                                                           
            CALL VALIDER_MPNR;                                          
 END VALIDER_INDB;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF INDKOMSTÅR                                     */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_ÅR: PROC (PARM_ÅR);                                            
 DCL     PARM_ÅR         CHAR      (04);                                
 DCL     PARM_ÅR1        PIC       '9999' BASED(ADDR(PARM_ÅR));         
         IF CURSOR_POS = 0            /* HVIS DER IKKE ER FUNDET FEJL */
         THEN                         /* I PARM.FELT TIDLIGERE        */
            BG25592O.HPARMA = ' ';                                      
         IF PARM_ÅR = '    ' ! PARM_ÅR = LOW(4)                         
         THEN                                                           
            DO;                                                         
               IF ( FEJLNR = 3 )                 /* ART OG ÅR MANGLER */
               THEN                                                     
                  FEJLNR = 12;                                          
               IF FEJLNR = 0                                            
               THEN                                                     
                  DO;                                                   
                     FEJLNR = 1;                                        
                     CURSOR_POS = 11;                                   
                  END;                                                  
               BG25592O.HPARMA = 'I';                                   
               RETURN;                                                  
            END;                                                        
         IF VERIFY(PARM_ÅR,'0123456789') ^= 0                           
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  DO;                                                   
                     FEJLNR = 2;                                        
                     CURSOR_POS = 11;                                   
                  END;                                                  
               BG25592O.HPARMA = 'I';                                   
               RETURN;                                                  
            END;                                                        
         IF (PARM_ÅR1 = DAGS_DATO1.ÅR)  !                               
           ((PARM_ÅR1 = DAGS_DATO1.ÅR - 1) & DAGS_DATO2.MD_DG < 0404)   
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  DO;                                                   
                     FEJLNR = 2;                                        
                     CURSOR_POS = 11;                                   
                  END;                                                  
               BG25592O.HPARMA = 'I';                                   
            END;                                                        
 END VALIDER_ÅR;                                                        
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF ART                                            */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_ART: PROC(ART,TXT);                                            
 DCL     ART             CHAR      (02);                                
 DCL     TXT             CHAR      (05);                                
         IF TXT = 'PARM'                                                
         THEN                                                           
            BG25592O.HPARMA = ' ';                                      
         ELSE                                                           
            BG25591O.NYARTA = '&';                                      
         IF TXT = 'PARM' & (ART = ' ' ! ART = LOW(2))                   
         THEN                                                           
            DO;                                                         
               FEJLNR = 3;                                              
               CURSOR_POS  = 8;                                         
               BG25592O.HPARMA = 'I';                                   
            END;                                                        
         ELSE                                                           
            DO;                                        /* SIS-32622 */  
               DO I = 1 TO 26 WHILE (ART ^= ART_KOD(I));  /* HERFRA */  
               END;                                                     
               IF I > 26                                                
               THEN                                                     
                  I = 26;                                 /* HERTIL */  
               IF ART ^= ART_KOD(I)                                     
               THEN                                                     
                  DO;                                                   
                     IF FEJLNR = 0                                      
                     THEN                                               
                        FEJLNR = 4;                                     
                     IF TXT = 'PARM'                                    
                     THEN                                               
                        DO;                                             
                           CURSOR_POS = 8;                              
                           BG25592O.HPARMA = 'I';                       
                        END;                                            
                     ELSE                                               
                        DO;                                             
                           BG25591I.NYARTL = -1;                        
                           BG25591O.NYARTA = 'R';                       
                        END;                                            
                  END;                                                  
            END;                                                        
 END VALIDER_ART;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF PERSONNUMMER ELLER SE-NUMMER                   */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_PNR: PROC;                                                     
         LINTAB(LININDX).PNRA = ' ';                                    
         TWA.CPR = TS_AREAL.PNR(LININDX);                               
         IF VERIFY(TWA.CPR,'0123456789') ^= 0 !                         
            TWA.CPR = '0000000000'            !                         
          ((TS_AREAL.ART = '12' !                                       
            TS_AREAL.ART = '13' !                                       
            TS_AREAL.ART = '14' !                                       
            TS_AREAL.ART = '15' !                                       
            TS_AREAL.ART = '16' !                                       
            TS_AREAL.ART = '17' !                                       
            TS_AREAL.ART = '18' !                                       
            TS_AREAL.ART = '20' !                                       
            TS_AREAL.ART = '40' !                                       
            TS_AREAL.ART = '69' !                                       
            TS_AREAL.ART = '70' !                                       
            TS_AREAL.ART = '72' !                                       
            TS_AREAL.ART = '78')  &                                     
            SUBSTR(TWA.CPR,1,2) = '00')                /* SE-NUMMER */  
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 5;                                           
               LINTAB(LININDX).PNRL = -1;                               
               LINTAB(LININDX).PNRA = 'I';                              
               RETURN;                                                  
            END;                                                        
         TWA.PARMPTR1 = ADDR(TWA.CPR_CHECK.CPR);                        
         TWA.PARMPTR2 = ADDR(TWA.CPR_CHECK.CRETUR);                     
         TWA.PARMPTR  = ADDR(TWA.PARMPTR1);                             
         IF SUBSTR(TWA.CPR,1,2) = '00'                 /* SE-NUMMER */  
         THEN                                                           
            CPR_CHECK.CRETUR = '2';                                     
         ELSE                                                           
            CPR_CHECK.CRETUR = '1';                   /* PERSONNUMMER */
         EXEC CICS LINK PROGRAM ('ZS61202')                             
         ;                                                              
         IF CPR_CHECK.CRETUR ^= '0'                                     
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 5;                                           
               LINTAB(LININDX).PNRL = -1;                               
               LINTAB(LININDX).PNRA = 'I';                              
            END;                                                        
 END VALIDER_PNR;                                                       
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF MODTAGERS PERSONNUMMER                         */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_MPNR: PROC;                                                    
         LINTAB(LININDX).MPNRA = '&';                                   
         TWA.CPR = TS_AREAL.MPNR(LININDX);                              
         IF TWA.CPR = '0000000000' !                                    
            TWA.CPR = '          ' !                                    
            TWA.CPR = LOW(10)                                           
         THEN                                                           
            DO;                                                         
               TS_AREAL.MPNR(LININDX) = ' ';                            
               RETURN;                                                  
            END;                                                        
         IF VERIFY(TWA.CPR,'0123456789') ^= 0 !                         
            TWA.CPR = '0000000000'                                      
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 13;                                          
               LINTAB(LININDX).MPNRL = -1;                              
               LINTAB(LININDX).MPNRA = 'I';                             
               RETURN;                                                  
            END;                                                        
         TWA.PARMPTR1 = ADDR(TWA.CPR_CHECK.CPR);                        
         TWA.PARMPTR2 = ADDR(TWA.CPR_CHECK.CRETUR);                     
         TWA.PARMPTR  = ADDR(TWA.PARMPTR1);                             
         CPR_CHECK.CRETUR = '1';                   /* PERSONNUMMER */   
         EXEC CICS LINK PROGRAM ('ZS61202')                             
         ;                                                              
         IF CPR_CHECK.CRETUR ^= '0'                                     
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 13;                                          
               LINTAB(LININDX).MPNRL = -1;                              
               LINTAB(LININDX).MPNRA = 'I';                             
            END;                                                        
 END VALIDER_MPNR;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF DATO                                           */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_DATO: PROC;                                                    
 DCL     INDB_DATO       CHAR      (06);                                
 DCL     INDB_DATO_PIC   PIC       '(6)9'   BASED(ADDR(INDB_DATO));     
 DCL     1 INDB_DATO_DEF                    BASED(ADDR(INDB_DATO)),     
           2 DAG        CHAR        (2),                                
           2 MD         CHAR        (2),                                
           2 ÅR         CHAR        (2);                                
 DCL     INDB_DATO1      PIC       '(8)9';                              
 DCL     INDB_DATO1_CH   CHAR      (08)     BASED(ADDR(INDB_DATO1));    
 DCL     1 INDB_DATO1_DEF                   BASED(ADDR(INDB_DATO1)),    
           2 ÅR          PIC     '9999',                                
           2 MD          PIC       '99',                                
           2 DAG         PIC       '99';                                
         LINTAB(LININDX).DATOA = '&';                                   
         IF VERIFY(TS_AREAL.DATO(LININDX),'0123456789') ^= 0            
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 8;                                           
               LINTAB(LININDX).DATOL = -1;                              
               LINTAB(LININDX).DATOA = 'R';                             
               RETURN;                                                  
            END;                                                        
         TWA.PARMPTR = ADDR(PARMLIST);                                  
         TWA.PARMPTR1 = ADDR(DATO_CHECK.DATO1);                         
         TWA.PARMPTR2 = ADDR(DATO_CHECK.DATO2);                         
         TWA.PARMPTR3 = ADDR(DATO_CHECK.ANT_DAG);                       
         TWA.PARMPTR4 = ADDR(DATO_CHECK.FORMAT);                        
         INDB_DATO          = TS_AREAL.DATO(LININDX);                   
         DATO_CHECK.DATO1   = INDB_DATO_PIC;                            
         DATO_CHECK.DATO2   = 0;                                        
         DATO_CHECK.ANT_DAG = 0;                                        
         DATO_CHECK.FORMAT  = 2;                                        
         EXEC CICS LINK PROGRAM ('ZS22402')                             
         ;                                                              
         IF DATO_CHECK.FORMAT ^= 0                                      
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 8;                                           
               LINTAB(LININDX).DATOL = -1;                              
               LINTAB(LININDX).DATOA = 'R';                             
               RETURN;                                                  
            END;                                                        
         /* DEN INDBERETTEDE DATO SKAL VÆRE MINDRE END */               
         /* ELLER LIG MED DAGS DATO                    */               
         INDB_DATO = TS_AREAL.DATO(LININDX);                            
         /* VEND DATO OG PÅSÆT ÅRHUNDREDE */                            
         IF INDB_DATO_DEF.ÅR >= '90' &                                  
            INDB_DATO_DEF.ÅR <= '99'                                    
         THEN                                                           
            INDB_DATO1_CH = '19' !!                                     
                            INDB_DATO_DEF.ÅR !!                         
                            INDB_DATO_DEF.MD !!                         
                            INDB_DATO_DEF.DAG;                          
         ELSE                                                           
            INDB_DATO1_CH = '20' !!                                     
                            INDB_DATO_DEF.ÅR !!                         
                            INDB_DATO_DEF.MD !!                         
                            INDB_DATO_DEF.DAG;                          
         IF INDB_DATO1 > DAGS_DATO                                      
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 8;                                           
               LINTAB(LININDX).DATOL = -1;                              
               LINTAB(LININDX).DATOA = 'R';                             
               RETURN;                                                  
            END;                                                        
         IF DAGS_DATO1.ÅR - INDB_DATO1_DEF.ÅR > 2  /* DATO MAKSIMUM   */
         THEN                                      /* 2 ÅR BAGUD      */
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 8;                                           
               LINTAB(LININDX).DATOL = -1;                              
               LINTAB(LININDX).DATOA = 'R';                             
               RETURN;                                                  
            END;                                                        
         IF INDB_DATO1_DEF.ÅR > SLUT_ÅR1           /* DATO MAKSIMUM   */
         THEN                                      /* TIL SLUTLIGN.ÅR */
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 8;                                           
               LINTAB(LININDX).DATOL = -1;                              
               LINTAB(LININDX).DATOA = 'R';                             
               RETURN;                                                  
            END;                                                        
 END VALIDER_DATO;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      VALIDERING AF BELØB                                          */
 /*                                                                   */
 /*********************************************************************/
 VALIDER_BELØB: PROC;                                                   
 DCL     START_POS       FIXED DEC (02);                                
 DCL     MINUS_POS       FIXED DEC (02);                                
 DCL     ANT_MINUS       FIXED DEC (02);                                
 DCL     FEJL            CHAR      (03);                                
 DCL     HJ_BELØB        CHAR      (10);                                
 DCL     BELØB_CIF(10)   CHAR      (01) BASED(ADDR(HJ_BELØB));          
         START_POS = 0;                                                 
         ANT_MINUS = 0;                                                 
         MINUS_POS = 0;                                                 
         FEJL      = 'NEJ';                                             
         LINTAB(LININDX).BELØBA = '&';                                  
         IF VERIFY(LIN_CH.BELØB(LININDX),' -0123456789') ^= 0           
         THEN                                                           
            FEJL = 'JA';                                                
         IF FEJL = 'NEJ'           /* DET UNDERSØGES OM DER ER MINUS- */
         THEN                      /* TEGN MIDT I BELØBET             */
            DO;                                                         
               HJ_BELØB = LIN_CH.BELØB(LININDX);                        
               DO I = 1 TO 10;                                          
                  IF  BELØB_CIF(I) ^= '0' & START_POS = 0               
                  THEN                                                  
                     START_POS = I;                                     
                  IF BELØB_CIF(I) = '-'                                 
                  THEN                                                  
                     DO;                                                
                        ANT_MINUS = ANT_MINUS + 1;                      
                        MINUS_POS = I;                                  
                     END;                                               
               END;                                                     
               IF ANT_MINUS > 1                                         
               THEN                                                     
                  FEJL = 'JA';                                          
               IF MINUS_POS ^= 0         &                              
                  MINUS_POS ^= 10        &                              
                  START_POS ^= MINUS_POS                                
               THEN                                                     
                  FEJL = 'JA';                                          
            END;                                                        
         IF FEJL = 'NEJ'                                                
         THEN                                                           
            DO;                                                         
               IF LINTAB(LININDX).BELØB >  999999999                    
               THEN                                                     
                  FEJL = 'JA';                                          
            END;                                                        
         IF FEJL = 'JA'                                                 
         THEN                                                           
            DO;                                                         
               IF FEJLNR = 0                                            
               THEN                                                     
                  FEJLNR = 6;                                           
               LINTAB(LININDX).BELØBL = -1;                             
               LINTAB(LININDX).BELØBA = 'R';                            
               LIN_CH(LININDX).BELØB  = LOW(10);                        
            END;                                                        
         ELSE                                                           
            EDIT_LIN(LININDX).BELØB  = TS_AREAL.BELØB(LININDX);         
         IF LINTAB(LININDX).BELØB = 0                                   
         THEN                                                           
            LIN_CH(LININDX).BELØB  = LOW(10);                           
 END VALIDER_BELØB;                                                     
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG INDBERETNINGSBILLEDE                                   */
 /*                                                                   */
 /*********************************************************************/
 OPBYG_INDBSKÆRM: PROC;                                                 
         SUBSTR (ADDR(BG25591O)->MAP_STRING,1,STG(BG25591O)) =          
                                                   LOW (STG(BG25591O)); 
         BG25591O.CTRANSO = 'B255';                                     
         BG25591O.DDATOO  = ZB67000N.B00STD1;                           
         KLOKKEN = EIBTIME;                                             
         BG25591O.DKLOKO  = KL.TIM !! '.' !! KL.MIN;                    
         BG25591O.CTERMO  = EIBTRMID;                                   
         BG25591O.EKNRO   = TWA.TERMKOM;                                
         BG25591O.SLUTÅRO = DEF_PARM.ÅR;                                
         SUBSTR(BG25591O.HPARMO,15,10) = ' ';                           
         IF TS_AREAL.NYART ^= '  '                                      
         THEN                                                           
            DO;                                                         
               BG25591O.NYARTO = TS_AREAL.NYART;                        
               BG25591O.CARTO  = TS_AREAL.NYART;                        
            END;                                                        
         ELSE                                                           
            DO;                                                         
               BG25591O.NYARTO = TWA.CART;                              
               BG25591O.CARTO  = TWA.CART;                              
            END;                                       /* SIS-32622 */  
         DO I = 1 TO 26 WHILE (BG25591I.CARTI ^= ART_KOD(I));           
         END;                                                           
         IF I > 26                                                      
         THEN                                                           
            I = 26;                                                     
         BG25591O.HTXTARTO = ART_TEKST(I);                              
         DEF_PARM.ART      = BG25591O.CARTO;                            
         BG25591O.HPARMO  = TWA.PARM;                                   
         BG25591O.HPARMA = '1';                                         
         BG25591O.GEMPARMO = DEF_PARM1;                                 
         CALL OPSÆT_FELTER;                                             
         IF ANT_DF = 1                                                  
         THEN                                                           
           BG25591O.HFEJLO =                                            
                   ANT_DF !! ' indberetning overført til datafangst';   
         IF ANT_DF > 1                                                  
         THEN                                                           
           BG25591O.HFEJLO =                                            
                   ANT_DF !! ' indberetninger overført til datafangst'; 
         BG25591I.EPNR01L = -1;                                         
         EXEC CICS SEND MAPSET('BG25590')                               
                         MAP('BG25591')                                 
                         ERASE                                          
                         FREEKB                                         
                         CURSOR                                         
      ;                                                                 
         EXEC CICS RETURN                                               
      ;                                                                 
 END OPBYG_INDBSKÆRM;                                                   
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG FEJLSKÆRM                                              */
 /*                                                                   */
 /*********************************************************************/
 OPBYG_FEJLSKÆRM: PROC;                                                 
         IF CURSOR_POS = 0             /* VED FEJL I INDBERETNINGEN */  
         THEN                                                           
            DO;                                                         
               BG25591O.CTRANSO = 'B255';                               
               BG25591O.CTRANSA = 'A';                                  
               BG25591O.HPARMO  = TWA.PARM;                             
               BG25591O.HPARMA = '1';                                   
               BG25591O.DDATOO  = ZB67000N.B00STD1;                     
               KLOKKEN = EIBTIME;                                       
               BG25591O.DKLOKO  = KL.TIM !! '.' !! KL.MIN;              
               BG25591O.CTERMO  = EIBTRMID;                             
               BG25591O.EKNRO   = TWA.TERMKOM;                          
               SUBSTR(BG25591O.HPARMO,15,10) = ' ';                     
               BG25591O.HFEJLO  = FEJLTAB(FEJLNR);                      
               CALL OPSÆT_FELTER;                                       
               EXEC CICS SEND MAP    ('BG25591')                        
                           MAPSET ('BG25590')                           
                           ALARM                                        
                           DATAONLY                                     
                           FREEKB                                       
                           CURSOR                                       
            ;                                                           
            END;                                                        
         ELSE                        /* VED FEJL I PARAMETERFELT */     
            DO;                                                         
               BG25592O.CTRANSO = 'B255';                               
               BG25592O.CTRANSA = 'A';                                  
               BG25592O.HPARMO  = TWA.PARM;                             
               BG25592O.DDATOO  = ZB67000N.B00STD1;                     
               KLOKKEN = EIBTIME;                                       
               BG25592O.DKLOKO  = KL.TIM !! '.' !! KL.MIN;              
               BG25592O.CTERMO  = EIBTRMID;                             
               BG25592O.EKNRO   = TWA.TERMKOM;                          
               SUBSTR(BG25592O.HPARMO,15,10) = ' ';                     
               BG25592O.HFEJLO  = FEJLTAB(FEJLNR);                      
               EXEC CICS SEND MAP    ('BG25592')                        
                           MAPSET ('BG25590')                           
                           ALARM                                        
                           ERASE                                        
                           FREEKB                                       
                           CURSOR(CURSOR_POS)                           
               ;                                                        
            END;                                                        
 RETUR:                                                                 
         EXEC CICS RETURN                                               
     ;                                                                  
 END OPBYG_FEJLSKÆRM;                                                   
 /*********************************************************************/
 /*                                                                   */
 /*      DATO, BELØBSFELT OG MODTAGERS PERSONNUMMER VISES KUN FOR DE  */
 /*      ARTER HVOR DET ER TILLADT AT INDTASTE DE PÅGÆLDENDE FELTER   */
 /*********************************************************************/
 OPSÆT_FELTER: PROC;                                                    
         IF BG25591O.CARTO = '12' !                                     
            BG25591O.CARTO = '13' !                                     
            BG25591O.CARTO = '14' !                                     
            BG25591O.CARTO = '15' !                                     
            BG25591O.CARTO = '16' !                                     
            BG25591O.CARTO = '17' !                                     
            BG25591O.CARTO = '18' !                                     
            BG25591O.CARTO = '20' !                                     
            BG25591O.CARTO = '40' !                                     
            BG25591O.CARTO = '69' !                                     
            BG25591O.CARTO = '70' !                                     
            BG25591O.CARTO = '72' !                                     
            BG25591O.CARTO = '78'                                       
         THEN                                                           
            DO;                                                         
               BG25591O.OVDATA = 'Ø';                                   
               DO I = 1 TO 16;                                          
                  LINTAB(I).DATOA = 'Ø';                                
               END;                                                     
            END;                                                        
         IF BG25591O.CARTO = '65'                                       
         THEN                                                           
            DO;                                                         
               BG25591O.OVBELA = 'Ø';                                   
               DO I = 1 TO 16;                                          
                  LINTAB(I).BELØBA = 'Ø';                               
               END;                                                     
            END;                                                        
         IF BG25591O.CARTO = '16' !                                     
            BG25591O.CARTO = '18' !                                     
            BG25591O.CARTO = '72' !                                     
            BG25591O.CARTO = '78'                                       
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               BG25591O.OVMPNRA = 'Ø';                                  
               DO I = 1 TO 16;                                          
                  LINTAB(I).MPNRA = 'Ø';                                
               END;                                                     
            END;                                                        
 END OPSÆT_FELTER;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*      OPBYG STRUKTUR TIL TEMPORÆR STORAGE.  VALIDE INDBERETNINGER  */
 /*                                                                   */
 /*********************************************************************/
 DAN_TS: PROC;                                                          
 DCL     HJ_PNR          CHAR      (10);                                
 DCL     1 HJ_PNR1       BASED(ADDR(HJ_PNR)),                           
           2 CIF1_8      CHAR      (08),                                
           2 CIF9_10     CHAR      (02);                                
 DCL     MINUS_POS       DEC FIXED (02);                                
         TS_AREAL.ART = TWA.CART;                                       
         IF LINTAB(LININDX).PNRL > 0                                    
         THEN                                                           
            DO;                         /* EVT. SE-NR VENSTRESTILLES */ 
               HJ_PNR = LINTAB(LININDX).PNR;                            
               IF HJ_PNR1.CIF9_10 = ' ' &                               
                  VERIFY(HJ_PNR1.CIF1_8,'0123456789') = 0               
               THEN                                                     
                  LINTAB(LININDX).PNR = '00' !! HJ_PNR1.CIF1_8;         
               TS_AREAL.PNR(LININDX) = LINTAB(LININDX).PNR;             
            END;                                                        
         IF LINTAB(LININDX).PNRA = HEX80                                
         THEN                                                           
            TS_AREAL.PNR(LININDX) = ' ';                                
         IF VERIFY(LIN_CH(LININDX).BELØB,'0123456789- ') = 0            
         THEN                                                           
            DO;                                                         
               IF LINTAB(LININDX).BELØBL > 0                            
               THEN                                                     
                  TS_AREAL.BELØB(LININDX) = LINTAB(LININDX).BELØB;      
               MINUS_POS = INDEX(LINTAB(LININDX).BELØB,'-');            
               IF MINUS_POS > 0                                         
               THEN                                                     
                  DO;                                                   
                     IF MINUS_POS = 10                                  
                     THEN                                               
                        TS_AREAL.BELØB(LININDX) =                       
                                         TS_AREAL(LININDX).BELØB * -0.1;
                     ELSE                                               
                        TS_AREAL.BELØB(LININDX) =                       
                                           TS_AREAL(LININDX).BELØB * -1;
                  END;                                                  
            END;                                                        
         IF LINTAB(LININDX).BELØBA = HEX80                              
         THEN                                                           
            TS_AREAL.BELØB(LININDX) = 0;                                
         IF LINTAB(LININDX).DATOL > 0                                   
         THEN                                                           
            TS_AREAL.DATO(LININDX) = LINTAB(LININDX).DATO;              
         IF LINTAB(LININDX).DATOA = HEX80                               
         THEN                                                           
            TS_AREAL.DATO(LININDX) = ' ';                               
         IF LINTAB(LININDX).MPNRL > 0                                   
         THEN                                                           
            TS_AREAL.MPNR(LININDX) = LINTAB(LININDX).MPNR;              
         IF LINTAB(LININDX).MPNRA = HEX80                               
         THEN                                                           
            TS_AREAL.MPNR(LININDX) = ' ';                               
 END DAN_TS;                                                            
 /*********************************************************************/
 /*                                                                   */
 /*      OVERFØR VALIDE INDBERETNINGER TIL DATAFANGST                 */
 /*                                                                   */
 /*********************************************************************/
 DATAFANGST: PROC;                                                      
 DCL     HEXE0_INIT     BIT        (08)   INIT('11100000');             
 DCL     HEXE0          CHAR        (1)   BASED (ADDR ( HEXE0_INIT ) );;
 DCL     1 TRANS,                                                       
           2 PLADSNR     CHAR      (03),       /* INDEHOLDER BLANK    */
           2 OPGOMR      CHAR      (01),       /* INDEHOLDER 'B'      */
           2 DSNR        CHAR      (02),       /* INDEHOLDER 11       */
           2 LBNR        CHAR      (05),       /* INDEHOLDER 0        */
           2 INDV        CHAR      (80);       /* TRANS TIL DF        */
 DCL     1 DF_INDV       BASED(ADDR(TRANS.INDV)),                       
           2  EÆKNR      CHAR      (3),        /* KOMMUNENUMMER       */
           2  CART       CHAR      (2),        /* ART                 */
           2  DCPRN      CHAR      (10),       /* PERSONMUMMER        */
           2  BBELØB     PIC       '(9)9R',    /* BELØB I HELE KRONER */
           2  DDATO      CHAR      (8),        /* DATO - DDMMÅÅÅÅ     */
           2  MPNR       CHAR      (10),       /* MODT. PERSONMUMMER  */
           2  HFLYD      CHAR      (36),                                
           2  CSLUT      CHAR      (01);                                
 DCL     TRANS_LGD       BIN FIXED (15);                                
 DCL     DF_CRETUR       CHAR      (01);                                
         TRANS_LGD     = 80;                                            
         DF_CRETUR     = ' ';                                           
         TRANS.PLADSNR = '';                                            
         TRANS.OPGOMR  = 'B';                                           
         IF MOD(SLUT_ÅR1,2) = 0                                         
         THEN                                                           
            TRANS.DSNR = '36';    /*  LIGE ÅR:  SYSTEMIDENT B36  */     
         ELSE                                                           
            TRANS.DSNR = '35';    /* ULIGE ÅR:  SYSTEMIDENT B35  */     
         TRANS.LBNR    = '';                                            
         TWA.PARMPTR1  = ADDR (TRANS);                                  
         TWA.PARMPTR2  = ADDR (TRANS_LGD);                              
         TWA.PARMPTR3  = ADDR (DF_CRETUR);                              
         TWA.PARMPTR   = ADDR (TWA.PARMPTR1);                           
         DO I = 1 TO 16;                                                
            IF TS_AREAL.PNR(I) ^= ' '                                   
            THEN                                                        
               DO;                                                      
                  DF_INDV.EÆKNR = TWA.EKOM;                             
                  DF_INDV.CART  = TS_AREAL.ART;                         
                  DF_INDV.DCPRN = TS_AREAL.PNR(I);                      
                  IF TS_AREAL.ART >= '94' & TS_AREAL.BELØB(I) = 0       
                  THEN                                                  
                     DF_INDV.BBELØB = 1;                                
                  ELSE                                                  
                     DF_INDV.BBELØB = TS_AREAL.BELØB(I);                
                  IF TS_AREAL.DATO(I) > '000000'                        
                  THEN                                                  
                     DO;                                                
                        IF SUBSTR(TS_AREAL.DATO(I),5,2) < '90'          
                        THEN                                            
                           DF_INDV.DDATO =                              
                           SUBSTR(TS_AREAL.DATO(I),1,4) !!              
                           '20' !!                                      
                           SUBSTR(TS_AREAL.DATO(I),5,2);                
                        ELSE                                            
                           DF_INDV.DDATO =                              
                           SUBSTR(TS_AREAL.DATO(I),1,4) !!              
                           '19' !!                                      
                           SUBSTR(TS_AREAL.DATO(I),5,2);                
                     END;                                               
                  ELSE                                                  
                     DF_INDV.DDATO = '00000000';                        
                  IF TS_AREAL.MPNR(I) > '0000000000'                    
                  THEN                                                  
                     DF_INDV.MPNR   = TS_AREAL.MPNR(I);                 
                  ELSE                                                  
                     DF_INDV.MPNR   = '0000000000';                     
                  DF_INDV.HFLYD = ' ';                                  
                  DF_INDV.CSLUT = HEXE0;                                
                  EXEC CICS HANDLE CONDITION ERROR(ABEND04)             
                  ;                                                     
                  EXEC CICS LINK PROGRAM ('ZP36300')                    
                  ;                                                     
                  IF ( DF_CRETUR ^= LOW(01) )                           
                  THEN                                                  
                     GO TO ABEND01;                                     
                  ANT_DF = ANT_DF + 1;                                  
               END;                                                     
         END;                                                           
 END DATAFANGST;                                                        
 %INCLUDE ZM70014M;                       /* ZM_HENT_KOMMUNE_KODE     */
 /*********************************************************************/
 /*                                                                   */
 /*      HER ABENDER PROGRAMMET                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABCOD           CHAR      (02);                                
 ABEND01:                                                               
         ABCOD = '01';                                                  
         GO TO ABEND;                                                   
 ABEND02:                                                               
         ABCOD = '02';                                                  
         GO TO ABEND;                                                   
 ABEND03:                                                               
         ABCOD = '03';                                                  
         GO TO ABEND;                                                   
 ABEND04:                                                               
         ABCOD = '04';                                                  
         GO TO ABEND;                                                   
 ABEND:                                                                 
         EXEC CICS ABEND ABCODE('XB' !! ABCOD )                         
         ;                                                              
         EXEC CICS RETURN                                               
         ;                                                              
 END BG25500;                                                           
