                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*                        B H 6 0 1 5 5 M                            */
 /*                                                                   */
 /*        BEREGN UREDUCEREDE LEJEVÆRDIER FOR EJENDOMMEN GÆLDENDE     */
 /*        FOR INDKOMSTÅR 1999  E L L E R  INDKOMSTÅR 2000            */
 /*                                                                   */
 /*        KALDES I : BH60110 ORDINÆR ESKEMA OG BOY                   */
 /*                   BH80110 BOY                                     */
 /*                   BH90110 FORSKUD                                 */
 /*                                                                   */
 /*  OBS.: I N D E N  KALD AF PROCEDUREN  ------                      */
 /*        HUSK AT OPSÆTTE GRÆNSEVÆRDI OG BEREGNINGS-PROCENTFAKTOR    */
 /*        FOR PENSIONISTER (67-ÅRIGE) FOR AKTUELT INDKOMSTÅR SAMT    */
 /*        AT VÆLGE FUNKTION INDEN KALDET:                            */
 /*                                                                   */
 /*        HVIS FUNKTION = '1' KALDES MED BH601.EJD_19??              */
 /*        FOR BEREGNING AF 'NORMALE' VÆRDIER.                        */
 /*                                                                   */
 /*        HVIS FUNKTION = '2' KALDES MED BH601.SOM_19??              */
 /*        FOR BEREGNING AF 'HELÅRSSOMMERHUSE'.                       */
 /*                                                                   */
 /*********************************************************************/
 LEJEVÆRDI_EJENDOM:                                                     
       PROC(TAB_LEJEV,PENS_PCT,FUNKTION);                               
                                                                        
 DCL   1 TAB_LEJEV(3),                  /* EJENDOMMENS 3 LEJEVÆRDIER  */
         2 BLEJE      FIXED  (11),      /* LEJEV. NORMAL              */
         2 BLEJEFP    FIXED  (11),      /* LEJEV. FØRTIDSPENS./EFTERL.*/
         2 BLEJE67    FIXED  (11);      /* LEJEVÆRDI PENSIONIST       */
                                        /* 1=SIDSTE ÅRS VÆRDIER       */
                                        /* 2=NYT ÅRS VÆRDIER UREDUC.  */
                                        /* 3=NYT ÅRS VÆRDIER REDUC.   */
                                                                        
 DCL     PENS_PCT       FIXED     (5,4);                                
 DCL     FUNKTION       CHAR      (1);                                  
 DCL     HJFELT         FIXED     (15,2)     INIT (0);                  
 DCL     PCT_NORM       FIXED     (5,4);                                
                                                                        
         BH601.C6_PCT = ' ';                                            
                                                                        
         /* BEREGN LEJEVÆRDI HVOR   */                                  
         /* EJER ELLER ÆGTEFÆLLE 67 */                                  
         /* ELLER CLEJE = 3         */                                  
                                                                        
         IF HJVÆRDI <= GRÆNSEVÆRDI                                      
         THEN                                                           
            HJFELT = HJVÆRDI * PENS_PCT;                                
         ELSE                                                           
            DO;                                                         
               HJFELT = GRÆNSEVÆRDI * PENS_PCT;                         
               HJFELT = HJFELT + ((HJVÆRDI-GRÆNSEVÆRDI) * 0.060);       
               BH601.C6_PCT = '1';                                      
            END;                                                        
                                                                        
         TAB_LEJEV(2).BLEJE67 = HJFELT;                                 
                                                                        
         /* BEREGN LEJEVÆRDI FOR FØRTIDSPENSIONISTER OG     */          
         /* IKKE PENSIONISTER.                              */          
                                                                        
         PCT_NORM = 0.020;       /* GENEREL PROCENT         */          
                                                                        
         IF (BH601.CBENYT = '08' !                                      
             BH601.CBENYT = '28' !                                      
             BH601.CBENYT = '48') &                                     
             FUNKTION = '1'                                             
         THEN                                                           
            PCT_NORM = 0.025;    /* SOMMERHUS - IKKE HELÅRS */          
                                                                        
         IF HJVÆRDI <= GRÆNSEVÆRDI                                      
         THEN                                                           
            HJFELT = HJVÆRDI * PCT_NORM;                                
         ELSE                                                           
            DO;                                                         
               HJFELT = GRÆNSEVÆRDI * PCT_NORM;                         
               HJFELT = HJFELT + ((HJVÆRDI-GRÆNSEVÆRDI) * 0.060);       
            END;                                                        
                                                                        
         TAB_LEJEV(2).BLEJEFP = HJFELT;                                 
         TAB_LEJEV(2).BLEJE   = HJFELT;                                 
                                                                        
 END LEJEVÆRDI_EJENDOM;                                                 
