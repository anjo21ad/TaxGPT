  PROCESS OPT(TIME);                                                    
 BH95300: /* DAN DATA TIL EVS EJENDOMSTABEL TIL FORSKUD   */            
         PROCEDURE (EXECPARM) OPTIONS (MAIN) REORDER;                   
 /*********************************************************************/
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2002');       
 /********************************************************************* 
 *                                                                     *
 * FORMÅL :                                                            *
 * LEVERING AF OPLYSNINGER TIL FORSKUDS EVS-EJENDOMSTABEL (DB2).       *
 *                                                                     *
 * INPUT  : B.H95001D  BEREGNINGSBÅND TIL FORSKUD - SORTERET           *
 *                                                                     *
 * OUTPUT : B.H95300D  UDTRÆK - ALLE PERSONER                          *
 *          BH95301Y   KØRSELSKONTROLLISTE                             *
 *          BH95302Y   FEJLLISTE FRA MASKINEL AFSTEMNING               *
 *                                                                     *
 * PROGRAMMØR: AAGE RASMUSSEN  AUG       1999                          *
 * RETTET:     AAGE RASMUSSEN  AUG       2000                          *
 * RETTET:     AAGE RASMUSSEN  AUG       2001                          *
 ***********************************************************************
 * RETTET:     OLUF MØRK       AUG       2002                          *
 *             ÅRSTILRETTET MARKERET MED F2003                         *
 * RETTET:     BO SCHMIDT      JUL       2003                          *
 * RETTET:     BO SCHMIDT      JUL       2004                          *
 * RETTET:     SUSANNE KROGH VILLUMSEN   JUL 2005       *FORSKUD06*    *
 *             VURDOPL1 RETTET TIL VURDOPLGL                           *
 * RETTET:     BO SCHMIDT                JUL 2006       *FORSKUD07*    *
 * RETTET:     BO SCHMIDT                JUL 2007       *FORSKUD08*    *
 * RETTET:     BO SCHMIDT                JUL 2008       *FORSKUD09*    *
 * RETTET:     GERD HOLM-PEDERSEN        APR 2009       *FORSKUD10*    *
 * RETTET:     GERD HOLM-PEDERSEN        JUN 2010       *FORSKUD11*    *
 *             CEJEV UDGÅET, DA DET ER UDGÅET FRA ESR.                 *
 * RETTET:     GERD HOLM-PEDERSEN        MAJ 2011       *FORSKUD12*    *
 * RETTET:     LARS KAAE HARRITSHØJ      MAJ 2012       *FORSKUD13*    *
 *             ÆNDRINGER FORETAGET I BH90100N                          *
 * RETTET:     LARS KAAE HARRITSHØJ      MAJ 2013       *FORSKUD14*    *
 * RETTET:     JØRGEN SAUGMANN           JUNI 2014      *FORSKUD15*    *
 * RETTET:     JØRGEN SAUGMANN           JUNI 2015      *FORSKUD16*    *
 * RETTET:     ESBEN BRODERSEN           JUNI 2016      *FORSKUD17*    *
 *             KUN KOMPILERET                                          *
 **********************************************************************/
         DEFAULT RANGE (*) STATIC;                                      
 /*********************************************************************/
 /*      PARAMETER TIL ANGIVELSE AF OM DET ER PRODUKTION ELLER        */
 /*      TESTKØRSEL AF HENSYN TIL MASKINEL AFSTEMNING                 */
 /*********************************************************************/
 DCL     EXECPARM        CHAR      (6) VAR;                             
 DCL     EX_PARM         CHAR      (4);                                 
         EX_PARM = EXECPARM;                                            
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH950I FILE RECORD INPUT;      /* BEREGN.BÅND EFTER BEREGN. */ 
 DCL     BH953O FILE RECORD OUTPUT;     /* LOAD-GRUNDLAG TIL EVS     */ 
 /********************************************************************* 
 *       DECLARE AF UDTRÆK                                            * 
 *********************************************************************/ 
 DCL     BH950_AR        CHAR      (6000) VAR;                          
 DCL     1 BH950         BASED     (ADDR(BH950_AR)),                    
           2 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90100N;;                                          
 DCL     1 BH95099       BASED     (ADDR(BH950_AR)),                    
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BH90099N;                                           
 /********************************************************************* 
 *       DECLARE AF UDTRÆK (BEREGNINGSBÅND)                           * 
 *********************************************************************/ 
 DCL     1 BH953,                                                       
           %INCLUDE BH95300N;;                              /* F2005 */ 
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                   /* TÆLLEVÆRKER TIL TOTALLISTEN */      
           2 LÆS_BH950      FIXED DEC (11),                             
           2 LÆS_BH950_EJD  FIXED DEC (11),                             
           2 LÆS_CLANDEKODE FIXED DEC (11),                             
           2 SKREVET        FIXED DEC (11);                             
 DCL     1 LÆST_FEJLNR(5000:6000), /* TÆLLEVÆRKER TIL TOTALLISTEN */    
           2 ANTAL       FIXED DEC (7);                                 
 DCL     1 SKR_FEJLNR(5000:6000),  /* TÆLLEVÆRKER TIL TOTALLISTEN */    
           2 ANTAL       FIXED DEC (7);                                 
 /********************************************************************* 
 *       DECLARE AF BUILTIN'S                                         * 
 *********************************************************************/ 
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 /********************************************************************* 
 *       DECLARE AF ENTRY'S                                           * 
 *********************************************************************/ 
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH95300L'),                                        
           2 F1          CHAR      (25)      INIT      (' '),           
           2 TEKST2      CHAR      (57)      INIT      (' '),           
           2 F2          CHAR      (12)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN,                                                      
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZZ',                       
           2 F2          CHAR      (61)      INIT      (' ');           
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
 EXTERNAL INIT ('NOTEST(ALL,,;)');                                      
 DCL     EOF_BH950       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
 DCL     INDEX           BIN FIXED (31);                                
 DCL     HJ_FEJLNR       CHAR      (4);                                 
 DCL     HJ_FEJLNR_PIC   PIC       '(4)9'  BASED(ADDR(HJ_FEJLNR));      
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;        /*#01*/                            
                                                                        
 /********************************************************************* 
 *       DECLARE AF HJÆLPEFELTER TIL MASKINEL AFSTEMNING              * 
 *********************************************************************/ 
 DCL     BH950_ANT_INDV  FIXED     (7)       INIT      (0);             
 DCL     SWFEJL          CHAR      (1)       INIT      (' ');           
 DCL     GLKOMNR         FIXED     (3)       INIT      (0);             
 DCL     GLEJDNR         FIXED     (7)       INIT      (0);             
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH950 = NEJ;                                               
         ON ERROR                                                       
            CALL PLIDUMP ('SNHOB');                                     
         OPEN FILE (BH950I) TITLE ('BH95001I');                         
         OPEN FILE (BH953O) TITLE ('BH95300O');                         
                                                                        
         ON ENDFILE (BH950I)                                            
            EOF_BH950 = JA;                                             
                                                                        
         CALL ZZ20390 ('*OPEN','BH95301Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
                                                                        
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
                                                                        
         TV = 0;                                                        
         OVERLIN1.TEKST2 = 'DAN LOADGRUNDLAG TIL EVS-EJENDOMSTABEL ' !! 
                           'FORSKUD ' !! BH65300M.INDKÅR2; /*#01*/      
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            LÆST_FEJLNR(INDEX).ANTAL = 0;                               
            SKR_FEJLNR(INDEX).ANTAL = 0;                                
         END;                                                           
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH950;                                                
                                                                        
         DO WHILE ( EOF_BH950 = NEJ );                                  
                                                                        
            CALL BEHANDL;                                               
                                                                        
            CALL LÆS_BH950;                                             
                                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS UDTRÆK FRA ESR                                           * 
 *********************************************************************/ 
 LÆS_BH950:                                                             
         PROC;                                                          
                                                                        
         READ FILE (BH950I) INTO (BH950_AR);                            
                                                                        
         IF BH950.SORT_CPR = 99999999999     /* OPTÆLL.INDV. 1 */       
         THEN                                                           
            DO;                                                         
               BH950_ANT_INDV = BH95099.ANT_INDV;                       
               READ FILE (BH950I) INTO (BH950_AR);                      
            END;                                                        
                                                                        
         IF EOF_BH950 = NEJ                                             
         THEN                                                           
            TV.LÆS_BH950 = TV.LÆS_BH950 + 1;                            
                                                                        
 END LÆS_BH950;                                                         
 /********************************************************************* 
 *       EJENDOMME TIL EVS - KUN 1 INDIVID PR. EKOMNR/EEJDNR          * 
 *********************************************************************/ 
 BEHANDL:                                                               
         PROC;                                                          
                                                                        
        IF BH950.AKT_VURD.CBENYT = '78'    /* IKKE EVS RELEVANT EJD.*/  
        THEN                               /* SKULLE HAVDE VÆRET    */  
           RETURN;                         /* OVERLÆST I BH31800L.  */  
                                                                        
        IF BH950.FEJLNR > 0                                             
        THEN                                                            
           DO;                                                          
              INDEX = BH950.FEJLNR;                                     
              LÆST_FEJLNR(INDEX).ANTAL = LÆST_FEJLNR(INDEX).ANTAL + 1;  
           END;                                                         
                                                                        
        IF (GLKOMNR = BH950.SORT_KOMNR &                                
            GLEJDNR = BH950.SORT_EJDNR)                                 
        THEN                                                            
           RETURN;                                                      
                                                                        
        LÆS_BH950_EJD = LÆS_BH950_EJD + 1;                              
                                                                        
        IF BH950.SORT_KOMNR = 999                                       
         & BH950.EJENDOM.CLANDEKODE > ' '                               
        THEN                                                            
           LÆS_CLANDEKODE = LÆS_CLANDEKODE + 1;                         
                                                                        
        GLKOMNR = BH950.SORT_KOMNR;                                     
        GLEJDNR = BH950.SORT_EJDNR;                                     
                                                                        
        BH953 = '';                                                     
        BH953.DINDKAAR  = BH65300M.FORSKÅR;           /*#01*/           
        BH953.EKOMNR    = BH950.SORT_KOMNR;                             
        BH953.EEJDNR    = BH950.SORT_EJDNR;                             
        BH953.EJDBELIG  = BH950.EJENDOM.EJDBELIG;                       
        BH953.LANDEKODE = BH950.EJENDOM.CLANDEKODE;          /* F2009 */
        BH953.CBERGRL   = BH950.EJENDOM.CBERGRL;                        
        BH953.CEJDTYPE  = BH950.EJENDOM.CEJDTYPE;                       
                                                                        
        IF BH950.FEJLNR = 5017 !                                        
           BH950.FEJLNR = 5018 !                                        
           BH950.FEJLNR = 5020 !                                        
           BH950.FEJLNR = 5021 !                                        
           BH950.FEJLNR = 5022 !                                        
           BH950.FEJLNR = 5028 !                                        
           BH950.FEJLNR = 5029 !                                        
           BH950.FEJLNR = 5030 !                                        
           BH950.FEJLNR = 5045                                          
        THEN                                                            
           BH953.FEJLNR = 0;                                            
        ELSE                                                            
           BH953.FEJLNR = BH950.FEJLNR;                                 
                                                                        
        BH953.CCICSID = ' ';                                            
        BH953.CBENYT = BH950.EJENDOM.CBENYT;                            
        BH953.CFRED = BH950.TIL_EVS.CFRED;                              
        BH953.BGLEVS10 = 0; /* UDGÅET FRA OG MED 2002 */                
        BH953.BGLEVSP  = 0; /* UDGÅET FRA OG MED 2002 */                
                                                                        
        BH953.DFRAGLD = DATO2.ÅR !! '-' !! DATO2.MD !! '-' !!           
                        DATO2.DG !! '-01.01.01.000001';                 
                                                                        
        IF BH950.EJENDOM.FLEJL > 999                                    
        THEN                                                            
           BH953.FLEJL = 999;                                           
        ELSE                                                            
           BH953.FLEJL = BH950.EJENDOM.FLEJL;                           
                                                                        
   /*   IF BH950.STAMOPL.CEJEV > '0'                                    
        THEN                                                            
           BH953.CEJV = BH950.STAMOPL.CEJEV;                 FORSKUD11*/
                                                                        
        IF BH950.EJERSKAB.CBERGRLANV > '0'                              
        THEN                                                            
           BH953.CEJBOLTYP1 = BH950.EJERSKAB.CBERGRLANV;                
                                                                        
        IF BH950.STAMOPL.VURDOPLGL.CUOP > '0'                           
        THEN                                                            
           BH953.CUOP_AKT = BH950.STAMOPL.VURDOPLGL.CUOP;               
                                                                        
        IF BH950.VURD_01OM.CBENYT_01OM ^= '  '                          
         & BH950.VURD_01OMNY.CBENYT_01OMNY ^= '  '                      
        THEN;                                                           
        ELSE                                                            
           DO;                                                          
              /* OPSÆTTES KUN HVIS DER  I K K E  ER TO OMBEREGNINGER */ 
              BH953.BBERGRL  = BH950.EJERSKAB.BBERGRL;                  
              BH953.BBER2FAM = BH950.EJERSKAB.BBER2FAM;                 
           END;                                                         
                                                                        
        BH953.BEJDVÆR_015PCT = BH950.VURD_015PCT.BEJDVÆR_015PCT;        
                                                                        
        IF BH950.VURD_015PCT.BEJDVÆR_015PCT_SW = -1                     
        THEN                                                            
           BH953.B_NULL1 = '?';                                         
                                                                        
        BH953.BEJDVÆR_02OM = BH950.VURD_02OM.BEJDVÆR_02OM;              
                                                                        
        IF BH950.VURD_02OM.BEJDVÆR_02OM_SW = -1                         
        THEN                                                            
           BH953.B_NULL2 = '?';                                         
                                                                        
        BH953.BEJDVÆR_02OMNY = BH950.VURD_02OMNY.BEJDVÆR_02OMNY;        
                                                                        
        IF BH950.VURD_02OMNY.BEJDVÆR_02OMNY_SW = -1                     
        THEN                                                            
           BH953.B_NULL3 = '?';                                         
                                                                        
        BH953.BEJDVÆR_02 = BH950.VURD_02.BEJDVÆR_02;                    
                                                                        
        IF BH950.VURD_02.BEJDVÆR_02_SW = -1                             
        THEN                                                            
           BH953.B_NULL4 = '?';                                         
                                                                        
        BH953.BEJDVÆR_01OM = BH950.VURD_01OM.BEJDVÆR_01OM;              
                                                                        
        IF BH950.VURD_01OM.BEJDVÆR_01OM_SW = -1                         
        THEN                                                            
           BH953.B_NULL5 = '?';                                         
                                                                        
        BH953.BEJDVÆR_01OMNY = BH950.VURD_01OMNY.BEJDVÆR_01OMNY;        
                                                                        
        IF BH950.VURD_01OMNY.BEJDVÆR_01OMNY_SW = -1                     
        THEN                                                            
           BH953.B_NULL6 = '?';                                         
                                                                        
        BH953.BEJDVÆR_AKT = BH950.VURD_AKT.BEJDVÆR_AKT;                 
                                                                        
        IF BH950.VURD_AKT.BEJDVÆR_AKT_SW = -1                           
        THEN                                                            
           BH953.B_NULL7 = '?';                                         
                                                                        
        BH953.BEJBOL1_AKT       = BH950.VURD_AKT.BEJBOL1_AKT;           
        BH953.CEJBOLTYP1_AKT    = BH950.VURD_AKT.CEJBOLTYP1_AKT;        
        BH953.BEJBOL2_AKT       = BH950.VURD_AKT.BEJBOL2_AKT;           
        BH953.CEJBOLTYP2_AKT    = BH950.VURD_AKT.CEJBOLTYP2_AKT;        
        BH953.BEJBOL1_02OM      = BH950.VURD_02OM.BEJBOL1_02OM;         
        BH953.CEJBOLTYP1_02OM   = BH950.VURD_02OM.CEJBOLTYP1_02OM;      
        BH953.BEJBOL2_02OM      = BH950.VURD_02OM.BEJBOL2_02OM;         
        BH953.CEJBOLTYP2_02OM   = BH950.VURD_02OM.CEJBOLTYP2_02OM;      
        BH953.BEJBOL1_02OMNY    = BH950.VURD_02OMNY.BEJBOL1_02OMNY;     
        BH953.CEJBOLTYP1_02OMNY = BH950.VURD_02OMNY.CEJBOLTYP1_02OMNY;  
        BH953.BEJBOL2_02OMNY    = BH950.VURD_02OMNY.BEJBOL2_02OMNY;     
        BH953.CEJBOLTYP2_02OMNY = BH950.VURD_02OMNY.CEJBOLTYP2_02OMNY;  
        BH953.BEJBOL1_02        = BH950.VURD_02.BEJBOL1_02;             
        BH953.CEJBOLTYP1_02     = BH950.VURD_02.CEJBOLTYP1_02;          
        BH953.BEJBOL2_02        = BH950.VURD_02.BEJBOL2_02;             
        BH953.CEJBOLTYP2_02     = BH950.VURD_02.CEJBOLTYP2_02;          
        BH953.BEJBOL1_01OM      = BH950.VURD_01OM.BEJBOL1_01OM;         
        BH953.CEJBOLTYP1_01OM   = BH950.VURD_01OM.CEJBOLTYP1_01OM;      
        BH953.BEJBOL2_01OM      = BH950.VURD_01OM.BEJBOL2_01OM;         
        BH953.CEJBOLTYP2_01OM   = BH950.VURD_01OM.CEJBOLTYP2_01OM;      
        BH953.BEJBOL1_01OMNY    = BH950.VURD_01OMNY.BEJBOL1_01OMNY;     
        BH953.CEJBOLTYP1_01OMNY = BH950.VURD_01OMNY.CEJBOLTYP1_01OMNY;  
        BH953.BEJBOL2_01OMNY    = BH950.VURD_01OMNY.BEJBOL2_01OMNY;     
        BH953.CEJBOLTYP2_01OMNY = BH950.VURD_01OMNY.CEJBOLTYP2_01OMNY;  
        BH953.BEJBOL1_015PCT    = BH950.VURD_015PCT.BEJBOL1_015PCT;     
        BH953.CEJBOLTYP1_015PCT = BH950.VURD_015PCT.CEJBOLTYP1_015PCT;  
        BH953.BEJBOL2_015PCT    = BH950.VURD_015PCT.BEJBOL2_015PCT;     
        BH953.CEJBOLTYP2_015PCT = BH950.VURD_015PCT.CEJBOLTYP2_015PCT;  
                                                                        
        IF BH953.FEJLNR > 0                                             
        THEN                                                            
           DO;                                                          
              INDEX = BH953.FEJLNR;                                     
              SKR_FEJLNR(INDEX).ANTAL = SKR_FEJLNR(INDEX).ANTAL + 1;    
           END;                                                         
                                                                        
        TV.SKREVET = TV.SKREVET + 1;                                    
                                                                        
        WRITE FILE (BH953O) FROM (BH953);                               
                                                                        
 END BEHANDL;                                                           
                                                                        
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.LÆS_BH950;                                   
         DETLIN.TEKST = 'ANTAL LÆSTE PÅ UDTRÆK             B.H95001D';  
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.LÆS_BH950_EJD;                               
         DETLIN.TEKST = 'ANTAL LÆSTE EJENDOMME PÅ UDTRÆK   B.H95001D';  
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.LÆS_CLANDEKODE;                              
         DETLIN.TEKST = '      HERAF UDL. EJENDOMME MED LANDEKODE';     
         CALL ZZ20300 (DETLIN,'01');                                    
                                                                        
         DO INDEX = 5000 TO 6000;                                       
            IF LÆST_FEJLNR(INDEX).ANTAL > 0                             
            THEN                                                        
               DO;                                                      
                  DETLIN.CCA = ZZWS1;                                   
                  HJ_FEJLNR_PIC = INDEX;                                
                  DETLIN.TEKST =                                        
                  '      HERAF MED FEJLNR. ' !! HJ_FEJLNR;              
                  DETLIN.ANTAL = LÆST_FEJLNR(INDEX).ANTAL;              
                  CALL ZZ20300 (DETLIN,'01');                           
               END;                                                     
         END;                                                           
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN.CCA = ZZWS2;                                            
         DETLIN.ANTAL = TV.SKREVET;                                     
         DETLIN.TEKST = 'ANTAL SKREVNE INDIVIDER           B.H95300D';  
         CALL ZZ20300 (DETLIN,'01');                                    
         DO INDEX = 5000 TO 6000;                                       
            IF SKR_FEJLNR(INDEX).ANTAL > 0                              
            THEN                                                        
               DO;                                                      
                  DETLIN.CCA = ZZWS1;                                   
                  HJ_FEJLNR_PIC = INDEX;                                
                  DETLIN.TEKST =                                        
                  '      HERAF MED FEJLNR. ' !! HJ_FEJLNR;              
                  DETLIN.ANTAL = SKR_FEJLNR(INDEX).ANTAL;               
                  CALL ZZ20300 (DETLIN,'01');                           
               END;                                                     
         END;                                                           
  END TOTALLISTE;                                                       
 /********************************************************************* 
 *       MASKINEL AFSTEMNING                                          * 
 *********************************************************************/ 
 MASK_AFSTEMNING:                                                       
         PROC;                                                          
         IF BH95099.SORT_KOMNR ^= 999                                   
         THEN                                                           
            CALL FEJLLISTE('1');                                        
         IF BH950_ANT_INDV ^= TV.LÆS_BH950 &                            
            EX_PARM ^= 'TEST'                                           
         THEN                                                           
            CALL FEJLLISTE('2');                                        
 END MASK_AFSTEMNING;                                                   
 /*********************************************************************/
 /*      UDSKRIV FEJLLISTE VEDR. KONSTATERET AFSTEMNINGFEJL PÅ ???    */
 /*********************************************************************/
 FEJLLISTE:                                                             
         PROC(FEJL);                                                    
 DCL     FEJL            CHAR      (01);                                
         CALL ZZ20390 ('*OPEN','BH95302Y');                             
         CALL ZZ20300(SIDELIN,'02');                                    
         CALL ZZ20300(OVERLIN1,'02');                                   
         OVERLIN2.CCA = ZZWS3;                                          
         OVERLIN2.TEKST1 = 'FEJLLISTE VEDR. MASKINEL AFSTEMNING';       
         CALL ZZ20300(OVERLIN2,'02');                                   
         IF FEJL = '1'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN.CCA    = ZZWS3;                                   
               DETLIN.TEKST   =                                         
               'AFSTEMNINGSINDIVID MANGLER PÅ B.H95001D';               
               DETLIN.ANTAL = '';                                       
               CALL ZZ20300(DETLIN,'02');                               
            END;                                                        
         IF FEJL = '2'                                                  
         THEN                                                           
            DO;                                                         
               DETLIN.CCA    = ZZWS3;                                   
               DETLIN.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.H95001D';     
               DETLIN.ANTAL = TV.LÆS_BH950;                             
               CALL ZZ20300(DETLIN,'02');                               
               DETLIN.TEKST =                                           
               'ANTAL IFØLGE AFSTEMNINGSINDIVID IALT  :';               
               DETLIN.ANTAL = BH950_ANT_INDV;                           
               CALL ZZ20300(DETLIN,'02');                               
            END;                                                        
            SWFEJL = '1';                                               
 END FEJLLISTE;                                                         
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
 AFSLUT:                                                                
         PROC;                                                          
         CALL TOTALLISTE;                                               
         CALL MASK_AFSTEMNING;                                          
         CALL ZZ20300((133)' ','99');                                   
         CLOSE FILE (BH950I),                                           
               FILE (BH953O);                                           
         IF SWFEJL = '1'                                                
         THEN                                                           
            CALL ZS30101(4,'BH95300 ** 251 ' !!                         
                 'MASKINEL KONSTATERET AFSTEMNINGSFEJL',2);             
 END AFSLUT;                                                            
 END  BH95300;                                                          
