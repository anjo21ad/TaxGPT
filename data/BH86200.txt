 BH86200: PROCEDURE OPTIONS (MAIN);                                     
 /**********************************************************************
 * PROJEKT:     EVS FORSKUD / EVS SLUT                                 *
 * PROGRAMNAVN: BH86200.                                               *
 * PROGRAMTYPE: BATCH.                                                 *
 * FUNKTION:    Konvertering af MTfil til DB2-tabel for gammelt år     *
 * PGMFORLØB:   1. ÅBEN MT-fil                                         *
 *              2. LÆS MT-fil                                          *
 *              3. SKRIV RÆKKE PÅ DB2-TABEL BQ66000T                   *
 *              5. SKRIV KONTROLLISTE.                                 *
 * INDDATA:     MT-fil gammelt år                                      *
 *                                                                     *
 * UDDATA:      BH86201Y - KONTROLLISTE.                               *
 *              BQ66000T - MANDTALSOPLYSNINGER                         *
 ***********************************************************************
 * OPRETTET   : Lene Maj Jensen   QLE             BALLERUP 26.02.2018  *
 **********************************************************************/
                                                                        
 DCL     COPYRIGHT      CHAR (30) INIT ('(C) KOMMUNEDATA A/S 2006');    
                                                                        
 DEFAULT RANGE (*) STATIC; /* LAGERTYPE AHT. KALD AF EKSTERNE MODULER */
                                                                        
 /*********************************************************************/
 /* DECLARE DATASÆT                                                   */
 /*********************************************************************/
                                                                        
 DCL     BH862I FILE RECORD INPUT;     /* Parameter Forskud-Slut */     
 DCL     BH655I FILE RECORD INPUT;     /* MT-fil                 */     
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
                                                                        
 /*********************************************************************/
 /* DECLARE AF DB2                                                    */
 /*********************************************************************/
                                                                        
         EXEC SQL INCLUDE SQLCA;           /* SQL KOMMUNIKATIONSAREAL */
                                                                        
         EXEC SQL INCLUDE BQ66000T;  /* MT              */              
                                                                        
 /*********************************************************************/
 /*      DECLARE AF MT-fil                                            */
 /*********************************************************************/
 DCL     BH655_AR CHAR (200) VAR;                                       
 DCL     1 BH655         BASED     (ADDR(BH655_AR)),                    
         3 LÆNGDE BIN FIXED (15),                                       
         %INCLUDE BH65501N;;                                            
                                                                        
 DCL     1 BH65599       BASED     (ADDR(BH655_AR)),                    
         2 LÆNGDE BIN FIXED (15),                                       
         %INCLUDE BH65599N;                                             
 /*********************************************************************/
 /* DECLARE AF DB2-strukturer                                         */
 /*********************************************************************/
 DCL     1 MT,                                                          
         %INCLUDE BQ6600TN;                                             
                                                                        
 /*********************************************************************/
 /* DECLARE AF DIVERSE                                                */
 /*********************************************************************/
 DCL     WS_FORSKUD_SLUT_TXT    CHAR(16) INIT (' ');                    
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     WS_TIL_EVS_AAR_GL              BIN FIXED (15);                 
 DCL     WS_SLUT_AFVIKLINGS_ID          CHAR(16) INIT (' ');            
                                                                        
 DCL     WS_PROGRAMNAVN         CHAR(8)  INIT ('BH86200');              
                                                                        
 DCL     1 BH862_PARM,                                                  
           %INCLUDE BH86200N;                                           
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;                                           
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     EOF_BH862       BIT       (01)      INIT      ('0'B);          
 DCL     EOF_BH655       BIT       (01)      INIT      ('0'B);          
 DCL     JA                 BIT       (1)     INIT ('1'B);              
 DCL     NEJ                BIT       (1)     INIT ('0'B);              
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH86200'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
                                                                        
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
                                                                        
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (1),                                 
          2 FILLER       CHAR      (132)     INIT      (' ');           
                                                                        
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (1),                                 
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH86200L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (43)      INIT      (                
           'GAMLE MANDTALSOPLYSNINGER '),                               
           2 ÅR          PIC       '9999',                              
           2 F2          CHAR      (21)      INIT      ((21)' '),       
           2 TEKST4      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
                                                                        
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (1),                                 
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
                                                                        
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (1),                                 
           2 TEKST       CHAR      (53),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (68)      INIT      (' ');           
                                                                        
         % INCLUDE ZZ20301M;                                            
                                                                        
 /*********************************************************************/
 /*      DECLARE AF ENTRIES                                           */
 /*********************************************************************/
                                                                        
 DCL     ZS61900         ENTRY;              /* UDSKRIV SSI-DATO      */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS30101         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
                                                                        
 /*********************************************************************/
 /* DECLARE AF BUILTIN                                                */
 /*********************************************************************/
                                                                        
 DCL     ADDR            BUILTIN;                                       
 DCL     NULL            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
                                                                        
 /*********************************************************************/
 /* DECLARE AF TÆLLERE                                                */
 /*********************************************************************/
                                                                        
 DCL     ANT_SKR BIN FIXED (31) INIT(0);   /* ANTAL SKREVET PÅ UDTRÆK */
 DCL     ANT_LÆS BIN FIXED (31) INIT(0);   /* LÆST PÅ MT-fil  */        
 DCL     ANT_COMM BIN FIXED (31) INIT (0); /* COMMIT TÆLLER  */         
                                                                        
 /*********************************************************************/
 /* Declare af Cursors                                                */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /* ON - CONDITIONS                                                   */
 /*********************************************************************/
                                                                        
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
                                                                        
         EOF_BH862 = NEJ;                                               
         EOF_BH655 = NEJ;                                               
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH86200L');             
            END;                                                        
                                                                        
         ON ENDFILE (BH655I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
            BH655.DCPRN = 9999999999;                                   
         END;                                                           
                                                                        
         ON ENDFILE (BH862I)                                            
         BEGIN;                                                         
            EOF_BH862 = JA;                                             
         END;                                                           
                                                                        
         OPEN FILE (BH862I) TITLE ('BH86200P');                         
         OPEN FILE (BH655I) TITLE ('BH65501I');                         
 /*********************************************************************/
 /* HOUSEKEEPING                                                      */
 /*********************************************************************/
                                                                        
         READ FILE (BH862I) INTO (BH862_PARM);                          
                                                                        
         IF EOF_BH862 = NEJ                                             
         THEN DO;                                                       
            SELECT (BH862_PARM.FS_MRK);                                 
               WHEN ('F')                                               
                  DO;                                                   
                     ABENDTXT = '101 KØRES KUN FOR SLUT';               
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
               WHEN ('S')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH862_PARM.SLUT_TXT;         
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR - 1;              
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 2;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH862_PARM.SLUT_TXT;       
                     OVERLIN1.TEKST2 = 'MANDTALSOPLYSNINGER SLUT';      
                  END;                                                  
               WHEN ('U')                                               
                  DO;                                                   
                     ABENDTXT = '101 KØRES KUN FOR SLUT';               
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     ABENDTXT = '101 PARAMETERFEJL';                    
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
         END;                                                           
                                                                        
         /********************************************************/     
         /*  NU STARTER NORMALT FLOW                             */     
         /********************************************************/     
                                                                        
         CALL ZZ20390('*OPEN','BH86201Y');       /* ÅBEN KONTROLLISTE */
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         OVERLIN1.ÅR   = GLSLUTÅR;                                      
         OVERLIN1.DATO = UDDATO;                                        
         SIDELIN.CCA   = ZZIK1;                                         
         OVERLIN1.CCA  = ZZWS2;                                         
         OVERLIN2.CCA  = ZZWS3;                                         
                                                                        
 /*********************************************************************/
 /* PROCES                                                            */
 /*********************************************************************/
                                                                        
         CALL RYD_DB2_TABELLER;                                         
                                                                        
         READ FILE (BH655I) INTO (BH655_AR);                            
                                                                        
         DO WHILE (^EOF_BH655);                                         
            ANT_LÆS = ANT_LÆS + 1;                                      
            IF BH65599.DCPRN >= 9999999999                              
            THEN DO;                                                    
               EOF_BH655 = '1'B;                                        
            END;                                                        
            ELSE DO;                                                    
               /* check om der findes en række, og  */                  
               /* insert eller update efterfølgende */                  
               CALL INSERT_MANDTALSOPL;                                 
               ANT_COMM = ANT_COMM + 1;                                 
               IF ANT_COMM > 9999                                       
               THEN DO;                                                 
                  EXEC SQL COMMIT;                                      
                  Put skip list ('Commit efter ' !! ANT_COMM !!         
                                 ' opdateringer.  Ialt ' !! ANT_SKR !!  
                                 ' opdateringer');                      
                  ANT_COMM = 0;                                         
               END;                                                     
               READ FILE (BH655I) INTO (BH655_AR);                      
            END;                                                        
         END;                                                           
                                                                        
         IF ANT_COMM > 0                                                
         THEN DO;                                                       
            EXEC SQL COMMIT;                                            
            Put skip list ('Commit efter ' !! ANT_COMM !!               
                           ' opdateringer.  Ialt ' !! ANT_SKR !!        
                           ' opdateringer');                            
            ANT_COMM = 0;                                               
         END;                                                           
         CALL AFSLUT;                                                   
                                                                        
 /*********************************************************************/
 /* Ryd db2-tabeller før start                                 RUTINE */
 /*********************************************************************/
 RYD_DB2_TABELLER: PROC;                                                
                                                                        
         EXEC SQL                                                       
            Delete from BQ66000T                                        
            where til_evs_aar = :WS_TIL_EVS_AAR                         
            and   afviklings_id = :WS_FORSKUD_SLUT_TXT                  
            and   kØrsels_id = 'MTGL1';                                 
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
            END;                                                        
            WHEN(100) DO;                                               
            END;                                                        
            OTHERWISE DO;                                               
               ABENDTXT = '101 DELETE BQ66000T';                        
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
 END RYD_DB2_TABELLER;                                                  
 /*********************************************************************/
 /* INSERT række på EVS UDTRÆK                                 RUTINE */
 /*********************************************************************/
 INSERT_MANDTALSOPL: PROC;                                              
                                                                        
      MT = '';                                                          
      MT.TIL_EVS_AAR = WS_TIL_EVS_AAR;                                  
      MT.AFVIKLINGS_ID = WS_FORSKUD_SLUT_TXT;                           
      MT.KØRSELS_ID = 'MTGL1';                                          
      MT.DCPRN = BH655.DCPRN;                                           
      MT.PROGRAMNAVN = WS_PROGRAMNAVN;                                  
      MT.DB2FUNKTION = 'I';                                             
      MT.CCIVS = BH655.CCIVS;                                           
      MT.CPENS = BH655.CPENS;                                           
      MT.CRGN = BH655.CRGN;                                             
      MT.DSORP = BH655.DSORP;                                           
      MT.CPART = BH655.CPART;                                           
      MT.CFRÅR = BH655.CFRÅR;                                           
      MT.CFRÅROPR = BH655.CFRÅROPR;                                     
      MT.DRGNÅRFRA = BH655.DRGNÅRFRA;                                   
      MT.DRGNÅRTIL = BH655.DRGNÅRTIL;                                   
      MT.BPENSIND = BH655.BPENSIND;                                     
      MT.BPENSGR = BH655.BPENSGR;                                       
      MT.CSTATUS = BH655.CSTATUS;                                       
      MT.CDØDÆGTF = BH655.CDØDÆGTF;                                     
      MT.CDUBLET = BH655.CDUBLET;                                       
      MT.CÆGTF_OK = BH655.CÆGTF_OK;                                     
      MT.CFØRFORS = BH655.CFØRFORS;                                     
      MT.CFØRCOR = BH655.CFØRCOR;                                       
      MT.CFØRPENS = BH655.CFØRPENS;                                     
      MT.COVF_DHE = BH655.COVF_DHE;                                     
      MT.FØROPL_DCIÆN_GL = BH655.FØROPL.DCIÆN_GL;                       
      MT.FØROPL_CCIVS_GL = BH655.FØROPL.CCIVS_GL;                       
      MT.HIST_OPD_SIDSTE_DHENVN = BH655.HIST_OPL(2015).DHENVN;          
      MT.HIST_OPD_SIDSTE_CSBSK = BH655.HIST_OPL(2015).CSBSK;            
      MT.HIST_OPD_SIDSTE_CFØREFT = BH655.HIST_OPL(2015).CFØREFT;        
      MT.HIST_OPD_SIDSTE_CSKPLOM = BH655.HIST_OPL(2015).CSKPLOM;        
      MT.HIST_OPD_SIDSTE_DCIÆN = BH655.HIST_OPL(2015).DCIÆN;            
      MT.HIST_OPD_DETTE_DHENVN = BH655.HIST_OPL(2016).DHENVN;           
      MT.HIST_OPD_DETTE_CSBSK = BH655.HIST_OPL(2016).CSBSK;             
      MT.HIST_OPD_DETTE_CFØREFT = BH655.HIST_OPL(2016).CFØREFT;         
      MT.HIST_OPD_DETTE_CSKPLOM = BH655.HIST_OPL(2016).CSKPLOM;         
      MT.HIST_OPD_DETTE_DCIÆN = BH655.HIST_OPL(2016).DCIÆN;             
      MT.HIST_OPD_NAESTE_DHENVN = BH655.HIST_OPL(2017).DHENVN;          
      MT.HIST_OPD_NAESTE_CSBSK = BH655.HIST_OPL(2017).CSBSK;            
      MT.HIST_OPD_NAESTE_CFØREFT = BH655.HIST_OPL(2017).CFØREFT;        
      MT.HIST_OPD_NAESTE_CSKPLOM = BH655.HIST_OPL(2017).CSKPLOM;        
      MT.HIST_OPD_NAESTE_DCIÆN = BH655.HIST_OPL(2017).DCIÆN;            
      MT.PER_DERHVPER_FRA1 = BH655.PER.DERHVPER_FRA1;                   
      MT.PER_DERHVPER_TIL1 = BH655.PER.DERHVPER_TIL1;                   
      MT.PER_DERHVPER_FRA2 = BH655.PER.DERHVPER_FRA2;                   
      MT.PER_DERHVPER_TIL2 = BH655.PER.DERHVPER_TIL2;                   
      MT.PER_DERHVPER_FRA3 = BH655.PER.DERHVPER_FRA3;                   
      MT.PER_DERHVPER_TIL3 = BH655.PER.DERHVPER_TIL3;                   
      MT.PER_DERHVPER_FRA4 = BH655.PER.DERHVPER_FRA4;                   
      MT.PER_DERHVPER_TIL4 = BH655.PER.DERHVPER_TIL4;                   
      MT.PER_DERHVPER_FRA5 = BH655.PER.DERHVPER_FRA5;                   
      MT.PER_DERHVPER_TIL5 = BH655.PER.DERHVPER_TIL5;                   
                                                                        
         EXEC SQL                                                       
            INSERT INTO BQ66000T                                        
            (                                                           
            TIL_EVS_AAR                                                 
            ,AFVIKLINGS_ID                                              
            ,KØRSELS_ID                                                 
            ,DCPRN                                                      
            ,PROGRAMNAVN                                                
            ,DB2FUNKTION                                                
            ,OPRET_TS                                                   
            ,CCIVS                                                      
            ,CPENS                                                      
            ,CRGN                                                       
            ,DSORP                                                      
            ,CPART                                                      
            ,CFRÅR                                                      
            ,CFRÅROPR                                                   
            ,DRGNÅRFRA                                                  
            ,DRGNÅRTIL                                                  
            ,BPENSIND                                                   
            ,BPENSGR                                                    
            ,CSTATUS                                                    
            ,CDØDÆGTF                                                   
            ,CDUBLET                                                    
            ,CÆGTF_OK                                                   
            ,CFØRFORS                                                   
            ,CFØRCOR                                                    
            ,CFØRPENS                                                   
            ,COVF_DHE                                                   
            ,FØROPL_DCIÆN_GL                                            
            ,FØROPL_CCIVS_GL                                            
            ,HIST_OPD_SIDSTE_DHENVN                                     
            ,HIST_OPD_SIDSTE_CSBSK                                      
            ,HIST_OPD_SIDSTE_CFØREFT                                    
            ,HIST_OPD_SIDSTE_CSKPLOM                                    
            ,HIST_OPD_SIDSTE_DCIÆN                                      
            ,HIST_OPD_DETTE_DHENVN                                      
            ,HIST_OPD_DETTE_CSBSK                                       
            ,HIST_OPD_DETTE_CFØREFT                                     
            ,HIST_OPD_DETTE_CSKPLOM                                     
            ,HIST_OPD_DETTE_DCIÆN                                       
            ,HIST_OPD_NAESTE_DHENVN                                     
            ,HIST_OPD_NAESTE_CSBSK                                      
            ,HIST_OPD_NAESTE_CFØREFT                                    
            ,HIST_OPD_NAESTE_CSKPLOM                                    
            ,HIST_OPD_NAESTE_DCIÆN                                      
            ,PER_DERHVPER_FRA1                                          
            ,PER_DERHVPER_TIL1                                          
            ,PER_DERHVPER_FRA2                                          
            ,PER_DERHVPER_TIL2                                          
            ,PER_DERHVPER_FRA3                                          
            ,PER_DERHVPER_TIL3                                          
            ,PER_DERHVPER_FRA4                                          
            ,PER_DERHVPER_TIL4                                          
            ,PER_DERHVPER_FRA5                                          
            ,PER_DERHVPER_TIL5                                          
                                                                        
            )                                                           
            values                                                      
            (                                                           
            :MT.TIL_EVS_AAR,                                            
            :MT.AFVIKLINGS_ID,                                          
            :MT.KØRSELS_ID,                                             
            :MT.DCPRN,                                                  
            :MT.PROGRAMNAVN,                                            
            :MT.DB2FUNKTION,                                            
            current timestamp,                                          
            :MT.CCIVS,                                                  
            :MT.CPENS,                                                  
            :MT.CRGN,                                                   
            :MT.DSORP,                                                  
            :MT.CPART,                                                  
            :MT.CFRÅR,                                                  
            :MT.CFRÅROPR,                                               
            :MT.DRGNÅRFRA,                                              
            :MT.DRGNÅRTIL,                                              
            :MT.BPENSIND,                                               
            :MT.BPENSGR,                                                
            :MT.CSTATUS,                                                
            :MT.CDØDÆGTF,                                               
            :MT.CDUBLET,                                                
            :MT.CÆGTF_OK,                                               
            :MT.CFØRFORS,                                               
            :MT.CFØRCOR,                                                
            :MT.CFØRPENS,                                               
            :MT.COVF_DHE,                                               
            :MT.FØROPL_DCIÆN_GL,                                        
            :MT.FØROPL_CCIVS_GL,                                        
            :MT.HIST_OPD_SIDSTE_DHENVN,                                 
            :MT.HIST_OPD_SIDSTE_CSBSK,                                  
            :MT.HIST_OPD_SIDSTE_CFØREFT,                                
            :MT.HIST_OPD_SIDSTE_CSKPLOM,                                
            :MT.HIST_OPD_SIDSTE_DCIÆN,                                  
            :MT.HIST_OPD_DETTE_DHENVN,                                  
            :MT.HIST_OPD_DETTE_CSBSK,                                   
            :MT.HIST_OPD_DETTE_CFØREFT,                                 
            :MT.HIST_OPD_DETTE_CSKPLOM,                                 
            :MT.HIST_OPD_DETTE_DCIÆN,                                   
            :MT.HIST_OPD_NAESTE_DHENVN,                                 
            :MT.HIST_OPD_NAESTE_CSBSK,                                  
            :MT.HIST_OPD_NAESTE_CFØREFT,                                
            :MT.HIST_OPD_NAESTE_CSKPLOM,                                
            :MT.HIST_OPD_NAESTE_DCIÆN,                                  
            :MT.PER_DERHVPER_FRA1,                                      
            :MT.PER_DERHVPER_TIL1,                                      
            :MT.PER_DERHVPER_FRA2,                                      
            :MT.PER_DERHVPER_TIL2,                                      
            :MT.PER_DERHVPER_FRA3,                                      
            :MT.PER_DERHVPER_TIL3,                                      
            :MT.PER_DERHVPER_FRA4,                                      
            :MT.PER_DERHVPER_TIL4,                                      
            :MT.PER_DERHVPER_FRA5,                                      
            :MT.PER_DERHVPER_TIL5                                       
            );                                                          
                                                                        
         SELECT(SQLCA.SQLCODE);                                         
            WHEN(0) DO;                                                 
               ANT_SKR = ANT_SKR + 1;                                   
            END;                                                        
            OTHERWISE DO;                                               
               ABENDTXT = '101 INSERT BQ66000T';                        
               CALL SQL_FEJL;                                           
            END;                                                        
         END;                                                           
                                                                        
 END INSERT_MANDTALSOPL;                                                
                                                                        
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
 PROC;                                                                  
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
                                                                        
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA   = ZZWS1;                                         
         DETLIN1.TEKST =                                                
         'ANTAL LÆSTE GL MANDTALSOPLYSNINGER PÅ B.H65500D';             
         DETLIN1.ANTAL = ANT_LÆS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DETLIN1.TEKST =                                                
         'ANTAL SKREVNE GL MANDTALSOPLYSNINGER PÅ BQ66000T';            
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
                                                                        
                                                                        
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* LUK FILER, CURSOR OG UDSKRIV AFSTEMNINGS- OG KONTROLLISTE  RUTINE */
 /*********************************************************************/
 AFSLUT: PROC;                                                          
                                                                        
         CALL KONTROLLISTE;                                             
         CLOSE FILE (BH862I);                                           
         CLOSE FILE (BH655I);                                           
         CALL ZZ20300(' ','99');                                        
                                                                        
 END AFSLUT;                                                            
 END BH86200;                                                           
