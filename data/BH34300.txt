 BH34300: PROC OPTIONS(MAIN);                                           
 /*********************************************************************/
 /* 1. DAN DATASET MED FIKTIVE PERSONNR PÅ BEREGNINGSBÅNDET FOR       */
 /*    SLUT                                                           */
 /*********************************************************************/
 /* RETTELSESLOG:                                                     */
 /*                                                                   */
 /* 25.11.2016  Lene Maj Jensen  QLE                                  */
 /*             NYT PROGRAM                                           */
 /*                                                                   */
 /*********************************************************************/
 /*     ENTRIES OG BUILTIN FUNKTIONS                                  */
 /*********************************************************************/
 DCL     ZS61900 ENTRY;                                                 
 DCL     ZS38100 ENTRY OPTIONS (INTER ASM); /* TIMESTAMP */             
 DCL    (TIME,DATE,PLIDUMP,ADDR,LOW,LENGTH) BUILTIN;                    
 /*********************************************************************/
 /*     DECLARES AF FILER                                             */
 /*********************************************************************/
 DCL     BH3431I  FILE RECORD INPUT;  /*BEREGNINGSBÅND              */  
 DCL     BH3431O  FILE RECORD OUTPUT; /*LISTE OVER FIKTIVE CPRNR    */  
                                                                        
 /*********************************************************************/
 /*      DECLARE AF SEQUENTIELLE RECORDS                              */
 /*********************************************************************/
                                                                        
 DCL     INDDATA_BEREGN       CHAR(4884);                               
 DCL     1 BEREGN_IN BASED    (ADDR(INDDATA_BEREGN)),                   
           %INCLUDE BH90000N;;                                          
                                                                        
 DCL     1 UDDATA,                                                      
           %INCLUDE BH34300N;                                           
                                                                        
                                                                        
 /*********************************************************************/
 /*      DIVERSE DECLARES                                             */
 /*********************************************************************/
                                                                        
 DCL  JA                   BIT (1) INIT ('1'B);                         
 DCL  NEJ                  BIT (1) INIT ('0'B);                         
 DCL  EOF_BH3431I          BIT (1) INIT ('0'B);                         
                                                                        
 DCL  1 WS,                                                             
      3 ANTAL_FIKTIV       DEC FIXED(9) INIT(0),                        
      3 ANTAL_LÆST          DEC FIXED (9) INIT (0);                     
                                                                        
                                                                        
 /*********************************************************************/
        CALL ZS61900;                       /*  UDSKRIVER SSI-DATO */   
 /*********************************************************************/
 /*      ON - CONDITIONS                                              */
 /*********************************************************************/
        EOF_BH3431I = NEJ;                                              
                                                                        
        OPEN FILE (BH3431O)  TITLE ('BH34310O');                        
        OPEN FILE (BH3431I)  TITLE ('BH34310I');                        
                                                                        
        ON ENDFILE (BH3431I)                                            
            EOF_BH3431I = JA;                                           
                                                                        
 /********************************************************************/ 
 /*         H O V E D S T Y R I N G                                  */ 
 /********************************************************************/ 
                                                                        
        ws = '';                                                        
                                                                        
        READ FILE (BH3431I) INTO (INDDATA_BEREGN);                      
                                                                        
        DO WHILE (^EOF_BH3431I);                                        
           ANTAL_LÆST = ANTAL_LÆST + 1;                                 
           IF BEREGN_IN.SORT_CPR >= 6101000001 &                        
              BEREGN_IN.SORT_CPR <= 9112999999                          
           THEN DO;                                                     
              /* Det er et fiktivt personnummer */                      
              UDDATA.CPR = BEREGN_IN.SORT_CPR;                          
              UDDATA.KOMNR = BEREGN_IN.SORT_KOMNR;                      
              UDDATA.EJDNR = BEREGN_IN.SORT_EJDNR;                      
              UDDATA.CBENYT = BEREGN_IN.EJENDOM.CBENYT;                 
              UDDATA.CEJDTYPE = BEREGN_IN.EJERSKAB.CEJDTYPE;            
              UDDATA.BEJDVÆR_02 = BEREGN_IN.VURD_02.BEJDVÆR_02;         
              UDDATA.BEJDVÆR_015PCT =                                   
                     BEREGN_IN.VURD_015PCT.BEJDVÆR_015PCT;              
              UDDATA.BEJDVÆR_AKT = BEREGN_IN.VURD_AKT.BEJDVÆR_AKT;      
              UDDATA.FLEJL = BEREGN_IN.EJENDOM.FLEJL;                   
              UDDATA.GANE = BEREGN_IN.AKT_EJER.GANE;                    
              UDDATA.DOVTG = BEREGN_IN.AKT_EJER.DOVTG;                  
              UDDATA.FEJLNR = BEREGN_IN.FEJLNR;                         
              UDDATA.EJD_FEJLNR = BEREGN_IN.EJENDOM.EJD_FEJLNR;         
              WRITE FILE (BH3431O) FROM (UDDATA);                       
              ANTAL_FIKTIV = ANTAL_FIKTIV + 1;                          
           END;                                                         
           READ FILE (BH3431I) INTO (INDDATA_BEREGN);                   
        END;                                                            
                                                                        
        CALL AFSLUT;                                                    
                                                                        
                                                                        
 /********************************************************************/ 
 /* AFSLUT                                                           */ 
 /********************************************************************/ 
 AFSLUT:PROC;                                                           
                                                                        
        CALL SKRIV_TOTAL;                                               
        CLOSE FILE (BH3431O);                                           
        CLOSE FILE (BH3431I);                                           
                                                                        
 END AFSLUT;                                                            
 /********************************************************************/ 
 SKRIV_TOTAL:PROC;                                                      
                                                                        
                                                                        
     PUT SKIP LIST('ANTAL LÆST PÅ BEREGNINGSBÅND SLUT: '                
                   !! ANTAL_LÆST);                                      
                                                                        
     PUT SKIP LIST('   HERAF FIKTIVE PERSONER: '                        
                   !! ANTAL_FIKTIV);                                    
                                                                        
 END SKRIV_TOTAL;                                                       
                                                                        
 /*********************************************************************/
 END BH34300;                                                           
