  PROCESS OPT(TIME);                                                    
 BE68000: /* DAN UDTRÆK TIL PÅSÆTNING AF MANGLENDE HENVISNINGSNUMRE */  
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 DCL COPYRIGHT CHAR (30) STATIC INIT ('COPYRIGHT KOMMUNEDATA I/S 2012');
 /********************************************************************* 
 * FORMÅL:                                                            * 
 * SKAF HENVISNINGSNUMRE FRA ÆGTEFÆLLE/PARTNER FOR AT 'REPARERE' EVT. * 
 * PERSONER UDEN HENVISNINGSNUMMER I MINI MT-FIL FRA FORSKUD.         * 
 *                                                                    * 
 * B.R*4250D OPDELES I:                                               * 
 * PERSONER MED DHENVN > 0                                            * 
 *  S A M T                                                           * 
 * PERSONER  U D E N  HENVISNINGSNUMMER.                              * 
 *                                                                    * 
 * DESUDEN SKRIV PÅ 'NØGLEN' OPLYSNINGER FRA PERSONER MED HENVISNINGS-* 
 * NUMMER > 0 TIL SENERE BEHANDLING D.V.S. EVENTUELT PÅSÆTTELSE AF    * 
 * MANGLENDE HENVISNINGSNUMMER PÅ PERSONER  U D E N  HENVISNINGSNUM-  * 
 * MER I PROGRAM BE68200L.                                            * 
 *                                                                    * 
 * PROGRAMMERET AF  AAGE RASMUSSEN, PUS SKAT DEC. 1999                * 
 *                                                                    * 
 * ÅRSTILRETTET AF  AAGE RASMUSSEN, PUS SKAT JUNI 2000                * 
 * ÅRSTILRETTET AF  AAGE RASMUSSEN, PUS SKAT MAJ  2001                * 
 * ÅRSTILRETTET AF  OLUF MØRK,      LSU SKAT JUNI 2002                * 
 * ÅRSTILRETTET AF  AAGE RASMUSSEN, LSU SKAT JUNI 2003                * 
 * ÅRSTILRETTET AF  BO SCHMIDT,     LSU SKAT JUNI 2004                * 
 * *FORSKUD06*  SUSANNE KROGH VILLUMSEN SKV,  25.05.2005.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2006              * 
 * *FORSKUD07*  BO SCHMIDT              BOB,  14.05.2006.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2007              * 
 * *FORSKUD08*  BO SCHMIDT              BOB,  23.05.2007.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2008              * 
 * *FORSKUD09*  BO SCHMIDT              BOB,  23.06.2008.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2009              * 
 * *FORSKUD10*  GERD HOLM-PEDERSEN      GEH,  14.04.2009.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2010              * 
 *              RUL: BR8 -> BR9                                       * 
 *                                                                    * 
 * *FORSKUD11*  GERD HOLM-PEDERSEN      GEH,  03.06.2010.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2011              * 
 *              RUL: BR9 -> BR0                                       * 
 *                                                                    * 
 * *FORSKUD12*  GERD HOLM-PEDERSEN      GEH,  JUNI  2011.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2012              * 
 *              RUL: BR0 -> BR1                                       * 
 *                                                                    * 
 * *FORSKUD13*  ESBEN BRODERSEN         ESB,  MAJ   2012.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2013              * 
 *              RUL: BR1 -> BR4                                       * 
 *                                                                    * 
 * *FORSKUD14*  ESBEN BRODERSEN         ESB,  MAJ   2013.             * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2014              * 
 *              RUL: BR4 -> BR3                                       * 
 *                                                                    * 
 * *FORSKUD15*  JØRGEN SAUGMANN         QQS,  JUN   2014              * 
 *              PROGRAMMET ER TILRETTET TIL FORSKUD 2015              * 
 *              RUL: BR3 -> BR4                                       * 
 *                                                                    * 
 * *FORSKUD16  Z6JNK.  ÅRSRULLET TIL FORSKUD16            08.06.2015  * 
 *             RUL: BR4 -> BR5                                        * 
 * *FORSKUD17  Z8QEB.  ÅRSRULLET TIL FORSKUD17            08.06.2016  * 
 *             RUL: BR5 -> BR6                                        * 
 * *FORSKUD18  Z8QEB.  ÅRSRULLET TIL FORSKUD18            05.06.2017  * 
 *             RUL: BR6 -> BR7                                        * 
 * *FORSKUD19  Z8QEB.  ÅRSRULLET TIL FORSKUD19            25.05.2018  * 
 *             RUL: BR7 -> BR8                                        * 
 *             ÆNDRET TÆLLER NAVN                                     * 
 * *FORSKUD20  Z6EBD.  ÅRSRULLET TIL FORSKUD20            22.03.2019  * 
 *             RUL: BR8 -> BR9                                        * 
 * *FORSKUD21  Z6PBR.  ÅRSRULLET TIL FORSKUD21            23.03.2020  * 
 *             RUL: BR9 -> BR0                                        * 
 * *FORSKUD22  Z6PBR.  ÅRSRULLET TIL FORSKUD22            24.02.2021  * 
 *             RUL: BR0 -> BR1                                        * 
 * *FORSKUD24  Z6EBD.  ÅRSRULLET TIL FORSKUD24            23.03.2021  * 
 *             RUL: BR1 -> BR3                                        * 
 * *FORSKUD25  Z6TIC.  ÅRSRULLET TIL FORSKUD25            17.02.2023  * 
 *             RUL: BR3 -> BR4                                        * 
 **********************************************************************/
                                                                        
         DEFAULT RANGE(*) STATIC UNALIGNED CONN;                        
                                                                        
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
     /*  RUL HERUNDER  */                                               
 DCL     BR442I          FILE RECORD INPUT;  /* MT-FIL FRA FORSKUD    */
 DCL     BE6801O         FILE RECORD OUTPUT; /* NØGLE MED DHENVN      */
 DCL     BE6802O         FILE RECORD OUTPUT; /* TIL EVT. REP.         */
 DCL     BE6803O         FILE RECORD OUTPUT; /* MED DHENVN OK         */
                                                                        
 /*********************************************************************/
 /*      DECLARE AF INPUT                                             */
 /*********************************************************************/
                                                                        
 /*********************************************************************/
 /*      DECLARE AF UDTRÆK FRA FORSKUD 2013 INDTÆGTSREGISTER          */
 /*********************************************************************/
 DCL     BR442_AR        CHAR      (100) VAR;   /*RUL*/                 
                                                                        
 DCL     1 BE68001       BASED(ADDR(BR442_AR)), /*RUL*/                 
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BE68001N;                                           
                                                                        
 DCL     1 BR44259       BASED(ADDR(BR442_AR)), /*RUL*/                 
           3 LÆNGDE      BIN FIXED (15),                                
           %INCLUDE BR44259N;                   /*RUL*/                 
                                                                        
 /*********************************************************************/
 /*      DECLARE AF UDTRÆK (NØGLE MED DHENVN)                         */
 /*********************************************************************/
 DCL     1 BE680,                                                       
           %INCLUDE BE68000N;                                           
                                                                        
 DCL     1 BE680UD,                                                     
           %INCLUDE BE68001N;                                           
                                                                        
 /*********************************************************************/
 /*      DECLARE AF DIVERSE                                           */
 /*********************************************************************/
 DCL     EOF_BR442       BIT       (1);            /*RUL*/              
 DCL     JA              BIT       (1) INIT ('1'B);                     
 DCL     NEJ             BIT       (1) INIT ('0'B);                     
 DCL     TVFORSK_LÆS       FIXED     (7) INIT (0);                      
 DCL     TVFORSK_FMED      FIXED     (7) INIT (0);                      
 DCL     TVFORSK_FUDEN     FIXED     (7) INIT (0);                      
 DCL     TVFORSK_FNØGLE    FIXED     (7) INIT (0);                      
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2                              BASED(ADDR(DATO1)),       
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     STG             BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF PRINTLINIER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     1 LIN1,                                                        
          2 CTL         CHAR     (1),                                   
          2 TX1         CHAR     (17)    INIT  ('PROGRAM: BE68000L'),   
          2 F1          CHAR     (26)    INIT  ((26)' '),               
          2 TX2         CHAR     (37)    INIT                           
                        ('SKAF EVT. MANGLENDE HENVISNINGSNUMMER'),      
          2 F2          CHAR     (26)    INIT  ((26)' '),               
          2 TX3         CHAR     (14)    INIT  ('UDSKREVET DEN '),      
          2 DATO        CHAR     (10),                                  
          2 REST        CHAR     (02)    INIT  ('  ');                  
 DCL     1 LIN2,                                                        
          2 CTL         CHAR     (1),                                   
          2 F1          CHAR     (54)    INIT  (' '),                   
          2 TX1         CHAR     (20)    INIT  ('KØRSELSKONTROLLISTE'), 
          2 REST        CHAR     (58)    INIT  (' ');                   
 DCL     1 LIN3,                                                        
          2 CTL         CHAR     (1),                                   
          2 TEKST       CHAR     (54),                                  
          2 ANT         PIC      'Z.ZZZ.ZZZ',                           
          2 REST        CHAR     (69)    INIT  (' ');                   
 DCL     1 LIN_BLANK,                                                   
          2 CTL         CHAR     (1),                                   
          2 F1          CHAR     (132)   INIT  (' ');                   
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      HOUSEKEEPING                                                 */
 /*********************************************************************/
                                                                        
         ON ERROR CALL PLIDUMP('SNHOB');                                
         CALL ZS61900;                     /* UDSKRIVNING AF KST-DATO */
                                                                        
         OPEN FILE (BR442I)  TITLE ('BR44250I'), /*RUL*/                
              FILE (BE6801O) TITLE ('BE68010O'),                        
              FILE (BE6802O) TITLE ('BE68020O'),                        
              FILE (BE6803O) TITLE ('BE68030O');                        
                                                                        
         EOF_BR442 = NEJ;                         /*RUL*/               
                                                                        
         ON ENDFILE (BR442I)                      /*RUL*/               
            BEGIN;                                                      
               EOF_BR442 = JA;                    /*RUL*/               
            END;                                                        
                                                                        
         LIN_BLANK.CTL = ZZIK1;                                         
         LIN1.CTL = ZZWS2;                                              
         LIN2.CTL = ZZWS3;                                              
         LIN3.CTL = ZZWS2;                                              
         CALL ZZ20390 ('*OPEN','BE68001Y');                             
                                                                        
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         LIN1.DATO = UDDATO;                                            
                                                                        
 /*********************************************************************/
 /*      HOVEDSTYRING                                                 */
 /*********************************************************************/
                                                                        
         CALL LÆS_BR442;                    /*RUL*/                     
                                                                        
         DO WHILE ( EOF_BR442 = NEJ );      /*RUL*/                     
            CALL BEHANDL_BR442;             /*RUL*/                     
            CALL LÆS_BR442;                 /*RUL*/                     
         END;                                                           
                                                                        
 /********************************************************************* 
 *       AFSLUT PROGRAM                                               * 
 *********************************************************************/ 
                                                                        
         CLOSE FILE (BR442I),              /*RUL*/                      
               FILE (BE6801O),                                          
               FILE (BE6802O),                                          
               FILE (BE6803O);                                          
                                                                        
         CALL SKRIV_TOTALER;                                            
         CALL ZZ20300(LIN_BLANK,'99');                                  
                                                                        
 /*********************************************************************/
 /*      LÆS PÅ B.R44250D                                             */
 /*********************************************************************/
 LÆS_BR442:PROC;                               /*RUL*/                  
                                                                        
         READ FILE (BR442I) INTO (BR442_AR);   /*RUL*/                  
                                                                        
 END LÆS_BR442;                                /*RUL*/                  
 /*********************************************************************/
 /*      OPSPLIT B.R44250D                                            */
 /*********************************************************************/
 BEHANDL_BR442:                          /*RUL*/                        
         PROC;                                                          
                                                                        
         IF BE68001.DCPRN >= 9999999999 /* SLUT-INDIVID */              
         THEN                                                           
            DO;                                                         
               WRITE FILE (BE6803O) FROM (BR442_AR);/*RUL*/             
               RETURN;                                                  
            END;                                                        
                                                                        
         TVFORSK_LÆS          = TVFORSK_LÆS + 1;                        
         BE68001.LÆNGDE     = STG(BE680UD);                             
         BE68001.COVF_DHE   = '0';                                      
         BE68001.FØROPL     = '';                                       
                                                                        
         IF BE68001.DHENVN = 0 !                                        
            BE68001.CSTATUS = '2' /* OMPLACEREDE SKAL IKKE REPARERES */ 
         THEN                                                           
            DO;                                                         
               WRITE FILE (BE6802O) FROM (BR442_AR);/*RUL*/             
               TVFORSK_FUDEN = TVFORSK_FUDEN + 1;                       
               RETURN;                                                  
            END;                                                        
                                                                        
         BE680.DHENVN   = BE68001.DHENVN;                               
         BE680.DCIÆN    = BE68001.DCIÆN;                                
         BE680.DCPRN    = BE68001.DCPRN;                                
         BE680.CCIVS    = BE68001.CCIVS;                                
                                                                        
         WRITE FILE (BE6801O) FROM (BE680);                             
         TVFORSK_FNØGLE = TVFORSK_FNØGLE + 1;                           
                                                                        
         WRITE FILE (BE6803O) FROM (BR442_AR); /*RUL*/                  
         TVFORSK_FMED = TVFORSK_FMED + 1;                               
                                                                        
 END BEHANDL_BR442;                      /*RUL*/                        
 /*********************************************************************/
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*********************************************************************/
 SKRIV_TOTALER:                                                         
 PROC;                                                                  
                                                                        
         CALL ZZ20300(LIN1,'01');                                       
         CALL ZZ20300(LIN2,'01');                                       
         /*RUL*/                                                        
         LIN3.TEKST = 'ANTAL LÆSTE INDIVIDER PÅ B.R04250D             ';
         LIN3.ANT = TVFORSK_LÆS;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H68010D (NØGLEN)  ';
         LIN3.ANT = TVFORSK_FNØGLE;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H68020D (UDEN)    ';
         LIN3.ANT = TVFORSK_FUDEN;                                      
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ B.H68030D (MED)     ';
         LIN3.ANT = TVFORSK_FMED;                                       
         CALL ZZ20300(LIN3,'01');                                       
                                                                        
 END SKRIV_TOTALER;                                                     
                                                                        
                                                                        
 END  BE68000;                                                          
