 /*********************************************************************/
 /*                  EJENDOMSSKATTER                                  */
 /*                                                                   */
 /* NB.: PÅ GRUND AF EN FEJL I ESR-UDTRÆKKET TIL SLUT 2004 DER HAR    */
 /*      BEVIRKET AT GL_BEJEB OG GL_BSTUE ALDRIG ER OPSAT, ER DET     */
 /*      AFTALT MED JØRGEN OLSEN T&S D. 25/1-05 AT:                   */
 /*                                                                   */
 /*      1. GL_BSTUE BENYTTES IKKE SOM KRITERIE FOR AT FORETAGE       */
 /*         FORDELING.                                                */
 /*      2. GL_BEJEB BENYTTES IKKE SOM KRITERIE FOR AT FORETAGE       */
 /*         FORDELING.                                                */
 /*      3. EJERBOLIGVÆRDI FRA SENESTE VURDERING BENYTTES SAMMEN      */
 /*         MED EJENDOMSVÆRDIEN FRA SENESTE VURDERING ISTEDET FOR     */
 /*         HENHOLDSVIS GL_BEJEB OG GL_BEJDV I BEREGNING AF FORDELING */
 /*         EFTER FORMEL NR. 1 (ANALYSE = 1).                         */
 /*                                                                   */
 /* 12102010 GEH: BFAMTSKST, BAFPGRVAMT OG BFAMTSKLA UDGIK I 2007 OG  */
 /* SLUT2010      ER DERFOR OGSÅ SLETTEDE I BH31800N                  */
 /* SLUT2010-2    OVENSTÅENDE VAR FORKERT RETTET                      */
 /*                                                                   */
 /* 25012012 GEH: DER ER 136, HVOR BERHVAN ER STØRRE END BFSKAT       */
 /* SLUT2011      SÅ DERFOR PUT SKIPPERNE                             */
 /*********************************************************************/
 EJDSKAT:                                                               
 PROC;                                                                  
                                                                        
 DCL     ANT_VÆRDIER    FIXED     (5);                                  
 DCL     ANALYSE        FIXED     (1);                                  
 DCL     RUNDE          FIXED     (15);                                 
 DCL     HJFLOAT        FIXED     (15);                                 
 DCL     HJPCT          FIXED     (5,4);                                
                                                                        
         IF BH945.ESR.EJER.DCPRCIR = 0 !                                
            BH945.ESR.BFSKAT = 0                                        
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BH945.TIL_EVS.CEJBEVS = '7' !                               
            BH945.TIL_EVS.CEJBEVS = '8'                                 
         THEN                                                           
            DO;                                                         
               /* UNDTAGELSE (DELVIS) */                                
               BH945.CLIN10 = 1;                                        
               BH945.CREDIG10 = '3';                                    
               RETURN;                                                  
            END;                                                        
                                                                        
         HJPCT = BH945.ESR.GBOANDPCT / 10000;                           
                                                                        
         SELECT ( BH945.AKT_VURD.CBENYT );  /* BENYTTELSESKODER HVOR  */
                                            /* DER INTET SKAL SKE     */
            WHEN ('08','09','16','21','26','27','28','29','45','48',    
                  '49','70','71','78')                                  
               RETURN;                                                  
            OTHERWISE;                                                  
         END;                                                           
                                                                        
         IF BH945.AKT_EJER.FEJER > 3 &                                  
            ((BH945.AKT_EJER.DOVTG = ÅR0101 ! KØBT_I_ÅR = JA) !         
            SOLGT_I_ÅR = JA)                                            
         THEN                                                           
            RETURN;                                                     
                                           /* UNDTAGELSE -> HANDLET + */
         IF BH945.MT.CFRÅR > '01' &        /* FORSKUDT REGNSKABSÅR    */
            ((BH945.AKT_EJER.DOVTG = ÅR0101 ! KØBT_I_ÅR = JA) !         
            SOLGT_I_ÅR = JA)                                            
         THEN                                                           
            RETURN;                                                     
                                                                        
         IF BH945.AKT_VURD.FLEJL > 2 !      /* UNDTAGELSE -> OVER 2   */
          ((BH945.AKT_VURD.CBENYT = '02' !  /* LEJLIGHEDER ELLER      */
            BH945.AKT_VURD.CBENYT = '22' !  /* BK=02,22,42 + 2 LEJL.  */
            BH945.AKT_VURD.CBENYT = '42') &                             
            BH945.AKT_VURD.FLEJL = 2)                                   
         THEN                                                           
            RETURN;                                                     
                                                                        
         BH945.ESR.BFSKAT = (BH945.ESR.BFSKAT * TÆLLER) / NÆVNER;       
         BH945.ESR.BDAF = (BH945.ESR.BDAF * TÆLLER) / NÆVNER;           
         BH945.ESR.BFKOMSKAT = (BH945.ESR.BFKOMSKAT * TÆLLER) / NÆVNER; 
         BH945.ESR.BFKOMSKST = (BH945.ESR.BFKOMSKST * TÆLLER) / NÆVNER; 
                                                                        
         /* HEREFTER OPSÆTTES BPRIVAN_SLS TIL MARKERING AF AT */        
         /* BFSKAT_SLS (SOM SÆTTES LIG ESR.BFSKAT I BH96000)  */        
         /* ALLEREDE ER PERIODEBEREGNET A.H.T. SENERE BEHAND- */        
         /* LING I PROGRAM BH61300 (DAN DATA TIL SLS).        */        
                                                                        
         BH945.BPRIVAN_SLS = BH945.ESR.BFSKAT;                          
                                                                        
         BH945.BPRIVAN = BH945.ESR.BFSKAT;                              
         HJFLOAT = BH945.ESR.BFSKAT - BH945.ESR.BDAF;                   
                                                                        
         ANT_VÆRDIER = BH945.AKT_VURD.ANT_FOR_4 +                       
                       BH945.AKT_VURD.ANT_FOR_5 +                       
                       BH945.AKT_VURD.ANT_FOR_6;                        
                                                                        
         ANALYSE = 0;                                                   
                                                                        
         SELECT ( BH945.AKT_VURD.CBENYT );                              
            WHEN ('01','41')                /* BEBOELSE               */
               DO;                          /* EN_FAMILIEHUSE         */
                  IF BH945.AKT_VURD.FLEJL < 2                           
                  THEN                                                  
                     DO;                                                
                        IF ANT_VÆRDIER = 0                              
                        THEN                                            
                           RETURN;         /* SÅ UDSKRIVES DER INTET */ 
                        ANALYSE = 0;                                    
                        CALL UNDERSØG_EJERBOLIGVÆRDI;                   
                        CALL UNDERSØG_STUEHUSVÆRDI;                     
                        CALL BEREGN_ERHVERV(ANALYSE);                   
                     END;                                               
                  ELSE                                                  
                     IF BH945.AKT_VURD.FLEJL = 2   /* TO_FAMILIEHUSE */ 
                     THEN                                               
                        DO;                                             
                           IF ANT_VÆRDIER = 2 &                         
                              BH945.AKT_VURD.ANT_FOR_4 = 0 &            
                              BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0 &    
                              BH945.AKT_VURD.EJEBOL(2).BEJEBOL > 0      
                           THEN                                         
                              RETURN;      /* SÅ UDSKRIVES DER INTET */ 
                           ANALYSE = 0;                                 
                           CALL UNDERSØG_EJERBOLIGVÆRDI;                
                           CALL UNDERSØG_STUEHUSVÆRDI;                  
                           CALL BEREGN_ERHVERV(ANALYSE);                
                        END;                                            
               END;                                                     
                                                                        
            WHEN ('02','22','34','42')                                  
               DO;                                                      
                  ANALYSE = 0;                                          
                  CALL UNDERSØG_EJERBOLIGVÆRDI;                         
                  CALL BEREGN_ERHVERV(ANALYSE);                         
               END;                                                     
                                                                        
            WHEN ('05','07')                                            
               DO;                                                      
                  ANALYSE = 0;                                          
                  CALL UNDERSØG_STUEHUSVÆRDI;                           
                  CALL BEREGN_ERHVERV(ANALYSE);                         
               END;                                                     
                                                                        
            WHEN ('06')                                                 
               DO;                                                      
                  ANALYSE = 0;                                          
                  CALL UNDERSØG_EJERBOLIGVÆRDI;                         
                  CALL UNDERSØG_STUEHUSVÆRDI;                           
                  CALL BEREGN_ERHVERV(ANALYSE);                         
               END;                                                     
                                                                        
            WHEN ('13')                                                 
               DO;                                                      
                  IF ANT_VÆRDIER = 0 &                                  
                     BH945.ESR.GL_BEJDV > 0                             
                  THEN                                                  
                     DO;                                                
                        BH945.CLIN10 = 1;                               
                        BH945.CREDIG10 = '3';                           
                        RETURN;                                         
                     END;                                               
                  ANALYSE = 0;                                          
                  CALL UNDERSØG_EJERBOLIGVÆRDI;                         
                  CALL UNDERSØG_STUEHUSVÆRDI;                           
                  CALL BEREGN_ERHVERV(ANALYSE);                         
               END;                                                     
                                                                        
            WHEN ('17',                 /* UBEBYGGEDE LANDBRUGSLODDER */
                  '25')                 /* EJERLEJLIGHED,IØVRIGT      */
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '3';                                 
               END;                                                     
                                                                        
            OTHERWISE;                                                  
                                                                        
         END; /* SELECT (BH945.AKT_VURD.CBENYT) */                      
                                                                        
         IF BH945.CLIN10 = 1                                            
         THEN                                                           
            DO;                                                         
               /* UNDTAGELSER */                                        
               IF BH945.AKT_VURD.DVURDÅR ^= INDKÅR1 !                   
                  BH945.AKT_EJER.FEJER > 3 !                            
                  ANT_VÆRDIER > 1          !                            
                  BH945.ESR.GL_BEJDV = 0                                
               THEN                                                     
                  DO;                                                   
                     BH945.CREDIG10 = '3';                              
                     RETURN;                                            
                  END;                                                  
            END;                                                        
                                                                        
 /*********************************************************************/
 /*      FIND EJENDOMME SOM SKAL BEREGNES/HAVE UDSKREVET OPLYSNINGER  */
 /*      PÅ GRUNDLAG AF FORMLER VEDR. EJERBOLIGVÆRDIER                */
 /*********************************************************************/
 UNDERSØG_EJERBOLIGVÆRDI:                                               
 PROC;                                                                  
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 0 &                              
            BH945.ESR.GL_BEJDV > 0 &                                    
         /* BH945.ESR.GL_BEJEB > 0 & */                                 
            BH945.AKT_VURD.BEJDVÆR > 0 &                                
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0                        
         THEN                                                           
            ANALYSE = 1;                                                
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 0 &                              
            BH945.ESR.GBOANDPCT > 0 &                                   
            BH945.ESR.GL_BEJDV > 0 &                                    
         /* BH945.ESR.GL_BEJEB > 0 & */                                 
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0                        
         THEN                                                           
            ANALYSE = 2;                                                
                                                                        
 END UNDERSØG_EJERBOLIGVÆRDI;                                           
                                                                        
 /*********************************************************************/
 /*      FIND EJENDOMME SOM SKAL BEREGNES/HAVE UDSKREVET OPLYSNINGER  */
 /*      PÅ GRUNDLAG AF FORMLER VEDR. STUEHUSVÆRDIER                  */
 /*********************************************************************/
 UNDERSØG_STUEHUSVÆRDI:                                                 
 PROC;                                                                  
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 1 &                              
            BH945.ESR.GL_BEJDV > 0 &                                    
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0 &                      
         /* BH945.ESR.GL_BSTUE > 0 & */                                 
            BH945.ESR.GL_BSTUGR > 0 &                                   
            BH945.ESR.BFKOMSKAT > 0 &                                   
            BH945.ESR.BFKOMSKST > 0                                     
         THEN                                                           
            ANALYSE = 3;                                                
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 1 &                              
            BH945.ESR.GL_BEJDV > 0 &                                    
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0 &                      
         /* BH945.ESR.GL_BSTUE > 0 & */                                 
            BH945.ESR.GL_BSTUGR > 0 &                                   
            BH945.ESR.BFKOMSKAT > 0 &                                   
            BH945.ESR.BFKOMSKST = 0 &                                   
            BH945.ESR.BAFPGRVKOM > 0                                    
         THEN                                                           
            ANALYSE = 4;                                                
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 1 &                              
            BH945.ESR.GL_BEJDV > 0 &                                    
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0 &                      
         /* BH945.ESR.GL_BSTUE > 0 & */                                 
            BH945.ESR.GL_BSTUGR > 0 &                                   
            BH945.ESR.BFKOMSKAT > 0 &                                   
            BH945.ESR.BFKOMSKST > 0                                     
         THEN                                                           
            ANALYSE = 5;                                                
                                                                        
         IF ANT_VÆRDIER = 1 &                                           
            BH945.AKT_VURD.ANT_FOR_4 = 1 &                              
            BH945.AKT_VURD.EJEBOL(1).BEJEBOL > 0 &                      
         /* BH945.ESR.GL_BSTUE > 0 &  */                                
            BH945.ESR.GL_BSTUGR > 0 &                                   
            BH945.ESR.BFKOMSKST = 0 &                                   
            BH945.ESR.BAFPGRVKOM > 0                                    
         THEN                                                           
            ANALYSE = 6;                                                
                                                                        
 END UNDERSØG_STUEHUSVÆRDI;                                             
                                                                        
 /*********************************************************************/
 /*      BEREGN PRIVAT ANDEL AF EJENDOMSSKATTER                       */
 /*********************************************************************/
 BEREGN_ERHVERV:                                                        
 PROC(ANALYSE);                                                         
                                                                        
 DCL     ANALYSE        FIXED     (1);                                  
                                                                        
         SELECT (ANALYSE);                                              
                                                                        
            WHEN (1)                                                    
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '1';                                 
                  RUNDE = (BH945.AKT_VURD.EJEBOL(1).BEJEBOL * HJFLOAT); 
                  RUNDE = (RUNDE / BH945.AKT_VURD.BEJDVÆR) + 0.5;       
                  BH945.BERHVAN = BH945.ESR.BFSKAT - RUNDE;             
                  BH945.BPRIVAN = BH945.ESR.BFSKAT - BH945.BERHVAN;     
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('1 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            WHEN (2)                                                    
               DO;                                                      
                  BH945.TIL_SLS.GERHVPCT =                              
                  10000 - BH945.ESR.GBOANDPCT;                          
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '1';                                 
                  RUNDE = (HJFLOAT * HJPCT) + 0.5;                      
                  BH945.BERHVAN = BH945.ESR.BFSKAT - RUNDE;             
                  BH945.BPRIVAN = BH945.ESR.BFSKAT - BH945.BERHVAN;     
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('2 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            WHEN (3)                                                    
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '2';                                 
                  BH945.BERHVAN = BH945.ESR.BFKOMSKAT +                 
                                  BH945.ESR.BDAF;                       
                  BH945.BPRIVAN = BH945.ESR.BFSKAT - BH945.BERHVAN;     
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('3 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            WHEN (4)                                                    
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '2';                                 
                  RUNDE = ((BH945.ESR.GL_BSTUGR *                       
                            BH945.ESR.BFKOMSKAT) /                      
                            BH945.ESR.BAFPGRVKOM) + 0.5;                
                  HJFLOAT = BH945.ESR.BFKOMSKAT - RUNDE;                
                  BH945.BERHVAN = BH945.ESR.BDAF + HJFLOAT;             
                  BH945.BPRIVAN = BH945.ESR.BFSKAT -                    
                                  BH945.BERHVAN;                        
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('4 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            WHEN (5)                                                    
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '2';                                 
                                                                        
 /*               RUNDE = ((BH945.ESR.GL_BSTUGR *                       
                            BH945.ESR.BFAMTSKLA) /                      
                            BH945.ESR.BAFPGRVAMT) + 0.5;                
                  HJFLOAT = RUNDE;                                      
                  BH945.BERHVAN = BH945.ESR.BFKOMSKAT +                 
                                  BH945.ESR.BDAF + HJFLOAT; */          
                                                                        
 /*SLUT2010-2*/   BH945.BERHVAN = BH945.ESR.BFKOMSKAT +                 
                                  BH945.ESR.BDAF;                       
                                                                        
                  BH945.BPRIVAN = BH945.ESR.BFSKAT -                    
                                  BH945.BERHVAN;                        
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('5 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            WHEN (6)                                                    
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '2';                                 
                  RUNDE = (HJFLOAT * BH945.ESR.GL_BSTUGR);              
                  RUNDE = (RUNDE / BH945.ESR.BAFPGRVKOM) + 0.5;         
                  BH945.BERHVAN = BH945.ESR.BFSKAT - RUNDE;             
                  BH945.BPRIVAN = BH945.ESR.BFSKAT - BH945.BERHVAN;     
 /*SLUT2011*/     IF BH945.BERHVAN > BH945.ESR.BFSKAT                   
                  THEN                                                  
                    DO;                                                 
                      PUT SKIP LIST ('6 CPRNR  : ',BH945.SORT_CPR);     
                      PUT SKIP LIST ('  KOMNR  : ',BH945.SORT_KOMNR);   
                      PUT SKIP LIST ('  EJDNR  : ',BH945.SORT_EJDNR);   
                      PUT SKIP LIST ('  BFSKAT : ',BH945.ESR.BFSKAT);   
                      PUT SKIP LIST ('  BERHVAN: ',BH945.BERHVAN);      
                      PUT SKIP LIST ('           ');                    
 /*SLUT2011*/       END;                                                
               END;                                                     
                                                                        
            OTHERWISE                                                   
               DO;                                                      
                  BH945.CLIN10 = 1;                                     
                  BH945.CREDIG10 = '3';                                 
               END;                                                     
                                                                        
         END;                                                           
                                                                        
 END BEREGN_ERHVERV;                                                    
                                                                        
 END EJDSKAT;                                                           
