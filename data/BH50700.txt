 BH50700:/***  DAN OG OPSPLIT TRANS'ER TIL ESR FRA EVS SLUT         ***/
         PROC OPTIONS (MAIN) ;                                          
 /*********************************************************************/
 /*                                                                   */
 /*      NAVN       :  DAN OG OPSPLIT TRANS'ER TIL ESR FRA EVS        */
 /*                                                                   */
 /*      FORMÅL     :  AT DANNE OG OPSPLITTE TRANSAKTIONER MED PENSI- */
 /*                    ONISTKODE PR. REGIONAL CENTRAL.                */
 /*                                                                   */
 /*      INDDATA    :  BH86011D  fra BH86000                          */
 /*                    (TRANSER TIL ESR - LEJEVÆRDIKODE/PENS.KODE)    */
 /*                                                                   */
 /*      UDDATA     :  BH507X0D (PENSIONISTKODE)                      */
 /*                    (X=KODE FOR REGIONAL CENTRAL)                  */
 /*                    BH50701Y (TOTALLISTE)                          */
 /*                                                                   */
 /*      PROGRAMMØR :  JESPER OLSEN     UC-P   SEPT. 1987             */
 /*      RETTET     :  AAGE RASMUSSEN   DIV-SKAT OKT. 1990            */
 /*                 :  AAGE RASMUSSEN   DIV-SKAT DEC. 1990            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT JAN. 1997            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT JAN. 1998            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT NOV. 1999            */
 /*                 :  AAGE RASMUSSEN   PFB SKAT DEC. 2000            */
 /*                 :  AAGE RASMUSSEN   LSU      DEC. 2002            */
 /*      RETTET     :  LENE MAJ JENSEN  QLE      MAR. 2017            */
 /*                    Omlægning af beregningsbånd til DB2            */
 /*      RETTET     :  Esben Brodersen  QEB      NOV. 2017            */
 /*                    BH960 -> BH860 undtagen struktur BH96011N      */
 /*      RETTET     :  Per Brunk PBR, kompileret      OKT. 2020       */
 /*********************************************************************/
         DEFAULT RANGE(*) STATIC UNALIGNED;                             
 /*********************************************************************/
 /*      DECLARE AF FILER                                             */
 /*********************************************************************/
 DCL     BH860IN         FILE RECORD INPUT;                             
 DCL     BH507X0         FILE RECORD OUTPUT;                            
 DCL     BH507I FILE RECORD INPUT;      /* Parameterfil    */           
 DCL     SYSPRINT        EXTERNAL FILE PRINT;                           
 /*********************************************************************/
 /*      DECLARE AF INPUT                                             */
 /*********************************************************************/
 DCL     1 BH860,                                                       
           %INCLUDE BH96011N;                                           
 DCL     BH860_ID        CHAR      (14) BASED(ADDR(BH860));             
 /*********************************************************************/
 /*      PARAMETER INPUT                                              */
 /*********************************************************************/
                                                                        
 DCL     1 BH507_PARM,                                                  
         %INCLUDE BH54900N;                                             
 /*********************************************************************/
 /*      DECLARE AF OUTPUT                                            */
 /*********************************************************************/
 DCL     BH507           CHAR      (80);                                
 DCL     1     Z300      BASED(ADDR(BH507)),                            
          2    CTRANS    CHAR      (4),                                 
          2    HDISP     CHAR      (1),                                 
          2    EBRUGER   PIC       '9999',                              
          2    CMEDIE    CHAR      (1),                                 
          2    EVOLBR    CHAR      (6),                                 
          2    DREGNR    CHAR      (3),                                 
          2    CBLAND    CHAR      (1),                                 
          2    EPLADS    CHAR      (3),                                 
          2    EOPGOM    CHAR      (1),                                 
          2    EDATNR    CHAR      (2),                                 
          2    HDISP1    CHAR      (54);                                
 DCL     1     SLUTD     BASED(ADDR(BH507)),                            
          2    CTRANS    CHAR    (5),                                   
          2    EANTAL    PIC    '(5)9',                                 
          2    HDISP     CHAR    (70);                                  
 DCL     1 BH50702       BASED(ADDR(BH507)),                            
           %INCLUDE BH60702N;                                           
 /*********************************************************************/
 /*      DECLARE AF ENTRY OG BUILTIN                                  */
 /*********************************************************************/
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ZS61900         ENTRY;                                         
 DCL     ZS67000         ENTRY;                                         
 DCL     ZS30101         ENTRY;                                         
 %INCLUDE ZS38000N;                /* DATORUTINE - SKAF D.D. + DAGNR. */
 /*********************************************************************/
 /*     DECLARE AF DB2-variabler                                      */
 /*********************************************************************/
  EXEC SQL INCLUDE SQLCA;                                               
 DCL    SQLKODE         FIXED     (5);                                  
 DCL    SQLCODE_CH      CHAR      (5);                                  
 DCL    SQLCODE_PIC     PIC       'SSSS9' DEF SQLCODE_CH;               
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF ABEND STRUKTUR m.v.                               */
 /*                                                                   */
 /*********************************************************************/
 DCL     ABENDKODE     FIXED       (2) INIT(5);                         
 DCL     1 ABENDMEDD,                                                   
          2 ABENDMODUL CHAR      (7) INIT ('BH50700'),                  
          2 ABENDFILL1 CHAR      (1) INIT (' '),                        
          2 ABENDMEDNR CHAR      (2) INIT ('**'),                       
          2 ABENDFILL2 CHAR      (1) INIT (' '),                        
          2 ABENDTXT   CHAR      (45);                                  
                                                                        
 DCL     ABENDRTKODE   FIXED     (1) INIT (2);                          
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER TIL KONTROLLISTEN                     */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH50700L'),                                        
           2 F1          CHAR      (23)      INIT      (' '),           
           2 TEKST2      CHAR      (44)      INIT      (                
           'OPSPLITNING AF PENSIONISTTRANS PR. CENTRAL'),               
           2 F2          CHAR      (24)      INIT      (' '),           
           2 TEKST3      CHAR      (14)      INIT      (                
           'UDSKREVET DEN '),                                           
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (53)      INIT      (' '),           
           2 TEKST1      CHAR      (79)      INIT                       
           ('KØRSELSKONTROLLISTE');                                     
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (48),                                
           2 ANTAL       PIC       'ZZZ.ZZZ.ZZ9',                       
           2 F1          CHAR      (73)      INIT      (' ');           
 % INCLUDE ZZ20301M;                                                    
 /*********************************************************************/
 /*      DECLARE AF DIVERSE HJÆLPEFELTER                              */
 /*********************************************************************/
 DCL     WS_FORSKUD_SLUT_TXT            CHAR(16) INIT (' ');            
                                                                        
 DCL     WS_TIL_EVS_AAR                 BIN FIXED (15);                 
                                                                        
 DCL     WS_TIL_EVS_AAR_GL              BIN FIXED (15);                 
 DCL     WS_SLUT_AFVIKLINGS_ID          CHAR(16) INIT (' ');            
                                                                        
 DCL     WS_GLSLUTÅR                    BIN FIXED (15);                 
                                                                        
 DCL     WS_PROGRAMNAVN                 CHAR(8)  INIT ('BH50700');      
                                                                        
 DCL     PUTSKIP         BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     1 BH65300M,                                                    
           %INCLUDE BH65300M;                                           
                                                                        
 DCL     INDEX           DEC FIXED (1);                                 
 DCL     GEM_ID          CHAR      (14);                                
 DCL     GEM_RC          PIC       '9';                                 
 DCL     GEM_KOM         PIC       '999';                               
 DCL     EOF_BH860       BIT       (1);                                 
 DCL     EOF_BH507       BIT       (1);                                 
 DCL     JA              BIT       (1)  INIT ('1'B);                    
 DCL     NEJ             BIT       (1)  INIT ('0'B);                    
 DCL     I               PIC       '9';                                 
 DCL     HEXE0           CHAR      (1)       INIT      ('\');           
                                                                        
 %INCLUDE ZS38002N;                /* DATORUTINE - DIVERSE DECLARE    */
                                                                        
 DCL     HJ_DAGS_DATO    CHAR      (08);                                
 DCL     1 HJ_DAGS_DATO1 DEF HJ_DAGS_DATO,                              
           2 ÅR          CHAR      (04),                                
           2 MD          CHAR      (02),                                
           2 DAG         CHAR      (02);                                
                                                                        
 DCL     DAGNR           CHAR      (3);                                 
                                                                        
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     ANT_LÆS         FIXED     (7);       /* ANTAL LÆSTE INDV.    */
 DCL     ANT_RC0(6)      FIXED     (7);       /* SKREVNE INDV. PR. RC */
 DCL     ANT_SKR         FIXED     (7);       /* SKREVNE INDV. IALT   */
 DCL     ANT_TR0         FIXED     (7);       /* ANTAL SKREVNE TRANS. */
 /*********************************************************************/
 /* M A C R O E R der SKAL ligge før koden grundet declare af cursors */
 /*********************************************************************/
 %INCLUDE BH54999M; /* CHECK OM OMKØRSEL, OG UDFØR EVT OPRYDNING */     
 /*********************************************************************/
 /*      On conditions og diverse init                                */
 /*********************************************************************/
         PUT SKIP LIST ('START ' !! WS_PROGRAMNAVN);                    
                                                                        
         CALL ZS61900;                            /* UDSKRIV SSI-DATO */
                                                                        
         ON ERROR                                                       
            BEGIN;                                                      
                ON ERROR SYSTEM;                                        
                PUT SKIP LIST ('PROGRAMFEJL');                          
                CALL PLIDUMP  ('SNHOB','PROGRAM BH50700L');             
            END;                                                        
                                                                        
         ON ENDFILE (BH860IN)                                           
            EOF_BH860 = '1'B;                                           
         OPEN FILE (BH860IN) TITLE ('BH86011I');                        
         READ FILE (BH860IN) INTO (BH860);                              
         GEM_ID = '';                                                   
         GEM_RC = BH860.CRCKO;                                          
         GEM_KOM = 0;                                                   
         INDEX = GEM_RC;                                                
         OPEN FILE (BH507X0) TITLE ('BH507'!!BH860.CRCKO!!'0O');        
         CALL ZZ20390 ('*OPEN','BH50701Y');                             
         /* SKAF DAGS DATO SAMT DAGNR */                                
         ZS380PARM.FUNKTION   = 'DAGSDATO';                             
         ZS380PARM.VALIDERING = 'N';                                    
         CALL ZS38000 (ZS380PARM.FUNKTION,                              
                       ZS380PARM.VALIDERING,                            
                       ZS380PARM.PARM,                                  
                       ZS380PARM.RETURKODE);                            
         DAGNR = SUBSTR(ZS380PARM3.DATO2,6,3);                          
         HJ_DAGS_DATO  = ZS380PARM3.DATO1;                              
         OVERLIN1.DATO = HJ_DAGS_DATO1.DAG  !! '/' !!                   
                         HJ_DAGS_DATO1.MD   !! '-' !!                   
                         HJ_DAGS_DATO1.ÅR;                              
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         ANT_LÆS    = 0;                                                
         ANT_SKR    = 0;                                                
         ANT_RC0(*) = 0;                                                
         ANT_TR0    = 0;                                                
                                                                        
         OPEN FILE (BH507I)  TITLE ('BH50700P');                        
         ON ENDFILE (BH507I)                                            
         BEGIN;                                                         
            EOF_BH507 = JA;                                             
         END;                                                           
                                                                        
         if PUTSKIP = '1'B                                              
         then do;                                                       
            put skip list ('*******************');                      
            put skip list ('hovedstyring');                             
            put skip list ('*******************');                      
         end;                                                           
                                                                        
         READ FILE (BH507I) INTO (BH507_PARM);                          
                                                                        
         SELECT (BH507_PARM.FS_MRK);                                    
            WHEN ('S')                                                  
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SLUT kørsel');         
              END;                                                      
            WHEN ('F')                                                  
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS FORSKUD kørsel');      
              END;                                                      
            WHEN ('U')                                                  
              DO;                                                       
                 PUT SKIP LIST ('Dette er en EVS SUPPLEMENT kørsel');   
              END;                                                      
            OTHERWISE                                                   
              DO;                                                       
                 ABENDTXT = '101 Parameter skal være S,F,U';            
                 CALL PROGRAM_FEJL;                                     
              END;                                                      
         END;                                                           
                                                                        
         IF EOF_BH507 = NEJ                                             
         THEN DO;                                                       
            SELECT (BH507_PARM.FS_MRK);                                 
               WHEN ('F')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH507_PARM.FORSKUD_TXT;      
                     WS_TIL_EVS_AAR = BH65300M.FORSKÅR;                 
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH507_PARM.SLUT_TXT;       
                  END;                                                  
               WHEN ('S')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH507_PARM.SLUT_TXT;         
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH507_PARM.SLUT_TXT;       
                  END;                                                  
               WHEN ('U')                                               
                  DO;                                                   
                     WS_FORSKUD_SLUT_TXT = BH507_PARM.SUPPL_TXT;        
                     WS_TIL_EVS_AAR = BH65300M.SLUTÅR;                  
                     /* N.B.  til_evs_aar_GL Tages ALTID fra SLUT */    
                     WS_TIL_EVS_AAR_GL = BH65300M.SLUTÅR - 1;           
                     /* afviklings-id til læs gammelt år altid SLUT */  
                     WS_SLUT_AFVIKLINGS_ID = BH507_PARM.SLUT_TXT;       
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     ABENDTXT = '101 PARAMETERFEJL';                    
                     CALL PROGRAM_FEJL;                                 
                  END;                                                  
            END; /* select */                                           
         END;                                                           
                                                                        
         /********************************************************/     
         /*  CHECK OM FORGÆNGERE ER KØRT                         */     
         /********************************************************/     
         if PUTSKIP = '1'B                                              
         then do;                                                       
            put skip list ('*******************');                      
            put skip list ('Check Sekvens');                            
            put skip list ('*******************');                      
         end;                                                           
                                                                        
         /* QEB QEB 2018-03-07 HACK*/                                   
         /*CALL CHECK_SEKVENS (BH507_PARM.FS_MRK); /* BH54997M */       
                                                                        
         /********************************************************/     
         /*  NU STARTER CHECK AF OMKØRSEL                        */     
         /********************************************************/     
         if PUTSKIP = '1'B                                              
         then do;                                                       
            put skip list ('*******************');                      
            put skip list ('Check_om_omkØrsel');                        
            put skip list ('*******************');                      
         end;                                                           
                                                                        
         /*QEB 2018-03-07 HACK*/                                        
         /*CALL CHECK_OM_OMKØRSEL; /* kode ligger i macro BH54999M */   
         DER_SKAL_KØRES_OM = NEJ;                                       
                                                                        
         IF DER_SKAL_KØRES_OM                                           
         THEN DO;                                                       
            PUT SKIP LIST ('Omkørsel af ' !! WS_PROGRAMNAVN);           
            CALL RYD_OP_FØR_OMKØRSEL;                                   
         END;                                                           
         ELSE DO;                                                       
            PUT SKIP LIST ('Normal kørsel af ' !! WS_PROGRAMNAVN);      
         END;                                                           
                                                                        
                                                                        
         /********************************************************/     
         /*  NU STARTER NORMALT FLOW                             */     
         /********************************************************/     
         if PUTSKIP = '1'B                                              
         then do;                                                       
            put skip list ('**************************');               
            put skip list ('opdater_kØrselstabel START');               
            put skip list ('**************************');               
         end;                                                           
         CALL OPDATER_KØRSELSTABEL('START',BH507_PARM.FS_MRK);          
 /*********************************************************************/
 /*      PROCESS                                                      */
 /*********************************************************************/
         DO WHILE (^EOF_BH860);                                         
            ANT_LÆS = ANT_LÆS + 1;                                      
            IF BH860.CRCKO ^= GEM_RC                                    
            THEN                                                        
               DO;                                                      
                  CALL SLUT_INDIVID;                                    
                  CALL LUK_ÅBN;                                         
               END;                                                     
            IF GEM_ID ^= BH860_ID                                       
            THEN                                                        
               CALL SKRIV;                                              
            READ FILE (BH860IN) INTO (BH860);                           
         END;  /* DO WHILE */                                           
         CALL SLUT_INDIVID;                                             
         CALL KONTROLLISTE;                                             
                                                                        
         EXEC SQL COMMIT; /* kun kørselstabel er opdateret */           
                                                                        
         CALL AFSLUT;                                                   
 /*********************************************************************/
 /*      SKRIV PENSIONISTTRANS                                        */
 /*********************************************************************/
 SKRIV:                                                                 
         PROC;                                                          
         IF BH860.EKOMNR ^= GEM_KOM                                     
         THEN                                                           
            DO;                                                         
               IF GEM_KOM ^= 0                                          
               THEN                                                     
                  CALL SLUT_INDIVID;                                    
               BH507 = ' ';                                             
               Z300.CTRANS = 'Z300';                                    
               Z300.EBRUGER = BH860.EKOMNR;                             
               Z300.CMEDIE = '4';                                       
               Z300.DREGNR = DAGNR;                                     
               Z300.CBLAND = '0';                                       
               Z300.EOPGOM = 'C';                                       
               Z300.EDATNR = '01';                                      
               WRITE FILE (BH507X0) FROM (BH507);                       
               GEM_KOM = BH860.EKOMNR;                                  
            END;                                                        
         BH507 = ' ';                                                   
         CALL SKRIV_TYP10;                                              
         GEM_ID = BH860_ID;                                             
 END SKRIV;                                                             
 /*********************************************************************/
 /*                      SKRIV TYPE 10                                */
 /*********************************************************************/
 SKRIV_TYP10:                                                           
         PROC;                                                          
         BH50702.CNART   = 'CN';                                        
         BH50702.EKOMNR  = BH860.EKOMNR;                                
         BH50702.EEJDNR  = BH860.EEJDNR;                                
         BH50702.CTRTYP  = 10;                                          
         BH50702.CRET    = 2;                                           
         BH50702.FELT1   = '&261';                                      
         BH50702.DCPRCIR = BH860.DCPRCIR;                               
         BH50702.FELT2   = '&296';                                      
         BH50702.CÅRSAG  = '1';                                         
         BH50702.FELT3   = '&297';                                      
         BH50702.CMYND   = 'K';                                         
         BH50702.FELT4   = '&282';                                      
         BH50702.CPENS   = BH860.CPENS;                                 
         BH50702.CSLUT   = HEXE0;                                       
         WRITE FILE (BH507X0) FROM (BH507);                             
         ANT_TR0 = ANT_TR0 + 1;                                         
         ANT_RC0(INDEX) = ANT_RC0(INDEX) + 1;                           
         ANT_SKR = ANT_SKR + 1;                                         
 END SKRIV_TYP10;                                                       
 /*********************************************************************/
 /*      SKRIV SLUT-KOMMUNEINDIVID                                    */
 /*********************************************************************/
 SLUT_INDIVID:                                                          
         PROC;                                                          
         BH507 = ' ';                                                   
         SLUTD.CTRANS = 'SLUTD';                                        
         SLUTD.EANTAL = ANT_TR0;                                        
         WRITE FILE (BH507X0) FROM (BH507);                             
         ANT_TR0 = 0;                                                   
 END SLUT_INDIVID;                                                      
 /*********************************************************************/
 /*      LUKNING OG ÅBNING AF FILER TIL REGIONALE CENTRALER           */
 /*********************************************************************/
 LUK_ÅBN:                                                               
         PROC;                                                          
         CLOSE FILE (BH507X0);                                          
         OPEN FILE (BH507X0) TITLE ('BH507'!!BH860.CRCKO!!'0O');        
         GEM_RC = BH860.CRCKO;                                          
         INDEX = GEM_RC;                                                
         GEM_KOM = 0;                                                   
 END LUK_ÅBN;                                                           
 /*********************************************************************/
 /*      UDSKRIV KØRSELSKONTROLLISTE                                  */
 /*********************************************************************/
 KONTROLLISTE:                                                          
         PROC;                                                          
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
         DETLIN1.CCA   = ZZWS3;                                         
         DETLIN1.TEKST = 'ANTAL LÆSTE RECORDS PÅ B.H96011D';            
         DETLIN1.ANTAL = ANT_LÆS;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
         DO I = 1 TO 6;                                                 
            IF ANT_RC0(I) ^= 0                                          
            THEN                                                        
               DO;                                                      
                  DETLIN1.CCA = ZZWS3;                                  
                  DETLIN1.TEKST =                                       
                  'ANTAL SKREVNE TRANSAKTIONER PÅ B.H607' !! I !! '0D'; 
                  DETLIN1.ANTAL = ANT_RC0(I);                           
                  CALL ZZ20300(DETLIN1,'01');                           
               END;                                                     
         END;                                                           
         DETLIN1.CCA   = ZZWS3;                                         
         DETLIN1.TEKST = 'ANTAL SKREVNE TRANSAKTIONER I ALT ';          
         DETLIN1.ANTAL = ANT_SKR;                                       
         CALL ZZ20300(DETLIN1,'01');                                    
 END KONTROLLISTE;                                                      
 /*********************************************************************/
 /* HER UDSKRIVES SQL FEJL                                     RUTINE */
 /*********************************************************************/
 SQL_FEJL: PROC;                                                        
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED SQLCODE: ' !! SQLCA.SQLCODE);  
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
                                                                        
         CALL ZS67000(SQLCA);                                           
                                                                        
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END SQL_FEJL;                                                          
 /*********************************************************************/
 /* HER UDSKRIVES ØVRIGE FEJL                                  RUTINE */
 /*********************************************************************/
 PROGRAM_FEJL: PROC;                                                    
                                                                        
         PUT SKIP LIST ('*****************************************');   
         PUT SKIP LIST ('DER ABENDES MED PROGRAMFEJL');                 
         PUT SKIP LIST ('ABENDTXT:                ' !! ABENDTXT);       
         PUT SKIP LIST ('*****************************************');   
         CALL ZS30101(ABENDKODE,ABENDMEDD,ABENDRTKODE);                 
                                                                        
 END PROGRAM_FEJL;                                                      
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
         CALL ZZ20300(' ','99');                                        
         CLOSE FILE (BH507I),                                           
               FILE (BH507X0),                                          
               FILE (BH860IN);                                          
                                                                        
         CALL OPDATER_KØRSELSTABEL('SLUT',BH507_PARM.FS_MRK);           
 END AFSLUT;                                                            
 /* %INCLUDE BH54997M; */     /* ER FORGÆNGERE KØRT. STOP HVIS IKKE */  
 %INCLUDE BH54998M;     /* OPDATER KØRSELSTABEL GL VERS */              
 END BH50700;                                                           
