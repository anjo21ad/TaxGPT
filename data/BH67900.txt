 BH67900:       /* BEHANDL BEREGNINGSBÅND MED SAMBESKATTEDE EJENDOMME */
         PROC OPTIONS (MAIN) REORDER;                                   
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 1999');       
 /*********************************************************************/
 /*                                                                   */
 /*      FORMÅL       EN PERSONS SAMLEDE STATUS VEDR. SAMBESKATNING,  */
 /*                   ANTAL LINIER(FLIN) M.V. OVERFØRES TIL EFTERFØL- */
 /*                   GENDE INDIVIDER TIL PERSONEN.                   */
 /*                                                                   */
 /*      INDDATA      B.H67301D BEREGNINGSBÅND (KUN SAMBESKATTEDE)    */
 /*                                                                   */
 /*      UDDATA       B.H67900D EJENDOMSOPLYSNINGER FOR SAMBESKATTEDE */
 /*                                                                   */
 /*      PROGRAMMØR   AAGE RASMUSSEN              PFB      JAN 1998   */
 /*                                                                   */
 /*      RETTET       AAGE RASMUSSEN              PFB   AUGUST 1998   */
 /*        -          AAGE RASMUSSEN              PUS      JAN 2000   */
 /*********************************************************************/
 /*                                                                   */
 /*      HUSK AT 'ÅRSRULLE' ÅRSTAL I SCOVG199?                        */
 /*                                                                   */
 /*********************************************************************/
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*                                                                   */
 /*      FILE-DECLARE                                                 */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH673I          FILE RECORD INPUT;                             
 DCL     BH679O          FILE RECORD OUTPUT;                            
 DCL     SYSPRINT EXTERNAL FILE PRINT;                                  
 /*********************************************************************/
 /*                                                                   */
 /*      ENTRY-DECLARE                                                */
 /*                                                                   */
 /*********************************************************************/
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM);   /* SSI-DATO     */
 /*********************************************************************/
 /*                                                                   */
 /*      STRUCTUR-DECLARE  INPUT                                      */
 /*                                                                   */
 /*********************************************************************/
 DCL     BH601_AR CHAR (4000) VAR;                                      
 DCL     1 BH601 BASED(ADDR(BH601_AR)),                                 
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH60100N;                                           
 DCL     1 BH60199,                                                     
           %INCLUDE BH60199N;                                           
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*                                                                   */
 /*********************************************************************/
 DCL     TV_LÆS          FIXED     (9)       INIT      (0);             
 DCL     TV_KOM          FIXED     (9)       INIT      (0);             
 DCL     TV_SKRIV        FIXED     (9)       INIT      (0);             
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF BUILTIN                                           */
 /*                                                                   */
 /*********************************************************************/
 DCL     ADDR            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 /*********************************************************************/
 /*                                                                   */
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*                                                                   */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     EOF             PIC       '9'       INIT (0);                  
                                                                        
 DCL     1 GEM           STATIC,    /* GEM FOR SAMME PERSON           */
         2 DCPRCIR       FIXED (10),/* CPRNR.                         */
         2 FLIN          FIXED (5), /* IALT ANTAL 'FASTE' LINIER      */
         2 FEJD          FIXED (5), /* ANTAL EJEDE EJENDOMME          */
         2 CUDLEJNING    CHAR  (1), /* EJER MINDST EEN UDLEJNINGSEJD. */
         2 CRENERKL      PIC   '9', /* EJER KUN ERKLÆRBARE            */
         2 CLIN16        PIC   '9', /* SLUTLINIE TIL USA              */
         2 FANT_SAM      FIXED (3), /* ANTAL SAMBESKATNINGSNUMRE      */
         2 SAMTAB(25),                                                  
           3 SKOM        FIXED (3), /* SAMBESKATNINGSNUMMER FOR MODER-*/
           3 SEJD        FIXED (7), /* EJENDOMMEN(E)                  */
           3 SKOMPONEN   CHAR  (1), /* KOMPONENTKODE TIL SLS          */
           3 SCERKLÆR    PIC   '9', /* KODE ERKLÆRBAR EJENDOM         */
           3 SCLEJE      CHAR  (1), /* LEJEVÆRDIKODE TIL BEREGNING    */
           3 SCEJEV      CHAR  (1), /* KODE FOR EJ VURDERET           */
           3 SBENYT      CHAR  (2), /* BENYTTELSESKODE                */
           3 SFLEJL      FIXED (5), /* ANTAL LEJLIGHEDER              */
           3 SCOVG1998   FIXED (1), /* KODE FOR OVERGANGSORDNING 1998 */
           3 SGANE       FIXED (5), /* EJERANDEL                      */
           3 SFRADAG     FIXED (9), /* STARTDATO FOR BEREGNING        */
           3 STILDAG     FIXED (9), /* SLUTDATO  FOR BEREGNING        */
           3 ANTMOD      FIXED (3), /* ANTAL MODEJD MED DETTE NUMMER  */
           3 GKOM        FIXED (3), /* EKOMNR+EEJDNR FOR  FØRSTE  BK09*/
           3 GEJD        FIXED (7), /* TIL SAMBESKATN. MED OVENNÆVNTE */
           3 GKOMPONEN   CHAR  (1), /* KOMPONENTKODE TIL SLS          */
           3 GCERKLÆR    PIC   '9', /* KODE ERKLÆRBAR EJENDOM         */
           3 GCLEJE      CHAR  (1), /* LEJEVÆRDIKODE TIL BEREGNING    */
           3 GCEJEV      CHAR  (1), /* KODE FOR EJ VURDERET           */
           3 GEJDVÆR     FIXED (11),/* EJENDOMSVÆRDI                  */
           3 GLEJE       FIXED (11),/* LEJEV. NORMAL                  */
           3 GLEJEFP     FIXED (11),/* LEJEV. FØRTIDSPENS./EFTERL.    */
           3 GLEJE67     FIXED (11),/* LEJEVÆRDI PENSIONIST           */
           3 GOVLEJE     FIXED (11),/* LEJEV. NORMAL                  */
           3 GOVLEJEFP   FIXED (11),/* LEJEV. FØRTIDSPENS./EFTERL.    */
           3 GOVLEJE67   FIXED (11),/* LEJEVÆRDI PENSIONIST           */
           3 GGANE       FIXED (5), /* EJERANDEL                      */
           3 GFRADAG     FIXED (9), /* STARTDATO FOR BEREGNING        */
           3 GTILDAG     FIXED (9), /* SLUTDATO  FOR BEREGNING        */
           3 ANT09       FIXED (3); /* ANTAL BK 09 MED DETTE NUMMER   */
 /*********************************************************************/
 /*                                                                   */
 /*      HOUSE-KEEPING                                                */
 /*                                                                   */
 /*********************************************************************/
         ON ERROR CALL PLIDUMP('SNHOB');                                
         CALL ZS61900;                 /*   UDSKRIVNING AF SSI-DATO   */
         OPEN FILE (BH673I)  TITLE ('BH67301I');                        
         OPEN FILE (BH679O)  TITLE ('BH67900O');                        
         ON ENDFILE (BH673I)                                            
         BEGIN;                                                         
            EOF = 1;                                                    
         END;                                                           
         GEM = '';                                                      
         DO I = 1 TO 25;                                                
            GEM.SAMTAB(I) = '';                                         
         END;                                                           
 /*********************************************************************/
 /*                                                                   */
 /*      PROCES                                                       */
 /*                                                                   */
 /*********************************************************************/
                                                                        
         READ FILE (BH673I) INTO (BH601_AR);                            
                                                                        
         DO WHILE (EOF = 0);                                            
            TV_LÆS = TV_LÆS + 1;                                        
            IF BH601.SORT_KOMNR = 999 /* KOMMUNE-INDIVID        */      
            THEN                                                        
               TV_KOM = TV_KOM + 1;                                     
            ELSE                                                        
               DO;                                                      
                  CALL BEHANDL;                                         
                  CALL SKRIV_DATASÆT;                                   
               END;                                                     
                                                                        
            READ FILE (BH673I) INTO (BH601_AR);                         
         END;                                                           
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*    AFSLUT PROGRAM                                                 */
 /*                                                                   */
 /*********************************************************************/
                                                                        
         CALL DAN_OPT_INDV;                                             
                                                                        
         CALL SKRIV_TOTALER;                                            
                                                                        
         CLOSE FILE (BH673I),                                           
               FILE (BH679O);                                           
                                                                        
 /********************************************************************/ 
 /*                                                                  */ 
 /*      OVERFØR DEN SAMLEDE STATUS FOR PERSONEN VEDR. AFGØRELSE OM  */ 
 /*      ØNSKE OM SAMBESKATNING ER MULIGT AT UDFØRE SAMT DIVERSE OP- */ 
 /*      LYSNINGER TIL BRUG VED STYRING AF UDSKRIVNING AF EJENDOMS-  */ 
 /*      OPLYSNINGER PÅ HENHOLDSVIS PSA, USA, BSA ELLER EJENDOMSSKE- */ 
 /*      MA.                                                         */ 
 /*                                                                  */ 
 /********************************************************************/ 
 BEHANDL:                                                               
 PROC;                                                                  
                                                                        
         IF GEM.DCPRCIR ^= BH601.DCPRCIR                                
         THEN                                                           
            DO;                                                         
               /* NY PERSON SÅ TAG LIGE OG INITIER DIV. TÆLLEVÆRKER */  
               /* M.V. TIL STYRING AF SLUTLINIE-REDIGERING          */  
               GEM = '';                                                
               DO I = 1 TO 25;                                          
                  GEM.SAMTAB(I) = '';                                   
               END;                                                     
               GEM.DCPRCIR    = BH601.DCPRCIR;                          
               GEM.FLIN       = BH601.FLIN;                             
               GEM.FEJD       = BH601.FEJD;                             
               GEM.CUDLEJNING = BH601.CUDLEJNING;                       
               GEM.CRENERKL   = BH601.CRENERKL;                         
               GEM.CLIN16     = BH601.CLIN16;                           
               GEM.FANT_SAM   = BH601.FANT_SAM;                         
               DO I = 1 TO BH601.FANT_SAM;                              
                  GEM.SAMTAB(I) = BH601.SAMTAB(I), BY NAME;             
               END;                                                     
            END;                                                        
                                                                        
         BH601.FLIN       = GEM.FLIN;                                   
         BH601.FEJD       = GEM.FEJD;                                   
         BH601.CUDLEJNING = GEM.CUDLEJNING;                             
         BH601.CRENERKL   = GEM.CRENERKL;                               
         BH601.CLIN16     = GEM.CLIN16;                                 
                                                                        
         /* OVERFØR STATUS VEDR. SAMBESKATNING TIL AKTUELT INDIVID */   
                                                                        
         BH601.FANT_SAM = GEM.FANT_SAM;                                 
         DO I = 1 TO GEM.FANT_SAM;                                      
            BH601.SAMTAB(I) = GEM.SAMTAB(I), BY NAME;                   
         END;                                                           
                                                                        
 END BEHANDL;                                                           
                                                                        
 /*********************************************************************/
 /*                                                                   */
 /*    SKRIV DATASÆT                                                  */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_DATASÆT:                                                         
 PROC;                                                                  
                                                                        
         BH601.SORT_DATO = BH601.ESR.DOVTG;                             
         WRITE FILE (BH679O) FROM (BH601_AR);                           
         TV_SKRIV = TV_SKRIV + 1;                                       
                                                                        
 END SKRIV_DATASÆT;                                                     
 /*********************************************************************/
 /*      DER DANNES ET SLUTINDIVID TIL BRUG FOR MASKINEL AFSTEMNING   */
 /*********************************************************************/
 DAN_OPT_INDV:                                                          
 PROC;                                                                  
                                                                        
         BH60199          = '';                                         
         BH60199.SORT_KOMNR = 999;                                      
         BH60199.SORT_EJDNR = 8888888;                                  
         BH60199.SORT_CPR = 99999999999;                                
         BH60199.SORT_CITYP = 9;                                        
         BH60199.SORT_EÆKNR = 999;                                      
         BH60199.ANT_INDV = TV_SKRIV;                                   
         WRITE FILE (BH679O) FROM (BH60199);                            
                                                                        
 END DAN_OPT_INDV;                                                      
 /*********************************************************************/
 /*                                                                   */
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*                                                                   */
 /*********************************************************************/
 SKRIV_TOTALER:                                                         
 PROC;                                                                  
                                                                        
         PUT SKIP LIST ('LÆSTE INDIVIDER PÅ BH67301D: ',TV_LÆS);        
         PUT SKIP LIST ('FRASORTEREDE KOMMUNEHEADERE: ',TV_KOM);        
         PUT SKIP LIST ('SKREVNE INDIVIDER          : ',TV_SKRIV);      
                                                                        
 END SKRIV_TOTALER;                                                     
                                                                        
                                                                        
 END BH67900;                                                           
