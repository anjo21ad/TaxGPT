 /*********************************************************************/
 /*      DAN EVT. GRUNDLAG FOR PENSIONISTKODE-TRANS'ER TIL ESR        */
 /*********************************************************************/
 OPBYG_ESR_TRANS:                                                       
         PROC;                                                          
         /* DAN EVT. TRANS MED PENSIONISTKODE :                       */
         /* BH945.MT.CPENS = '1' --> HVIS PERSONEN ER PENSIONIST      */
         /* SAMT HVIS PERSONENS ALDER ER UNDER 60 ÅR ELLER OVER 66 ÅR */
         /* BH945.MT.CPENS = ' ' --> HVIS  I K K E  PENSIONIST        */
         /* E L L E R  HVIS PENSIONIST OG ALDER ER LIG 60 TIL 66 ÅR   */
         BH96011N.CPENS = '-';             /* INGEN TRANS             */
         IF BH945.MT.CPENS = BH945.CPENS_FESR !                         
           (BH945.MT.HIST_OPL(SLUTÅR).CFØREFT = '1' &                   
            BH945.CPENS_FESR = '2')                                     
         THEN;                             /* MÅSKE INGEN TRANS */      
         ELSE                                                           
            DO;                                                         
               IF BH945.MT.CPENS = '1'                                  
               THEN                                                     
                  BH96011N.CPENS = '1';    /* DAN TRANS */              
               ELSE                                                     
                  BH96011N.CPENS = ' ';    /* DAN TRANS */              
            END;                                                        
         IF BH945.MT.HIST_OPL(SLUTÅR).CFØREFT = '1' &                   
            BH945.CPENS_FESR ^= '2'                                     
         THEN                              /* DAN TRANS VEDR.      */   
            BH96011N.CPENS = '2';          /* FØRTIDSPENSION. M.V. */   
         IF BH945.TIL_EVS.DAFSTAA >= BH945.MT.DRGNÅRFRA !               
            BH945.ESR.EJER.DCPRCIR = 0                                  
         THEN                                                           
            DO;                                                         
               /* DER SKAL IKKE DANNES TRANS'ER HVIS        */          
               /* SALG I AKTUELT INDKOMST-/REGNSKABSÅR      */          
               /* ELLER EJENDOMMEN IKKE EJES JVF. ESR       */          
               BH96011N.CPENS = '-';                                    
            END;                                                        
         BH945.CPENS_TESR = BH96011N.CPENS;                             
         /* SKAL TRANS GRUNDLAG SKRIVES? */                             
         IF BH96011N.CPENS = '-'                                        
         THEN                                                           
            RETURN;                                                     
         BH96011N.CRCKO  = BH945.CRCKO;                                 
         BH96011N.EKOMNR = BH945.ESR.EKOMNR;                            
         BH96011N.EEJDNR = BH945.ESR.EEJDNR;                            
         BH96011N.CINDV = 2;                                            
         BH96011N.DCPRCIR = BH945.ESR.EJER.DCPRCIR;                     
         WRITE FILE (BH9601O) FROM (BH96011N);                          
         TV.PENS_TR = TV.PENS_TR + 1;                                   
 END OPBYG_ESR_TRANS;                                                   
