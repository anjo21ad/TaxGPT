 /********************************************************************/ 
 /*  MACRO BE24406M : KONVERTER OG INDSAET FELT                      */ 
 /*  BRUGES AF PROGRAMMERNE                                          */ 
 /*  -  BQ21400                                                      */ 
 /*  -  BQ21600                                                      */ 
 /*                Årsrullet                           ONE Maj  2021 */ 
 /*                BQ19 --> BQ20                                     */ 
 /*                Årsrullet                           ONE Nov  2021 */ 
 /*                BQ20 --> BQ21                                     */ 
 /*                Årsrullet                           ONE Nov  2022 */ 
 /*                BQ21 --> BQ22                                     */ 
 /*                Årsrullet                           JJJ Sep  2023 */ 
 /*                BQ22 --> BQ24                                     */ 
 /*                BQ24 --> BE24 NY EVS                HNK NOV  2023 */ 
 /********************************************************************/ 
 /* KONVERTER OG INDSAET FELT                                        */ 
 /********************************************************************/ 
                                                                        
 /********************************************************************/ 
                                                                        
 KONVERTER_OG_INDSAET_FELT: PROC(DATA,FELTNR,START_IDX,SLUT_IDX)        
                            RETURNS(BIT(1));                            
    DECLARE DATA      CHAR(127);                                        
    DECLARE FELTNR    BINARY FIXED(15);                                 
    DECLARE START_IDX BINARY FIXED(15);                                 
    DECLARE SLUT_IDX  BINARY FIXED(15);                                 
                                                                        
    DECLARE HJ_LGD    BINARY FIXED(15);                                 
    DECLARE FELTTYPE  BINARY FIXED(15);                                 
    DECLARE SVAR      BIT(1);                                           
    DECLARE CHARFELT  BIT(1);                                           
    DECLARE NEGATIVFELT   BIT(1);                                       
    DECLARE HJ_POS    FIXED BIN(15)      INIT(0);                       
                                                                        
    DECLARE CSC_FFLAG BIT(1)             INIT('0'B);                    
                                                                        
    CSC_FFLAG = '0'B;                                                   
    SVAR        = ('0'B);                                               
    CHARFELT    = ('0'B);                                               
    NEGATIVFELT = ('0'B);                                               
    HJ_POS      = 0;                                                    
    FELTTYPE = PLACERING_BASED(FELTNR).TYPE;                            
                                                                        
     SELECT;                                                            
       WHEN (FELTTYPE = CHAR_1_FELT !                                   
             FELTTYPE = CHAR_2_FELT !                                   
             FELTTYPE = CHAR_3_FELT !                                   
             FELTTYPE = CHAR_4_FELT !                                   
             FELTTYPE = CHAR_5_FELT !                                   
             FELTTYPE = CHAR_6_FELT !                                   
             FELTTYPE = CHAR_7_FELT !                                   
             FELTTYPE = CHAR_8_FELT !                                   
             FELTTYPE = CHAR_9_FELT !                                   
             FELTTYPE = CHAR_10_FELT !                                  
             FELTTYPE = CHAR_14_FELT !                                  
             FELTTYPE = CHAR_15_FELT !                                  
             FELTTYPE = CHAR_16_FELT !                                  
             FELTTYPE = CHAR_26_FELT !                                  
             FELTTYPE = CHAR_30_FELT !                                  
             FELTTYPE = CHAR_32_FELT !                                  
             FELTTYPE = CHAR_34_FELT !                                  
             FELTTYPE = CHAR_125_FELT)                                  
           DO;                                                          
             CHARFELT = TRUE;                                           
             /*DER SKAL KOMPENSERES FOR 2 ANFØRSELSTEGN I EN STRENG*/   
             IF ANT_ANF_TGN = 2 THEN                                    
                HJ_LGD = TYPE_CHAR_DEF(FELTTYPE).LGD + 2;               
             ELSE                                                       
             /*NOGEN GANGE ER DER IKKE ANFØRSELSTEGN SELVOM FELTET      
               ER DEFINERET SOM CHAR */                                 
                HJ_LGD = TYPE_CHAR_DEF(FELTTYPE).LGD;                   
       END;                                                             
       WHEN(FELTTYPE = FIXED_DEC_5_FELT_F  !                            
            FELTTYPE = FIXED_DEC_11_DEC_F  !                            
            FELTTYPE = FIXED_DEC_13_DEC_F  !                            
            FELTTYPE = FIXED_DEC_13_DEC_FELT)                           
         DO;                                                            
         IF SUBSTR(DATA,1,1) = '-'                                      
         THEN DO;     /*TO EKSTRA P.G.A. KOMMA OG NEGATIVTEGN */        
              HJ_LGD = (TYPE_CHAR_DEF(FELTTYPE).LGD * 2) - 1 + 2;       
              NEGATIVFELT  = '1'B;                                      
            END;                                                        
         ELSE      /*EN EKSTRA P.G.A. KOMMA*/                           
            HJ_LGD = (TYPE_CHAR_DEF(FELTTYPE).LGD * 2) - 1 + 1;         
                                                                        
         END;                                                           
       WHEN(FELTTYPE = FIXED_DEC_3_FELT  !                              
            FELTTYPE = FIXED_DEC_5_FELT  !                              
            FELTTYPE = FIXED_DEC_7_FELT  !                              
            FELTTYPE = FIXED_DEC_11_FELT )                              
         DO;                                                            
         IF SUBSTR(DATA,1,1) = '-'                                      
         THEN DO;      /*EN EKSTRA P.G.A. NEGATIVTEGN */                
             HJ_LGD = (TYPE_CHAR_DEF(FELTTYPE).LGD * 2) - 1 + 1;        
             NEGATIVFELT  = '1'B;                                       
            END;                                                        
         ELSE      /*EN EKSTRA P.G.A. KOMMA*/                           
            HJ_LGD = (TYPE_CHAR_DEF(FELTTYPE).LGD * 2) - 1;             
       END;                                                             
       OTHERWISE DO;                                                    
         HJ_LGD = TYPE_CHAR_DEF(FELTTYPE).LGD;                          
       END;                                                             
    END;                                                                
                                                                        
    IF DATA = '' THEN                                                   
       SVAR = FALSE;                                                    
    ELSE DO;                                                            
       SVAR = TJEK_GRAENSE(201,1013,FELTTYPE,START_IDX,                 
                           SLUT_IDX,0,HJ_LGD);                          
       IF SVAR & I = 692 THEN DO;                                       
          CSC_FFLAG = '1'B;                                             
          SVAR      = '0'B;                                             
       END;                                                             
    END;                                                                
                                                                        
    IF SVAR = TRUE THEN DO;                                             
      FANTAL_FEJL = FANTAL_FEJL + 1;                                    
      WRITE FILE (BE2469O) FROM (INPUTFIL_IND);               /*ÅR-RET*/
    END;                                                                
    ELSE DO;                                                            
   /*   HJ_POS = ADRESSE(FELTNR) + 2   Reguleres qaht. lgd. felt */     
        HJ_POS = ADRESSE(FELTNR);                                       
       SELECT;                                                          
         WHEN(CHARFELT = TRUE &  ANT_ANF_TGN = 2) DO;                   
                                            /* STAT BITTABEL OPSÆTTES */
              BITTAB(FELTNR) = FELTBIT;                                 
             SUBSTR(UDDATA,HJ_POS,                                      
                    TYPE_CHAR_DEF(FELTTYPE).LGD) =                      
                           SUBSTR(DATA,(START_IDX+1),(SLUT_IDX-1));     
         END;                                                           
         WHEN(CHARFELT = TRUE) DO;                                      
                                         /* STAT BITTABEL OPSÆTTES */   
              BITTAB(FELTNR) = FELTBIT;                                 
             SUBSTR(UDDATA,HJ_POS,                                      
                    TYPE_CHAR_DEF(FELTTYPE).LGD) =                      
                           SUBSTR(DATA,(START_IDX),(SLUT_IDX));         
         END;                                                           
         WHEN(FELTTYPE = FIXED_DEC_3_FELT) DO;                          
              BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */     
             KONVERT_FELT_FD_3 = SUBSTR(DATA,START_IDX,SLUT_IDX);       
             HJ_FD_3 = KONVERT_FELT_FD_3;                               
             IF NEGATIVFELT                                             
             THEN                                                       
               HJ_FD_3  = HJ_FD_3 * (-1);                               
               SUBSTR(UDDATA,HJ_POS,                                    
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_2;           
             END;                                                       
         WHEN(FELTTYPE = FIXED_DEC_5_FELT) DO;                          
              BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */     
             KONVERT_FELT_FD_5 = SUBSTR(DATA,START_IDX,SLUT_IDX);       
             HJ_FD_5 = KONVERT_FELT_FD_5;                               
             IF NEGATIVFELT                                             
             THEN                                                       
               HJ_FD_5  = HJ_FD_5 * (-1);                               
               SUBSTR(UDDATA,HJ_POS,                                    
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_3;           
             END;                                                       
          WHEN(FELTTYPE = FIXED_DEC_5_FELT_F) DO;                       
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_5_DEC = SUBSTR(DATA,START_IDX,SLUT_IDX);  
             /* HJ_FD_5  = KONVERT_FELT_FD_5_DEC;*/                     
              HJ_FD_5_F = KONVERT_FELT_FD_5_DEC;                        
              IF NEGATIVFELT                                            
              THEN                                                      
                HJ_FD_5_F  = HJ_FD_5_F * (-1);                          
              SUBSTR(UDDATA,HJ_POS,                                     
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_3_F;         
             END;                                                       
          WHEN(FELTTYPE = FIXED_DEC_7_FELT) DO;                         
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_7 = SUBSTR(DATA,START_IDX,SLUT_IDX);      
              HJ_FD_7 = KONVERT_FELT_FD_7;                              
              IF NEGATIVFELT                                            
              THEN                                                      
                HJ_FD_7  = HJ_FD_7 * (-1);                              
                SUBSTR(UDDATA,HJ_POS,                                   
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_4;           
             END;                                                       
          WHEN(FELTTYPE = FIXED_DEC_11_FELT) DO;                        
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_11 = SUBSTR(DATA,START_IDX,SLUT_IDX);     
              HJ_FD_11 = KONVERT_FELT_FD_11;                            
              IF NEGATIVFELT                                            
              THEN                                                      
                HJ_FD_11 = HJ_FD_11 * (-1);                             
                SUBSTR(UDDATA,HJ_POS,                                   
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_6;           
             END;                                                       
          WHEN(FELTTYPE = FIXED_DEC_11_DEC_F) DO;                       
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_11_DEC = SUBSTR(DATA,START_IDX,SLUT_IDX); 
              HJ_FD_11 = KONVERT_FELT_FD_11_DEC;                        
              IF NEGATIVFELT                                            
              THEN                                                      
                 HJ_FD_11 = HJ_FD_11 * (-1);                            
               SUBSTR(UDDATA,HJ_POS,                                    
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_6;           
              END;                                                      
          WHEN(FELTTYPE = FIXED_DEC_13_DEC_F) DO;                       
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_13_DEC = SUBSTR(DATA,START_IDX,SLUT_IDX); 
              IF CONVERT_SW THEN DO;                                    
                 KONVERT_FELT_FD_13_DEC = 0;                            
                 CONVERT_SW             = NEJ;                          
              END;                                                      
              HJ_FD_13 = KONVERT_FELT_FD_13_DEC;                        
              IF NEGATIVFELT                                            
              THEN                                                      
                HJ_FD_13  = HJ_FD_13 * (-1);                            
                SUBSTR(UDDATA,HJ_POS,                                   
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_7;           
          END;                                                          
          WHEN(FELTTYPE = FIXED_DEC_13_DEC_FELT) DO;                    
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_FD_13_DEC = SUBSTR(DATA,START_IDX,SLUT_IDX); 
              IF CONVERT_SW THEN DO;                                    
                 KONVERT_FELT_FD_13_DEC = 0;                            
                 CONVERT_SW             = NEJ;                          
              END;                                                      
              HJ_FD_13 = KONVERT_FELT_FD_13_DEC;                        
              IF NEGATIVFELT                                            
              THEN                                                      
                HJ_FD_13  = HJ_FD_13 * (-1);                            
                SUBSTR(UDDATA,HJ_POS,                                   
                    TYPE_CHAR_DEF(FELTTYPE).LGD) = HJ_CHAR_7;           
              END;                                                      
          WHEN(FELTTYPE = PIC_999_FELT)   DO; /*1*/                     
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              IF CSC_FFLAG THEN DO; /*2A*/                              
                 CSC_F_ANT = CSC_F_ANT + 1;                             
                 KONVERT_FELT_PIC_999 = 000;                            
              END; /*2A*/                                               
              ELSE DO; /*2B*/                                           
                 IF I = 692 THEN DO; /*3A*/                             
                    IF VERIFY(SUBSTR(DATA,START_IDX,SLUT_IDX),          
                                     '0123456789') = 0                  
                    THEN DO; /*4A*/                                     
                       KONVERT_FELT_PIC_999 =                           
                                        SUBSTR(DATA,START_IDX,SLUT_IDX);
                    END;                                                
                    ELSE DO; /*4B*/                                     
                       CSC_F_ANT = CSC_F_ANT + 1;                       
                       KONVERT_FELT_PIC_999 = 000;                      
                                       /*ÅR-RET*/                       
                       PUT SKIP LIST(' BE24*00: DATA IKKE NUMERISKE');  
                       PUT SKIP LIST('       PNR        :' !!           
                                                           INDIVID.PNR);
                       PUT SKIP LIST('       0132_REC_NR:' !!           
                                                           FANTAL_LAES);
                       PUT SKIP LIST('       INP_FELT_NR:' !! I);       
                       PUT SKIP LIST('          FELTTYPE:' !! FELTTYPE);
                       PUT SKIP LIST('         START_IDX:' !!           
                                                             START_IDX);
                       PUT SKIP LIST('          SLUT_IDX:' !! SLUT_IDX);
                       PUT SKIP LIST('            LÆNGDE:' !!           
                                                (SLUT_IDX-START_IDX+1));
                       PUT SKIP LIST('              DATA:' !!           
                                               SUBSTR(OUTPUT_TXT,1,30));
                       PUT SKIP LIST('         INPUT_POS:' !! IND_POS); 
                       PUT SKIP LIST('TYPE_CHAR_DEF(FELTTYPE).LGD ' !!  
                                           TYPE_CHAR_DEF(FELTTYPE).LGD);
                       PUT SKIP LIST('TYPE_CHAR_DEF(FELTTYPE).FELTTXT ' 
                                     !!TYPE_CHAR_DEF(FELTTYPE).FELTTXT);
                    END; /*4B*/                                         
                 END; /*3A I = 692*/                                    
                 ELSE DO; /*3B QEB INDSAT 10-06-2017 I <> 692*/         
                       KONVERT_FELT_PIC_999 =                           
                                        SUBSTR(DATA,START_IDX,SLUT_IDX);
                 END; /*3B I <> 692*/                                   
              END; /*2B*/                                               
              SUBSTR(UDDATA,HJ_POS,TYPE_CHAR_DEF(FELTTYPE).LGD) =       
                                                   KONVERT_FELT_PIC_999;
          END; /*1*/                                                    
          WHEN(FELTTYPE = PIC_9999_FELT)  DO;                           
                BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */   
               KONVERT_FELT_PIC_9999 = SUBSTR(DATA,START_IDX,SLUT_IDX); 
               SUBSTR(UDDATA,HJ_POS,                                    
               TYPE_CHAR_DEF(FELTTYPE).LGD) = KONVERT_FELT_PIC_9999;    
          END;                                                          
          WHEN(FELTTYPE = PIC_10_9_FELT)  DO;                           
               BITTAB(FELTNR) = FELTBIT;/* STAT BITTABEL OPSÆTTES */    
              KONVERT_FELT_PIC_10_9 = SUBSTR(DATA,START_IDX,SLUT_IDX);  
              SUBSTR(UDDATA,HJ_POS,                                     
              TYPE_CHAR_DEF(FELTTYPE).LGD) = KONVERT_FELT_PIC_10_9;     
          END;                                                          
          OTHERWISE DO;                                                 
            PUT SKIP LIST('FELTNR: ' !! FELTNR !!' HAR FORKERT VÆRDI'); 
           END;                                                         
       END;                                                             
   END;                                                                 
                                                                        
   IF TFLAG THEN DO;                                                    
      PUT SKIP LIST('  I='!!I!!'  ADRESSE(I)='!!ADRESSE(I)!!            
                    '  HJ_POS='!!HJ_POS);                               
      PUT SKIP LIST('  START_IDX='!!START_IDX!!                         
                    '  SLUT_IDX='!!SLUT_IDX!!                           
                    '  FELTTYPE='!!FELTTYPE!!                           
                    '  TYPE_CHAR_DEF.LGD='!!                            
                    TYPE_CHAR_DEF(FELTTYPE).LGD);                       
      PUT SKIP LIST('DATA  ='!!SUBSTR(DATA,1,20));                      
      PUT SKIP LIST('UDDATA='!!SUBSTR(UDDATA,304,50));                  
   END;                                                                 
                                                                        
   RETURN(SVAR);                                                        
                                                                        
 END KONVERTER_OG_INDSAET_FELT;                                         
