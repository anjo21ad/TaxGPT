 PROCESS OPT(TIME);                                                     
 BH61300: /* DAN UDTRÆK TIL SLS-U & SLS-P FRA EVS SLUT */               
         PROC OPTIONS (MAIN) REORDER;                                   
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2006');       
 /*********************************************************************/
 /*      NAVN         UDTRÆK TIL SLS-U & SLS-P                        */
 /*                                                                  */ 
 /*      INDDATA      B.H96000D BEREGNINGSBÅND                        */
 /*                                                                   */
 /*      UDDATA       D0314314.A206 EJD.OPLYSNINGER TIL SLS-U & -P    */
 /*                   BH61301Y KØRSELSKONTROLLISTE                    */
 /*                                                                   */
 /*      PROGRAMMØR   AKSEL HANSEN                    AC   DEC 1977   */
 /*                                                                   */
 /*      RETTET       AAGE RASMUSSEN              PFB      JAN 1999   */
 /*      RETTET       AAGE RASMUSSEN              PUS      JAN 2000   */
 /*      (OVERTAGELSESDATO BLEV  K U N  SENDT TIL SLS FOR SÆLGERE)    */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      JAN 2001   */
 /*      LINIER (INDIVID 2214) SKAL IKKE DANNES LÆNGERE, DA DISSE FRA */
 /*      OG MED EVS SLUT 2000 DANNES AF BQ0X400+BQ0X410.              */
 /*                                                                   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      NOV 2001   */
 /*      OBS.: HVAD MED FORSKUDT REGNSKABSÅR HIST OG HER ??????????   */
 /*            I.F.M. ÅR0101 OG ÅR1231 SAMT FDAGE        ??????????   */
 /*                                                                   */
 /*      TILRETTET TIL EVS 2001 ESBEN BRODERSEN   LSU      JAN 2002   */
 /*                                                                   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      DEC 2002   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      DEC 2003   */
 /*      ÅRSTILRETTET AAGE RASMUSSEN              LSU      NOV 2004   */
 /*                                                                   */
 /*  SLUT05   SUSANNE KROGH VILLUMSEN                    11/11-2005   */
 /*           TILRETTET SLUT 2005                                     */
 /*                                                                   */
 /*  SLUT06   SUSANNE KROGH VILLUMSEN                    04/10-2006   */
 /*           TILRETTET SLUT 2006                                     */
 /*                                                                   */
 /*  SLUT07   INGRID FISCHER IFI                         14/11-2007   */
 /*           TILRETTET SLUT 2007 (KUN ÅRSTAL ÆNDRET)                 */
 /*                                                                   */
 /*  SLUT08   BO SCHMIDT                                 15/11-2008   */
 /*           TILRETTET SLUT 2008                                     */
 /*                                                                   */
 /*  SLUT09   GERD HOLM-PEDERSEN                         09/10-2009   */
 /*           TILRETTET SLUT 2009                                     */
 /*                                                                   */
 /*  SLUT10   GERD HOLM-PEDERSEN                         05/10-2010   */
 /*           TILRETTET SLUT 2010                                     */
 /*  SLUT10   GERD HOLM-PEDERSEN                         25/03-2011   */
 /*           TILFØJET TÆLLER FOR AT SE HVOR MANGE FOREKOMSTER, DER   */
 /*           ER MED BERHVAN > BFSKAT, SOM IKKE ER MULIGT, DA         */
 /*           BFSKAT  = TOTALE EJENDOMSSKAT OG                        */
 /*           BERHVAN = DEN ERHVERSMÆSSIGE ANDEL AF TOTAL             */
 /*                                                                   */
 /*  SLUT11   GERD HOLM-PEDERSEN                         18/10-2011   */
 /*           TILRETTET SLUT 2011. DVS. 2010 -> 2011                  */
 /*                                                                   */
 /*  SLUT12   LARS KAAE HARRITSHØJ                       SEP 2012     */
 /*           TILRETTET SLUT 2012. DVS. 2011 -> 2012                  */
 /*                                                                   */
 /*  SLUT13   JØRGEN SAUGMANN                            OKT 2012     */
 /*           TILRETTET SLUT 2013. DVS. 2012 -> 2013                  */
 /*                                                                   */
 /*  SLUT14   JØRGEN SAUGMANN                            OKT 2014     */
 /*           TILRETTET SLUT 2014. DVS. 2013 -> 2014                  */
 /*                                                                   */
 /*  SLUT15   Z6JNK                                      OKT 2015     */
 /*           Årsrullet: 2014 -> 2015.  (Søg efter   *RUL*)           */
 /*                                                                   */
 /*  SLUT16   Lene Maj Jensen QLE                        JUN 2016     */
 /*           Årsrullet: 2015 -> 2016.                                */
 /*           Årstal i lister er udskiftet til årstalsmacro,          */
 /*           så der ikke behøves årsrulning                          */
 /*                                                                   */
 /********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /*********************************************************************/
 /*      FILE-DECLARE                                                 */
 /*********************************************************************/
 DCL     BH960I          FILE RECORD INPUT;                             
 DCL     BH6130O         FILE RECORD OUTPUT;                            
 /*********************************************************************/
 /*      ENTRY-DECLARE                                                */
 /*********************************************************************/
 DCL     ZS61900         ENTRY OPTIONS (INTER ASM);   /* SSI-DATO     */
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS34900         ENTRY OPTIONS (INTER ASM);   /* FAKTURERING  */
 /*********************************************************************/
 /*      STRUCTUR-DECLARE  INPUT                                      */
 /*********************************************************************/
 DCL     BH960_AR CHAR (6000) VAR;                                      
 DCL     1 BH960 BASED(ADDR(BH960_AR)),                                 
           2 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH90000N;;                                          
 DCL     BH960_ABELIG    CHAR      (31) BASED(ADDR(BH960.ABELMAT));     
 /*********************************************************************/
 /*      STRUCTURE-DECLARE  OUTPUT                                    */
 /*********************************************************************/
 DCL     1 BH61310N ALIGNED,                                            
         %INCLUDE BH61310N;                                             
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL     1 LIN1,                                                        
          2 CTL          CHAR      (1),                                 
          2 TX1          CHAR      (47)      INIT                       
                         ('PROGRAM: BH61300L'),                         
          2 TX2          CHAR      (59)      INIT                       
                         ('UDTRÆK FRA EVS SLUT TIL SLS PÅ CSC'),        
          2 TX3          CHAR      (14)      INIT                       
                         ('UDSKREVET DEN '),                            
          2 DATO         CHAR      (10),                                
          2 REST         CHAR      (02)      INIT      (' ');           
 DCL     1 LIN2,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (54)      INIT      ('  '),          
          2 TX1          CHAR      (20)      INIT                       
                         ('KØRSELSKONTROLLISTE'),                       
          2 REST         CHAR      (58)      INIT      (' ');           
 DCL     1 LIN3,                                                        
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (2)       INIT      (' '),           
          2 TEKST        CHAR      (52),                                
          2 ANT          PIC       'ZZZ.ZZZ.ZZZ.ZZZ.ZZZ',               
          2 REST         CHAR      (59)      INIT      (' ');           
 DCL     1 LIN_BLANK,                                                   
          2 CTL          CHAR      (1),                                 
          2 F1           CHAR      (132)     INIT      (' ');           
 DCL     LIN_DEB         CHAR      (133);                               
         %INCLUDE ZZ20301M;                                             
 /*********************************************************************/
 /*      DECLARE AF TÆLLEVÆRKER                                       */
 /*********************************************************************/
 DCL     TV_LÆS              FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_CPR          FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_SENR         FIXED     (9)       INIT      (0);         
 DCL     TV_LÆS_ØVR          FIXED     (9)       INIT      (0);         
 DCL     TV_KOM              FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_CPR         FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_SENR        FIXED     (9)       INIT      (0);         
 DCL     TV_FRAS_ØVR         FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_CPR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_SENR     FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_KØB_ØVR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_CPR      FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_SENR     FIXED     (9)       INIT      (0);         
 DCL     TV_FRA_OFF_ØVR      FIXED     (9)       INIT      (0);         
 DCL     TV_SKRIV            FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_CPR        FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_SENR       FIXED     (15)      INIT      (0);         
 DCL     TV_SKRIV_ØVR        FIXED     (15)      INIT      (0);         
 DCL     TV_UDLAND           FIXED     (9)       INIT      (0);         
 DCL     TV_EJ_FRA_ESR       FIXED     (9)       INIT      (0);         
 DCL     TV_KØB              FIXED     (9)       INIT      (0);         
 DCL     TV_SALG             FIXED     (9)       INIT      (0);         
 DCL     TV_BERHVAN_FEJL     FIXED     (15)      INIT      (0);         
 /*********************************************************************/
 /*      DECLARE AF BUILTIN                                           */
 /*********************************************************************/
 DCL     LOW             BUILTIN;                                       
 DCL     HIGH            BUILTIN;                                       
 DCL     SUBSTR          BUILTIN;                                       
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 /*********************************************************************/
 /*      DECLARE AF DIVERSE FELTER                                    */
 /*********************************************************************/
 DCL     I               BIN FIXED (15);                                
                                                                        
 DCL     UDDATO          CHAR      (10);                                
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2                              BASED(ADDR(DATO1)),       
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 DCL     PDATO           PIC       '(8)9';                              
 DCL     1 PDATO1                             BASED(ADDR(PDATO)),       
           2 ÅÅ          CHAR      (04),                                
           2 MM          CHAR      (02),                                
           2 DD          CHAR      (02);                                
                                                                        
 DCL     VDATO           PIC       '(8)9';                              
 DCL     1 VDATO1                             BASED(ADDR(VDATO)),       
           2 DD          CHAR      (02),                                
           2 MM          CHAR      (02),                                
           2 ÅÅ          CHAR      (04);                                
                                                                        
 DCL     HJ_DOVTG        PIC       '(8)9';                              
 DCL     HJ_DAFSTÅ       PIC       '(8)9';                              
 DCL     HJ_DSLUTS       PIC       '(8)9';                              
                                                                        
 DCL     1 BH65300M,                                                    
         % INCLUDE BH65300M;                                            
 DCL     EOF             PIC       '9'       INIT (0);                  
 /*********************************************************************/
 /*      HOUSE-KEEPING                                                */
 /*********************************************************************/
         ON ERROR CALL PLIDUMP('SNHOB');                                
         CALL ZS61900;                 /*   UDSKRIVNING AF SSI-DATO   */
         LIN_BLANK.CTL = ZZIK1;                                         
         LIN1.CTL = ZZWS2;                                              
         LIN2.CTL = ZZWS3;                                              
         LIN3.CTL = ZZWS2;                                              
         OPEN FILE (BH960I)  TITLE ('BH96000I');                        
         OPEN FILE (BH6130O) TITLE ('BH61300O');                        
         CALL ZZ20390 ('*OPEN','BH61301Y');                             
         ON ENDFILE (BH960I)                                            
         BEGIN;                                                         
            EOF = 1;                                                    
         END;                                                           
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !! '-' !! DATO2.ÅR;       
         LIN1.DATO = UDDATO;                                            
         BH61310N  = '';                                                
 /*********************************************************************/
 /*      PROCES                                                       */
 /*********************************************************************/
         READ FILE (BH960I) INTO (BH960_AR);                            
         DO WHILE (EOF = 0);                                            
            TV_LÆS = TV_LÆS + 1;                                        
            IF BH960.SORT_CPR >= 9999999999         /* OPTÆLL.INDV. */  
            THEN                                                        
               TV_KOM = TV_KOM + 1;                                     
            ELSE                                                        
               CALL DAN_SLS_UDTRÆK;                                     
            READ FILE (BH960I) INTO (BH960_AR);                         
         END;                                                           
 /*********************************************************************/
 /*    AFSLUT PROGRAM                                                 */
 /*********************************************************************/
         CALL FAKTURER_TS;                                              
         CALL SLUT;                                                     
         CALL ZZ20300(LIN_BLANK,'99');                                  
         CLOSE FILE (BH960I),                                           
               FILE (BH6130O);                                          
 /*********************************************************************/
 /*    UNDERSØG OM DER SKAL DANNES UDTRÆK                             */
 /*********************************************************************/
 DAN_SLS_UDTRÆK: PROC;                                                  
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
         THEN                                                           
            TV_LÆS_CPR = TV_LÆS_CPR + 1;                                
         ELSE                                                           
         IF BH960.SORT_CPR > 999999                                     
         THEN                                                           
            TV_LÆS_SENR = TV_LÆS_SENR + 1;                              
         ELSE                                                           
            TV_LÆS_ØVR = TV_LÆS_ØVR + 1;                                
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
          & BH960.TIL_EVS.DOVTG >            /* KØB EFTER 31.12.XXXX */ 
            BH960.MT.DRGNÅRTIL                                          
         THEN                                                           
            DO;                                                         
               TV_FRA_KØB_CPR = TV_FRA_KØB_CPR + 1;                     
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.SORT_CPR < 100000000                                  
          & BH960.AKT_EJER.DOVTG >          /* KØB EFTER 31.12.XXXX */  
            BH960.MT.DRGNÅRTIL                                          
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRA_KØB_SENR = TV_FRA_KØB_SENR + 1;                
               ELSE                                                     
                  TV_FRA_KØB_ØVR = TV_FRA_KØB_ØVR + 1;                  
               RETURN;                                                  
            END;                                                        
                                                                        
         IF (BH960.AKT_EJER.CEJFORH = '50'  !  /* HVIS  EJEREN ER   */  
             BH960.AKT_EJER.CEJFORH = '60'  !  /* OFFENTLIG D.V.S.  */  
             BH960.AKT_EJER.CEJFORH = '70'  !  /* KOMM./AMT/STAT    */  
             BH960.AKT_EJER.CEJFORH = '80') &                           
             BH960.CINVPNR > '0'               /* IKKE VALIDT CPRNR */  
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRA_OFF_CPR = TV_FRA_OFF_CPR + 1;                  
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRA_OFF_SENR = TV_FRA_OFF_SENR + 1;                
               ELSE                                                     
                  TV_FRA_OFF_ØVR = TV_FRA_OFF_ØVR + 1;                  
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.CINVPNR = '0' !              /* HVIS VALIDT CPRNR */  
            BH960.CINVPNR = '2'                /* HVIS SE-NR        */  
         THEN;                                                          
         ELSE                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         IF BH960.FEJLNR = 5060                                         
         THEN                                                           
            DO;                                                         
               IF BH960.SORT_CPR > 99999999                             
               THEN                                                     
                  TV_FRAS_CPR = TV_FRAS_CPR + 1;                        
               ELSE                                                     
               IF BH960.SORT_CPR > 999999                               
               THEN                                                     
                  TV_FRAS_SENR = TV_FRAS_SENR + 1;                      
               ELSE                                                     
                  TV_FRAS_ØVR = TV_FRAS_ØVR + 1;                        
               RETURN;                                                  
            END;                                                        
                                                                        
         BH61310N = '';                                                 
                                                                        
         IF BH960.FEJLNR = 5001 !                                       
            BH960.CINVPNR = '2'                /* HVIS SE-NR        */  
         THEN                                                           
            DO;                                                         
               /* DETTE ER EJENDOMME SOM IKKE ER OMFATTET */            
               /* AF EVS-REGLERNE, MEN DE SKAL OVERSENDES */            
               /* TIL SLS PÅ CSC                          */            
               BH960.CSKATBER = '0';                                    
            END;                                                        
                                                                        
         IF BH960.SORT_KOMNR = 999 !                                    
           (BH960.AKT_EJER.DCPRCIR = 0 &                                
            BH960.EVS_UDTRÆK.EVS_FORSK_EJER.DCPRN > 0 &                 
            BH960.EVS_UDTRÆK.EVS_FORSK_EJER.CEJBEVS ^= '9')             
         THEN                                                           
           DO;                                                          
              CALL BEHANDL_EJ_FRA_ESR;                                  
              RETURN;                                                   
           END;                                                         
                                                                        
         /* BEHANDL EJENDOMME FRA ESR-UDTRÆK */                         
                                                                        
         /* KØB FØR 0101XXXX */                                         
         /* E L L E R        */                                         
         /* KØB I INDK. ÅR   */                                         
         /* OG MÅSKE SOLGT   */                                         
         CALL OPSÆT_KØB_SALG;                                           
         CALL OVF_EJSÆLGER_OPL;                                         
         CALL OVF_GENEREL_OPL;                                          
         CALL SKRIV_SLS_UDTRÆK;                                         
                                                                        
 END DAN_SLS_UDTRÆK;                                                    
 /*********************************************************************/
 /*    DAN SLS-UDTRÆK P.G.A. UDENLANDSKE EJENDOMME SAMT EJENDOMME     */
 /*    OPRETTET VIA EVS-UDTRÆK FRA FORSKUD SOM IKKE FANDTES PÅ UDTRÆK */
 /*    FRA ESR.                                                       */
 /*********************************************************************/
 BEHANDL_EJ_FRA_ESR: PROC;                                              
                                                                        
         BH61310N.DOVTG = BH960.TIL_EVS.DOVTG;                          
                                                                        
         IF BH960.TIL_EVS.DOVTG >= ÅR0101 &                             
            BH960.TIL_EVS.DOVTG <= ÅR1231                               
         THEN                                                           
            CKØB = 'K';  /* KØB I INDK. ÅR */                           
                                                                        
         IF BH960.TIL_EVS.DAFSTAA > 0 &                                 
            BH960.TIL_EVS.DAFSTAA <= ÅR1231                             
         THEN    /* SÆLGER    */                                        
            DO;                                                         
               BH61310N.DAFSTÅ =                                        
               BH960.TIL_EVS.DAFSTAA;                                   
               BH61310N.CSALG  = 'S';                                   
            END;                                                        
                                                                        
         IF BH61310N.CKØB = 'K'                                         
         THEN                                                           
            TV_KØB = TV_KØB + 1;                                        
                                                                        
         IF BH61310N.CSALG = 'S'                                        
         THEN                                                           
            TV_SALG = TV_SALG + 1;                                      
                                                                        
         IF BH960.EVS_UDTRÆK.EVS_FORSK_EJER.CEJBEVS = '0' !             
            BH960.EVS_UDTRÆK.EVS_FORSK_EJER.CEJBEVS = '4'               
         THEN                                                           
            BH61310N.CEJ_100 = '1';                                     
         ELSE                                                           
            BH61310N.CEJ_100 = '0';                                     
                                                                        
         IF BH960.EVS_UDTRÆK.EVS_FORSK_EJER.GPCTEJER < 10000            
         THEN                                                           
            BH61310N.GANE = BH960.EVS_UDTRÆK.EVS_FORSK_EJER.GPCTEJER;   
                                                                        
         BH61310N.CFORDEL = '0';                                        
         TV_EJ_FRA_ESR = TV_EJ_FRA_ESR + 1;                             
         CALL SKRIV_SLS_UDTRÆK;                                         
                                                                        
 END BEHANDL_EJ_FRA_ESR;                                                
 /*********************************************************************/
 /*    SKRIV SLS-UDTRÆK                                               */
 /*********************************************************************/
 SKRIV_SLS_UDTRÆK: PROC;                                                
                                                                        
         IF BH960.SORT_CPR > 99999999                                   
         THEN                                                           
            TV_SKRIV_CPR = TV_SKRIV_CPR + 1;                            
         ELSE                                                           
         IF BH960.SORT_CPR > 999999                                     
         THEN                                                           
            TV_SKRIV_SENR = TV_SKRIV_SENR + 1;                          
         ELSE                                                           
            TV_SKRIV_ØVR = TV_SKRIV_ØVR + 1;                            
                                                                        
         /* OPSÆT FÆLLES FOR ALLE */                                    
         BH61310N.INDVNR    = 2212;                                     
         BH61310N.FELTLG    = 130;                                      
         BH61310N.INDVLG    = 130;                                      
         BH61310N.DCPRCIR   = BH960.SORT_CPR;                           
         BH61310N.EKOMNR    = BH960.SORT_KOMNR;                         
         BH61310N.EEJDNR    = BH960.SORT_EJDNR;                         
         BH61310N.UDSKRLBNR = BH960.UDSKRLBNR;                          
         BH61310N.CSKATBER  = BH960.CSKATBER;                           
         BH61310N.CRETTELSE = '0';                                      
                                                                        
         IF BH960.TIL_EVS.CFRED = '1'                                   
         THEN                                                           
            BH61310N.CFRED = BH960.TIL_EVS.CFRED;                       
                                                                        
         WRITE FILE (BH6130O) FROM (BH61310N);                          
         TV_SKRIV = TV_SKRIV + 1;                                       
         IF BH960.SORT_KOMNR = 999                                      
         THEN                                                           
            TV_UDLAND = TV_UDLAND + 1;                                  
                                                                        
         IF BH61310N.BERHVAN > BH61310N.BFSKAT                          
         THEN                                                           
            DO;                                                         
              PUT SKIP LIST ('TÆL CPRNR  : ',BH960.SORT_CPR);           
              PUT SKIP LIST ('    KOMNR  : ',BH960.SORT_KOMNR);         
              PUT SKIP LIST ('    EJDNR  : ',BH960.SORT_EJDNR);         
              PUT SKIP LIST ('    BFSKAT : ',BH61310N.BFSKAT);          
              PUT SKIP LIST ('    BERHVAN: ',BH61310N.BERHVAN);         
              PUT SKIP LIST ('           ');                            
              TV_BERHVAN_FEJL = TV_BERHVAN_FEJL + 1;                    
            END;                                                        
                                                                        
 END SKRIV_SLS_UDTRÆK;                                                  
 /*********************************************************************/
 /*    PROCEDUREN OVERFØRER KØBS- OG SALGSDATO OG OPSÆTTER KODER FOR  */
 /*    KØB OG SALG I HENHOLD TIL SKEMA OVER SKÆRINGSTILFÆLDE          */
 /*    FOR SÆLGERE OVERFØRES ENDVIDERE SLUTSEDDELDATO                 */
 /*    OBS.: HER BEHANDLES KUN EJERE FRA ESR                          */
 /*********************************************************************/
 OPSÆT_KØB_SALG: PROC;                                                  
         /* BEMÆRK AT DIVERSE      */                                   
         /* DATOER VENDES IKKE MERE*/                                   
         BH61310N.DOVTG = BH960.AKT_EJER.DOVTG;                         
         IF BH960.AKT_EJER.DOVTG >= ÅR0101 &   /* KØB I INDK. ÅR  */    
            BH960.AKT_EJER.DOVTG <= ÅR1231                              
         THEN                                                           
            CKØB = 'K';                                                 
         IF BH960.AKT_EJER.DAFSTÅ > 0 &        /* SÆLGER          */    
            BH960.AKT_EJER.DAFSTÅ <= ÅR1231                             
         THEN                                                           
            DO;                                                         
               BH61310N.DAFSTÅ = BH960.AKT_EJER.DAFSTÅ;                 
               BH61310N.DSLUTS = BH960.AKT_EJER.DSLUTS;                 
               BH61310N.CSALG  = 'S';                                   
            END;                                                        
         IF BH61310N.CKØB = 'K'                                         
         THEN                                                           
            TV_KØB = TV_KØB + 1;                                        
         IF BH61310N.CSALG = 'S'                                        
         THEN                                                           
            TV_SALG = TV_SALG + 1;                                      
 END OPSÆT_KØB_SALG;                                                    
 /********************************************************************/ 
 /*      PROCEDUREN OVERFØRER DE OPLYSNINGER TIL SLS, SOM SKAL       */ 
 /*      OVERFØRES FOR BÅDE SÆLGERE OG ANDRE                         */ 
 /*      OBS.: HER BEHANDLES KUN EJERE FRA ESR                       */ 
 /********************************************************************/ 
 OVF_GENEREL_OPL: PROC;                                                 
         BH61310N.CEJFORH = BH960.AKT_EJER.CEJFORH;                     
         BH61310N.CBENYT  = BH960.AKT_VURD.CBENYT;                      
         BH61310N.HEJLTX  = BH960.ESR.STAMOPL.HEJLTX;                   
         BH61310N.GANE    = BH960.AKT_EJER.GANE;                        
         BH61310N.BFSKAT  = BH960.TIL_SLS.BFSKAT_SLS;                   
                                                                        
         BH61310N.CEJDVSKATKOD = '0';                                   
                                                                        
         IF BH960.TIL_EVS.CEJBEVS = '7'                                 
         THEN                                                           
           BH61310N.CEJDVSKATKOD = '7';                                 
                                                                        
         IF BH960.TIL_EVS.CEJBEVS = '8'                                 
         THEN                                                           
           BH61310N.CEJDVSKATKOD = '6';                                 
                                                                        
         IF BH960.TIL_SLS.BFSKAT_SLS > 0 &  /* EJENDOMSSKAT ER IKKE  */ 
            BH960.TIL_SLS.BPRIVAN_SLS = 0 & /* PERIODEBEREGNET       */ 
           (BH960.FDAGE > 0 & BH960.FDAGE < 360)                        
         THEN                                                           
            CALL PERIODEFORDEL_SKAT;                                    
         BH61310N.BERHVAN = BH960.BERHVAN_SLS;                          
         BH61310N.FLEJL = BH960.AKT_VURD.FLEJL;                         
         BH61310N.FEJER = BH960.AKT_EJER.FEJER;                         
         SELECT (BH960.AKT_VURD.CBENYT);                                
            WHEN ('02','03','04','05','06','07','13','16','17','33',    
                  '34','42','43','44','45','70','71','78','98')         
               BH61310N.BYDAF = BH960.BYDAF;                            
            WHEN ('01','41')                                            
               DO;                                                      
                  IF BH960.AKT_VURD.FLEJL > 1                           
                  THEN                                                  
                     BH61310N.BYDAF = BH960.ESR.BYDAF;                  
               END;                                                     
            OTHERWISE                                                   
               BH61310N.BYDAF = 0;                                      
         END;                                                           
         BH61310N.CEJ_100 = BH960.AKT_EJER.CEJ_100;                     
         IF BH960.AKT_EJER.CEJ_100 = '2'                                
         THEN                                                           
            DO;                                                         
               IF BH960.AKT_EJER.FEJER > 2                              
               THEN                                                     
                  BH61310N.CEJ_100 = '0';                               
               ELSE                                                     
                  BH61310N.CEJ_100 = '1';                               
            END;                                                        
         IF BH960.AKT_EJER.FEJER = 1 &                                  
           (BH960.AKT_EJER.GANE = 0 !                                   
            BH960.AKT_EJER.GANE = 10000)                                
         THEN                                                           
            BH61310N.CEJ_100 = '1';                                     
         IF BH61310N.CEJ_100 ^= '1'    /* BLANK */                      
         THEN                                                           
            BH61310N.CEJ_100 = '0';                                     
                                                                        
         /*Forskud 2018/slut 2017 CEJSO skal ikke bruges mere */        
         IF  BH960.AKT_VURD.CEJERL = '1'     &                          
             BH960.AKT_EJER.DOVTG <= ÅR0101  &                          
             BH960.AKT_EJER.DAFSTÅ > ÅR0101  &                          
             BH960.AKT_EJER.DAFSTÅ <= ÅR1231 &                          
            (BH960.AKT_VURD.CBENYT = '21' !                             
             BH960.AKT_VURD.CBENYT = '22' !                             
             BH960.AKT_VURD.CBENYT = '25' !                             
             BH960.AKT_VURD.CBENYT = '26' !                             
             BH960.AKT_VURD.CBENYT = '27' !                             
             BH960.AKT_VURD.CBENYT = '28' !                             
             BH960.AKT_VURD.CBENYT = '29')                              
         THEN                                                           
            BH61310N.CUDLEJ = '1';                                      
         ELSE                                                           
            BH61310N.CUDLEJ = '0';                                      
                                                                        
         IF (BH960.AKT_VURD.CEJERL = '1'      &                         
             BH960.AKT_EJER.DOVTG <= ÅR0101   &                         
            (BH960.AKT_EJER.DAFSTÅ = 0        !                         
             BH960.AKT_EJER.DAFSTÅ > ÅR1231)) &                         
            (BH960.AKT_VURD.CBENYT = '21'     !                         
             BH960.AKT_VURD.CBENYT = '22'     !                         
             BH960.AKT_VURD.CBENYT = '25'     !                         
             BH960.AKT_VURD.CBENYT = '26'     !                         
             BH960.AKT_VURD.CBENYT = '27'     !                         
             BH960.AKT_VURD.CBENYT = '28'     !                         
             BH960.AKT_VURD.CBENYT = '29')                              
         THEN                                                           
            BH61310N.CUDLEJ = '1';                                      
                                                                        
         IF BH960.AKT_VURD.CUOP = '5'                                   
         THEN                                                           
            BH61310N.CUNOPFØR = '1';                                    
         ELSE                                                           
            BH61310N.CUNOPFØR = '0';                                    
                                                                        
         IF BH960.AKT_VURD.CBENYT = '70' !                              
            BH960.AKT_VURD.CBENYT = '71' !                              
            BH960.AKT_VURD.CBENYT < '01'                                
         THEN                                                           
            BH61310N.CVURD = '0';                                       
         ELSE                                                           
            BH61310N.CVURD = '1';                                       
                                                                        
         BH61310N.CFORDEL = '0';                                        
         IF BH960.AKT_VURD.EJEBOL(1).EJBOLTYP = '4' !   /*STUEHUSVÆRDI*/
            BH960.AKT_VURD.EJEBOL(2).EJBOLTYP = '4' !   /*STUEHUSVÆRDI*/
            BH960.AKT_VURD.BSKOV ^= 0                                   
         THEN                                                           
            BH61310N.CFORDEL = '1';                                     
         IF BH960.AKT_VURD.EJEBOL(1).EJBOLTYP > '4' ! /*EJERBOLIGVÆRDI*/
            BH960.AKT_VURD.EJEBOL(2).EJBOLTYP > '4'   /*EJERBOLIGVÆRDI*/
         THEN                                                           
            BH61310N.CFORDEL = '1';                                     
         IF ((BH960.AKT_VURD.CBENYT = '01' !                            
              BH960.AKT_VURD.CBENYT = '41') &                           
              BH960.AKT_VURD.FLEJL = 2) &                               
             (BH960.AKT_VURD.EJEBOL(1).EJBOLTYP >= '4' !                
              BH960.AKT_VURD.EJEBOL(2).EJBOLTYP >= '4')                 
         THEN                                                           
            BH61310N.CFORDEL = '0';                                     
         IF BH960.AKT_VURD.EJEBOL(1).EJBOLTYP = '8' ! /*MINDRE LANDEJD*/
            BH960.AKT_VURD.EJEBOL(1).EJBOLTYP = '9' ! /*VINDMØL.PARCEL*/
            BH960.AKT_VURD.EJEBOL(2).EJBOLTYP = '8' !                   
            BH960.AKT_VURD.EJEBOL(2).EJBOLTYP = '9'                     
         THEN                                                           
            BH61310N.CFORDEL = '0';                                     
         BH61310N.BEJDVÆ_P4B = BH960.ESR.BEJDVÆP4B;                     
         IF BH960.AKT_VURD.ANT_FOR_1 > 0 !                              
            BH960.AKT_VURD.ANT_FOR_2 > 0                                
         THEN                                                           
            BH61310N.EJBOLTYP = '1';                                    
 END OVF_GENEREL_OPL;                                                   
 /*********************************************************************/
 /*   PROCEDUREN OVERFØRER DE OPLYSNINGER TIL SLS , SOM IKKE SKAL     */
 /*   OVERFØRES FOR SÆLGERE ( EJENDOMSVÆRDIER FOR SLUTÅRET )          */
 /*   OBS.: HER BEHANDLES KUN EJERE FRA ESR                           */
 /*********************************************************************/
 OVF_EJSÆLGER_OPL: PROC;                                                
         IF (BH61310N.CSALG ^= 'S' !                                    
            (BH960.AKT_EJER.DAFSTÅ > 0 &                                
             BH960.AKT_EJER.DAFSTÅ > ÅR1231))                           
         THEN                                                           
            DO;                                                         
               BH61310N.BEJDVÆR    = BH960.AKT_VURD.BEJDVÆR;            
               BH61310N.BEJDVÆ_P4A = BH960.AKT_VURD.BEJDVÆP4A;          
               IF BH960.AKT_VURD.BEJDVÆP4A  = 0                         
               THEN                                                     
                  DO;                                                   
                    IF BH960.AKT_VURD.BEJDVÆR > 999999999               
                    THEN                                                
                       BH61310N.BKONT_AND = 0;                          
                    ELSE                                                
                       BH61310N.BKONT_AND = BH960.AKT_VURD.BEJDVÆR;     
                  END;                                                  
               ELSE                                                     
                  DO;                                                   
                    IF BH960.AKT_VURD.BEJDVÆP4A > 999999999             
                    THEN                                                
                       BH61310N.BKONT_AND = 0;                          
                    ELSE                                                
                       BH61310N.BKONT_AND = BH960.AKT_VURD.BEJDVÆP4A;   
                  END;                                                  
               IF BH960.AKT_EJER.GANE > 0 &                             
                  BH960.AKT_EJER.GANE < 10000                           
               THEN                                                     
                  BH61310N.BKONT_AND =                                  
                  BH61310N.BKONT_AND * BH960.AKT_EJER.GANE / 10000;     
            END;                                                        
 END OVF_EJSÆLGER_OPL;                                                  
 /*********************************************************************/
 /*   PROCEDUREN BEREGNER EJENDOMSSKAT FOR EJERPERIODEN, DA           */
 /*   EJD.SKATTEN ER IKKE PERIODEFORDELT I BH96000 I ALLE TILFÆLDE    */
 /*   OBS.: HER BEHANDLES KUN EJERE FRA ESR                           */
 /*********************************************************************/
 PERIODEFORDEL_SKAT:                                                    
         PROC;                                                          
         BH61310N.BFSKAT =                                              
                    (BH960.TIL_SLS.BFSKAT_SLS * BH960.FDAGE) / 360;     
 /* OBS.: DER SKAL P.T. IKKE TAGES HØJDE FOR FORSKUDT REGNSKABSÅR */    
 /*       VED DENNE PERIODEFORDELING                              */    
 END PERIODEFORDEL_SKAT;                                                
 /*********************************************************************/
 /*          UDSKRIVNING AF TOTALLISTE                                */
 /*********************************************************************/
 SLUT:                                                                  
         PROC;                                                          
         CALL ZZ20300(LIN_BLANK,'01');                                  
         CALL ZZ20300(LIN1,'01');                                       
         CALL ZZ20300(LIN2,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST ='ANTAL LÆSTE INDIVIDER PÅ BH96000D BEREGNINGSBÅND';
         LIN3.ANT = TV_LÆS;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = '     HERAF FRASORTEREDE OPTÆLL.INDV.  ';         
         LIN3.ANT = TV_KOM;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE PERSONER PÅ BH9600D ';              
         LIN3.ANT = TV_LÆS_CPR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE SELSKABER PÅ BH9600D ';             
         LIN3.ANT = TV_LÆS_SENR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL LÆSTE ØVRIGE PÅ BH9600D ';                
         LIN3.ANT = TV_LÆS_ØVR;                                         
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE PERSONER';                   
         LIN3.ANT = TV_FRAS_CPR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE SELSKABER';                  
         LIN3.ANT = TV_FRAS_SENR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE ØVRIGE';                     
         LIN3.ANT = TV_FRAS_ØVR;                                        
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH65300M.INDKÅR1 !! ' CPR';                       
         LIN3.ANT = TV_FRA_KØB_CPR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH65300M.INDKÅR1 !! ' SENR';                      
         LIN3.ANT = TV_FRA_KØB_SENR;                                    
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE KØBT ' !!                    
                      BH65300M.INDKÅR1 !! ' ØVR';                       
         LIN3.ANT = TV_FRA_KØB_ØVR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = CPR';             
         LIN3.ANT = TV_FRA_OFF_CPR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = SENR';            
         LIN3.ANT = TV_FRA_OFF_SENR;                                    
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = ' ANTAL FRASORTEREDE OFF.EJER = ØVR';             
         LIN3.ANT = TV_FRA_OFF_ØVR;                                     
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = 'ANTAL SKREVNE INDIVIDER PÅ D0314314.A206';       
         LIN3.ANT = TV_SKRIV;                                           
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = '      HERAF UDENLANDSKE EJENDOMME';              
         LIN3.ANT = TV_UDLAND;                                          
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = '      HERAF ØVRIGE EJ FRA ESR     ';             
         LIN3.ANT = TV_EJ_FRA_ESR;                                      
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE PERSONER PÅ D0314314.A206';       
         LIN3.ANT = TV_SKRIV_CPR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE SELSKABER PÅ D0314314.A206';      
         LIN3.ANT = TV_SKRIV_SENR;                                      
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS1;                                              
         LIN3.TEKST = ' ANTAL SKREVNE ØVRIGE PÅ D0314314.A206';         
         LIN3.ANT = TV_SKRIV_ØVR;                                       
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL OVERFØRTE KØBERE';                         
         LIN3.ANT = TV_KØB;                                             
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL OVERFØRTE SÆLGERE';                        
         LIN3.ANT = TV_SALG;                                            
         CALL ZZ20300(LIN3,'01');                                       
         LIN3.CTL = ZZWS3;                                              
         LIN3.TEKST = 'ANTAL MED FEJL I DEN ERHVERVSMÆSSIGE SKAT ';     
         LIN3.ANT = TV_BERHVAN_FEJL;                                    
         CALL ZZ20300(LIN3,'01');                                       
 END SLUT;                                                              
 %INCLUDE BH61300M;   /* FAKTURING TIL T&S */                           
 END BH61300;                                                           
