 /*********************************************************************/
 /* FREMFINDING AF PERSONOPLYSNINGER FRA CSRP-DATA           BR94102M */
 /* FRA FORSKUD 2017 HENTES DER OPLYSNINGER PÅ NOGLE PERSONER MED MT  */
 /* DATA. DERFOR ER DER UNDERSØGES FELTERNE ALLEREDE ER UDFYLDT MED   */
 /* MT OPLYSNINGER                                                    */
 /*********************************************************************/
 LÆS_PERSON_CSRPDATA: PROC;                                             
                                                                        
         DCL HJ_PIC8       PIC       '(08)9';                           
                                                                        
         HJ_PIC8         = 0;                                           
         DCLBQ70000T.PNR = BH946.SORT_CPR;                              
                                                                        
         EXEC SQL OPEN CSRP_PERSON_CUR;                                 
                                                                        
         CALL FETCH_CSRP_PERSON_CUR;                                    
                                                                        
         IF SQLCODE = 0                                                 
         THEN                                                           
            DO;                                                         
              BH946.MT.DCPRN = BH946.SORT_CPR;                          
                                                                        
              IF BH946.MT.CCIVS = ' '                                   
              THEN                                                      
                BH946.MT.CCIVS = DCLBQ70000T.CIVISKOD;                  
                                                                        
           /* UDVIDDET MED AEGTFPNR FOR AT KUNNE REGNE ALDER */         
           /* PÅ ÆGTEFÆLLER TIL BEGRÆNSET SKATTEPLITIGE      */         
              IF (DCLBQ70000T.CIVISKOD = 'G' !                          
                  DCLBQ70000T.CIVISKOD = 'P') &                         
                  BH946.MT.DHENVN(SLUTÅR) = 0                           
              THEN                                                      
                DO;                                                     
                  IF DCLBQ70000T.AEGTFPNR > 0                           
                  THEN                                                  
                    DO;                                                 
                      BH946.MT.DHENVN(SLUTÅR) = DCLBQ70000T.AEGTFPNR;   
                      CSRP_ÆGTEFÆLLE_FLAG = '1'B;                       
                    END;                                                
                  ELSE                                                  
                    DO;                                                 
                      CSRP_ÆGTEFÆLLE_FLAG = '0'B;                       
                    END;                                                
                END;                                                    
                                                                        
              IF BH946.MT.HIST_OPL(SLUTÅR).CSBSK = ' '                  
              THEN                                                      
                BH946.MT.HIST_OPL(SLUTÅR).CSBSK = '1';                  
                                                                        
              IF DCLBQ70000T.CVISFRADTO ^= ' ' &                        
                 BH946.MT.HIST_OPL(SLUTÅR).DCIÆN = 0                    
              THEN                                                      
                DO;                                                     
                  HJ_PIC8 = DCLBQ70000T.CVISFRADTO;                     
                  BH946.MT.HIST_OPL(SLUTÅR).DCIÆN = HJ_PIC8;            
                END;                                                    
                                                                        
              IF DCLBQ70000T.CIVISKOD = 'D' &                           
                 BH946.MT.HIST_OPL(SLUTÅR).CSKPLOM = ' '                
              THEN                                                      
                DO;                                                     
                  IF BH946.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF     
                                   > ' '                                
                  THEN                                                  
                    BH946.MT.HIST_OPL(SLUTÅR).CSKPLOM =                 
                      BH946.EVS_UDTRÆK.EVS_SLUT_EJER.SIDSTE.CSKPLOMF;   
                  ELSE                                                  
                  /* Default værdi Forskud 2017 Ændret fra 1 til 0 */   
                  /* efter aftaler med ADA                         */   
                    BH946.MT.HIST_OPL(SLUTÅR).CSKPLOM = '0';            
                END;                                                    
              TV.CSRPDATA = TV.CSRPDATA + 1;                            
            END;                                                        
                                                                        
         EXEC SQL CLOSE CSRP_PERSON_CUR;                                
                                                                        
 END LÆS_PERSON_CSRPDATA;                                               
 /*********************************************************************/
 /* LÆS AKTUEL PERSON OPLYSNINGER FRA CSRP                            */
 /*********************************************************************/
 DECLARE_CSRP_PERSON_CUR: PROC;                                         
                                                                        
      EXEC SQL DECLARE CSRP_PERSON_CUR CURSOR FOR                       
           SELECT  T1.PNR,                                              
                   T1.AEGTFPNR,                                         
                   T1.CIVISKOD,                                         
                   T1.CVISFRADTO,                                       
                   T1.ADRFRADTO,                                        
                   T1.BOPAEKMNR                                         
           FROM BQ70000T T1                                             
           WHERE  T1.PNR   = :DCLBQ70000T.PNR;                          
                                                                        
 END DECLARE_CSRP_PERSON_CUR;                                           
 /*********************************************************************/
 /*  Fetch EJENDOM CSRP.                                              */
 /*********************************************************************/
 FETCH_CSRP_PERSON_CUR: PROC;                                           
                                                                        
      EXEC SQL FETCH CSRP_PERSON_CUR                                    
         INTO :DCLBQ70000T.PNR,                                         
              :DCLBQ70000T.AEGTFPNR,                                    
              :DCLBQ70000T.CIVISKOD,                                    
              :DCLBQ70000T.CVISFRADTO,                                  
              :DCLBQ70000T.ADRFRADTO,                                   
              :DCLBQ70000T.BOPAEKMNR;                                   
                                                                        
      SELECT (SQLCA.SQLCODE);                                           
         WHEN (0);                                                      
         WHEN (100);                                                    
        OTHERWISE                                                       
         PUT SKIP LIST                                                  
          ('PERSON_CUR FEJL SØG I CSRP-DATA SQLKODE = ',SQLCA.SQLCODE); 
      END;                                                              
                                                                        
 END FETCH_CSRP_PERSON_CUR;                                             
