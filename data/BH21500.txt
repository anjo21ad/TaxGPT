  PROCESS OPT(TIME);                                                    
 BH21500: /* PÅFØR EVT. HENVISNINGSNR. FRA GL. SLUTÅR PÅ EVS-UDTRÆK */  
         PROCEDURE OPTIONS (MAIN) REORDER;                              
 /********************************************************************/ 
         DCL COPYRIGHT   CHAR      (30) STATIC                          
                         INIT ('COPYRIGHT KOMMUNEDATA I/S 2001');       
 /********************************************************************* 
 *                                                                    * 
 * FORMÅL : AT SKAFFE NUVÆRENDE ELLER TIDLIGERE ÆGTEFÆLLES PERSONNUM- * 
 *          MER FRA MINI MT-FIL FRA GL. SLUTÅR TIL PÅSÆTNING PÅ EVS   * 
 *          UDTRÆK FRA GL. SLUTÅR AF HENSYN TIL BEREGNINGEN AF UD-    * 
 *          GANGSPUNKT FOR EVT. STIGNINGSBEGRÆNSNING FRA GL. SLUTÅR   * 
 *          TIL NYT SLUTÅR. DETTE UDGANGSPUNKT SKAL VED OVERTAGELSE   * 
 *          AF EJENDOM FRA ÆGTEFÆLLEN BEREGNES UDFRA DENNES EJENDOMS- * 
 *          VÆRDISKAT FOR GL. SLUTÅR HVIS DEN AKTUELLE EJER FØRST HAR * 
 *          KØBT/OVERTAGET EJENDOMMEN I LØBET AF NYT SLUTÅR F.EKS.    * 
 *          VED SKILSMISSE, DØDSFALD ELLER HANDEL MELLEM ÆGTEFÆLLER.  * 
 *                                                                    * 
 *          HVIS ÆGTEFÆLLE PERSONNUMMER SKAFFES SKRIVES TO INDIVIDER  * 
 *          1 MED SORTCPR=DHENVN OG 1 MED SORTCPR=DCPRN AF HENSYN TIL * 
 *          ALTID AT KUNNE BEREGNE UDGANGSPUNKT FOR STIGNINGSBEGRÆNS- * 
 *          NING JVF. OVENFOR.                                        * 
 *                                                                    * 
 * INPUT  : B.H21400D  UDTRÆK FRA EVS GL. SLUTÅR.                     * 
 *          B.H65501D  MINI MT-FIL FRA EVS GL. SLUTÅR (HOVEDLEV.).    * 
 *                                                                    * 
 * OUTPUT : B.H21500D  EVS UDTRÆK EFTER PÅFØRSEL AF EVT. ÆGTEFÆLLE    * 
 *                     PERSONNUMMER.                                  * 
 *          BH21501Y   KØRSELSKONTROLLISTE                            * 
 *                                                                    * 
 * PROGRAMMØR: AAGE RASMUSSEN   LSU T&S EVS            AUG 2001       * 
 * RETTET:                                                            * 
 *                                                                    * 
 *********************************************************************/ 
         DEFAULT RANGE (*) STATIC UNALIGNED CONN;                       
 /********************************************************************* 
 *                       DECLARE AF REGISTRE                          * 
 *********************************************************************/ 
 DCL     BH655I FILE RECORD INPUT;     /* MINI MT-FIL */                
 DCL     BH214I FILE RECORD INPUT;     /* EVS SLUT UDTRÆK */            
 DCL     BH215O FILE RECORD OUTPUT;    /* BEHANDLET EVS SLUT UDTRÆK */  
 DCL     SYSPRINT FILE OUTPUT;                                          
 /********************************************************************* 
 *       DECLARE AF MINI MT-FIL FRA GL. SLUTÅR                        * 
 *********************************************************************/ 
 DCL     BH655_AR CHAR (200) VAR;                                       
 DCL     1 BH655 BASED(ADDR(BH655_AR)),                                 
           3 LÆNGDE BIN FIXED (15),                                     
           %INCLUDE BH65501N;;                                          
 /********************************************************************* 
 *       DECLARE AF EVS UDTRÆK INPUT                                  * 
 *********************************************************************/ 
 DCL     1 BH214,                                                       
           %INCLUDE BH20000N;;                                          
 /********************************************************************* 
 *       DECLARE AF EVS UDTRÆK OUTPUT                                 * 
 *********************************************************************/ 
 DCL     1 BH215,                                                       
           %INCLUDE BH21500N;;                                          
 /********************************************************************* 
 *               DECLARE AF DIV. TÆLLEVÆRKER                          * 
 *********************************************************************/ 
 DCL     1 TV,                        /* TÆLLEVÆRKER TIL TOTALLISTEN */ 
           2 LÆS_BH655   FIXED DEC (7),                                 
           2 LÆS_BH214   FIXED DEC (7),                                 
           2 FUNDET      FIXED DEC (7),                                 
           2 FDHENVN     FIXED DEC (7),                                 
           2 SKREVET     FIXED DEC (7);                                 
 /********************************************************************* 
 *               DECLARE AF DIV. HJÆLPEFELTER                         * 
 *********************************************************************/ 
 DCL     EOF_BH655       BIT       (1);                                 
 DCL     EOF_BH214       BIT       (1);                                 
 DCL     JA              BIT       (1)     INIT ('1'B);                 
 DCL     NEJ             BIT       (1)     INIT ('0'B);                 
                                                                        
 DCL     UDDATO          CHAR      (10);                                
                                                                        
 DCL     DATO1           CHAR      (26);                                
                                                                        
 DCL     1 DATO2         DEF DATO1,                                     
           2 ÅR          CHAR      (04),                                
           2 F1          CHAR      (01),                                
           2 MD          CHAR      (02),                                
           2 F2          CHAR      (01),                                
           2 DG          CHAR      (02);                                
                                                                        
 /********************************************************************* 
 *       DECLARE AF ENTRY OG BUILTIN                                  * 
 *********************************************************************/ 
 DCL     ADDR            BUILTIN;                                       
 DCL     PLIDUMP         BUILTIN;                                       
 DCL     ZS38100         ENTRY OPTIONS (INTER ASM);   /* TIMESTAMP    */
 DCL     ZS61900         ENTRY;                                         
 DCL     PLIXOPT         CHAR      (40)      VAR STATIC                 
         EXTERNAL INIT ('NOTEST(ALL,,;)');                              
 /*********************************************************************/
 /*      DECLARE AF PRINTLINIER                                       */
 /*********************************************************************/
 DCL    1 SIDELIN,                                                      
          2 CCA          CHAR      (01),                                
          2 FILLER       CHAR      (132)     INIT      (' ');           
 DCL     1 OVERLIN1,                                                    
           2 CCA         CHAR      (01),                                
           2 TEKST1      CHAR      (17)      INIT      (                
           'PROGRAM: BH21500L'),                                        
           2 F1          CHAR      (21)      INIT      (' '),           
           2 TEKST2      CHAR      (52)      INIT      (                
           'SKAF ÆGTEFÆLLE (DHENVN) TIL EVS SLUT UDTRÆK'),              
           2 F2          CHAR      (21)      INIT      (' '),           
           2 TEKST3      CHAR      (11)      INIT      (                
           'UDSKREVET:'),                                               
           2 DATO        CHAR      (10);                                
 DCL     1 OVERLIN2,                                                    
           2 CCA         CHAR      (01),                                
           2 F1          CHAR      (46)      INIT      (' '),           
           2 TEKST1      CHAR      (86)      INIT                       
           ('          KØRSELSKONTROLLISTE');                           
 DCL     1 DETLIN1,                                                     
           2 CCA         CHAR      (01),                                
           2 TEKST       CHAR      (60),                                
           2 ANTAL       PIC       'Z.ZZZ.ZZ9',                         
           2 F2          CHAR      (63)      INIT      ((63)' ');       
         % INCLUDE ZZ20301M;                                            
 /********************************************************************* 
 *       HOUSEKEEPING                                                 * 
 *********************************************************************/ 
         CALL ZS61900;                          /* UDSKRIVNING AF SSI */
         EOF_BH655 = NEJ;                                               
         EOF_BH214 = NEJ;                                               
         ON ERROR CALL PLIDUMP ('SNHOB');                               
         OPEN FILE (BH655I) TITLE ('BH65501I');                         
         OPEN FILE (BH214I) TITLE ('BH21400I');                         
         OPEN FILE (BH215O) TITLE ('BH21500O');                         
         ON ENDFILE (BH655I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
         END;                                                           
         ON ENDFILE (BH214I)                                            
         BEGIN;                                                         
            EOF_BH655 = JA;                                             
            EOF_BH214 = JA;                                             
         END;                                                           
         CALL ZZ20390 ('*OPEN','BH21501Y');                             
         CALL ZS38100(DATO1);                                           
         UDDATO = DATO2.DG !! '/' !! DATO2.MD !!                        
                  '-' !! DATO2.ÅR;                                      
         OVERLIN1.DATO  = UDDATO;                                       
         SIDELIN.CCA    = ZZIK1;                                        
         OVERLIN1.CCA   = ZZWS2;                                        
         OVERLIN2.CCA   = ZZWS3;                                        
         TV = '';                                                       
                                                                        
 /********************************************************************* 
 *       HOVEDSTYRING                                                 * 
 *********************************************************************/ 
                                                                        
         CALL LÆS_BH655;                                                
         CALL LÆS_BH214;                                                
                                                                        
         DO WHILE (EOF_BH655 = NEJ ! EOF_BH214 = NEJ);                  
            SELECT;                                                     
               WHEN (BH214.DCPRN > BH655.DCPRN & EOF_BH655 = NEJ)       
                  CALL LÆS_BH655;                                       
               WHEN (BH214.DCPRN < BH655.DCPRN & EOF_BH214 = NEJ)       
                  DO;                                                   
                     CALL SKRIV_SINGLE;                                 
                     CALL LÆS_BH214;                                    
                  END;                                                  
               OTHERWISE                                                
                  DO;                                                   
                     TV.FUNDET = TV.FUNDET + 1;                         
                     IF BH655.HIST_OPL(2000).DHENVN > 0                 
                     THEN                                               
                        CALL SKRIV_DUBLET;                              
                     CALL SKRIV_SINGLE;                                 
                     CALL LÆS_BH214;                                    
                  END;                                                  
            END;                                                        
         END;                                                           
                                                                        
         CALL AFSLUT;                                                   
                                                                        
 /********************************************************************* 
 *       LÆS EVS UDTRÆK                                               * 
 *********************************************************************/ 
 LÆS_BH214:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH214I) INTO (BH214);                               
                                                                        
         IF EOF_BH214 = NEJ                                             
         THEN                                                           
            TV.LÆS_BH214 = TV.LÆS_BH214 + 1;                            
                                                                        
 END LÆS_BH214;                                                         
 /********************************************************************* 
 *       LÆS MINI MT-FIL                                              * 
 *********************************************************************/ 
 LÆS_BH655:                                                             
 PROC;                                                                  
                                                                        
         READ FILE (BH655I) INTO (BH655_AR);                            
                                                                        
         IF EOF_BH655 = NEJ & BH655.DCPRN < 9999999999                  
         THEN                                                           
            TV.LÆS_BH655 = TV.LÆS_BH655 + 1;                            
                                                                        
 END LÆS_BH655;                                                         
 /********************************************************************* 
 *       SKRIV EVS UDTRÆK                                             * 
 *********************************************************************/ 
 SKRIV_SINGLE:                                                          
 PROC;                                                                  
                                                                        
         BH215.EVS_SLUT = BH214, BY NAME;                               
         BH215.SORTCPR  = BH214.DCPRN;                                  
         BH215.SORTKOM  = BH214.EKOMNR;                                 
         BH215.SORTEJD  = BH214.EEJDNR;                                 
         BH215.CFRATYPE = '1'; /* FRA EVS SLUT */                       
                                                                        
         WRITE FILE (BH215O) FROM (BH215);                              
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
                                                                        
 END SKRIV_SINGLE;                                                      
 /********************************************************************* 
 *       SKRIV EVS UDTRÆK                                             * 
 *********************************************************************/ 
 SKRIV_DUBLET:                                                          
 PROC;                                                                  
                                                                        
         BH215.EVS_SLUT = BH214, BY NAME;                               
         BH215.SORTCPR  = BH655.HIST_OPL(2000).DHENVN;                  
         BH215.SORTKOM  = BH214.EKOMNR;                                 
         BH215.SORTEJD  = BH214.EEJDNR;                                 
         BH215.CFRATYPE = '0'; /* DUBLET FRA EVS SLUT */                
                                                                        
         WRITE FILE (BH215O) FROM (BH215);                              
                                                                        
         TV.SKREVET = TV.SKREVET + 1;                                   
         TV.FDHENVN = TV.FDHENVN + 1;                                   
                                                                        
 END SKRIV_DUBLET;                                                      
 /********************************************************************* 
 *                       TOTALER UDSKRIVES.                           * 
 *********************************************************************/ 
 TOTALLISTE:                                                            
         PROC;                                                          
                                                                        
         CALL ZZ20300(SIDELIN,'01');                                    
         CALL ZZ20300(OVERLIN1,'01');                                   
         CALL ZZ20300(OVERLIN2,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.LÆS_BH655;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ MINI MT-FIL GL. SLUTÅR    B.H65501D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.LÆS_BH214;                                  
         DETLIN1.TEKST = 'LÆSTE PÅ EVS SLUT UDTRÆK           B.H21400D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.FUNDET;                                     
         DETLIN1.TEKST = 'HERAF EJER FUNDET PÅ MINI MT-FIL            ';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.FDHENVN;                                    
         DETLIN1.TEKST = 'HERAF ÆGTEFÆLLE FUNDET PÅ MINI MT-FIL';       
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
         DETLIN1.CCA = ZZWS2;                                           
         DETLIN1.ANTAL = TV.SKREVET;                                    
         DETLIN1.TEKST = 'SKREVNE INDIVIDER INCL. DUBLETTER  B.H21500D';
         CALL ZZ20300 (DETLIN1,'01');                                   
                                                                        
  END TOTALLISTE;                                                       
 /*********************************************************************/
 /*      CLOSE AF FILER OG SPOOL                                      */
 /*********************************************************************/
 AFSLUT:                                                                
         PROC;                                                          
                                                                        
         CALL TOTALLISTE;                                               
                                                                        
         CLOSE FILE (BH214I),                                           
               FILE (BH655I),                                           
               FILE (BH215O);                                           
                                                                        
         CALL ZZ20300((133)' ','99');                                   
                                                                        
 END AFSLUT;                                                            
                                                                        
 END  BH21500;                                                          
