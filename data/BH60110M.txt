                                                                        
 /*********************************************************************/
 /* BH60110M: OPSÆT DIVERSE OPLYSNINGER VEDR. KØB ELLER SALG.         */
 /*                                                                   */
 /*           BRUGES VED BEREGNING AF LEJEVÆRDI M.V. TIL ORD ESKEMA   */
 /*                                                                   */
 /*           SKAL RETTES VED ÅRSTILRETNINGEN IDET ÅRSTAL I DIVERSE   */
 /*           DATA-NAVNE SAMT I DENNE TEKST 'RULLES'                  */
 /*                                                                   */
 /*           INDGÅR I PROGRAM BH60100                                */
 /*********************************************************************/
 OPSÆT_KØB_SALG:                                                        
 PROC;                                                                  
                                                                        
 DCL     FRADATO         PIC       '(8)9'    STATIC;                    
 DCL     1 FRA           BASED(ADDR(FRADATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
 DCL     TILDATO         PIC       '(8)9'    STATIC;                    
 DCL     1 TIL           BASED(ADDR(TILDATO)),                          
         2 ÅR            PIC       '9999',                              
         2 MD            PIC       '99',                                
         2 DG            PIC       '99';                                
                                                                        
         /* UNDERSØG OM EJEREN HAR HANDLET I INDKOMSTÅRET */            
                                                                        
         KØBT_I_ÅR  = NEJ;                                              
         SOLGT_I_ÅR = NEJ;                                              
                                                                        
         IF BH601.DOVTG > ÅR0101 & BH601.DOVTG <= ÅR1231                
         THEN                                                           
            KØBT_I_ÅR = JA;                                             
                                                                        
         IF BH601.DAFSTÅ > ÅR0101 & BH601.DAFSTÅ <= ÅR1231              
         THEN                                                           
            SOLGT_I_ÅR = JA;                                            
                                                                        
         BH601.FRADAG = BH601.DOVTG;                                    
         BH601.TILDAG = BH601.DAFSTÅ;                                   
                                                                        
         IF BH601.FRADAG < ÅR0101                                       
         THEN                                                           
            BH601.FRADAG = ÅR0101;                                      
         IF BH601.TILDAG > ÅR1231 !                                     
            BH601.TILDAG = 0                                            
         THEN                                                           
            BH601.TILDAG = ÅR1231;                                      
                                                                        
         FRADATO = BH601.FRADAG;                                        
         TILDATO = BH601.TILDAG;                                        
                                                                        
         IF (KØBT_I_ÅR  = NEJ &      /* PERSONEN HAR EJET EJENDOMMEN */ 
             SOLGT_I_ÅR = NEJ)       /*  H E L E  DETTE INDKOMSTÅR   */ 
         THEN                                                           
            TÆLLER, NÆVNER = 1;      /* INGEN PERIODEBEREGNING       */ 
         ELSE                                                           
            DO;                                                         
               /* BEREGN BRØK TIL PERIODEBEREGNING. ALLE  H E L E  */   
               /* MÅNEDER BEREGNES SOM VÆRENDE PÅ 30 DAGE          */   
               /* D.V.S. ET  H E L T  ÅR ER PÅ 360 DAGE            */   
               NÆVNER = 360;                                            
               TÆLLER = (((TIL.MD - 1) * 30) + TIL.DG) -                
                        (((FRA.MD - 1) * 30) + FRA.DG);                 
               BH601.CPERIOD = 1;                                       
               IF TÆLLER = 0                                            
               THEN                                                     
                  TÆLLER = 1; /* KØB OG SALG SAMME DATO */              
               BH601.FDAGE = TÆLLER;                                    
            END;                                                        
                                                                        
 END OPSÆT_KØB_SALG;                                                    
